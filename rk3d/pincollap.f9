      SUBROUTINE PINCOLLAP(SW,SWS,SWC,TPF,TPFS,COLSWS,COLSWC,
     $  COLTPF,COLTPFS,COLTNF,COLTXT,COLTCD,TNF,TXT,TCD,
     $  COLSIGDSN,SIGDSN,XHIP,COLXHIP,NCOLXY,NCOLZ)
C
C     This subroutine collapses kappa-sigma-fission cross sections
C     and fluxes for NG>2 into two group cross sections and fluxes
C     for the pin power reconstruction (NSHAP = "HEXA" only). 
C     NG>2 introduces complexities in determining pin-wise fluxes.
C
      include 'param.fcb'
      include 'nonfue.fcb'
      include 'cntl.fcb'

      DIMENSION SW(NXYL,NZ,NG), SWS(6,NXYL,NZ,NG), SWC(6,NXYL,NZ,NG),
     $  TPF(NXYL,NZ,NG), TPFS(6,NXYL,NZ,NG),
     $  COLSWS(6,NXYL,NZ,NG), COLSWC(6,NXYL,NZ,NG),
     $  COLTPF(NXYL,NZ,NG), COLTPFS(6,NXYL,NZ,NG), COLTNF(NXYL,NZ,NG),
     $  COLTXT(NXYL,NZ,NG), COLTCD(NXYL,NZ,NG),
     $  TNF(NXYL,NZ,NG), TXT(NXYL,NZ,NG), TCD(NXYL,NZ,NG),
     $  SIGDSN(NXYL,NZ,NG,*),COLSIGDSN(NXYL,NZ,NG,*),
     $  XHIP(NXYL,NZ,*),COLXHIP(NXYL,NZ,*)

      DIMENSION NCOLXY(NXYLMAX,*),NCOLZ(*)
C
C-----ASSIGN SURFACE KAPPA-SIGMA-F = NODE KAPPA-SIGMA-F IF 
C-----NO BURNUP GRADIENT TREATMENT IS SELECTED
C
      IF(HEXBUGRAD.EQ.'N')THEN
       DO IG=1,NG
        DO IZ=IZCOLS,IZCOLE
         DO IXY=1,NXYL
          DO ISUR=1,6
           TPFS(ISUR,IXY,IZ,IG)=TPF(IXY,IZ,IG)
          ENDDO
         ENDDO
        ENDDO
       ENDDO
      ENDIF

      DO 101 IZ=IZCOLS,IZCOLE
       DO 100 IXY=1,NXYL
        I=NCOLXY(IXY,NCOLZ(IZ))
        IF(I.LE.NFUEXY)GOTO 100
C
C-----GROUP COLLAPSE SURFACE AVERAGE AND CORNER POINT FLUXES
C     AND KAPPA-SIGMA FISION CROSS SECTIONS
C
        DO 200 IS=1,6
         DO IG=1,2
          COLSWS(IS,IXY,IZ,IG)=0.0
          COLSWC(IS,IXY,IZ,IG)=0.0
          COLTPFS(IS,IXY,IZ,IG)=0.0
         ENDDO
 200    CONTINUE
        DO 300 IG=1,2
         COLTPF(IXY,IZ,IG)=0.0
         COLTNF(IXY,IZ,IG)=0.0
         COLTXT(IXY,IZ,IG)=0.0
         COLTCD(IXY,IZ,IG)=0.0
         COLXHIP(IXY,IZ,IG)=0.0
 300    CONTINUE
C        
C     FAST GROUP
C     
        DO 103 ISUR=1,6  
         SUMFLUX=0.0
         DO 102 IG=1,NGF
          COLSWS(ISUR,IXY,IZ,1)=COLSWS(ISUR,IXY,IZ,1)
     $      +SWS(ISUR,IXY,IZ,IG)
          COLSWC(ISUR,IXY,IZ,1)=COLSWC(ISUR,IXY,IZ,1)
     $      +SWC(ISUR,IXY,IZ,IG)
          COLTPFS(ISUR,IXY,IZ,1)=COLTPFS(ISUR,IXY,IZ,1)
     $      +TPFS(ISUR,IXY,IZ,IG)*SWS(ISUR,IXY,IZ,IG)
          SUMFLUX=SUMFLUX+SWS(ISUR,IXY,IZ,IG)
 102     CONTINUE
         COLTPFS(ISUR,IXY,IZ,1)=COLTPFS(ISUR,IXY,IZ,1)/SUMFLUX
 103    CONTINUE
        SUMFLUX=0.0
        SUMCHI=0.0
        DO 105 IG=1,NGF
         COLTPF(IXY,IZ,1)=COLTPF(IXY,IZ,1)
     $     +TPF(IXY,IZ,IG)*SW(IXY,IZ,IG)
         COLTNF(IXY,IZ,1)=COLTNF(IXY,IZ,1)
     $     +TNF(IXY,IZ,IG)*SW(IXY,IZ,IG)
         COLTXT(IXY,IZ,1)=COLTXT(IXY,IZ,1)
     $     +TXT(IXY,IZ,IG)*SW(IXY,IZ,IG)
         COLTCD(IXY,IZ,1)=COLTCD(IXY,IZ,1)
     $     +TCD(IXY,IZ,IG)*SW(IXY,IZ,IG)
         COLXHIP(IXY,IZ,1)=COLXHIP(IXY,IZ,1)+XHIP(IXY,IZ,IG)
         DO IGG=NGF+1,NG
          COLSIGDSN(IXY,IZ,2,1)=COLSIGDSN(IXY,IZ,2,1)
     $      +SIGDSN(IXY,IZ,IGG,IG)
         ENDDO
         COLSIGDSN(IXY,IZ,2,1)=COLSIGDSN(IXY,IZ,2,1)*SW(IXY,IZ,IG)
         SUMFLUX=SUMFLUX+SW(IXY,IZ,IG)
         SUMCHI=SUMCHI+XHIP(IXY,IZ,IG)
 105    CONTINUE
        COLTPF(IXY,IZ,1)=COLTPF(IXY,IZ,1)/SUMFLUX
        COLTNF(IXY,IZ,1)=COLTNF(IXY,IZ,1)/SUMFLUX
        COLTXT(IXY,IZ,1)=COLTXT(IXY,IZ,1)/SUMFLUX
        COLTCD(IXY,IZ,1)=COLTCD(IXY,IZ,1)/SUMFLUX
        COLSIGDSN(IXY,IZ,2,1)=COLSIGDSN(IXY,IZ,2,1)/SUMFLUX
C        
C     THERMAL GROUP
C     
        DO 203 ISUR=1,6  
         SUMFLUX=0.0
         DO 202 IG=NGF+1,NG
          COLSWS(ISUR,IXY,IZ,2)=COLSWS(ISUR,IXY,IZ,2)
     $      +SWS(ISUR,IXY,IZ,IG)
          COLSWC(ISUR,IXY,IZ,2)=COLSWC(ISUR,IXY,IZ,2)
     $      +SWC(ISUR,IXY,IZ,IG)
          COLTPFS(ISUR,IXY,IZ,2)=COLTPFS(ISUR,IXY,IZ,2)
     $      +TPFS(ISUR,IXY,IZ,IG)*SWS(ISUR,IXY,IZ,IG)
          SUMFLUX=SUMFLUX+SWS(ISUR,IXY,IZ,IG)
 202     CONTINUE
         COLTPFS(ISUR,IXY,IZ,2)=COLTPFS(ISUR,IXY,IZ,2)/SUMFLUX
 203    CONTINUE
        SUMFLUX=0.0
        DO 205 IG=NGF+1,NG
         COLTPF(IXY,IZ,2)=COLTPF(IXY,IZ,2)
     $     +TPF(IXY,IZ,IG)*SW(IXY,IZ,IG)
         COLTNF(IXY,IZ,2)=COLTNF(IXY,IZ,2)
     $     +TNF(IXY,IZ,IG)*SW(IXY,IZ,IG)
         COLTXT(IXY,IZ,2)=COLTXT(IXY,IZ,2)
     $     +TXT(IXY,IZ,IG)*SW(IXY,IZ,IG)
         COLTCD(IXY,IZ,2)=COLTCD(IXY,IZ,2)
     $     +TCD(IXY,IZ,IG)*SW(IXY,IZ,IG)
         COLXHIP(IXY,IZ,2)=COLXHIP(IXY,IZ,2)+XHIP(IXY,IZ,IG)
         SUMFLUX=SUMFLUX+SW(IXY,IZ,IG)
         SUMCHI=SUMCHI+XHIP(IXY,IZ,IG)
 205    CONTINUE
        COLTPF(IXY,IZ,2)=COLTPF(IXY,IZ,2)/SUMFLUX
        COLTNF(IXY,IZ,2)=COLTNF(IXY,IZ,2)/SUMFLUX
        COLTXT(IXY,IZ,2)=COLTXT(IXY,IZ,2)/SUMFLUX
        COLTCD(IXY,IZ,2)=COLTCD(IXY,IZ,2)/SUMFLUX
        COLXHIP(IXY,IZ,1)=COLXHIP(IXY,IZ,1)/SUMCHI
        COLXHIP(IXY,IZ,2)=COLXHIP(IXY,IZ,2)/SUMCHI
 100   CONTINUE
 101  CONTINUE
       
      RETURN
      END
