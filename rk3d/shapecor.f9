C23456789012345678901234567890123456789012345678901234567890123456789012
C                                                                      C
                           SUBROUTINE SHAPECOR
     &   ( TCD,TNF,TXT,SIGDSN,XHIP,B2SPEC,VOLU,RIVELO,
     &     SEFF,SEXT,SW,BETAT,BETATLAM,FILAMDT1,
     &     LSEXTZ,LSEXTR )
C                                                                      C
C23456789012345678901234567890123456789012345678901234567890123456789012
C                                                                      C
C
C  THIS SUBROUTINE ADJUSTS THE FLUX FOR COOLANT SPECTRAL EFFECTS
C  WITHIN THE FIXED-SOURCE SCALE FACTOR METHOD
C
C
C23456789012345678901234567890123456789012345678901234567890123456789012
C
      include 'param.fcb'
      include 'veloc.fcb'
      include 'xsec1.fcb'
      include 'geom.fcb'
      include 'che.fcb'
      include 'extsor.fcb'
 
       DIMENSION
     &   TCD(NXYL,NZ,*)          ,
     &   TNF(NXYL,NZ,*)          ,
     &   TXT(NXYL,NZ,*)       ,
     &   SIGDSN(NXYL,NZ,NG,*)    ,XHIP(NXYL,NZ,*)      ,
     &   B2SPEC(NXYL,NZ,*)       ,VOLU(NXYL,*)         ,
     &   RIVELO(NXYL,NZ,*)       ,
     &   SEFF(NXYL,NZ,*)         ,SEXT(*)              ,
     &   SW(NXYL,NZ,*)           ,BETAT(NXYL,*)        ,
     &   BETATLAM(NXYL,NZ,*)     ,FILAMDT1(NXYL,NZ,*)  ,
     &   LSEXTZ(*)               ,LSEXTR(*)
 
      DIMENSION SCAT1(4),FIS1(4),Q1(4),A1(4,4)
C
C---- COMPUTE THE NEW SHAPE ADJUSTED FLUX
C---- BASED ON THE NEW TIME STEP X-SECTION, AND OLD TIME STEP
C---- BUCKLING VALUE.
C
 
      IG1=1
      IF(NG.EQ.2) THEN
        IG=2
 
        DO 100 IZ=1,NZ
         DO 100 IXY=1,NXYL
          SCAT1(IG)=SIGDSN(IXY,IZ,IG,IG1)*SW(IXY,IZ,IG1)
 
          IF (ITRANSIT.EQ.0)THEN
C
C.........FOR THE STEADY-STATE CALCULATION
C
            FIS1(IG)=((1.0-BETAT(IXY,IZ))
     &                    *XHIP(IXY,IZ,IG)+BETATLAM(IXY,IZ,IG))
     &                    *TNF(IXY,IZ,IG1)*SW(IXY,IZ,IG1)/FLAMDA
            Q1(IG)=SCAT1(IG)+FIS1(IG)
     &                    +LSEXTR(IXY)*LSEXTZ(IZ)*SEXT(IG)
            A1(IG,IG)=TCD(IXY,IZ,IG)*B2SPEC(IXY,IZ,IG)
     &                    +TXT(IXY,IZ,IG)
            A1(IG,IG)=A1(IG,IG)-((1.0-BETAT(IXY,IZ))
     &                    *XHIP(IXY,IZ,IG)+ BETATLAM(IXY,IZ,IG))
     &                    *TNF(IXY,IZ,IG)/FLAMDA
 
          ELSEIF(ITRANSIT.EQ.1)THEN
C
C.........FOR THE TRANSIENT CALCULATION
C
            FIS1(IG)=((1.0-BETAT(IXY,IZ))
     &                   *XHIP(IXY,IZ,IG)+FILAMDT1(IXY,IZ,IG))
     &                   *TNF(IXY,IZ,IG1)*SW(IXY,IZ,IG1)/FLAMDA
            Q1(IG)=SCAT1(IG)+FIS1(IG)
     &                   +SEFF(IXY,IZ,IG)/VOLU(IXY,IZ)
            A1(IG,IG)=TCD(IXY,IZ,IG)*B2SPEC(IXY,IZ,IG)
     &                   +TXT(IXY,IZ,IG)
     &                   +RIVELO(IXY,IZ,IG)
            A1(IG,IG)=A1(IG,IG)-((1.0-BETAT(IXY,IZ))
     &                   *XHIP(IXY,IZ,IG)+FILAMDT1(IXY,IZ,IG))
     &                   *TNF(IXY,IZ,IG)/FLAMDA
 
          ENDIF
 
          SW(IXY,IZ,IG)=Q1(IG)/A1(IG,IG)
 
100     CONTINUE
 
      ELSEIF(NG.EQ.4) THEN
        DO 400 IZ=1,NZ
          DO 400 IXY=1,NXYL
            DO 380 IG=2,NG
              SCAT1(IG)=SIGDSN(IXY,IZ,IG,IG1)*SW(IXY,IZ,IG1)
 
              IF (ITRANSIT.EQ.0)THEN
C
C.............FOR THE STEADY-STATE CALCULATION
C
                FIS1(IG)=((1.0-BETAT(IXY,IZ))
     &                      *XHIP(IXY,IZ,IG)+BETATLAM(IXY,IZ,IG))
     &                      *TNF(IXY,IZ,IG1)*SW(IXY,IZ,IG1)/FLAMDA
                Q1(IG)=SCAT1(IG)+FIS1(IG)
     &                      +LSEXTR(IXY)*LSEXTZ(IZ)*SEXT(IG)
                A1(IG,IG)=TCD(IXY,IZ,IG)*B2SPEC(IXY,IZ,IG)
     &                      +TXT(IXY,IZ,IG)
                A1(IG,IG)=A1(IG,IG)-((1.0-BETAT(IXY,IZ))
     &                      *XHIP(IXY,IZ,IG)+BETATLAM(IXY,IZ,IG))
     &                      *TNF(IXY,IZ,IG)/FLAMDA
 
                DO 320 IGP=2,NG
                  IF(IGP.EQ.IG) GOTO 320
                  IGPP=IGP
                  IF(IGP.GT.IG) IGPP=IGP-1
                  A1(IG,IGP)=-((1.0-BETAT(IXY,IZ))
     &                         *XHIP(IXY,IZ,IG)+BETATLAM(IXY,IZ,IG))
     &                         *TNF(IXY,IZ,IGP)/FLAMDA
     &                         -SIGDSN(IXY,IZ,IG,IGPP)
320             CONTINUE
 
              ELSEIF(ITRANSIT.EQ.1)THEN
C
C.............FOR THE TRANSIENT CALCULATION
C
                FIS1(IG)=((1.0-BETAT(IXY,IZ))
     &                    *XHIP(IXY,IZ,IG)+FILAMDT1(IXY,IZ,IG))
     &                    *TNF(IXY,IZ,IG1)*SW(IXY,IZ,IG1)/FLAMDA
                Q1(IG)=SCAT1(IG)+FIS1(IG)
     &                    +SEFF(IXY,IZ,IG)/VOLU(IXY,IZ)
                A1(IG,IG)=TCD(IXY,IZ,IG)*B2SPEC(IXY,IZ,IG)
     &                    +TXT(IXY,IZ,IG)
     &                    +RIVELO(IXY,IZ,IG)
                A1(IG,IG)=A1(IG,IG)-((1.0-BETAT(IXY,IZ))
     &                    *XHIP(IXY,IZ,IG)+FILAMDT1(IXY,IZ,IG))
     &                    *TNF(IXY,IZ,IG)/FLAMDA
                DO 340 IGP=2,NG
                  IF(IGP.EQ.IG) GOTO 340
                  IGPP=IGP
                  IF(IGP.GT.IG) IGPP=IGP-1
                  A1(IG,IGP)=-((1.0-BETAT(IXY,IZ))
     &                    *XHIP(IXY,IZ,IG)+FILAMDT1(IXY,IZ,IG))
     &                    *TNF(IXY,IZ,IGP)/FLAMDA
     *                    -SIGDSN(IXY,IZ,IG,IGPP)
340             CONTINUE
              ENDIF
 
380         CONTINUE
            NNG = NG
            B13=A1(3,2)*A1(2,3)-A1(2,2)*A1(3,3)
            B14=A1(3,2)*A1(2,4)-A1(2,2)*A1(3,4)
            B23=A1(4,2)*A1(3,3)-A1(3,2)*A1(4,3)
            B24=A1(4,2)*A1(3,4)-A1(3,2)-A1(4,4)
            QT1=A1(3,2)*Q1(2)-A1(2,2)*Q1(3)
            QT2=A1(4,2)*Q1(3)-A1(3,2)*Q1(4)
            SW(IXY,IZ,NNG)=(B23*QT1-B13*QT2)/(B23*B14-B13*B24)
            SW(IXY,IZ,NNG-1)=(QT1-B14*SW(IXY,IZ,NNG))/B13
            SW(IXY,IZ,NNG-2)=(Q1(2)-A1(2,3)*SW(IXY,IZ,NNG-1)
     &                      -A1(2,4)*SW(IXY,IZ,NNG))/A1(2,2)
 
400     CONTINUE
 
      ENDIF
 
      RETURN
      END
