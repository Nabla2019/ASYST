      SUBROUTINE NONPLMH(
     &  DZ,VOLU,FNSWT,FNOLDT,BETATLAM,FILAMDT0,
     &  FILAMDT1,EXPLAMDT,CURNX,CURNV,CURNU,CURNZ,TLX,TLV,
     &  TLU,TLZ,PLMZX1,
     &  PLMZV1,PLMZU1,PLMXY1,PLMXY2,SWOLD,RIVELO,SEFVE,SW,
     &  NBC,NXSTART,NXEND,NDNUM,TL,TLP,TLM,PLMZX2,PLMZV2,PLMZU2)
C     
C     THIS SUBROUTINE CALCULATES THE LEAKAGES FOR EVERY
C     NODE, IN EVERY DIRECTION, AND THE EXPANSION COEFFICIENTS
C     FOR THE QUADRATIC LEAKAGE APPROXIMATION.
C     ********** FOR HEXAGONAL GEOMETRY **************
C     
      include 'param.fcb'
      include 'veloc.fcb'
      include 'hexdim.fcb'
      include 'xsec1.fcb'
      include 'bcs.fcb'
      include 'cntl.fcb'
      include 'geom.fcb'
      include 'che.fcb'
      
      DIMENSION TL(NG),   TLP(NG),   TLM(NG)
C     
C     CALCULATE TRANSVERSE LEAKAGES IN X,V,U AND Z-DIRECTIONS.
C     CALCULATE LEAKAGE SUMS FOR X,V AND U-DIRECTIONS.
C     
      DIMENSION
     &  NBC(*)                 ,
     &  NXSTART(*)                    ,NXEND(*)               ,
     &  NDNUM(-1:NX+2,0:*)            ,RIVELO(NXYL,NZ,*)      ,
     &  SEFVE(NXYL,NZ,*)              ,BETATLAM(NXYL,NZ,*)    ,
     &  SWOLD(NXYL,NZ,*)              ,SW(NXYL,NZ,*)          ,
     &  DZ(0:*)                       ,VOLU(NXYL,*)           ,
     &  FNSWT(NXYL,*)                 ,FNOLDT(NXYL,*)         ,
     &  FILAMDT0(NXYL,NZ,*)           ,FILAMDT1(NXYL,NZ,*)    ,
     &  EXPLAMDT(NXYL,NZ,*)           ,
     &  CURNX(NXYT,NZ,*)              ,CURNV(NXYT,NZ,*)       ,
     &  CURNU(NXYT,NZ,*)              ,CURNZ(NXYL,NZ+1,*)     ,
     &  TLX(NXYL,NZ,*)                ,TLV(NXYL,NZ,*)         ,
     &  TLU(NXYL,NZ,*)                ,TLZ(NXYL,NZ,*)         ,
     &  PLMZX1(NXYL,NZ,*)             ,PLMZV1(NXYL,NZ,*)      ,
     &  PLMZU1(NXYL,NZ,*)             ,PLMXY1(NXYL,NZ,*)      ,
     &  PLMXY2(NXYL,NZ,*)             ,PLMZX2(NXYL,NZ,NG)     ,
     $  PLMZV2(NXYL,NZ,NG)            ,PLMZU2(NXYL,NZ,NG)
      
      CONS=2./(3.*DELH)
      DO 16 IG=1,NG
       DO 14 IZ=1,NZ
        DO 12 IY=1,NY
         DO 10 IX=NXSTART(IY),NXEND(IY),2
          IXY=NDNUM(IX,IY)
          IXYX=NDNUM(IX+2,IY)
          IXYV=NDNUM(IX+1,IY+1)
          IXYU=NDNUM(IX-1,IY+1)
          SEFVE(IXY,IZ,IG)=0.0
          IF (ITRANSIT.EQ.0) GOTO 2
          SEFVE(IXY,IZ,IG)=(FILAMDT0(IXY,IZ,IG)*FNOLDT(IXY,IZ)
     @      / FLAMDA +
     @      EXPLAMDT(IXY,IZ,IG)+(FILAMDT1(IXY,IZ,IG)-
     &      BETATLAM(IXY,IZ,IG))*
     @      FNSWT(IXY,IZ) / FLAMDA)/VOLU(IXY,IZ)
     &      +  RIVELO(IXY,IZ,IG)*
     @      (SWOLD(IXY,IZ,IG)-SW(IXY,IZ,IG))
C     
C     CALCULATE NET LEAKAGES AND CROSS LEAKAGES.
C     
 2        CONTINUE
          TLZ(IXY,IZ,IG)=(CURNZ(IXY,IZ+1,IG)-CURNZ(IXY,IZ,IG))
     *      /DZ(IZ)
          TLX(IXY,IZ,IG)=(CURNX(IXYX,IZ,IG)-CURNX(IXY,IZ,IG))*CONS
          TLV(IXY,IZ,IG)=(CURNV(IXYV,IZ,IG)-CURNV(IXY,IZ,IG))*CONS
          TLU(IXY,IZ,IG)=(CURNU(IXYU,IZ,IG)-CURNU(IXY,IZ,IG))*CONS
 10      CONTINUE
 12     CONTINUE
 14    CONTINUE
 16   CONTINUE
      
C     
C     CALCULATE EXPANSION COEFFICIENTS FOR RADIAL LEAKAGE SWEEPING ALONG Z.
C     
      DO 20 IG=1,NG
       DO 22 IZ=1,NZ
        DO 24 IY=1,NY
         DO 26 IX=NXSTART(IY),NXEND(IY),2
          IXY=NDNUM(IX,IY)
          W=DZ(IZ)
          TL(IG)=TLX(IXY,IZ,IG)+TLV(IXY,IZ,IG)+TLU(IXY,IZ,IG)-
     @      SEFVE(IXY,IZ,IG)
          IF (IZ.EQ.1) THEN
           IF(LIZD.EQ.0) THEN
            WM=W
            TLM(IG)=TL(IG)
           ELSEIF(LIZD.EQ.1.OR.LIZD.EQ.2) THEN
            WM=.0
            TLM(IG)=.0
           ENDIF
           IF(IZ.NE.NZ) THEN
            WP=DZ(IZ+1)
            TLP(IG)=TLX(IXY,IZ+1,IG)+TLV(IXY,IZ+1,IG)+
     @        TLU(IXY,IZ+1,IG)-SEFVE(IXY,IZ+1,IG)
           ENDIF
          ENDIF
          IF (IZ.GT.1.AND.IZ.LT.NZ) THEN
           WM=DZ(IZ-1)
           WP=DZ(IZ+1)
           TLP(IG)=TLX(IXY,IZ+1,IG)+TLV(IXY,IZ+1,IG)+
     @       TLU(IXY,IZ+1,IG)-SEFVE(IXY,IZ+1,IG)
           TLM(IG)=TLX(IXY,IZ-1,IG)+TLV(IXY,IZ-1,IG)+
     @       TLU(IXY,IZ-1,IG)-SEFVE(IXY,IZ-1,IG)
          ENDIF
          IF (IZ.EQ.NZ) THEN
           IF(LIZU.EQ.0) THEN
            WP=W
            TLP(IG)=TL(IG)
           ELSEIF(LIZU.EQ.1.OR.LIZU.EQ.2) THEN
            WP=.0
            TLP(IG)=.0
           ENDIF
           IF(IZ.GT.1) THEN
            WM=DZ(IZ-1)
            TLM(IG)=TLX(IXY,IZ-1,IG)+TLV(IXY,IZ-1,IG)+
     @        TLU(IXY,IZ-1,IG)-SEFVE(IXY,IZ-1,IG)
           ENDIF
          ENDIF
          PLMXY1(IXY,IZ,IG)=POL1(W,WP,WM,TL(IG),TLP(IG),TLM(IG))
          PLMXY2(IXY,IZ,IG)=POL2(W,WP,WM,TL(IG),TLP(IG),TLM(IG))
 26      CONTINUE
 24     CONTINUE
 22    CONTINUE
 20   CONTINUE
C     
C     CALCULATE EXPANSION COEFFICIENTS FOR AXIAL LEAKAGE SWEEPING ALONG X.
C     
      DO 30 IG=1,NG
       DO 32 IZ=1,NZ
        DO 34 IY=1,NY
         DO 36 IX=NXSTART(IY),NXEND(IY),2
          IXY=NDNUM(IX,IY)
          TL(IG)=TLZ(IXY,IZ,IG)-SEFVE(IXY,IZ,IG)
          IF (IX.EQ.NXSTART(IY)) THEN
           IXYM=NDNUM(IX-2,IY)
           IBC=NBC(IXYM-NXYL)
           IF(IBC.LT.0) THEN
            IXYM=-1*IBC
            TLM(IG)=TLZ(IXYM,IZ,IG)-SEFVE(IXYM,IZ,IG)
           ELSEIF(IBC.EQ.0) THEN
            TLM(IG)=TL(IG)
           ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
            TLM(IG)=.0
           ENDIF
           IF(IX.LT.NXEND(IY)) THEN
            IXYP=NDNUM(IX+2,IY)
            TLP(IG)=TLZ(IXYP,IZ,IG)-SEFVE(IXYP,IZ,IG)
           ENDIF
          ENDIF
          IF (IX.GT.NXSTART(IY).AND.IX.LT.NXEND(IY)) THEN
           IXYP=NDNUM(IX+2,IY)
           IXYM=NDNUM(IX-2,IY)
           TLP(IG)=TLZ(IXYP,IZ,IG)-SEFVE(IXYP,IZ,IG)
           TLM(IG)=TLZ(IXYM,IZ,IG)-SEFVE(IXYM,IZ,IG)
          ENDIF
          IF (IX.EQ.NXEND(IY)) THEN
           IXYP=NDNUM(IX+2,IY)
           IBC=NBC(IXYP-NXYL)
           IF(IBC.LT.0) THEN
            IXYP=-1*IBC
            TLP(IG)=TLZ(IXYP,IZ,IG)-SEFVE(IXYP,IZ,IG)
           ELSEIF(IBC.EQ.0) THEN
            TLP(IG)=TL(IG)
           ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
            TLP(IG)=.0
           ENDIF
           IF(IX.GT.NXSTART(IY)) THEN
            IXYM=NDNUM(IX-2,IY)
            TLM(IG)=TLZ(IXYM,IZ,IG)-SEFVE(IXYM,IZ,IG)
           ENDIF
          ENDIF
           PLMZX1(IXY,IZ,IG)=(TLP(IG)-TLM(IG))/4.
           PLMZX2(IXY,IZ,IG)=(TLP(IG)-TLM(IG))/12.-TL(IG)/6.

 36      CONTINUE
 34     CONTINUE
 32    CONTINUE
 30   CONTINUE
C     
C     CALCULATE EXPANSION COEFFICIENTS FOR AXIAL LEAKAGE SWEEPING ALONG V.
C     
      DO 40 IG=1,NG
       DO 42 IZ=1,NZ
        DO 45 IYST=1,NY
         DO 44 IXST=NXSTART(IYST),NXEND(IYST),2
          IF(NDNUM(IXST-1,IYST-1).GT.NXYL) THEN
           IX=IXST-1
           DO 46 IY=IYST,1000
            IX=IX+1
            IXY=NDNUM(IX,IY)
            IF(IXY.GT.NXYL) GOTO 44
            TL(IG)=TLZ(IXY,IZ,IG)-SEFVE(IXY,IZ,IG)
            IF(IXY.GT.NXYL) GOTO 44
            IXYM=NDNUM(IX-1,IY-1)
            IF(IXYM.GT.NXYL) THEN
             IBC=NBC(IXYM-NXYL)
             IF(IBC.LT.0) THEN
              IXYM=-1*IBC
              TLM(IG)=TLZ(IXYM,IZ,IG)-SEFVE(IXYM,IZ,IG)
             ELSEIF(IBC.EQ.0) THEN
              TLM(IG)=TL(IG)
             ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
              TLM(IG)=.0
             ENDIF
             IXYP=NDNUM(IX+1,IY+1)
             IF(IXYP.LE.NXYL) THEN
              TLP(IG)=TLZ(IXYP,IZ,IG)-SEFVE(IXYP,IZ,IG)
             ENDIF
            ENDIF
            IXYP=NDNUM(IX+1,IY+1)
            IXYM=NDNUM(IX-1,IY-1)
            IF (IXYM.LE.NXYL.AND.IXYP.LE.NXYL) THEN
             TLP(IG)=TLZ(IXYP,IZ,IG)-SEFVE(IXYP,IZ,IG)
             TLM(IG)=TLZ(IXYM,IZ,IG)-SEFVE(IXYM,IZ,IG)
            ENDIF
            IF (IXYP.GT.NXYL) THEN
             IBC=NBC(IXYP-NXYL)
             IF(IBC.EQ.0) THEN
              TLP(IG)=TL(IG)
             ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
              TLP(IG)=.0
             ELSEIF(IBC.LT.0)THEN
              IXYP=-1*IBC
              TLP(IG)=TLZ(IXYP,IZ,IG)-SEFVE(IXYP,IZ,IG)
             ENDIF
             IXYM=NDNUM(IX-1,IY-1)
             IF(IXYM.LE.NXYL) THEN
              TLM(IG)=TLZ(IXYM,IZ,IG)-SEFVE(IXYM,IZ,IG)
             ENDIF
            ENDIF
             PLMZV1(IXY,IZ,IG)=(TLP(IG)-TLM(IG))/4.
             PLMZV2(IXY,IZ,IG)=(TLP(IG)-TLM(IG))/12.-TL(IG)/6.
 46        CONTINUE
          ENDIF
 44      CONTINUE
 45     CONTINUE
 42    CONTINUE
 40   CONTINUE
C     
C     CALCULATE EXPANSION COEFFICIENTS FOR AXIAL LEAKAGE SWEEPING ALONG U.
C     
      DO 50 IG=1,NG
       DO 52 IZ=1,NZ
        DO 55 IYST=1,NY
         DO 54 IXST=NXSTART(IYST),NXEND(IYST),2
          IF(NDNUM(IXST+1,IYST-1).GT.NXYL) THEN
           IX=IXST+1
           DO 56 IY=IYST,1000
            IX=IX-1
            IXY=NDNUM(IX,IY)
            IF(IXY.GT.NXYL) GOTO 54
            TL(IG)=TLZ(IXY,IZ,IG)-SEFVE(IXY,IZ,IG)
            IF(IXY.GT.NXYL) GOTO 54
            IXYM=NDNUM(IX+1,IY-1)
            IF (IXYM.GT.NXYL) THEN
             IBC=NBC(IXYM-NXYL)
             IF(IBC.LT.0) THEN
              IXYM=-1*IBC
              TLM(IG)=TLZ(IXYM,IZ,IG)-SEFVE(IXYM,IZ,IG)
             ELSEIF(IBC.EQ.0) THEN
              TLM(IG)=TL(IG)
             ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
              TLM(IG)=.0
             ENDIF
             IXYP=NDNUM(IX-1,IY+1)
             IF(IXYP.LE.NXYL) THEN
              TLP(IG)=TLZ(IXYP,IZ,IG)-SEFVE(IXYP,IZ,IG)
             ENDIF
            ENDIF
            IXYP=NDNUM(IX-1,IY+1)
            IXYM=NDNUM(IX+1,IY-1)
            IF (IXYP.LE.NXYL.AND.IXYM.LE.NXYL) THEN
             TLP(IG)=TLZ(IXYP,IZ,IG)-SEFVE(IXYP,IZ,IG)
             TLM(IG)=TLZ(IXYM,IZ,IG)-SEFVE(IXYM,IZ,IG)
            ENDIF
            IF (IXYP.GT.NXYL) THEN
             IBC=NBC(IXYP-NXYL)
             IF(IBC.EQ.0) THEN
              TLP(IG)=TL(IG)
             ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
              TLP(IG)=.0
             ELSEIF(IBC.LT.0)THEN
              IXYP=-1*IBC
              TLP(IG)=TLZ(IXYP,IZ,IG)-SEFVE(IXYP,IZ,IG)
             ENDIF
             IXYM=NDNUM(IX+1,IY-1)
             IF(IXYM.LE.NXYL) THEN
              TLM(IG)=TLZ(IXYM,IZ,IG)-SEFVE(IXYM,IZ,IG)
             ENDIF
            ENDIF
             PLMZU1(IXY,IZ,IG)=(TLP(IG)-TLM(IG))/4.
             PLMZU2(IXY,IZ,IG)=(TLP(IG)-TLM(IG))/12.-TL(IG)/6.
 56        CONTINUE
          ENDIF
 54      CONTINUE
 55     CONTINUE
 52    CONTINUE
 50   CONTINUE
      
      RETURN
      END
