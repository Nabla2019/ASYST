C     23456789012345678901234567890123456789012345678901234567890123456789012
C     C
      SUBROUTINE FLUIDCON(
     &  IXY,IZ,TCOLINTMP,TCOLC,RHOW,UNEW)
C     C
C     23456789012345678901234567890123456789012345678901234567890123456789012
C     
C     
C     THIS SUBROUTINE CALCULATES COOLANT TEMPERATURE, DENSITY & VOID
C     FRACTION BASED UPON INTERNAL ENERGY.
C     
C     
C     23456789012345678901234567890123456789012345678901234567890123456789012
C     
      include 'param.fcb'
      include 'varlen.fcb'
      include 'thermo.fcb'
      include 'thcoef.fcb'
      include 'th_cof.fcb'
      include 'array.fcb'
      
      DIMENSION
     &  TCOLC(NXYL,*)                      ,RHOW(NXYL,*)    ,
     &  UNEW(NXYL,*)
C     
C......CHECK WHETHER COOLANT SUBCOOLED OR AT SATURATION
C     
      IF (UNEW(IXY,IZ).LT.UCOLL)THEN
C     
C------SUBCOOLED LIQUID FLUID CONDITION -------------
C     
       RHOW(IXY,IZ) = PROPPOLY(NPOLRHOWC,COF_RHOWC,UNEW(IXY,IZ),
     &   SC0_RHOWC,SC1_RHOWC)
      ELSE
C     
C-----SATURATED MIXTURE EQUATIONS ------------
C     
       ADEN= RHOG*(UCOLG-UNEW(IXY,IZ))+RHOL*(UNEW(IXY,IZ)-UCOLL)
       ANUM= RHOL*(UNEW(IXY,IZ)-UCOLL)
       ALPHAG=ANUM/ADEN
       RHOW(IXY,IZ)=ALPHAG*RHOG+(1.-ALPHAG)*RHOL
      ENDIF
      
      IF(IZ.EQ.1) THEN
       UCOLBOT=PROPPOLY(NPOLUCOL,COF_UCOL,TCOLINTMP,
     &   SC0_UCOL,SC1_UCOL)
      ELSE
       UCOLBOT=UNEW(IXY,IZ-1)
      ENDIF
      
      UCOLAVGT=(UNEW(IXY,IZ)+UCOLBOT)/2.
      IF(UCOLAVGT.LT.UCOLL) THEN
       TCOLC(IXY,IZ) = PROPPOLY(NPOLTCOL,COF_TCOL,UCOLAVGT,
     &   SC0_TCOL,SC1_TCOL)
      ELSE
       TCOLC(IXY,IZ)=TCOLSAT
      ENDIF
      
      RETURN
      END
