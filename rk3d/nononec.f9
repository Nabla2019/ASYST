      SUBROUTINE NONONEC
     &  (           IXY,IZ,DEL,TL,PL1,PL2,JNEM,DIR1N,IB,
     &  TCD,TNF,TXT,SIGDSN,SW,SEXT,VOLU,BETAT,
     &  BETATLAM,SCAT,XHIP,LSEXTZ,LSEXTR,AL,TF,
     &  TD,FL,SXT,SSCAT,AMAT1,AMAT2,EXC,BRHS1,XOUT1,BRHS2,XOUT2  )
C     
C     THIS ROUTINE SOLVES A ONE-NODE PROBLEM. IT IS USED ONLY FOR
C     EDGE NODES WITHOUT ZERO CURRENT BOUNDARY CONDITION
C     WHEN UPDATING THE NET CURRENTS BY NONNEM.
C     ********** FOR CARTESIAN GEOMETRY **************
C     
C     
C     VARIABLE DESCRIPTION :
C     
C     AL   :TOTAL X-SEC
C     TF   :FISSION X-SEC
C     TD   :DIFFUSION COEFFICIENT
C     FL   :FLUX
C     FS   :FISSION SOURCE
C     EXC  :NODAL EXPANSION COEFFICIENTS
C     AMAT1:MATRIX OF THE EVEN EXPANSION COEFFICIENTS
C     BRHS1:RHS FOR AMAT1
C     AMAT2:MATRIX OF THE EXC(.,3)
C     BRHS2:RHS FOR AMAT2
C     WE TAKE ADVANTAGE OF THE MATRIX FORM : WE SOLVE ONE SYSTEM OF ORDER 2*
C     FOR THE EVEN EXPANSION COEFFICIENTS AND ONE SYSTEM OF ORDER NG FOR
C     THE EXPANSION COEFFICIENTS OF THIRD ORDER
C     
      include 'param.fcb'
      include 'xsec1.fcb'
      include 'che.fcb'
      include 'geom.fcb'
      include 'extsor.fcb'
      
      DIMENSION AL(NG),TF(NG),TD(NG),FL(NG)
      DIMENSION TL(NG),PL1(NG),PL2(NG)
      DIMENSION SXT(NG),SSCAT(NG)
      DIMENSION AMAT1(2*NG*NG+2*NG),AMAT2(NG*NG)
      DIMENSION EXC(4,NG)
      DIMENSION BRHS1(2*NG),XOUT1(2*NG),BRHS2(NG),XOUT2(NG)
      REAL*8 JNEM(NG) !vmq
      INTEGER*8 DIR1N !vmq

      DIMENSION
     &  SW(NXYL,NZ,*)                     ,SCAT(NXYL,NZ,*)        ,
     &  SEXT(*)                           ,VOLU(NXYL,*)           ,
     &  LSEXTZ(*)                         ,LSEXTR(*)              ,
     &  TCD(NXYL,NZ,*)                    ,TNF(NXYL,NZ,*)         ,
     &  TXT(NXYL,NZ,*)         ,
     &  SIGDSN(NXYL,NZ,NG,*)              ,BETAT(NXYL,*)          ,
     &  BETATLAM(NXYL,NZ,*)               ,XHIP(NXYL,NZ,*)
      
      DO 5 I=1,2*NG*NG+2*NG
       AMAT1(I)=0.0
 5    CONTINUE
      
      DO 6 I=1,NG*NG
       AMAT2(I)=0.0
 6    CONTINUE
      
      DO 7 I=1,2*NG
       BRHS1(I)=0.0
 7    CONTINUE
      
      DO 8 I=1,NG
       BRHS2(I)=0.0
 8    CONTINUE
      
      FS=0.0
      
      DO 10 IG=1,NG
       AL(IG)=TXT(IXY,IZ,IG)
       TF(IG)=TNF(IXY,IZ,IG)/FLAMDA
       TD(IG)=TCD(IXY,IZ,IG)
       FL(IG)=SW(IXY,IZ,IG)
       FS=FS+TF(IG)*FL(IG)
       SXT(IG)=0.0
 10   CONTINUE
      DO 20 IG=1,NG
       SSCAT(IG)=SCAT(IXY,IZ,IG)/VOLU(IXY,IZ)
 20   CONTINUE
      
      DO 419 IG=1,NG
       SXT(IG)=DBLE(LSEXTR(IXY))*DBLE(LSEXTZ(IZ))*SEXT(IG) !vmq
 419  CONTINUE
      
      DO 30 IG=1,NG
       DO 40 IGG=1,NG
        IF(IG.NE.IGG) THEN
         IGP=IGG
         IF(IGG.GT.IG) IGP=IGG-1
         AINT=TF(IGG)*( (1.0 - BETAT(IXY,IZ))
     &     *XHIP(IXY,IZ,IG)+ BETATLAM(IXY,IZ,IG) )
     &     +SIGDSN(IXY,IZ,IG,IGP)
        ELSE
         AINT=TF(IGG)*( (1.0 - BETAT(IXY,IZ))
     &     *XHIP(IXY,IZ,IG)+ BETATLAM(IXY,IZ,IG) )
        ENDIF
C     
C.....SECOND MOMENT CONSTRAINTS
C     
        AMAT1(2*NG*IG+IGG)=35.*AINT
        AMAT1(2*NG*IG+NG+IGG)=-AINT
 40    CONTINUE
       AMAT1(2*NG*IG+IG)=AMAT1(2*NG*IG+IG)-35.*AL(IG)
       AMAT1(2*NG*IG+IG+NG)=
     &   AMAT1(2*NG*IG+IG+NG)+AL(IG)+140.*TD(IG)/(DEL*DEL)
       BRHS1(IG+NG)=35.*PL2(IG)
       BRHS1(IG)=(-TL(IG)-AL(IG)*FL(IG)
     &   +SSCAT(IG)+FS*( (1.0 - BETAT(IXY,IZ)) *
     &   XHIP(IXY,IZ,IG)+ BETATLAM(IXY,IZ,IG) )
     &   +SXT(IG))*DEL*DEL
       
C.....OTH MOMENT CONSTRAINT
C     
       AMAT1(2*IG-1)=-6.*TD(IG)
       AMAT1(2*IG)=-0.4*TD(IG)
 30   CONTINUE
      
C.....WE SOLVE FOR THE EVEN EXPANSION COEFFICIENTS ONLY
C..........IE EXC(2,IG) & EXC(4,IG)
C     
      NDIM=2*NG
      IF(NDIM.EQ.4) THEN
       CALL DIRECT4(AMAT1,BRHS1,XOUT1,NDIM)
      ELSE
       CALL DIRECT8B(AMAT1,BRHS1,XOUT1,NDIM)
      ENDIF
      
C.....WE RETRIEVE THE EVEN EXPANSION COEFFICIENTS
C     
      DO 50 IG=1,NG
       EXC(2,IG)=XOUT1(IG)
       EXC(4,IG)=XOUT1(NG+IG)
 50   CONTI NUE
      
      IF(IB.NE.1) GOTO 90
      
C     
C...........FLUX(EDGE)=0.0
C     
      DO 320 IG=1,NG
       IF(DIR1N.LT.0) THEN
        EXC(1,IG)=+(2.0*FL(IG)+EXC(2,IG)) !vmq
       ELSE
        EXC(1,IG)=-(2.0*FL(IG)+EXC(2,IG)) !vmq
       ENDIF
 320  CONTINUE
C     
C...........1ST MOMENT CONSTRAINT
C     
      DO 60 IG=1,NG
       BRHS2(IG)=10.*PL1(IG)+AL(IG)*EXC(1,IG)*10.
       DO 70 IGG=1,NG
        IF(IG.NE.IGG) THEN
         IGP=IGG
         IF(IGG.GT.IG) IGP=IGG-1
         AINT=TF(IGG)*( (1.0 - BETAT(IXY,IZ))
     &     *XHIP(IXY,IZ,IG)+ BETATLAM(IXY,IZ,IG) )
     &     +SIGDSN(IXY,IZ,IG,IGP)
        ELSE
         AINT=( (1.0 - BETAT(IXY,IZ))*XHIP(IXY,IZ,IG)+
     &     BETATLAM(IXY,IZ,IG) )*TF(IGG)
        ENDIF
        AMAT2(NG*(IG-1)+IGG)=-AINT
        BRHS2(IG)=BRHS2(IG)-AINT*EXC(1,IGG)*10.
 70    CONTINUE
       AMAT2(NG*(IG-1)+IG)=AMAT2(NG*(IG-1)+IG)
     &   +60.*TD(IG)/(DEL*DEL)+AL(IG)
 60   CONTINUE
      
C......WE SOLVE FOR THE EXC(3,IG)
C     
      NDIM=NG
      IF(NDIM.EQ.4) THEN
       CALL DIR4FULL(AMAT2,BRHS2,XOUT2,NDIM)
      ELSE
       CALL DIR2FULL(AMAT2,BRHS2,XOUT2,NDIM)
      ENDIF
      
C.......WE RETRIEVE THE EXC(3,IG)
C     
      DO 80 IG=1,NG
       EXC(3,IG)=XOUT2(IG)
 80   CONTINUE
      GOTO 550
 90   CONTINUE
      DO 95 I=1,2*NG*NG+2*NG
       AMAT1(I)=0.0
 95   CONTINUE
      
      DO 100 I=1,2*NG
       BRHS1(I)=0.0
 100  CONTINUE
      
C     
C...........1ST MOMENT CONSTRAINT
C     
      DO 300 IG=1,NG
       DO 400 IGG=1,NG
        IF(IG.NE.IGG) THEN
         IGP=IGG
         IF(IGG.GT.IG) IGP=IGG-1
         AINT=TF(IGG)*( (1.0 - BETAT(IXY,IZ))*
     &     XHIP(IXY,IZ,IG)+ BETATLAM(IXY,IZ,IG) )
     &     +SIGDSN(IXY,IZ,IG,IGP)
        ELSE
         AINT=TF(IGG)*( (1.0 - BETAT(IXY,IZ))*
     &     XHIP(IXY,IZ,IG)+ BETATLAM(IXY,IZ,IG) )
        ENDIF
        AMAT1(2*NG*IG+IGG)=-AINT
        AMAT1(2*NG*IG+NG+IGG)=AINT*10.
 400   CONTINUE
       AMAT1(2*NG*IG+IG)=AMAT1(2*NG*IG+IG)
     &   +60.*TD(IG)/(DEL*DEL)+AL(IG)
       AMAT1(2*NG*IG+IG+NG)=
     &   AMAT1(2*NG*IG+IG+NG)-10.*AL(IG)
       BRHS1(IG+NG)=10.*PL1(IG)
       
C.....BC CONSTRAINT
C     
       IF(DIR1N.EQ.1) THEN
        BRHS1(IG)=-FL(IG)-(.5+6.*(TD(IG)/DEL))*EXC(2,IG)
     @    -.4*(TD(IG)/DEL)*EXC(4,IG)
        AMAT1(2*IG-1)=TD(IG)/DEL
        AMAT1(2*IG)=.5+2.*(TD(IG)/DEL)
       ELSE
        BRHS1(IG)=-FL(IG)-(.5+6.*(TD(IG)/DEL))*EXC(2,IG)
     @    -.4*(TD(IG)/DEL)*EXC(4,IG)
        AMAT1(2*IG-1)=-TD(IG)/DEL
        AMAT1(2*IG)=-(.5+2.*(TD(IG)/DEL))
       ENDIF
 300  CONTINUE
      
C.....WE SOLVE FOR THE ODD EXPANSION COEFFICIENTS ONLY
C..........IE EXC(1,IG) & EXC(3,IG)
C     
      NDIM=2*NG
      IF(NDIM.EQ.4) THEN
       CALL DIRECT4(AMAT1,BRHS1,XOUT1,NDIM)
      ELSE
       CALL DIRECT8B(AMAT1,BRHS1,XOUT1,NDIM)
      ENDIF
      
C.....WE RETRIEVE THE ODD EXPANSION COEFFICIENTS
C     
      DO 500 IG=1,NG
       EXC(3,IG)=XOUT1(IG)
       EXC(1,IG)=XOUT1(NG+IG)
 500  CONTINUE
      
 550  CONTINUE
      
C......WE COMPUTE THE EDGE CURRENT USING FLUX EXPANSION COEFFICIENTS
C.......AND FICK'S LAW
C     
      DO 900 IG=1,NG
       IF(DIR1N.LT.0) THEN
        JNEM(IG)=-TD(IG)*(EXC(1,IG)-3.*EXC(2,IG)+.5*EXC(3,IG)
     *    -.2*EXC(4,IG))/DEL
       ELSE
        JNEM(IG)=-TD(IG)*(EXC(1,IG)+3.*EXC(2,IG)+.5*EXC(3,IG)
     &    +.2*EXC(4,IG))/DEL
       ENDIF
 900  CONTINUE
      
      RETURN
      END
