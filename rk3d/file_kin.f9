      SUBROUTINE FILE_KIN(IOERROR,
     &  TIMETR,PMT,TINT,GMINT,ZBT,TIMEPR,TIMESP,
     &  DELT,TIMEFP,PRFP,PMFP,TINFP,ZBFP,DELTFP)
C     
C     THIS SUBROUTINE READS THE KINETIC PARAMETERS REQUIRED FOR
C     TRANSIENT RUNS.
C     
      include 'param.fcb'
      include 'crod.fcb'
      include 'perttr.fcb'
      include 'cntl.fcb'
      include 'xeopt.fcb'
      include 'dataf.fcb'

      DIMENSION TIMETR(*),PMT(*)            ,TINT(*)         ,
     &  GMINT(*)         ,ZBT(NBMAX,*)      ,
     &  TIMEPR(*)        ,TIMESP(*)         ,DELT(*)         ,
     &  TIMEFP(*)        ,PRFP(*)           ,PMFP(*)         ,
     &  TINFP(*)         ,ZBFP(NBMAX,*)     ,DELTFP(*)
      
C     
C==== CHECK FOR THE CORE TRANSIENT OPTION
C     
      IF(ITRAN.EQ.'Y')THEN
C     
C------------------START OF THE CORE TRANSIENT OPTION ------------------
       
       
C     
C-------TIME & INPUT PARAMETERS AT THE PERTURBATION INCIDENTS
C     PARAMETERS ARE :
C     TIMETR==> TIME @ WHICH PERTURBATION WILL OCCUR : [SEC]
C     PMT   ==> SOLUBLE POISON CONCENTRATION                  : [PPM
C     TINT  ==> INLET COOLANT TEMPERATURE            : [F]
C     GMINT ==> INLET VELOCITY MASS                  : [LB/HR-FT^2]
C     ZBT==> CONTROL BANKS AXIAL WITHDRAWAL          : [INCH]
C     
       DO 87 IT=1,NPERT
        READ(4,*) TIMETR(IT),PMT(IT),TINT(IT),GMINT(IT)
        READ(4,*) TIMETR(IT),(ZBT(IBK,IT),IBK=1,NBACU)
 87    CONTINUE
       
       IF(TIMETR(1).NE.0)THEN
        WRITE(6,*)'####ERROR, THE FIRST PERTURBED TIME SHOULD BE = 0'
        IOERROR = 1
       ENDIF
       
       DO 98 II=1,NPERT-1
        IF(TIMETR(II+1).LE.TIMETR(II))THEN
         WRITE(6,*)'####ERROR, INPUTING WRONG PERTURBATION TIME '
         WRITE(6,*)'TIMETR(N), TIMETR(N+1) SHOULD BE INCREMENTAL'
         IOERROR = 1
        ENDIF
 98    CONTINUE
       
C     
C-------TIME [ SEC ] AT WHICH OUTPUT IS TO BE PRINTED
C     
       READ(4,*) (TIMEPR(IT),IT=1,NPRINT)
C     
C-------TIME VS. TIME-STEP SIZE FOR THE DIFFERENT TIME SPAN REGIONS
C     
       READ (4,*) (TIMESP(IT), DELT(IT),IT=1,NTSPN)
       
      ENDIF
C     
C------------------END OF THE CORE TRANSIENT OPTION ------------------
C     
C     
C     
C==== CHECK FOR THE CORE TRANSIENT OPTION
C     
      IF(IXE(1).EQ.4.OR.IXE(1).EQ.5)THEN
C     
C------------------START OF THE TRANSIENT XE AND/OR SM CASE-------------
       
C     
C-------TIME & INPUT PARAMETERS FOR THE FISSION PRODUCT TRANSIENT
C     PARAMETERS ARE :
C     TIMEFP==> TIME @ WHICH PERTURBATION WILL OCCUR  : [HRS]
C     PRFP  ==> CORE RELATIVE POWER @ TIMEFP            (%)
C     PMFP ==> SOLUBLE POISON CONCENTRATION @ TIMEFP : [PPM]
C     TINFP ==> INLET COOLANT TEMPERATURE             : [F]
C     ZBFP==> CONTROL BANKS AXIAL WITHDRAWAL          : [INCH]
C     
       DO 88 IFP=1,NFP
        READ(4,*)TIMEFP(IFP),PRFP(IFP),PMFP(IFP),TINFP(IFP)
        READ(4,*)TIMEFP(IFP),(ZBFP(IBK,IFP),IBK=1,NBACU)
 88    CONTINUE
       
C.... CHANGE INPUT POWER PERCENTAGE TO FRACTION
C     
       DO 89 IFP=1,NFP
        PRFP(IFP) = PRFP(IFP)/100.00
 89    CONTINUE
       
C     
C-----CALCULATE THE XE-SM TRANSIENT TIME DIFFERENCE DELTFP [HRS]
C     
       DELTFP(1) = TIMEFP(1)
       DO 23 IFP=2,NFP
        DELTFP(IFP) = TIMEFP(IFP) - TIMEFP(IFP-1)
 23    CONTINUE
       
      ENDIF
C     
C------------------END OF THE XE AND/OR SM TRANSIENT OPTION ------------
C     
      
      CLOSE(UNIT=4)
      
      RETURN
      END
