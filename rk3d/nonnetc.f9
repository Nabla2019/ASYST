C1234567890123456789012345678901234567890123456789012345678901234567890
C
                         SUBROUTINE NONNETC(
     &  CURNX,CURNY,CURNZ,SW,DCL,DLN,DCW,
     &  DLL,DLW,DCN,NBC,
     &  NXSTART,NXEND,NDNUM,NYSTART,NYEND )
C
C1234567890123456789012345678901234567890123456789012345678901234567890
C
C
C  THIS SUBROUTINE DETERMINES THE CURRENTS FOR NEM
C      ********** FOR CARTESIAN GEOMETRY **************
C
C
C1234567890123456789012345678901234567890123456789012345678901234567890
C
      include 'param.fcb'
      include 'cntl.fcb'
      include 'bcs.fcb'
 
       DIMENSION
     &   NXSTART(*)                       ,NXEND(*)                ,
     &   NBC(*)                           ,NDNUM(-1:NX+2,0:*)      ,
     &   NYSTART(*)                       ,NYEND(*)                ,
     &   DCL(NXYL,NZ+1,*)                 ,DLN(NXYT,NZ,*)          ,
     &   DCW(NXYT,NZ,*)                   ,DLL(NXYL,NZ+1,*)        ,
     &   DLW(NXYT,NZ,*)                   ,DCN(NXYT,NZ,*)          ,
     &   CURNX(NXYT,NZ,*)                 ,CURNY(NXYT,NZ,*)        ,
     &   CURNZ(NXYL,NZ+1,*)               ,SW(NXYL,NZ,*)
 
 
      DO 16 IG = 1,NG
        DO 14 IZ = 1,NZ
          DO 12 IY = 1,NY
            DO 10 IX = NXSTART(IY),NXEND(IY)
              IXY=NDNUM(IX,IY)
              IXYM=NDNUM(IX-1,IY)
              IF (IX.NE.NXSTART(IY)) THEN
                CURNX(IXY,IZ,IG) = - DCW(IXY,IZ,IG)*
     &           (SW(IXY,IZ,IG)-SW(IXYM,IZ,IG))
     &           -DLW(IXY,IZ,IG) *
     &           (SW(IXY,IZ,IG) + SW(IXYM,IZ,IG) )
              ELSE
                IBC=NBC(IXYM-NXYL)
                IF(IBC.EQ.0) THEN
                  CURNX(IXY,IZ,IG)=0.0
                ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
                  CURNX(IXY,IZ,IG)=-(DCW(IXY,IZ,IG)+DLW(IXY,IZ,IG))
     &                                *SW(IXY,IZ,IG)
                ELSEIF(IBC.LT.0) THEN
                  IXYM=-1*IBC
                  CURNX(IXY,IZ,IG) = - DCW(IXY,IZ,IG)*
     &               (SW(IXY,IZ,IG)-SW(IXYM,IZ,IG))
     &               -DLW(IXY,IZ,IG) *
     &               (SW(IXY,IZ,IG) + SW(IXYM,IZ,IG) )
                ENDIF
              ENDIF
 
              IF (IX.EQ.NXEND(IY)) THEN
                IXYP=NDNUM(IX+1,IY)
                IBC=NBC(IXYP-NXYL)
                IF(IBC.EQ.0) THEN
                  CURNX(IXYP,IZ,IG)=0.0
                ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
                  CURNX(IXYP,IZ,IG)=(DCW(IXYP,IZ,IG)-DLW(IXYP,IZ,IG))
     &                        *SW(IXY,IZ,IG)
                ENDIF
              ENDIF
 
              IXYM=NDNUM(IX,IY-1)
              IF (IY.NE.NYSTART(IX)) THEN
                CURNY(IXY,IZ,IG) = - DCN(IXY,IZ,IG)*
     &             ( SW(IXY,IZ,IG)-SW(IXYM,IZ,IG) )
     &             - DLN(IXY,IZ,IG) *
     &             ( SW(IXY,IZ,IG) + SW(IXYM,IZ,IG) )
              ELSE
                IBC=NBC(IXYM-NXYL)
                IF(IBC.EQ.0) THEN
                  CURNY(IXY,IZ,IG)=0.0
                ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
                  CURNY(IXY,IZ,IG)=-(DCN(IXY,IZ,IG)+DLN(IXY,IZ,IG))
     &                                  *SW(IXY,IZ,IG)
                ELSEIF(IBC.LT.0) THEN
                  IXYM=-1*IBC
                  CURNY(IXY,IZ,IG) = - DCN(IXY,IZ,IG)*
     &               ( SW(IXY,IZ,IG)-SW(IXYM,IZ,IG) )
     &               - DLN(IXY,IZ,IG) *
     &               ( SW(IXY,IZ,IG) + SW(IXYM,IZ,IG) )
                ENDIF
              ENDIF
 
              IF (IY.EQ.NYEND(IX)) THEN
                IXYP=NDNUM(IX,IY+1)
                IBC=NBC(IXYP-NXYL)
                IF(IBC.EQ.0) THEN
                  CURNY(IXYP,IZ,IG)=0.0
                ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
             CURNY(IXYP,IZ,IG) =(DCN(IXYP,IZ,IG)-DLN(IXYP,IZ,IG))
     &                                  *SW(IXY,IZ,IG)
                ENDIF
              ENDIF
C
              IF (IZ.NE.1) THEN
                CURNZ(IXY,IZ,IG) = - DCL(IXY,IZ,IG)*
     &                 (  SW(IXY,IZ,IG)-SW(IXY,IZ-1,IG)  )
     &                    - DLL(IXY,IZ,IG) *
     &                 ( SW(IXY,IZ,IG) + SW(IXY,IZ-1,IG) )
              ELSE
                IF(LIZD.EQ.0) THEN
                  CURNZ(IXY,1,IG)=0.0
                ELSEIF(LIZD.EQ.1.OR.LIZD.EQ.2) THEN
                  CURNZ(IXY,1,IG)=-(DCL(IXY,IZ,IG)+DLL(IXY,IZ,IG))
     &                              *SW(IXY,IZ,IG)
                ENDIF
              ENDIF
 
              IF (IZ.EQ.NZ) THEN
                IZP=IZ+1
                IF(LIZU.EQ.0) THEN
                  CURNZ(IXY,IZP,IG)=0.0
                ELSEIF(LIZU.EQ.1.OR.LIZU.EQ.2) THEN
                  CURNZ(IXY,IZP,IG)=(DCL(IXY,IZP,IG)-DLL(IXY,IZP,IG))
     &                              *SW(IXY,IZ,IG)
                ENDIF
              ENDIF
 
 10         CONTINUE
 12       CONTINUE
 14     CONTINUE
 16   CONTINUE
 
      RETURN
      END
