      SUBROUTINE BURNNODE(E,AB,DELTAT,IXY,IZ,
     &  FU4,FU5,FU6,FU8,FPU9,FPU0,FPU1,FPU2,
     &  U4A,U5A,U6A,U8A,P9A,P0A,P1A,P2A,FL,BPA,FAM1,A1A)
C
C     THIS SUBROUTINE SOLVES THE ISOTOPIC DEPLETION EQUATIONS
C 
      include 'param.fcb'
      include 'burn.fcb'
      
      DIMENSION
     &  FU5(NXYL,NZ,*)             ,FU6(NXYL,NZ,*)       ,
     &  FU8(NXYL,NZ,*)             ,FU4(NXYL,NZ,*)       ,
     &  FPU9(NXYL,NZ,*)            ,FPU0(NXYL,NZ,*)      ,
     &  FPU2(NXYL,NZ,*),FPU1(NXYL,NZ,*),FAM1(NXYL,NZ,*),
     &  U5A(*)                     ,U6A(*)               ,
     &  U4A(*)                     ,U8A(*)               ,
     &  P9A(*)                     ,P0A(*)               ,
     &  P1A(*),P2A(*),A1A(*),
     &  FL(*)                      ,BPA(*)
      
      REAL*8 AB(12),E(12)
      REAL*8 ABD(12),CAP(12),COF(10,0:9),EX(12),AKT
      PARAMETER (NAM241=12, NPU241=7)
      
      KK=1
      
      DO 20 II=1,2
       CALL CALACV(KK,II,AKT,IXY,IZ,
     &   FU4,FU5,FU6,FU8,FPU9,FPU0,FPU1,FPU2,
     &   U4A,U5A,U6A,U8A,P9A,P0A,P1A,P2A,FL,BPA,A1A,FAM1)
       
       CAP(II)=AKT
 20   CONTINUE
      
      DO 40 II=4,7
       CALL CALACV(KK,II,AKT,IXY,IZ,
     &   FU4,FU5,FU6,FU8,FPU9,FPU0,FPU1,FPU2,
     &   U4A,U5A,U6A,U8A,P9A,P0A,P1A,P2A,FL,BPA,A1A,FAM1)
       
       CAP(II)=AKT
 40   CONTINUE
C     AM-241
      II=NAM241
      CALL CALACV(KK,II,AKT,IXY,IZ,
     &  FU4,FU5,FU6,FU8,FPU9,FPU0,FPU1,FPU2,
     &  U4A,U5A,U6A,U8A,P9A,P0A,P1A,P2A,FL,BPA,A1A,FAM1)
      CAP(II)=AKT
C     
c     DCAY41=1.464E-9   !ORIGINAL VALUE IN 3DKIN
      DCAY41=1.5306E-9          !FROM FORCIP V2.1 
      KK=3
      DO 50 II=1,9
       CALL CALACV(KK,II,AKT,IXY,IZ,
     &   FU4,FU5,FU6,FU8,FPU9,FPU0,FPU1,FPU2,
     &   U4A,U5A,U6A,U8A,P9A,P0A,P1A,P2A,FL,BPA,A1A,FAM1)
       ABD(II)=AKT
 50   CONTINUE
C     AM-241
      II=NAM241
      CALL CALACV(KK,II,AKT,IXY,IZ,
     &  FU4,FU5,FU6,FU8,FPU9,FPU0,FPU1,FPU2,
     &  U4A,U5A,U6A,U8A,P9A,P0A,P1A,P2A,FL,BPA,A1A,FAM1)
      ABD(II)=AKT
C     
      ABD(7)=ABD(7)+DCAY41
      
      DO 80 II=1,9
       EX(II)=DEXP(-ABD(II)*DELTAT)
 80   CONTINUE
C     AM-241
      II=NAM241
      EX(II)=DEXP(-ABD(II)*DELTAT)
C     
      CALL CHAIN(NV3,E(1),EX(1),ABD(1),CAP(1),COF,AB(1))
      CALL CHAIN(N5,E(4),EX(4),ABD(4),CAP(4),COF,AB(4))
      CALL CHAIN(N1,E(11),EX(9),ABD,CAP,COF,AB(11))
      
      F1=0.0
      F2=0.0
C     AM-241
      CONST41=DCAY41*(EX(NPU241)-EX(NAM241))/
     &  (ABD(NAM241)-ABD(NPU241))
      AB(NAM241)=E(NAM241)*EX(NAM241)+AB(NPU241)*CONST41
      KK=2
      DO 150 II=1,8
       CALL CALACV(KK,II,AKT,IXY,IZ,
     &   FU4,FU5,FU6,FU8,FPU9,FPU0,FPU1,FPU2,
     &   U4A,U5A,U6A,U8A,P9A,P0A,P1A,P2A,FL,BPA,A1A,FAM1)
       F1=F1+(AB(II)+E(II))*GLFP1*.5*AKT
       F2=F2+(AB(II)+E(II))*GLFP2*.5*AKT
 150  CONTINUE
C     AM-241
      II=NAM241
      CALL CALACV(KK,II,AKT,IXY,IZ,
     &  FU4,FU5,FU6,FU8,FPU9,FPU0,FPU1,FPU2,
     &  U4A,U5A,U6A,U8A,P9A,P0A,P1A,P2A,FL,BPA,A1A,FAM1)
      F1=F1+(AB(II)+E(II))*GLFP1*.5*AKT
      F2=F2+(AB(II)+E(II))*GLFP2*.5*AKT
      
      AB(9)=E(9)+F1*DELTAT
      AB(10)=E(10)+F2*DELTAT
      
      RETURN
      END
 
 
 
 
 
