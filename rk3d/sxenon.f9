C23456789012345678901234567890123456789012345678901234567890123456789012
C                                                                      C
                      SUBROUTINE SXENON(
     &   SW,XXEAB,XSMAB,VOLU,GI,GX,GP,RNU,
     &   DXE,DIO,DPM,DSM,TNF,TPF,NCOLXY,NCOLZ,FRACTB)
C                                                                      C
C23456789012345678901234567890123456789012345678901234567890123456789012
C
C
C  THIS SUBROUTINE SOLVES FOR THE STEADY-STATE NUMBER DENSITIES OF
C  THE TRANSIENT FISSION PRODUCTS
C
C
C1234567890123456789012345678901234567890123456789012345678901234567890
C                                                                     C
      include 'param.fcb'
      include 'nonfue.fcb'
      include 'cntl.fcb'
      include 'soln2.fcb'
      include 'xsec1.fcb'
      include 'geom.fcb'
      include 'fpxs.fcb'
      include 'start.fcb'
 
       DIMENSION
     &   SW(NXYL,NZ,*)                       ,NCOLXY(NXYLMAX,*)    ,
     &   NCOLZ(*)                            ,XXEAB(NXYL,NZ,*)     ,
     &   XSMAB(NXYL,NZ,*)                    ,
     &   VOLU(NXYL,*)                        ,GI(NXYL,NZ,*)        ,
     &   GX(NXYL,NZ,*)                       ,GP(NXYL,NZ,*)        ,
     &   RNU(NXYL,NZ,*)                      ,DXE(NXYL,*)         ,
     &   DIO(NXYL,*)                        ,DPM(NXYL,*)         ,
     &   DSM(NXYL,*)                        ,TNF(NXYL,NZ,*)       ,
     &   TPF(NXYL,NZ,*)                      ,FRACTB(*)
 
C
C----- NORMALIZING THE FLUX TO POWER LEVEL 1.0000
C
      IF(ITYPE.EQ.'EVP')THEN
 
        VOLFUEL = 0.000
        SK1     = 0.000
        DO 100 IZ=1,NZ
          DO 100 IXY=1,NXYL
            ICOL=NCOLXY(IXY,NCOLZ(IZ))
            IF(ICOL.LE.NFUEXY)GO TO 100
            VOLFUEL=VOLFUEL+VOLU(IXY,IZ)*FRACTB(IXY)
            DO 95 IG=1,NG
              SK1 = SK1 +(TPF(IXY,IZ,IG)*
     &            SW(IXY,IZ,IG))*VOLU(IXY,IZ)*FRACTB(IXY)
 95         CONTINUE
 100    CONTINUE
 
        RN = VOLFUEL / SK1
 
      ELSEIF(ITYPE.EQ.'FSP')THEN
 
        RN = 1.00
 
      ENDIF
C
C------ SCALING THE FLUX TO THE TARGET POWER
C
      IF(ITYPE.EQ.'FSP')THEN
          AMPL = 1.00
      ELSEIF(ITYPE.EQ.'EVP')THEN
          AMPL = PR*QT*RN
      ENDIF
 
      IF(ITYPE.EQ.'FSP'.AND.ASRCH.EQ.'Y')THEN
          AMPL = PR
      ENDIF
 
      DO 10 IZ=1,NZ
        DO 10 IXY=1,NXYL
          IF (NCOLXY(IXY,NCOLZ(IZ)).LE.NFUEXY) THEN
            DXE(IXY,IZ)=0.0
            DSM(IXY,IZ)=0.0
            DIO(IXY,IZ)=0.0
            DPM(IXY,IZ)=0.0
            GO TO 10
          ENDIF
          FP = 0.
          XAW = 0.
          SAW = 1.E-10
          SIO = 0.
          SXE = 0.
          SPM = 0.
          DO 11 IG=1,NG
            SIO=SIO+ GI(IXY,IZ,IG) *(TNF(IXY,IZ,IG)*SW(IXY,IZ,IG)
     &       /RNU(IXY,IZ,IG) )
            SXE=SXE+ GX(IXY,IZ,IG)*(TNF(IXY,IZ,IG)*SW(IXY,IZ,IG)
     &       /RNU(IXY,IZ,IG) )
            SPM=SPM+ GP(IXY,IZ,IG)*(TNF(IXY,IZ,IG)*SW(IXY,IZ,IG)
     &       /RNU(IXY,IZ,IG) )
            XAW=XAW+( XXEAB(IXY,IZ,IG)*SW(IXY,IZ,IG) )
            SAW=SAW+( XSMAB(IXY,IZ,IG)*SW(IXY,IZ,IG) )
 11       CONTINUE
          DIO(IXY,IZ)= SIO*AMPL/RLI
          DXE(IXY,IZ)=  ( SXE*AMPL + RLI*DIO(IXY,IZ) )
     &                 / (  RLX + AMPL*XAW*1.E-24 )
          DPM(IXY,IZ)= SPM*AMPL/RLPM
          DSM(IXY,IZ)= RLPM*DPM(IXY,IZ)
     &                 / (  AMPL*SAW*1.E-24  )
10    CONTINUE
 
      RETURN
      END
