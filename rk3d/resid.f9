C23456789012345678901234567890123456789012345678901234567890123456789012
C                                                                      C
                            SUBROUTINE RESID(
     &   VOLU,BETAT,BETATLAM,FILAMDT1,SW,TNF,XHIP,
     &   SEFF,LSEXTR,LSEXTZ,SEXT,SCW,FNSHIFT,EIGEN,RESIDUAL )
C                                                                      C
C23456789012345678901234567890123456789012345678901234567890123456789012
C
C
C  THIS SUBROUTINE DETERMINES RELATIVE RESIDUAL FOR ALL TYPES OF PROBLEMS
C
C
C23456789012345678901234567890123456789012345678901234567890123456789012
C
      include 'param.fcb'
      include 'che.fcb'
      include 'cntl.fcb'
      include 'geom.fcb'
      include 'veloc.fcb'
      include 'adj.fcb'
      include 'xsec1.fcb'
 
       DIMENSION
     &    VOLU(NXYL,NZ)                    ,SCW(NXYL,NZ,*)       ,
     &    TNF(NXYL,NZ,*)                   ,LSEXTR(*)            , 
     &    BETAT(NXYL,*)                    ,SW(NXYL,NZ,*)        ,
     &    FILAMDT1(NXYL,NZ,*)  ,
     &    XHIP(NXYL,NZ,*)                  ,FNSHIFT(NXYL,NZ,*)   ,
     &    BETATLAM(NXYL,NZ,*)              ,SEFF(NXYL,NZ,*)      ,
     &    SEXT(*)                          ,LSEXTZ(*)
	
       REAL*8 APHINORM,FPHINORM,SPHINORM,FPHINORMSQ,RESIDSQ

       APHINORM=0.0
       FPHINORM=0.0
       SPHINORM=0.0
       FPHINORMSQ=0.0
       SOURCESQ=0.0
       RESIDSQ=0.0
	 
       DO 300 IZ=1,NZ
         DO 300 IXY=1,NXYL

           FNSTEMP=0.0
           IF(IWFLAG.EQ.1.OR.IWFLAG.EQ.3) THEN 
             DO 5 IG=1,NG
               FNSTEMP=FNSTEMP+
     *            TNF(IXY,IZ,IG)*SW(IXY,IZ,IG)*VOLU(IXY,IZ)
5            CONTINUE
           ELSE
             DO 6 IG=1,NG
               FNSTEMP=FNSTEMP+
     &          ((1.0-BETAT(IXY,IZ))*XHIP(IXY,IZ,IG)
     &          +BETATLAM(IXY,IZ,IG))*SW(IXY,IZ,IG)*VOLU(IXY,IZ)
6            CONTINUE
           ENDIF

           DO 10 IG=1,NG
             IF(IWFLAG.EQ.1.OR.IWFLAG.EQ.3) THEN 
               IF(IWFLAG.EQ.3) THEN
                 TRANFAC=(1.0-BETAT(IXY,IZ))*XHIP(IXY,IZ,IG)
     &                 +FILAMDT1(IXY,IZ,IG)
               ELSE
                 TRANFAC=(1.0-BETAT(IXY,IZ))*XHIP(IXY,IZ,IG)
     &                 +BETATLAM(IXY,IZ,IG)
               ENDIF
             ELSE
               TRANFAC=TNF(IXY,IZ,IG)
             ENDIF
                                  
             APHI=SCW(IXY,IZ,IG)+FNSHIFT(IXY,IZ,IG)*SW(IXY,IZ,IG)
     
             FPHI=TRANFAC*FNSTEMP
               
             IF(ITYPE.EQ.'FSP') THEN
               SOURCE=LSEXTR(IXY)*LSEXTZ(IZ)*SEXT(IG)*VOLU(IXY,IZ)
             ELSEIF(IWFLAG.EQ.3) THEN
               SOURCE=SEFF(IXY,IZ,IG)
	     ELSE
	       SOURCE=0.0
	     ENDIF

	     APHINORM=APHINORM+SW(IXY,IZ,IG)*APHI
	     FPHINORM=FPHINORM+SW(IXY,IZ,IG)*FPHI
             SPHINORM=SPHINORM+SW(IXY,IZ,IG)*SOURCE
             FPHINORMSQ=FPHINORMSQ+FPHI*FPHI
             SOURCESQ=SOURCESQ+SOURCE*SOURCE
             RESIDSQ=RESIDSQ+(APHI-FPHI/FLAMDA-SOURCE)**2
               
10         CONTINUE
 
300    CONTINUE
         
       IF(IWFLAG.EQ.1.OR.IWFLAG.EQ.2) THEN
         EIGEN=FPHINORM/APHINORM
         RESIDUAL=SQRT(RESIDSQ/(FPHINORMSQ/(FLAMDA**2)))
       ELSE
         EIGEN=FLAMDA
         RESIDUAL=SQRT(RESIDSQ/SOURCESQ) 
       ENDIF  

       RETURN
       END
