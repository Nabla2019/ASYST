      SUBROUTINE NONNETH(
     &  CURNX,CURNV,CURNU,CURNZ,DCL,DCW,DCNV,DCNU,DLL,DLW,
     &  DLNV,DLNU,SW,NBC,NXSTART,NXEND,NDNUM)
C     
C     THIS SUBROUTINE DETERMINES THE CURRENTS FOR NEM
C     ********** FOR HEXAGONAL GEOMETRY **************
C     
      include 'param.fcb'
      include 'cntl.fcb'
      include 'bcs.fcb'
      
      DIMENSION
     &  NXSTART(*)                 ,NXEND(*)              ,
     &  NDNUM(-1:NX+2,0:*)         ,NBC(*)                ,
     &  CURNX(NXYT,NZ,*)           ,CURNV(NXYT,NZ,*) ,
     &  CURNU(NXYT,NZ,*)           ,CURNZ(NXYL,NZ+1,*)    ,
     &  SW(NXYL,NZ,*)              ,
     *  DCL(NXYL,NZ+1,*)           ,
     *  DCW(NXYT,NZ,*)             ,DCNV(NXYT,NZ,*)       ,
     *  DCNU(NXYT,NZ,*)            ,DLL(NXYL,NZ+1,*)      ,
     *  DLW(NXYT,NZ,*)             ,
     *  DLNV(NXYT,NZ,*)            ,DLNU(NXYT,NZ,*)
            
      DO 10 IG=1,NG
       DO 12 IZ=1,NZ
        
C     
C     BEGIN CALCULATION OF CURRENTS IN X-DIRECTION (HEX).
C     
        DO 14 IY=1,NY
C     
C     SWEEP THROUGH HEX DIRECTIONS WEST-EAST.
C     
         DO 16 IX=NXSTART(IY),NXEND(IY),2
          IXY=NDNUM(IX,IY)
C     
C     INCLUDE Z-DIRECTION CURRENT CALCULATION IN WEST-EAST SWEEP.
C     
          IF (IZ.EQ.1) THEN
           IF(LIZD.EQ.0) THEN
            CURNZ(IXY,1,IG)=0.0
           ELSEIF(LIZD.EQ.1.OR.LIZD.EQ.2) THEN
            CURNZ(IXY,1,IG)=-(DCL(IXY,1,IG)+DLL(IXY,1,IG))
     @        *SW(IXY,1,IG)
           ENDIF
          ELSE
           CURNZ(IXY,IZ,IG)=-DCL(IXY,IZ,IG)*(SW(IXY,IZ,IG)-
     @       SW(IXY,IZ-1,IG))-DLL(IXY,IZ,IG)*(SW(IXY,IZ,IG)+
     @       SW(IXY,IZ-1,IG))
          ENDIF
          IF (IZ.EQ.NZ) THEN
           IZP=NZ+1
           IF(LIZU.EQ.0) THEN
            CURNZ(IXY,IZP,IG)=0.0
           ELSEIF(LIZU.EQ.1.OR.LIZU.EQ.2) THEN
            CURNZ(IXY,IZP,IG)=(DCL(IXY,IZP,IG)-DLL(IXY,IZP,IG))
     @        *SW(IXY,NZ,IG)
           ENDIF
          ENDIF
C     
C     CALCULATE CURRENT FOR WESTERN B.C. OF ROW IY.
C     
          IXYM=NDNUM(IX-2,IY)
          IF (IX.EQ.NXSTART(IY)) THEN
           IBC=NBC(IXYM-NXYL)
           IF(IBC.LT.0) THEN
            IXYM=-1*IBC
           ELSEIF(IBC.EQ.0) THEN
            CURNX(IXY,IZ,IG)=0.0
            GOTO 15
           ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
            CURNX(IXY,IZ,IG)=-(DCW(IXY,IZ,IG)+DLW(IXY,IZ,IG))
     @        *SW(IXY,IZ,IG)
            GOTO 15
           ENDIF
          ENDIF
C     
C     CALCULATE CURRENTS FOR INTERIOR NODES OF ROW IY.
C     
          CURNX(IXY,IZ,IG)= -DCW(IXY,IZ,IG)*(SW(IXY,IZ,IG)-
     @      SW(IXYM,IZ,IG))-DLW(IXY,IZ,IG)*(SW(IXY,IZ,IG)+
     @      SW(IXYM,IZ,IG))
 15       CONTINUE
C     
C     CALCULATE CURRENT FOR EASTERN B.C. OF ROW IY.
C     
          IF (IX.EQ.NXEND(IY)) THEN
           IXYP=NDNUM(IX+2,IY)
           IBC=NBC(IXYP-NXYL)
           IF(IBC.LT.0) THEN
            IXYPS=-1*IBC
           ELSEIF(IBC.EQ.0) THEN
            CURNX(IXYP,IZ,IG)=0.0
            GOTO 16
           ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
            CURNX(IXYP,IZ,IG)=+(DCW(IXYP,IZ,IG)-DLW(IXYP,IZ,IG))
     @        *SW(IXY,IZ,IG)
            GOTO 16
           ENDIF
           CURNX(IXYP,IZ,IG)= -DCW(IXYP,IZ,IG)*(SW(IXYPS,IZ,IG)-
     @       SW(IXY,IZ,IG))-DLW(IXYP,IZ,IG)*(SW(IXYPS,IZ,IG)+
     @       SW(IXY,IZ,IG))
          ENDIF
 16      CONTINUE
 14     CONTINUE
        
C     
C     BEGIN CALCULATION OF CURRENTS IN V-DIRECTION (HEX).
C     
C     SWEEP THROUGH HEX DIRECTIONS NORTHWEST-SOUTHEAST CALCULATING CURRENTS.
C     
        DO 29 IYST=1,NY
         DO 28 IXST=NXSTART(IYST),NXEND(IYST),2
          IF(NDNUM(IXST-1,IYST-1).GT.NXYL) THEN
           IX=IXST-1
           DO 26 IY=IYST,1000
            IX=IX+1
            IXY=NDNUM(IX,IY)
            IF(IXY.GT.NXYL) GOTO 28
            IXYM=NDNUM(IX-1,IY-1)
C     
C     CALCULATE CURRENT FOR NORTHWEST B.C. OF ROW IX.
C     
            IF(IXYM.GT.NXYL) THEN
             IBC=NBC(IXYM-NXYL)
             IF(IBC.LT.0) THEN
              IXYM=-1*IBC
             ELSEIF(IBC.EQ.0) THEN
              CURNV(IXY,IZ,IG)=0.0
              GOTO 25
             ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
              CURNV(IXY,IZ,IG)=-(DCNV(IXY,IZ,IG)+
     @          DLNV(IXY,IZ,IG))*SW(IXY,IZ,IG)
              GOTO 25
             ENDIF
            ENDIF
C     
C     CALCULATE CURRENTS FOR INTERIOR NODES OF ROW IX
C     
            CURNV(IXY,IZ,IG)= -DCNV(IXY,IZ,IG)*(SW(IXY,IZ,IG)-
     @        SW(IXYM,IZ,IG))-DLNV(IXY,IZ,IG)*(SW(IXY,IZ,IG)+
     @        SW(IXYM,IZ,IG))
 25         CONTINUE
C     
C     CALCULATE CURRENT FOR SOUTHEASTERN B.C. OF ROW IX.
C     
            IXYP=NDNUM(IX+1,IY+1)
            IF (IXYP.GT.NXYL) THEN
             IBC=NBC(IXYP-NXYL)
             IF(IBC.EQ.0) THEN
              CURNV(IXYP,IZ,IG)=0.0
             ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
              CURNV(IXYP,IZ,IG)=+(DCNV(IXYP,IZ,IG)-
     @          DLNV(IXYP,IZ,IG))*SW(IXY,IZ,IG)
             ENDIF
            ENDIF
 26        CONTINUE
          ENDIF
 28      CONTINUE
 29     CONTINUE
C     
C     BEGIN CALCULATION OF CURRENTS IN U-DIRECTION (HEX).
C     
C     
C     SWEEP THROUGH HEX DIRECTIONS NORTHEAST-SOUTHWEST CALCULATING CURRENTS.
C     
        DO 39 IYST=1,NY
         DO 38 IXST=NXSTART(IYST),NXEND(IYST),2
          IF(NDNUM(IXST+1,IYST-1).GT.NXYL) THEN
           IX=IXST+1
           DO 36 IY=IYST,1000
            IX=IX-1
            IXY=NDNUM(IX,IY)
            IF(IXY.GT.NXYL) GOTO 38
            IXYM=NDNUM(IX+1,IY-1)
            IF(IXYM.GT.NXYL) THEN
             IBC=NBC(IXYM-NXYL)
             IF(IBC.LT.0) THEN
              IXYM=-1*IBC
              
             ELSEIF(IBC.EQ.0) THEN
              CURNU(IXY,IZ,IG)=0.0
              GOTO 35
             ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
              CURNU(IXY,IZ,IG)=-(DCNU(IXY,IZ,IG)+
     @          DLNU(IXY,IZ,IG))*SW(IXY,IZ,IG)
              GOTO 35
             ENDIF
            ENDIF
C     
C     CALCULATE CURRENTS FOR INTERIOR NODES OF ROW IX
C     
            CURNU(IXY,IZ,IG)= -DCNU(IXY,IZ,IG)*(SW(IXY,IZ,IG)-
     @        SW(IXYM,IZ,IG))-DLNU(IXY,IZ,IG)*(SW(IXY,IZ,IG)+
     @        SW(IXYM,IZ,IG))
            
 35         CONTINUE
            
C     
C     CALCULATE CURRENT FOR SOUTHWESTERN B.C. OF ROW IX.
C     
            IXYP=NDNUM(IX-1,IY+1)
            IF (IXYP.GT.NXYL) THEN
             IBC=NBC(IXYP-NXYL)
             IF(IBC.EQ.0) THEN
              CURNU(IXYP,IZ,IG)=0.0
             ELSEIF(IBC.EQ.1.OR.IBC.EQ.2) THEN
              CURNU(IXYP,IZ,IG)=+(DCNU(IXYP,IZ,IG)-
     @          DLNU(IXYP,IZ,IG))*SW(IXY,IZ,IG)
             ENDIF
            ENDIF
 36        CONTINUE
          ENDIF
 38      CONTINUE
 39     CONTINUE
 12    CONTINUE
 10   CONTINUE
      
      RETURN
      END
