        SUBROUTINE PRECR(
     &  BETA,FILAMDT0,FILAMDT1,XHIDMI,CI,
     &  FNOLDT,FNSWT,ALXPLAMD,EXPLAMD,FACT0,FACT1,
     &  NCOLXY,NCOLZ,ALAMDAT)
C
C     THIS SUBROUTINE SOLVES THE DELAYED NEUTRONS PRECURSOR EQUATIONS
C     
      include 'param.fcb'
      include 'che.fcb'
      include 'geom.fcb'
      include 'cntl.fcb'
      include 'perttr.fcb'
      include 'nonfue.fcb'
      
      DIMENSION
     &  BETA(NXYL,NZ,*)              ,ALAMDAT(NXYL,NZ,*),
     &  ALXPLAMD(NXYL,NZ,NG,*)       ,CI(NXYL,NZ,*)       ,
     &  XHIDMI(NG,*)                 ,FILAMDT0(NXYL,NZ,*) ,
     &  FILAMDT1(NXYL,NZ,*)          ,FNSWT(NXYL,*)       ,
     &  FNOLDT(NXYL,*)               ,FACT0(*)            ,
     &  FACT1(*)                     ,
     &  NCOLXY(NXYLMAX,*)            ,NCOLZ(*)
      
      REAL*8 ALAMDADP,EXPLAMD(*)
C     
C...  CI     = PRECURSOR DENSITIES FOR SIX GROUP AT (N+1) TIME STEP
C...  ALAMDAT= NODE-WISE DECAY CONSTANTS FOR THE SIX GROUPS.
C...  BETA   = DELAYED NEUTRON FRACTION
C...  DELTAT = TIME INTERVAL
C...  NPREC  = NUMBER OF PRECURSORS
C     
C     
C     PRECURSOR DENSITIES
C=========================
C     
      IF(DELTATOLD.GT.0.0) THEN
       DO 110 IZ=1,NZ
        DO 10 IXY=1,NXYL
         ICOL=NCOLXY(IXY,NCOLZ(IZ))
         IF(ICOL.LE.NFUEXY) GO TO 10
         DO 11 I=1,NPREC
          ALAMDADP = ALAMDAT(IXY,IZ,I) * DELTATOLD
          EXPLAMD(I) = DEXP(- ALAMDADP )
          ALAMDADP = 1. - EXPLAMD(I)
          FACT1(I) = ( DELTATOLD - ( 1.0/ALAMDAT(IXY,IZ,I) ) *
     &      ALAMDADP )  *
     &      (BETA(IXY,IZ,I)/ALAMDAT(IXY,IZ,I))*(1.0/DELTATOLD)
          FACT0(I) = ALAMDADP *
     &      ( BETA(IXY,IZ,I)/ALAMDAT(IXY,IZ,I) ) - FACT1(I)
          CI(IXY,IZ,I) =
     &      FACT0(I) *  FNOLDT(IXY,IZ)/FLAMDA  +
     &      FACT1(I) *  FNSWT(IXY,IZ)/FLAMDA   +
     &      CI(IXY,IZ,I) * EXPLAMD(I)
 11      CONTINUE
 10     CONTINUE
 110   CONTINUE
      ENDIF
      
      DO 150 IZ=1,NZ
       DO 50 IXY=1,NXYL
        ICOL=NCOLXY(IXY,NCOLZ(IZ))
        IF(ICOL.LE.NFUEXY) GO TO 50
        DO 51 I=1,NPREC
         ALAMDADP = ALAMDAT(IXY,IZ,I) * DELTAT
         EXPLAMD(I) = DEXP(- ALAMDADP )
         ALAMDADP = 1. - EXPLAMD(I)
         FACT1(I) = ( DELTAT - ( 1.0/ALAMDAT(IXY,IZ,I) ) *
     &     ALAMDADP )  *
     &     ( BETA(IXY,IZ,I)/ALAMDAT(IXY,IZ,I) )*(1.0/DELTAT)
         FACT0(I) = ALAMDADP *
     &     ( BETA(IXY,IZ,I)/ALAMDAT(IXY,IZ,I) ) - FACT1(I)
         DO IG=1,2
          ALXPLAMD(IXY,IZ,IG,I)=ALAMDAT(IXY,IZ,I)*EXPLAMD(I)
     &      *XHIDMI(IG,I)
         ENDDO
 51     CONTINUE
        DO 52 IG = 1, NG
         FILAMDT1(IXY,IZ,IG) = 0.0
         FILAMDT0(IXY,IZ,IG) = 0.0
         DO 53 I=1,NPREC
          FILAMDT0(IXY,IZ,IG) = FILAMDT0(IXY,IZ,IG)+
     &      ALAMDAT(IXY,IZ,I)*FACT0(I)*XHIDMI(IG,I)
          FILAMDT1(IXY,IZ,IG) = FILAMDT1(IXY,IZ,IG)+
     &      ALAMDAT(IXY,IZ,I)*FACT1(I)*XHIDMI(IG,I)
 53      CONTINUE
 52     CONTINUE
 50    CONTINUE
 150  CONTINUE
      
      RETURN
      END
      
