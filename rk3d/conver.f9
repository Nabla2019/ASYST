      SUBROUTINE CONVER
     &  ( ZB,DIST,DX,DY,DZ,VOLU,AREA,NXSTART,NXEND,NDNUM,RHOWREF,
     &  RHOWMREF )
C     
C     THIS SUBROUTINE CONVERTS THE INPUT DATA UNITS TO 3DKIN'S
C     INTERNAL WORKING UNITS
C     
      include 'param.fcb'
      include 'crod.fcb'
      include 'thermo.fcb'
      include 'thermk.fcb'
      include 'start.fcb'
      include 'nonfue.fcb'
      include 'geom.fcb'
      include 'cntl.fcb'
      include 'hexdim.fcb'
      include 'bpitch.fcb'
      include 'thcoef.fcb'
      include 'xsec1.fcb'
      include 'th_cof.fcb'
      include 'nterm.fcb'
      include 'confmap.fcb'
     
      DIMENSION
     &  ZB(*)                                 ,DIST(0:*)          ,
     &  DX(0:*)                               ,DY(0:*)            ,
     &  DZ(0:*)                               ,VOLU(NXYL,*)       ,
     &  AREA(*)                               ,RHOWREF(*)         ,
     &  NXSTART(*)                            ,NXEND(*)           ,
     &  NDNUM(-1:NX+2,0:*)                    ,RHOWMREF(*)
C     
C     CONVERSION COEFFICIENTS FOR POLYNOMIAL FITS
C     
      SC0_TAF=1.0
      SC1_TAF=1.0
      SC0_TSF=1.0
      SC1_TSF=1.0
C     
C     INTERNAL UNITS (SEC,CM,MEV,GRAMS,DEGREE-F/EXCEPT DOPPLER
C     CORRECTION IN DEGREE-R)
C     
C     CONVERSION FROM BTU/LBM TO MEV/GM
C     
      SC0_UCOL=1.4519633E13
      SC1_UCOL=1.0
      UCOLL=SC0_UCOL*UCOLL
      UCOLG=SC0_UCOL*UCOLG
C     
C     CONVERSION FROM BTU/LB-DEGREE-F TO MEV/GM-DEGREE-F
C     
      SC0_CPF=1.4519633E13
      SC1_CPF=1.0
C     
C     CONVERSION FROM LB/FT**3 TO GM/CM**3 AND
C     CONVERSION FROM MEV/GM TO BTU/LBM FOR "ARGUMENT"
C     
      SC0_RHOWC=0.016018732
      SC1_RHOWC=1.4519633E13
      RHOL=SC0_RHOWC*RHOL
      RHOG=SC0_RHOWC*RHOG
      
      RHOWTF=SC0_RHOWC*RHOWTF
      
      DO 4 ICOL=1,ICOLXY
       RHOWREF(ICOL)=RHOWREF(ICOL)*0.016018732
       IF(NMOD.EQ.'Y') THEN 
        RHOWMREF(ICOL)=RHOWMREF(ICOL)*0.016018732
       ENDIF
 4    CONTINUE
C     
C     CONVERSION FROM KW/FT**2-DEG-F TO MEV/CM**2-DEG-F
C     
      SC0_HFF=6.7186258E+12
      SC1_HFF=1.0
C     
C     CONVERSION FROM MEV/GM TO BTU/LBM FOR "ARGUMENT"
C     
      SC0_TCOL=1.0
      SC1_TCOL=1.4519633E13
C     
C     DETERMINATION OF POLYNOMIAL COEFFICIENTS FOR DENSITY DERIVATIVE
C     
      NPOLDRHOWC=NPOLRHOWC-1
      DO 7 IPOWER=0,NPOLDRHOWC-1
       COF_DRHOWC(IPOWER)=COF_RHOWC(IPOWER+1)*(IPOWER+1)
 7    CONTINUE
      SC0_DRHOWC=0.016018732
      SC1_DRHOWC=1.4519633E13
C     
C     CONVERSION FACTOR FROM MEV/SEC-CM TO KW/FT
C     
      CONVKWFT=30.48/6.2418076E+15
C     
C     CONVERSION INCHES TO CM :
C     
      SRFF = SRFF * 2.54
      IF(NSHAP.EQ.'CART') THEN
       
       BPITCHX=2.54*BPITCHX
       BPITCHY=2.54*BPITCHY
       BAREA=BPITCHX*BPITCHY
       
       DO 10 I = 1,NX
        DX(I)=2.54*DX(I)
 10    CONTINUE
       DO 20 J=1,NY
        DY(J)=2.54*DY(J)
 20    CONTINUE
      ENDIF
      
      DO 25 K=1,NZ
       DZ(K)=2.54*DZ(K)
       DIST(K)=2.54*DIST(K)
 25   CONTINUE
      
      IF(NSHAP.EQ.'CART') THEN
       DO 32 IZ=1,NZ
        DO 31 IY=1,NY
         DO 30 IX=NXSTART(IY),NXEND(IY)
          IXY=NDNUM(IX,IY)
          AREA(IXY) = DX(IX) *DY(IY)
          VOLU(IXY,IZ)=DX(IX)*DY(IY)*DZ(IZ)
 30      CONTINUE
 31     CONTINUE
 32    CONTINUE
      ENDIF
      IF(NSHAP.EQ.'HEXA') THEN
       DELH=2.54*DELH
       AREAHEX=(SQRT(3.)/2.)*(DELH**2)
       BAREA=AREAHEX
C
C     CALCULATE DIMENSION FOR THE CONFORMALLY MAPPED RECTANGLE
C
       RHEXA=DELH/SQRT(3.)
       CBASE=1.82308*RHEXA
       CHEIGHT=1.42510*RHEXA
       FLRAT=3.*SQRT(3.)/2*(RHEXA**2/(CBASE*CHEIGHT))       
C
C     CALCULATE COEFFICIENTS OF ORTHOGONAL POLYNOMIALS Wn(u)
C     K0,K1,K2
C
        adum=1./0.99078
        G0=   2.195*adum
        G1= -10.535*adum
        G2=  57.779*adum
        G3=-181.569*adum
        G4= 293.520*adum
        G5=-232.075*adum
        G6=  71.196*adum
        G21=0.507 !0.515755
        K0= 1.0000
        K1=4.197489747/CBASE
c        K2=11.52343929/(CBASE*CBASE)
        K2=11.478196/(CBASE*CBASE)

        W2DENOM=SQRT((K1/K2)**2-(K0/K1)**2)

       DO 36 IXY=1,NXYL
        AREA(IXY)=AREAHEX
        DO 35 IZ=1,NZ
         VOLU(IXY,IZ)=AREAHEX*DZ(IZ)
 35     CONTINUE
 36    CONTINUE
      ENDIF
      
      DO 40 IBK=1,NBACU
       ZB(IBK) = ZB(IBK)*2.54
 40   CONTINUE
      
      RODLNGTH = RODLNGTH * 2.54
      RODOFFSET = RODOFFSET * 2.54
C     
C     CONVERSION FROM W/L TO MEV/SEC-CM**3 :
C     
      QT = 6.2418076E+12 * QQT
C     
C---- TRANSFER AVERAGE MASS VELOCITY :
C---- FROM LB/HR-FT**2 TO GM/SEC-CM**2 :
C     
      GMASS=GMASS*1.3562527E-4
      
      RETURN
      END

