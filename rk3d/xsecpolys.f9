      FUNCTION XSECPOLYS(FRACT,NTERM,IXSCPOLY,XSECCOEF,ISUR)
C
C     This subroutine calculates cross sections 
C     accounting for coolant temperature, coolant density, 
C     fuel temperature and soluble poison feedback corrections 
C     on the six surfaces of the hexagonal assembly 
C     (NSHAP = "HEXA" only).
C     
      include 'param.fcb'
      include 'varlen.fcb'
      include 'cntl.fcb'
      include 'xspolycom.fcb'
      
      DIMENSION IXSCPOLY(11),XSECCOEF(6,NXYL,NZ,NG,NGTEMP,*)
      
      XSECPOLYS=0.0
      IF(NTERM.EQ.0.OR.FRACT.EQ.0.0) GOTO 2000
      DO 1000 ITERM=1,NTERM
       ITERMID=IXSCPOLY(ITERM)
       GOTO(100,200,300,400,500,600,700,750,
     &   800,850,900) ITERMID
       
C...  BASE X-SECTION CORRECTION
 100   CONTINUE
       XSECPOLYS=XSECPOLYS+
     &   XSECCOEF(ISUR,IXY,IZ,IG,IGP,ITERM)
       GOTO 1000
C...  LINEAR COOLANT DENSITY X-SECTION CORRECTION
 200   CONTINUE
       IF(ANFDBK.EQ.'N') GOTO 1000
       XSECPOLYS=XSECPOLYS+
     &   XSECCOEF(ISUR,IXY,IZ,IG,IGP,ITERM)*DRHOW
       GOTO 1000
C...  QUADRATIC COOLANT DENSITY X-SECTION CORRECTION
 300   CONTINUE
       IF(ANFDBK.EQ.'N') GOTO 1000
       XSECPOLYS=XSECPOLYS+
     &   XSECCOEF(ISUR,IXY,IZ,IG,IGP,ITERM)*(DRHOW)**2
       GOTO 1000
C...  LINEAR COOLANT TEMPERATURE X-SECTION CORRECTION
 400   CONTINUE
       IF(ANFDBK.EQ.'N') GOTO 1000
       XSECPOLYS=XSECPOLYS+
     &   XSECCOEF(ISUR,IXY,IZ,IG,IGP,ITERM)*DTCOLC
       GOTO 1000
C...  LINEAR SQRT(FUEL TEMPERATURE) X-SECTION CORRECTION
 500   CONTINUE
       IF(ANFDBK.EQ.'N') GOTO 1000
       XSECPOLYS=XSECPOLYS+
     &   XSECCOEF(ISUR,IXY,IZ,IG,IGP,ITERM)*DSQTAVF
       GOTO 1000
C...  LINEAR SOLUBLE POISON X-SECTION CORRECTION
 600   CONTINUE
       XSECPOLYS=XSECPOLYS+
     &   XSECCOEF(ISUR,IXY,IZ,IG,IGP,ITERM)*DELSOLP            
       GOTO 1000
C...  QUADRATIC SOLUBLE POISON X-SECTION CORRECTION
 700   CONTINUE
       XSECPOLYS=XSECPOLYS+
     &   XSECCOEF(ISUR,IXY,IZ,IG,IGP,ITERM)*(DELSOLP)**2
       GOTO 1000
C...  CUBIC SOLUBLE POISON X-SECTION CORRECTION
 750   CONTINUE
       XSECPOLYS=XSECPOLYS+
     &   XSECCOEF(ISUR,IXY,IZ,IG,IGP,ITERM)*(DELSOLP)**3
       GOTO 1000
C...  LINEAR MODERATOR DENSITY X-SECTION CORRECTION
 800   CONTINUE
       IF(ANFDBK.EQ.'N') GOTO 1000
       XSECPOLYS=XSECPOLYS+
     &   XSECCOEF(ISUR,IXY,IZ,IG,IGP,ITERM)*DRHOWM
       GOTO 1000
C...  QUADRATIC MODERATOR DENSITY X-SECTION CORRECTION
 850   CONTINUE
       IF(ANFDBK.EQ.'N') GOTO 1000
       XSECPOLYS=XSECPOLYS+
     &   XSECCOEF(ISUR,IXY,IZ,IG,IGP,ITERM)*(DRHOWM)**2
       GOTO 1000
C...  LINEAR MODERATOR TEMPERATURE X-SECTION CORRECTION
 900   CONTINUE
       IF(ANFDBK.EQ.'N') GOTO 1000
       XSECPOLYS=XSECPOLYS+
     &   XSECCOEF(ISUR,IXY,IZ,IG,IGP,ITERM)*DTMODC
       GOTO 1000
 1000 CONTINUE
      
 2000 CONTINUE
      XSECPOLYS=FRACT*XSECPOLYS
      
      RETURN
      END







