*mlist
      subroutine chfpgf(p,w,xi,q,yta,d,tr,hfg,fg,kg,out)
       implicit none
c
c     pgf correlation for critical heat flux ratio calculation
c         flux form
c
c  pgf(out)         critical heat flux ratio (dnbr,chfr)
c  p    [MPa]       pressure
c  w    [kg/s/m**2] mass flux
c  xi               inlet quality
c  q    [MW/m**2]   local heat flux in the point of a predicted CHF
c  yta  [m]         distance from channel inlet to the point of
c                   predicted CHF multiplied by the factor of the axial
c                   heat flux shape
c  d    [m]         equivalent diameter of a subchannel or a tube (d)
c  tr               factor of the radial heat flux distribution
c                   tr=1. for a tube
c  hfg  [MJ/kg]     latent heat of vaporization
c  fg               rod bundle factor - it is recommended to use fg=1
c                   if you lack experimental rod bundle data base;
c                   fg=1 for tube and annulus
c  kg               key which identifies geometry form
c    kg = 21   tube
c    kg = 22   internally heated annulus
c    kg = 23   rod bundle - isolated subchannel model
c    kg = 24   rod bundle - isolated subchannel model + fg function
c
c
      real  p,w,xi,q,yta,d,tr,hfg,fg,out
      real  dtr,k1,k2,pr,fgg,f1,f2,f3,f4,f5,fp,h1,h2
      integer kg
c
      pr = p/22.115
      dtr = d*tr
      fgg= yta / dtr
      f1 = w**(0.126 + 0.22*pr)
      f2 = 1.9 + 8. * pr**10 - pr - xi
      f3 = 1. + (40. / (30. + fgg))
      f4 = 1.+ ((400. + (10. / (0.016 + pr**1.8)))
     &     / (30. + q * fgg))
      h2 = 4. * q * fgg / (w * hfg)
      h1 = max(1.e-9,1.6 + 4. * pr - xi - h2)
      f5 = (1. + 1.8 / h1)
     &     * (1. + 1.e-3 / (6.e-3 + h2**3))
      if (kg .eq. 21) then
         k1 = 70.9
         k2 = 0.15
         fp = 0.17 + pr + 1.82 * pr**2 + 17.7 * pr**12
      else
         k2 = 0.04
         fp = 0.2 + pr + 1.2 * pr**2 + 14.4 * pr**11
         if ( kg .eq. 22 ) then
            k1 = 102.1
         elseif ( kg .eq. 23) then
            k1 = 109.8
         elseif ( kg .eq. 24) then
            k1 = 109.8
            fg = (1.+5.e-5*w)*fg
         endif
      endif
      out = k1 * fg * f1 * f2 /
     &      ( fp * dtr**k2 * (q+1.e-9) * fgg * f3 * f4 * f5 )
      return
      end
