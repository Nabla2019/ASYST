*mlist
      subroutine chfpg(p,w,xi,xl,q,d,tr,yta,hfg,fg,kg,out)
       implicit none
c
c     pg correlation for critical heat flux ratio calculation
c
c  pg(out)          critical heat flux ratio (dnbr,chfr)
c  p    [MPa]       pressure
c  w    [kg/s/m**2] mass flux
c  xi               inlet quality
c  xl               local quality in the point of a predicted CHF
c  q    [MW/m**2]   local heat flux in the point of a predicted CHF
c  d    [m]         equivalent diameter of a subchannel or a tube (d)
c  tr               factor of the radial heat flux distribution
c                   tr=1. for a tube
c  yta  [m]         distance from channel inlet to the point of
c                   predicted CHF multiplied by the factor of the axial
c                   heat flux shape
c  hfg  [MJ/kg]     latent heat of vaporization
c  fg               rod bundle factor - it is recommended to use fg=1
c                   if you lack experimental rod bundle data base;
c                   fg=1 for tube and annulus
c  kg               key which identifies geometry form
c    kg = 11   tube
c    kg = 12   internally heated annulus
c    kg = 13   rod bundle - isolated subchannel model
c    kg = 14   rod bundle - subchannel code + fg function
c    kg = 15   rod bundle - subchannel code - mixing coefficient=0.0064
c
      real  p,w,xi,xl,q,d,tr,yta,hfg,fg,out
      real  dtr,k1,k2,pr,fx,f1,f2,f3,f4,f5,fp,h1,h2
      integer kg
c
      pr = p/22.115
      dtr = d*tr
c
      fx = 0.25 * w * hfg * max(1.e-9,(xl-xi))
      f1 = w**(0.126 + 0.22*pr)
      f2 = 1.9 + 8. * pr**10 - pr - xi
      f3 = 1. + (40. / (30. + fx/(q+1.e-9)))
      f4 = 1.+ ((400. + (10. / (0.016 + pr**1.8)))
     &     / (30. + fx))
      fx = max(fx, 0.5*yta*q/dtr)
      h1 = max(xi,xl)
      h2 = h1 - xi
      h1 = max(1.e-9,1.6 + 4.* pr - h1)
      f5 = (1. + 1.8 / h1)
     &     * (1. + 1.e-3 / (6.e-3 + h2**3))
      if (kg .eq. 11) then
         k1 = 70.9
         k2 = 0.15
         fp = 0.17 + pr + 1.82* pr**2 + 17.7 * pr**12
      else
         k2 = 0.04
         fp = 0.2 + pr + 1.2 * pr**2 + 14.4 * pr**11
         if ( kg .eq. 12 ) then
            k1 = 102.1
         elseif ( kg .eq. 13 ) then
            k1 = 109.8
         elseif ( kg .eq. 14 ) then
            k1 = 109.8
            fg = (1.+5.e-5*w)*fg
         elseif ( kg .eq. 15 ) then
            k1 = 105.3
         endif
      endif
      out = k1 * fg * f1 * f2 / ( fp * dtr**k2 * fx * f3 * f4 * f5 )
      return
      end
