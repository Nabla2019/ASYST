*mlist
       subroutine  surftn (ifluid,ihld,nh,iscskp,temp,sigma,err)
!  Computes surface tension for a given fluid.
!
!  Cognizant engineer: jet.
!
*in32 ihld
*in32end
!
!      Calling sequence:
!
!                call  surftn (ip1,ip2,ip3,ip4,rp5,rp6,lp7)
!
!      Parameters:
!
!                ip1 = ifluid = fluid number (input)
!
!                ip2 = ihld   = list vector holding subscripts for the
!                               temperature and surface tension arrays
!                               (input)
!
!                ip3 = nh     = vector length (input)
!
!                ip4 = iscskp = skip factor for ip2 (input)
!
!                rp5 = temp   = temperature array (input)
!
!                rp6 = sigma  = surface tension array (output)
!
!                lp7 = err    = error flag (output)
!
$if def,iua
       use uncdata
$endif
       implicit none
!
!  Arguments:
!
       real  sigma(*),temp(*)
       integer  ifluid,ihld(*),iscskp,nh
       logical  err
!
!  Local variables:
!
       real  term
       integer  i,ih,m
!
!  Execution:
!
!--initialize error flag
!
       err = .false.
!
!--initialize pointer index
!
       ih = 1
!
!--branch to correlation for given fluid
!
       go to (10,20), ifluid
       err = .true.
       go to 999
!
!--light water
!
!--correlation from Schmidt, "Properties of Water and
!--Steam in S. I. Units", 1969;  FORTRAN coding obtained from R. J.
!--Wagner, EG&G Idaho, Inc.
!
   10  do m = 1,nh
         i = ihld(ih)
         term = max(647.3 - temp(i),3.0)
         sigma(i) = (((1.286274650e-11 - 1.14971929e-14*term)*term -
     *   5.75280518e-9)*term + 1.160936807e-4/(1.0+0.83*term) +
     *   1.121404688e-6)*term*term
         sigma(i) = max(sigma(i),1.0e-4)
         ih = ih + iscskp
       enddo
       go to 999
!
!--heavy water
!
!--correlation from Flowtran program (Savannah River);
!--FORTRAN coding from C. S. Miller and R. J. Wagner, EG&G Idaho, Inc.
!
   20  do m = 1,nh
         i = ihld(ih)
         term = min( max(temp(i) - 273.15,1.0e-6),366.33)
         sigma(i) = 80.61755e-3 - 0.22006382e-3*term
         sigma(i) = max(sigma(i),1.0e-4)
         ih = ih + iscskp
       enddo
$if def,iua
  999  if (.not.err) then
         if (uncmode) call appwtsft(ifluid,ihld,nh,sigma,iscskp)
       endif
       return
$endif
$if -def,iua
  999  return
$endif
       end
