*mlist
       subroutine tchf
c
c
c  Computes the wall temperature at the critical heat flux using the
c  TRAC-B method.
c
c  Cognizant engineer: ras
c
c*** SUBROUTINE ARGUMENT DEFINITION
c
c     CA       =   CHEN CORRELATION COEFFICIENT
c     GMA      =   LOCAL FLUID MASS FLUX
c     HDAVG    =   LOCAL HYDRAULIC DIAMETER
c     HFG      =   LOCAL LATENT HEAT
c     ROV      =   VAPOR DENSITY
c     HFORC    =   FORCED CONVECTION HT COEFFICIENT
c     HNUCB    =   NUCLEATE BOILING HT COEFFICIENT
c     P        =   LOCAL PRESSURE
c     TL       =   LOCAL LIQUID TEMPERATURE
c     TSAT     =   LOCAL SATURATION TEMPERATURE OF THE FLUID
c     TW       =   LOCAL WALL TEMPERTURE
c     X        =   EQUILIBRIUM THERMODYNAIC QUALITY
c     CL       =   LIQUID CONDUCTIVITY
c     CPL      =   LIQUID SPECIFIC HEAT
c     ROL      =   LIQUID DENSITY
c     SIG      =   SURFACE TENSION
c     VLZ      =   LIQUID VELOCITY
c     VVZ      =   VAPOR VELOCITY
c     ALP      =   VOID FRACTION
c*** DIRECT OUTPUTS --
c
c     TCHF     =   PEAK HEAT FLUX TEMPERATURE
c     QPPCHF   =   PEAK HEAT FLUX
*in32 is
*in32 ls
*in32end
c
       implicit none
       include 'comctl.h'
       include 'contrl.h'
       include 'fast.h'
       include 'flood.h'
       include 'htrcom.h'
       include 'statec.h'
       include 'stcblk.h'
       include 'stcom.h'
       include 'ufiles.h'
       include 'voldat.h'
c
       logical err
       real dpdt
       real qppchf,htctmp,htc,htcp,psat,delp,deltc,da,pa,hnb,
     & ftw,ftwp,dtchf,ca
       integer i
c               DETERMINE tchf AS IN RELAP5/MOD2
c
c  find chf heat flux.
       call psatpd(tw,psat,dpdt,1,err)
       if(err)psat=2.212e7
       if(err)dpdt=0.0
       delp = psat - p(iv)
       ca = hmic/(delp**0.75*dtsat**0.24)
c
       qppchf = chf
       if (qppchf .le. 1.e4) qppchf=1.e4
       htctmp = hmac*(max(tw-tempf(iv),dtsat))/(tw-satt(iv) + 0.001)
       HTC=HTCTMP+Hmic
       twchf = satt(iv) + qppchf/htc
       TwCHF=MIN(TwCHF,SATt(iv)+100.0)
       TwCHF=MAX(TwCHF,SATt(iv)+0.5)
c
       DO 150 I=1,10
         call psatpd(twchf,psat,dpdt,1,err)
         if(err)psat=2.212e7
         if(err)dpdt=0.0
         delp = max(psat - p(iv), 1.e-5)
         deltc = twchf - satt(iv)
         da = deltc**0.24
         pa = delp**0.75
         hnb = ca*da*pa
         htc = hnb + htctmp
         htcp = HNB/(DELP*DELTC)*(0.24*DELP+0.75*DELTC*dpdt)
         FTW=TwCHF-SATt(iv)-QPPCHF/HTC
         FTWP=1.0+QPPCHF/(HTC*HTC)*HTCP
         DTCHF=-FTW/FTWP
         TwCHF=TwCHF+DTCHF
         IF(ABS(DTCHF).LT.1.0) GO TO 160
         twchf = max(twchf,satt(iv) + 2.)
  150  continue
       itrchf=i
       write (output,2001) itrchf,volno(iv),twchf
 2001  format ('0******** iteration = ',i3,' failure in tchf.'
     &,' volume number=',i10,' twchf= ',f10.3)
       fail = .true.
  160 continue
       if (help .ne. 0) then
         itrchf=i
       write (output,1200) twchf,itrchf
 1200    format(/,1x,' calculated twchf= ',f10.3,
     & ', iteration number = ',i4)
       endif
       return
       end
