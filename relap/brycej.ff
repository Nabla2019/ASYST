*mlist
       subroutine brycej (vgj,c0,alpg,alpf,gflux,i,kk)
c
c  brycej - calcs vgj and c0 using bryce correaltion
c
c  Cognizant engineer: rar,wlw (jmp).
c
       implicit none
c
c************************** prologue ***********************************
c    author  j. m. putney (cerl)
c
c    purpose
c     this subroutine calculates a drift velocity (zero) and
c     distribution coefficient using the bryce correlation. the
c     subroutine is called by fidisj for horizonal flow conditions
c     (bubbly and slug flow regimes).
c
c    documentation
c     the definition of the bryce correlation has been taken from
c       whalley, p. b. and ward, j. a., 1981, two-phase pressure drop
c       design report part 4: the calculation of void fraction in
c       two-phase flow. htfs dr28 (revised), aere-r 9792.
c
c    argument list
c     vgj    = drift velocity
c     c0     = distribution coefficent
c     alpg   = vapour volume fraction
c     alpf   = liquid volume fraction
c     gflux  = mixture mass flux
c     i      = junction index in junction block arrays
c     kk     = donor volume index in volume block arrays
c
c    direct outputs
c     vgj
c     c0
c
c************************** common blocks etc **************************
       include 'comctl.h'
       include 'fast.h'
       include 'jundat.h'
       include 'voldat.h'
c
c************************** declarations *******************************
      integer i,kk
      real sqrtp,g1,g2,alpf,alpg,xm,ghs,gflux,ffac,b1,b2,b0,a0,c0,vgj
c
c************************** execution **********************************
c  Distribution coefficient.
      sqrtp =  sqrt(p(kk))
      g1 =-251.7 + 401.0*sqrtp + 54.6*p(kk)
      g2 = -4.8+6650.*rhogj(i)/rhofj(i)+13.1*sqrtp+42.2*p(kk)
      xm = alpg*rhogj(i)/rho(kk)
      ghs = ( xm/(g2*g2) + (1.0-xm)/(g1*g1) )**(-0.5)
      ffac =  exp( -0.62*abs(gflux)/ghs )
      b1 = 2.94 + 0.0023*p(kk) + 8.56e-5*p(kk)*p(kk)
      b2 =  sqrt(rhofj(i)/rhogj(i))
      b0 = b1*b2/( 1.0 + ffac*(b2-1.0) )
      a0 = 1.0 - 0.328*ffac*ffac*( 1.0 - p(kk)/221.0 )
      c0 = 1.0/( a0 + (1.0-a0)*alpg**b0 )
c  Drift velocity.
      vgj = 0.0
      return
      end
