*mlist
       subroutine fildmp
c
c  This subroutine prints the FTB bookkeeping link information on the
c  output file for the FTB files in RELAP5.  It is called from trnset
c  after the initialization.   The FTB files are labeled with their
c  appropriate RELAP5 common block labels.
c
c  Cognizant engineer: gam
c
*in32 lvjun
*in32 lvvol
*in32end
c
       implicit none
       include 'comctl.h'
       include 'ufiles.h'
c
c  Local variables.
       integer i,j,k,gnofil,gamndx(nfiles),gamndy(nfiles)
       character*9 name(nfiles)
c  Data statements
       data name
     &   /'scrtch.h ','not used ','cmpdat.h ','voldat.h ','jundat.h ',
     &    'stcom.h  ','not used ','htsrcm.h ','matdat.h ','not used ',
     &    'not used ','miedtc.h ','not used ','htsttab.h','not used ',
     &    'miedtc.h ','not used ','trpblk.h ','not used ','not used ',
     &    'rkinc.h  ','not used ','not used ','not used ','rvoldt.h ',
     &    'not used ','convarc.h','sysdatc.h','rrtbl.h  ','not used ',
     &    'r5pvmcp.h','htrflb.h ','not used ','fpfast.h ','lvectr.h ',
     &    'not used ','ssiblk.h ','radhtc.h ','not used ','not used ',
     &    '         ','         ','lev.st.h ','         ','         ',
     &    '         ','         ','         ','         ','         '/
c
c  Fill the gamndx array with vaild file id numbers that occur in this
c  problem
       j = 1
       do 100 i = 1,nfiles
         gamndx(i) = 0
         if (filid(i) .ne. 0.0) then
           gamndx(j) = i
           gamndy(j) = filndx(i)
           j = j + 1
         endif
  100  continue
c
c  Number of files to sort through
c
       gnofil = j - 1
c
c  Sort the gamndx array by increasing filndx values
c
       do 150 i = 1, gnofil-1
         do 140 j = i+1, gnofil
           if (gamndy(j) .lt. gamndy(i)) then
             k = gamndx(i)
             gamndx(i) = gamndx(j)
             gamndx(j) = k
             k = gamndy(i)
             gamndy(i) = gamndy(j)
             gamndy(j) = k
           endif
  140    continue
  150  continue
c
c  Write out the table in order
c
       write (output,2004)
 2004  format ('0',30x,'List of dynamic storage information for',
     &         ' transient calculation',/,
     &         '0 num.    filid  file index   name      file size')
       do 200 j = 1,gnofil
         i = gamndx(j)
         write (output,2005) i,filid(i),filndx(i),name(i),filsiz(i)
 2005  format (i5,f10.0,i12,a10,i12)
  200  continue
       return
       end
