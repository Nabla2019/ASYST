*mlist
      subroutine grdnrj(vgj,c0,alpg,alpf,i,kk)
c
c
c  grdnrj calcs vgj and c0 using gardner correlation
c
c  Cognizant engineer: rar,wlw (jmp).
c
      implicit none
c
c************************** prologue ***********************************
c
c    author  j. m. putney (cerl)
c
c    purpose
c
c     this subroutine calculates a drift velocity and distribution
c     coefficient using the gardner correlation with woodford-scriven
c     extension to zero void fraction. the values calculated are
c     convenient values for the interphase drag calculation rather
c     than the values which comply with drift flux theory (which are
c     not given by the correlation). the subroutine is called by
c     fidisj for low flows in large vertical pipes (bubbly and slug
c     flow regimes). the drift velocity is calculated relative to an
c     upwards directed channel.
c
c    documentation
c    -------------
c
c     the definition of the gardner correlation with the
c     woodford-scriven extension is taken from
c
c       woodford, d. j. and scriven, a. h., 1983, the rise velocity
c       of bubbles and the distribution of vapour in a liquid pool
c       during depressurization. cegb report tprd/l/2413/n82.
c
c    argument list
c    -------------
c
c     vgj    = drift velocity
c     c0     = distribution coefficient
c     alpg   = vapour volume fraction
c     alpf   = liquid volume fraction
c     i      = junction index in junction block arrays
c     kk     = donor volume index in volume block arrays
c
c    direct outputs
c    --------------
c
c     vgj
c     c0
c
c************************** common blocks etc **************************
c
       include 'fast.h'
       include 'comctl.h'
       include 'contrl.h'
       include 'jundat.h'
       include 'voldat.h'
c
c************************** declarations *******************************
c
      integer i,kk
      real conkx,conm,s153,rfg,rhof2,rpgrpm,fpgrp,alpg,alpf,
     &vrcor,vgj,c0
c
c************************** data ***************************************
c
      data conkx  /0.451155875e0/
      data conm   /0.16e0/
      data s153   /2.3409e0/
c
c************************** execution **********************************
c
c
c=======================================================================
c relative velocity
c=======================================================================
c
c rpgrpm is the reciprocal of the dimensionless group raised to the
c power of conm.
c
       rfg = max( (rhofj(i) - rhogj(i)), 1.0e-05 )
      rhof2 = rhofj(i)*rhofj(i)
      rpgrpm = rhof2*sigma(kk)**1.5e0
      rpgrpm = rpgrpm /
     &            (rhogj(i)*viscf(kk)*viscf(kk)* sqrt(rfg*gravcn))
      rpgrpm = rpgrpm ** conm
      fpgrp = conkx*conkx*rpgrpm*rpgrpm
      vrcor = (sigma(kk)*gravcn*rfg/rhof2)**0.25e0 *
     &        ( (s153 - fpgrp*alpg)* exp(-400.0e0*alpg*alpg)
     &         + fpgrp*alpg/(alpf**1.5e0) )**0.5e0
c
c=======================================================================
c distribution coefficient and drift velocity for interphase drag calc
c=======================================================================
c
c for the purpose of the interphase drag calculation, set c0 to one and
c determine a corresponding value of vgj from the calculated relative
c velocity
      vgj = alpf*vrcor
      c0 = 1.0e0
c
      return
      end
