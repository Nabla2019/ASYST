*mlist
       subroutine rusrvr
!
!  Process user-supplied variables to be added to rstplt for plotting
!  and/or interactive mode.
!
!  Cognizant engineer: rjw.
!
*in32 iscr
*in32end
!
       use usrvarc
       implicit none
       include 'comctl.h'
       include 'contrl.h'
       include 'fast.h'
       include 'ufiles.h'
!
       type (usrvars), allocatable :: usrvart(:)
       real scr(2)
       integer i,iscr(2),ix,j,l3a(8),n3,n4
       logical hout
       equivalence (scr(1),iscr(1))
!
       data l3a/2*0,1,2,0,1,-1,0/
!
!  Check for presence of additional variables for plot records.
       if (.not.allocated(usrvar)) nusrvar = 0
       l3a(1) = 20800001
       call inplnk (l3a(1),ix,n3,n4,fa(filndx(1)))
       if (n4.ne.0 .or. (ix.ne.-1 .and. ix.lt.20810000)) then
!  Input present.
         allocate (usrvart(9999))
         usrvart%ntabno = 0
         if (allocated (usrvar)) then
!  Data present from restart, delete it.
           deallocate (usrvar)
           nusrvar = 0
         endif
!  Process additional variable input.
         hout = .false.
         do
           call inplnk (l3a(1),ix,n3,n4,fa(filndx(1)))
!  Check status of card search.
           if (n4 .ne. 0) then
!  Read card 2080xxxx.
             l3a(6) = 1
             call inp2 (fa(filndx(1)),iscr,l3a)
             if (l3a(6) .lt. 0) then
               fail = .true.
             else
!  If numeric value not entered on request, set to zero.
               if (l3a(6) .eq. 1) iscr(2) = 0
               if (.not.hout) then
                 hout = .true.
                 if (iand(ihlppr(2),ishft(1,22)).ne.0) write (output,"
     & ('0  User-supplied rstplt file variables'/
     & ' req.num.',4x,'variable code',5x,'parameter')")
               endif
               if (iand(ihlppr(2),ishft(1,22)) .ne. 0)
     &         write (output,"(i9,4x,a10,1x,i16)") l3a(1),scr(1),iscr(2)
!  Check size of word 2, move data to array, and save card number.
               if (iscr(2) .gt. 1000000000) then
                 write (output,"('0******** Number on user-variable requ
     &est', i8,' is out of range.')") l3a(1)
                 fail = .true.
               else
                 j = l3a(1) - 20800000
                 write (usrvart(j)%ntabla,"(a8)") scr(1)
                 usrvart(j)%ntabl = iscr(2)
                 usrvart(j)%ntabno = j
!  Increment number of additional plot variables.
                 nusrvar = nusrvar + 1
               endif
             endif
           endif
           if (ix.ge.20810000 .or. ix.eq.-1) exit
           l3a(1) = ix
         enddo
!  If data has been entered, transfer to condensed file.
         if (count(usrvart%ntabno .ne. 0) .ne. nusrvar) then
           write (output,"('0******** Programming error in rusrvr.')")
           call fabend
         endif
         if (nusrvar .ne. 0) then
           allocate (usrvar(nusrvar))
           j = 0
           do i = 1,nusrvar
             do
               j = j + 1
               if (usrvart(j)%ntabno .ne. 0) exit
             enddo
             usrvar(i) = usrvart(j)
           enddo
         endif
         deallocate (usrvart)
       endif
       return
       end
