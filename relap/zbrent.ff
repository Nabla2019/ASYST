*mlist
       subroutine zbrent (func,tol,nsig,x1,x2,itmax,ier)
c
c
c  Using Brent's method, find root of a function func known to lie
c  between x1,and x2.
c
c  Cognizant engineer: rjw.
c
c  The root, return as x2, will be refined until its accuracy is tol.
c  itmax .. maximum allowed number of iteration.
c  nsig  .. significant digial of machine floading point prescision.
c  Ref: 'Numerical Recipes - The Art of Scientific Computing' by
c  W. H. Press, et. el., Cambrige University Press. 1986
c  The subroutine is modified from the function zbrent in the book.
c
       implicit none
       real a,b,c,d,e,eps,fa,fb,fc,func,p,q,r,s,tol,tol1,xm,x1,x2
       integer ier,iter,itmax,nsig
       external func
c
      ier = 0
      eps=3.0*10.0**(-nsig)
      a=x1
      b=x2
      fa=func(a)
      fb=func(b)
      if(fb*fa.gt.0.) then
        ier=1
       return
      endif
      fc=fb
      do 11 iter=1,itmax
        if(fb*fc.gt.0.) then
          c=a
          fc=fa
          d=b-a
          e=d
        endif
        if(abs(fc).lt.abs(fb)) then
          a=b
          b=c
          c=a
          fa=fb
          fb=fc
          fc=fa
        endif
        tol1=2.*eps*abs(b)+0.5*tol
        xm=.5*(c-b)
        if(abs(xm).le.tol1 .or. fb.eq.0.)then
          x2=b
          return
        endif
        if(abs(e).ge.tol1 .and. abs(fa).gt.abs(fb)) then
          s=fb/fa
          if(a.eq.c) then
            p=2.*xm*s
            q=1.-s
          else
            q=fa/fc
            r=fb/fc
            p=s*(2.*xm*q*(q-r)-(b-a)*(r-1.))
            q=(q-1.)*(r-1.)*(s-1.)
          endif
          if(p.gt.0.) q=-q
          p=abs(p)
          if(2.*p .lt. min(3.*xm*q-abs(tol1*q),abs(e*q))) then
            e=d
            d=p/q
          else
            d=xm
            e=d
          endif
        else
          d=xm
          e=d
        endif
        a=b
        fa=fb
        if(abs(d) .gt. tol1) then
          b=b+d
        else
          b=b+sign(tol1,xm)
        endif
        fb=func(b)
   11  continue
       ier =2
c      write(output) ' ***** zbrent exceeding maximum iterations.'
       x2=b
       return
       end
