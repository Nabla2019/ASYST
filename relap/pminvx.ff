*mlist
       subroutine pminvx (a,irn,ip,n,np,aws,avect,mtype)
c
c
c  Cognizant engineer: rjw.
c
c  pminvx computes a**(-t)*b.
c
c  This subroutine is similar to the pminvm subroutine in the
c  environmental library except that only the mtype option below is
c  available.  This subroutine should be faster since only coding for
c  the one option is present and no tests are needed.
c
c      mtype = 2      a**(-t)
c
       use pminve
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
c
       integer mtype,n,np
       integer irn(:),ip(:,:)
       real(kr) a(:),aws(:),avect(:)
c
       include 'ufiles.h'
c
c  Local variables.
       integer j,j1,jcol,jro,jseq,kentry,kerr,mt
c
       kerr = 1
       mt = mtype
       mtype = 0
       kentry = 2
c  Test for invalid entry.
       if (ip(n+1,2) .eq. 0) go to 2205
       if (ip(n+1,2) .ne. n + 1) go to 2207
       kerr = 3
       if (ip(n+1,3) .ne. 0) go to 2205
       mtype = mt
c  Jump according to required operation.
       if (mtype .ne. 2) go to 2204
c  First divide by u**t or multiply by u.
       do j = 1,n
         jcol = ip(j,2)
         j1 = ip(jcol,1)
         do
           jseq = ip(irn(j1),5)
           if (jseq .eq. j) exit
           avect(jcol) = avect(jcol) - aws(jseq)*a(j1)
           j1 = j1 + 1
         enddo
         aws(jseq) = avect(jcol)/a(j1)
       enddo
c  Now divide by l**t or multiply by l.
       j = n
       do
         j1 = ip(ip(j+1,2),1)
         do
           j1 = j1 - 1
           jro = irn(j1)
           if (ip(jro,5) .eq. j) exit
           aws(j) = aws(j) - avect(jro)*a(j1)
         enddo
         avect(jro) = aws(j)
         j = j - 1
         if (j .le. 0) return
       enddo
c  Diagnostic printing.
 2204  write (output,2304) aname(2),mtype
 2304  format ('0Error return from',a8,'because mtype =',i5,' which is o
     & ut of range.')
       return
 2205  write (output,2305) aname(kentry),aname(kerr)
 2305  format ('0Error return from',a8,'because previous entry to',a8,
     & 'gave error return.')
       return
 2207  write (output,2307) aname(kentry),aname(1)
 2307  format ('0Error return from',a8,'because no previous entry to ',
     & a8,'.')
       return
      end
