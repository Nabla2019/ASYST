*mlist
       subroutine pstpd2 (t,press,presdt,itype,err)
c
c
c  Calculates saturation pressure, "press", and dpdt, "presdt",
c  for a given temperature, "t", if itype=1 and calculates saturation
c  temperature, "t", and dpdt, "presdt", for a given pressure if
c  itype=2.
c
c  Cognizant engineer: rjw (hxc).
c
c    Modified for d20  11/88
c
       implicit none
c
       real t,press,presdt
       integer itype
       logical err
c
       real a1,a2,crp,da1,da2,dt,dta,k(5),p,pp,pr,pxxy(5),tcinv,theta,
     & theta1,txxy(5)
       integer i
       logical istop
       data k/-7.81583,17.6012,-18.1747,-3.92488,4.19174/
       data pxxy/1.229e5,1.16e6,2.641e6,5.276e6,9.566e6/
       data txxy/326.0,420.0,480.0,520.0,560.0/
       data crp/21660000.0/,tcinv/1.55306e-3/
c
       if(itype.eq.2) go to 102
c  Calculate saturation pressure,press, and dpdt,presdt,
c  for a given temperature,t.
       if(t.lt.277.0 .or. t.gt.643.89) go to 201
       theta=t*tcinv
       theta1=1.0-theta
       if(theta1.le.0.) theta1=1.e-6
       dta=(-tcinv)
       a1=1./theta
       da1=(-a1)/t
       a2=k(1)*theta1+k(2)*theta1**1.9+k(3)*theta1**2.+
     & k(4)*theta1**5.5+k(5)*theta1**10.
       da2=(k(1)+1.9*k(2)*theta1**.9+2.*k(3)*theta1+
     & 5.5*k(4)*theta1**4.5+10.*k(5)*theta1**9.)*dta
       press=crp*exp(a1*a2)
       presdt=press*(a1*da2+a2*da1)
       err=.false.
       return
c
c  Find saturation temperature,t, and dpdt,presdt, for a
c  given pressure
 102   continue
       if (press .gt. crp) then
         go to 201
       endif
       if (press .lt. 1000.0) then
         t = 277.0
         go to 134
       endif
       pr=press/crp
       pp=log(pr)
       t=372.28924+88.331901*pp+10.970708*pp**2+.97251708*pp**3
     & +.051713769*pp**4+.12129545e-2*pp**5+273.15
  106  istop=.false.
       do 130 i=1,15
         theta=t*tcinv
         theta1=1.0-theta
         if(theta1.le.0.) theta1=1.e-6
         dta=(-tcinv)
         a1=1./theta
         da1=(-a1)/t
         a2=k(1)*theta1+k(2)*theta1**1.9+k(3)*theta1**2.+
     &   k(4)*theta1**5.5+k(5)*theta1**10.
         da2=(k(1)+1.9*k(2)*theta1**.9+2.*k(3)*theta1+
     &   5.5*k(4)*theta1**4.5+10.*k(5)*theta1**9.)*dta
         p=crp*exp(a1*a2)
         presdt=p*(a1*da2+a2*da1)
         if(istop) go to 134
         dt=(press-p)/presdt
         t=max(min(t+dt,643.89),277.0)
         if(abs(dt).lt.t*1.0e-5) istop=.true.
  130  continue
  134  err=.false.
       return
  201  err=.true.
       return
       end
