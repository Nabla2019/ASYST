*mlist
        subroutine khoo(subcoo,ren,prandl,jakob,zod,hifk,i)
c
c
c Brown-Khoo-Sonin hif.
c Cognizant Eng: rws
c
       implicit none
       include 'fast.h'
       include 'cons.h'
       include 'contrl.h'
       include 'voldat.h'
c
c Local variables
       real zod,refig4,subcoo,ren,prandl,hifk,jakob,phi,qdd,vb
     &   ,upv,ri,st,st0,st35
       integer i
c
c  See Sonin-Shimko-Chun Figure 4
       refig4 = ren*piovr4
       if (refig4 .gt. 25000.) then
         phi = 21.1
       elseif (refig4 .lt. 5000.) then
         phi = 35.
       else
         phi = 35. - 13.9*(refig4-5000.)/20000.
       endif
       qdd = refig4*viscf(i)/(diamv(i)*rhof(i))
       vb = phi*qdd*exp(-1.2*zod)
       upv = 0.24*diamv(i)
c  Brown-Khoo-Sonin, ri=Richardson Number.
       ri  = betaff(i)*gravcn*subcoo*upv/vb**2
c  if ri is less than 3.5; linear interpolate between st0 and st(3.5)
       if (ri .lt. 3.5) then
         st0 = 0.0198/prandl**.33
         st35=0.0136 - 3.5*0.00081
         st = st0 + (st35-st0)*ri/3.5
       else
         ri = min(ri,15.0)
         st = 0.0136 - ri*0.00081
       endif
       st = st*(1.-0.5*jakob)
       hifk = st*rhof(i)*csubpf(i)*vb
       return
       end
