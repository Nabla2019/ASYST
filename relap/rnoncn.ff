*mlist
       subroutine rnoncn
c
c
c  Processes input data for constants needed in noncondensible
c  and molten metal calculations.
c
c  Cognizant engineer:  rjw.
c
       implicit none
       include 'comctl.h'
       include 'contrl.h'
       include 'fast.h'
       include 'statec.h'
       include 'ufiles.h'
       include 'scddat.h'
c
       integer(8) iair(11)
       integer l3(9),l3a(9)
       integer i,j,n
       real advnon(11),wmole(11),dcva(11),cvao(11),uao(11),tre(11),
     & visa(11),thcax(11),thcbx(11)
cjmk-b
       real dcon(11)
cjmk-e
       real rmolg,sum
       logical tfail
       character type(11)*8,itype*8
$if def,mmfld,2
       real mma0d(7),mma1d(7),mma2d(7),mmu0d(7)
       character mtype(7)*8
c
       data type/'helium','hydrogen','nitrogen','krypton','xenon',
     & 'air','argon','sf6','oxygen','co2','co'/
       data l3/110,0,0,5,0,1,2,-1,-1/
       data l3a/115,0,0,5,0,1,2,1,1/
       data rmolg/8314.3/
       data wmole/4.002598, 2.01593, 28.01403, 83.800, 131.300, 28.963,
     & 39.948, 146.05, 31.9988, 44.010, 28.010/
       data dcva/0.003455924,0.522573,0.1184518,0.0035,0.0035,
     & 0.10329037,0.003517,1.0e-6,0.3537302,0.7563373,0.3421647/
       data cvao/3115.839, 10310.75, 741.9764, 148.824, 94.9084, 715.0,
     & 312.192, 793.399, 639.8541, 658.7377, 693.2758/
       data uao/13256.44, 182783.4, 145725.884, 122666.5, 122666.5,
     & 158990.52, 122666.5, 0.0, 1641.42, -41467.2, 14231.1/
       data tre/80.3, 83.0, 102.7, 188.0, 252.01, 114.0, 147.0, 0.0,
     & 169., 268., 138./
       data visa/1.473e-6, 6.675e-7, 1.381e-6, 2.386e-6, 3.455e-6,
     & 1.492e-6, 1.935e-6, 2.306654e-6, 1.826e-6, 1.651e-6, 1.463e-6/
       data thcax/2.639e-3, 1.097e-3, 5.314e-4, 8.247e-5, 4.351e-5,
     & 1.945e-4, 2.986e-4, 2.374568e-2, 1.766e-4, 3.110e-5, 5.050e-4/
       data thcbx/0.7085, 0.8785, 0.6898, 0.8363, 0.8616, 0.8586,
     & 0.7224, 0.0, 0.8824, 1.1136, 0.6954/
       data advnon/2.67,6.12,18.5,24.5,32.7,19.7,16.2,71.3,16.3,
     & 26.9,18.0/
cjmk-b
c
c  Data Statement:  Constant for evaluation of the Diffusion
c                   Coefficient of NC gas in Water Vapor.
c
c                   Diffc = Dconst * T**1.75 / P
c
c       Ref:  eq. 11-4.1 of "Properties of Gases and Liquids"
c             by Reid, Praudnitz & Sherwood.
c             3rd edition, McGraw-Hill Book Co., 1977.
c
c     NC gases are: Helium, Hydrogen, Nitrogen, Krypton, Xenon,
c                   Air, Argon, SF6. + oxygen, co2 and co
c
!mpf dconst = 0.0101325*(1/Mv+1/Mi)**2 / (ev**1/3+ei**1/3)**2
!    Mv=18.03154, ev=13.1, Mi=wmole, ei=advnon
!    the precision seems to depend on how 1/3 = 0.3333 (# of 3's)
      data dcon / 3.9934e-4, 4.2941e-4, 1.2232e-4, 9.5060e-5,
     &            8.2510e-5, 1.1886e-4, 1.2039e-4, 5.9826e-5,
     &            1.2469e-4, 9.8899e-5, 1.2350e-4 /
c
cjmk-e
$if def,mmfld
       data mma0d/79.15,326.5875,106.8,0.0,0.0,0.0,0.0/
       data mma1d/0.041,0.296,-0.001,0.0,0.0,0.0,0.0/
       data mma2d/-2.5e-5,-9.375e-5,1.0625e-4,0.0,0.0,0.0,0.0/
       data mmu0d/2.67e4,1.221e5,3.84e4,0.0,0.0,0.0,0.0/
       data mtype/'al','fe','u','zr','o2','dum2','dum3'/
$endif
c
       tao = 250.0
c  Process noncondensible data cards, 110 and 115.
       l3(1) = 110
       l3(6) = 1
       call inp2 (fa(filndx(1)),iair,l3)
       l3a(1) = 115
       l3a(6) = 1
       call inp2 (fa(filndx(1)),qn,l3a)
       if (l3(6).lt.0 .or. l3a(6).lt.0) go to 20
       if (l3(6).eq.1 .and. l3a(6).eq.0) then
         l3a(6) = 1
         qn(1) = 1.0
       endif
       if (l3(6) .ne. l3a(6)) then
         write (output,2001)
 2001  format ('0******** Numbers of items on Cards 110 and 115 are not
     & equal or one of the cards is missing.')
         go to 20
       endif
       noncn = l3(6)
       if (noncn .eq. 0) go to 100
       tfail = .false.
       nonhe = 0
       nonhy = 0
       nonni = 0
       nonkr = 0
       nonxe = 0
       nonair= 0
       nonar = 0
       nonsf6 = 0
       nonoxy = 0
       nonco2 = 0
       noncom = 0
       do i = 1,noncn
         write (itype,"(a8)") iair(i)
         read (itype,"(a8)") nctype(i)
         do j = 1,11
           if (itype .eq. type(j)) go to 50
         enddo
         write (output,2002) itype
 2002 format ('0******** ',a,' is an incorrect noncondensible species na
     &me on card 110.')
         tfail =.true.
         cycle
   50    wmolea(i) = wmole(j)
         dcvax(i) = dcva(j)
         cvaox(i) = cvao(j)
         uaox(i) = uao(j)
         tref(i) = tre(j)
         visao(i) = visa(j)
         thca(i) = thcax(j)
         thcb(i) = thcbx(j)
         advn(i) = advnon(j)
         rax(i) = rmolg/wmolea(i)
cjmk-b
         dconst(i) = dcon(j)
cjmk-e
         if (j .eq. 1) nonhe = i
         if (j .eq. 2) nonhy = i
         if (j .eq. 3) nonni = i
         if (j .eq. 4) nonkr = i
         if (j .eq. 5) nonxe = i
         if (j .eq. 6) nonair= i
         if (j .eq. 7) nonar = i
         if (j .eq. 8) nonsf6 = i
         if (j .eq. 9) nonoxy = i
         if (j .eq. 10) nonco2 = i
         if (j .eq. 11) noncom = i
       enddo
       if( iand(ihlppr(2),ishft(1,22)).ne.0 )
     & write (output,2003) (nctype(i),qn(i),i=1,noncn)
 2003  format ('0 Noncondensible species, initial mass fraction:'/1p,
     & (4(5x,a8,g15.6)))
       if (tfail) then
         write (output,2004)
 2004  format ('0******** Because of prior error, nitrogen used as non-c
     &ondensible gas.')
         go to 22
       endif
       sum = 0.0
       do 14 n = 1,noncn
         sum = sum + qn(n)
   14  continue
       if (sum .eq. 0.0) then
         fail = .true.
         write (output,2009)
 2009  format ('0******** Sum of noncondensible mass fractions is zero,
     &first mass fraction set to 1.0.')
         qn(1) = 1.0
         go to 100
       endif
       if (abs(sum - 1.0) .gt. 1.0e-10) then
         fail = .true.
         write (output,2006)
 2006  format ('0******** Sum of noncondensible mass fractions is not 1.
     &0.')
         sum = 1.0/sum
         do 15 n = 1,noncn
           qn(n) = qn(n)*sum
   15    continue
       endif
       go to 100
c
   20  write (output,2007)
 2007  format ('0******** Because of errors, nitrogen being used for the
     & non-condensible gas.')
   22  fail = .true.
       noncn = 1
       read (type(3),'(a8)') nctype(1)
       qn(1) = 1.0
c  Process molten metal data cards, 111 and 116.
$if -def,mmfld,1
  100  return
$if def,mmfld
  100  l3(1) = 111
       l3(6) = 1
       call inp2 (fa(filndx(1)),iair,l3)
       l3a(1) = 116
       l3a(6) = 1
       call inp2 (fa(filndx(1)),qnm,l3a)
       if (l3(6).lt.0 .or. l3a(6).lt.0) go to 120
       if (l3(6).eq.1 .and. l3a(6).eq.0) then
         l3a(6) = 1
         qnm(1) = 1.0
       endif
       if (l3(6) .ne. l3a(6)) then
         write (output,2011)
 2011  format ('0******** Numbers of items on Cards 111 and 116 are not
     & equal or one of the cards is missing.')
         go to 120
       endif
       nonmf = l3(6)
       if (nonmf .eq. 0) return
       tfail = .false.
       do i = 1,nonmf
         write (itype,"(a8)") iair(i)
         read (itype,"(a8)") mctype(i)
         do j = 1,7
           if (itype .eq. mtype(j)) go to 150
         enddo
         write (output,2012) itype
 2012 format ('0******** ',a,' is an incorrect metal species name on car
     &d 111.')
         tfail =.true.
         cycle
  150    mma0(i) = mma0d(j)
         mma1(i) = mma1d(j)
         mma2(i) = mma2d(j)
         mmu0(i) = mmu0d(j)
       enddo
       if( iand(ihlppr(2),ishft(1,22)).ne.0 )
     & write (output,2013) (mctype(i),qnm(i),i=1,nonmf)
 2013  format ('0 Metal species, initial mass fraction:'/1p,
     & (4(5x,a8,g15.6)))
       if (tfail) then
         write (output,2014)
 2014  format ('0******** Because of prior error, al used as metal field
     &.')
         go to 122
       endif
       sum = 0.0
       do 114 n = 1,nonmf
         sum = sum + qnm(n)
  114  continue
       if (sum .eq. 0.0) then
         fail = .true.
         write (output,2019)
 2019  format ('0******** Sum of metal mass fractions is zero, first mas
     &s fraction set to 1.0.')
         qnm(1) = 1.0
         return
       endif
       if (abs(sum - 1.0) .gt. 1.0e-10) then
         fail = .true.
         write (output,2016)
 2016  format ('0******** Sum of metal mass fractions is not 1.0.')
         sum = 1.0/sum
         do 115 n = 1,nonmf
           qnm(n) = qnm(n)*sum
  115    continue
       endif
       return
c
  120  write (output,2017)
 2017  format ('0******** Because of errors, al being used for the metal
     & field.')
  122  fail = .true.
       nonmf = 1
       read (mtype(3),'(a8)') mctype(1)
       qnm(1) = 1.0
       return
$endif
       end
