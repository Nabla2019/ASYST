*mlist
      subroutine gasthc (thconx,ngas,indgas,xmol,tmpgas)
c  gasthc computes thermal conductivity for a mixture of gases.
c  Cognizant engineer: rjw (hxc).
c
       implicit none
c
       real thconx,tmpgas
       integer ngas
c    thconx   thermal conductivity
c    ngas      number of gas components
c    indgas   gas component index array
c    xmol     mole fraction array
c    tmpgas   gas temperature
c
       integer i,j,k,l
       integer indgas(ngas)
       real sumx,term
       real xmol(ngas)
       real mw(7),mwi(7),coefa(7),coefb(7),gask(7)
c                he   ar     kr   xe     n2      h2     o2
       data mw/4.0026,39.948,83.8,131.30,28.0134,2.0159,31.9994/
c               he          ar           kr           xe
       data mwi/0.249837606,2.5032542e-2,1.1933174e-2,7.6161462e-3,
     &         3.5697202e-2,0.496056352,3.1250585e-2/
c              n2           h2          o2
c
c                he       ar       kr       xe       n2
      data coefa/2.639e-3,2.986e-4,8.247e-5,4.351e-5,5.314e-4,
     1           1.097e-3,1.853e-4/
c                h2       o2
c
c                he     ar     kr     xe     n2     h2     o2
      data coefb/0.7085,0.7224,0.8363,0.8616,0.6898,0.8785,0.8729/
c
      do 100 i=1,ngas
      k=indgas(i)
      gask(i)=coefa(k)*(tmpgas)**coefb(k)
  100 continue
      if(ngas.gt.1) go to 108
      thconx=gask(1)
      return
  108 thconx=0.0
      do 124 i=1,ngas
      sumx=xmol(i)
      l=indgas(i)
      do 116 j=1,ngas
      if(i.eq.j) go to 116
      k=indgas(j)
      term=mw(l)*mwi(k)
      sumx=sumx+xmol(j)*(1.0+sqrt(gask(i)*sqrt(term)/gask(j)))**2
     1    /sqrt(8.0*(1.0+term))
  116 continue
      thconx=thconx+xmol(i)*gask(i)/sumx
  124 continue
      return
      end
