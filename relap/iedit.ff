*mlist
       subroutine iedit
*in32 ipx
*in32end
c
c  Writes summary of the hydrodynamic volume and junction conditions
c  after completion of input processing.
c
c  Cognizant engineer: rar.
c
       use lpdatc
       implicit none
       include 'fast.h'
       include 'cmpalf.h'
       include 'cmpdac.h'
       include 'cmpdat.h'
       include 'comctl.h'
       include 'contrl.h'
       include 'jundat.h'
       include 'przdat.h'
       include 'pumpblk.h'
       include 'turbin.h'
       include 'ufiles.h'
       include 'voldat.h'
c
c  Local variables.
       integer i,i3,i4,i5,i6,i7,i8,i9,ij,ije,ir,is,it,iv,ive,
     & ipx(13),j,jj,jy,k,l,n
       integer loop,lines,m,linep1
       real btupsc,crsec,ctrq,cvlb,degf1,degf2,den,eng,ft,ft2,ft3,
     & psia,px(13),xlb,xmdt
       equivalence (ipx(1),px(1))
       character lb1a(2)*9,lb4(2)*10,lb5(2)*10,lb6(2)*9,lb7(2)*4,
     & lb8(2)*3,lb9(2)*5
       character lm(2)*5,lv(2)*5,nrg(25)*4
       parameter (lines=59)
c
c  Data statements
      data ft/3.2808399/, ft2/10.763910/,
     & ft3/35.314662/, eng/4.2992261e-4/, psia/1.4503774e-4/,
     & degf1/1.8/, degf2/255.3722222222/, den/6.242796396e-2/
       data cvlb/2.204622622/, lm/' kg,',' lb,'/, lv/' m3.',' ft3.'/
       data btupsc/9.478171200e-4/, xlb/2.204622622/
       data crsec/0.10471975511963/
       data ctrq/0.7375621495/
       data xmdt/6.242796057e-2/
       data nrg/'x','ctb','ctt','ctm','bby','slg','anm','mpr',
     &      'ian','isl','mst','mpo','hst','vst','mwy','mwa','mam','mms',
     &      'mws','mwp','mpl','mps','msl','mpb','mbb'/
c  Floregime symbols starting with mwy and ending with mbb are for the
c     eccmix component. mwy= wavy, mwa= wavy/annular, mam= annular/mist,
c     mms= mist, mws= wavy/slug, mwp= wavy/plug/slug, mpl= plug,
c     mps= plug/slug, msl= slug, mpb= plug/bubbly, mbb= bubbly.
c                                                            szr-890411.
       data lb1a /'(Watt)', '(Btu/sec)'/, lb4/' (rad/sec)',' (rev/min)'/
       data lb5/' (Pa)',' (lbf/in2)'/, lb6/' (n-m)',' (lbf-ft)'/
       data lb7/'m3,','ft3,'/,lb8/'m,','ft,'/,lb9/'K,','degF,'/
c
       linep1 = lines + 1
       if (iand(ihlppr(2),ishft(1,22)).ne.0)
     & write (output,2000)
 2000  format ('1=======================================================
     &========================'/
     & ,33x,'Edit of hydrodynamic components after input processing')
       iv = filndx(4)
       ive = iv + (nvols(iv)-1)*ivskp
       ij = filndx(5)
       ije = ij + (njuns(ij)-1)*ijskp
       px(1) = tmass
       px(2) = gerr
       i = 1
       if (.not.unito) then
         px(1) = px(1)*cvlb
         px(2) = px(2)*ft3
         i = 2
       endif
       if (iand(ihlppr(2),ishft(1,22)).ne.0) then
         write (output,2020) nvols(iv),njuns(ij),px(1),lm(i),px(2),lv(i)
 2020  format ('0   Number of volumes =',i5,', number of junctions =',
     & i5'.'/'0   total system mass =',1p,g13.5,a5,
     & 'total system volume =',g13.5,a4)
c  Edit volume information.
         write (output,2005)
 2005  format ('0   Vol.no.  pressure',5x,'voidg',9x,'tempf',9x,'tempg',
     & 6x,'sat. temp.',2x,'noncond.',3x,'boron den.',4x,'uf',11x,'ug',
     & 7x,'vol-flag')
         if (.not.unito) then
           write (output,2006)
 2006  format (13x,'(lbf/in2)',18x,2('(degF)',8x),'(degF)',
     & 3x,'vapor qual.',1x,'(lb/ft3)',3x,'(Btu/lb)',4x,'(Btu/lb)',
     & 4x,'tlpvbfe')
         else
           write (output,2007)
 2007  format (13x,'(Pa)',24x,2('(K)',10x),'(K)',6x,'vapor qual.',3x,
     & '(kg/m3)',4x,'(J/kg)',7x,'(J/kg)',4x,'tlpvbfe')
         endif
       endif
       n = filndx(3)
       j = iv
       do 14 is = 1,nloops
         if (.not.unito) then
           px(1) = lpdat(is)%systms*cvlb
           px(2) = lpdat(is)%sysmer*ft3
           if (iand(ihlppr(2),ishft(1,22)).ne.0)
     &     write (output,2001) is,lpdat(is)%lsysnm,px(1),px(2)
 2001  format ('0System',i3,' ',a10,' mass =',1p,g13.5,' lb    volume=',
     & g13.5,' ft3')
         else
           if (iand(ihlppr(2),ishft(1,22)).ne.0)
     &     write (output,2002) is,lpdat(is)%lsysnm,lpdat(is)%systms,
     &     lpdat(is)%sysmer
 2002  format ('0System',i3,' ',a10,' mass =',1p,g13.5,' kg    volume=',
     & g13.5,' m3')
         endif
         lpdat(is)%sysmer = 0.0
         if (iand(ihlppr(2),ishft(1,22)).ne.0) then
           do 17 it = 1,lpdat(is)%licn
             jy = nvc(n)
             if (jy .eq. 0) go to 18
             write (output,2003) cmpnam(n),cmpalf(cmptyp(n))
 2003  format (2(1x,a8),' component')
             if (cmptyp(n) .ne. 4) go to 48
             px(1) = pmpold(n)
             px(2) = pmpthd(n)
             px(3) = pmpttk(n)
             px(4) = pmpmt(n)
             ipx(5) = iand(ishft(cmpopt(n),-24),15)
             l = 1
             if (unito) go to 49
             px(1) = px(1) / crsec
             px(2) = px(2)*psia
             px(3) = px(3)*ctrq
             px(4) = px(4)*ctrq
             l = 2
   49        write (output,2032) px(1),lb4(l),px(2),lb5(l),px(3),lb6(l),
     &       ipx(5),px(4),lb6(l)
 2032  format ('   rpm =',1p,g13.5,a10,5x,'head =',g13.5,a10,5x,'torque
     & =',g13.5,a10/'   octant =',i2,53x,'mtr.torque =',g13.5,a10)
   48        if (cmptyp(n) .ne. 13) go to 37
             px(1) =  vliq(n)
             px(2) =  vliq(n)*rhof(j)
             px(3) =  vliq(n)/avol(j)
             px(4) =  ttank(n)
             l = 1
             if (unito) go to 38
             px(1) =  px(1)*ft3
             px(2) =  px(2)*xlb
             px(3) =  px(3)*ft
             px(4) = (px(4) - degf2)*degf1
             l =  2
   38        write (output,2035) px(1),lb7(l),px(2),lm(l),px(3),lb8(l),
     &       px(4),lb9(l)
 2035   format(' Liq. volume =',1p,g13.5,a10,'mass =',g13.5,a10,'level =
     &',g13.5,a10,'wall temp =',g13.5,a10)
   37        if (cmptyp(n) .eq. 14) then
               px(1) = turpow(n)
               px(2) = turtrq(n)
               px(3) = turvel(n)
               l = 1
               if (.not.unito) then
                 px(1) = px(1)*btupsc
                 px(2) = px(2)*ctrq
                 px(3) = px(3)/crsec
                 l = 2
               endif
               write (output,2040) px(1),lb1a(l),px(2),lb6(l),px(3),
     &         lb4(l),tureff(n)
 2040  format (' power=',1p,g13.5,a10,5x,'torque=',g13.5,a10,5x,
     & 'speed=',g13.5,a10,5x,'efficiency=',g13.5)
             endif
             if (cmptyp(n) .eq. 17) then
               if (unito) then
                 px(1) = przlvl(n)
                 l = 1
               else
                 px(1) = przlvl(n)*ft
                 l = 2
               endif
               write (output,2036) px(1),lb8(l)
 2036  format (' Gas Pressurizer level =',1pg13.5,a10)
             endif
             do l = 1,jy
               ipx(1) = volno(j)/1000000
               ipx(2) = volno(j) - ipx(1)*1000000
               i = 1000000*iand(ishft(vctrl(j),-2),1) +
     &         100000*iand(ishft(imap(j),-28),1) +
     &         10000*iand(ishft(vctrl(j),-7),1) +
     &         1000*iand(ishft(imap(j),-9),1) +
     &         100*iand(ishft(vctrl(j),-30),1) +
     &         200*iand(ishft(imap(j),-27),1) +
     &         10*iand(ishft(imap(j),-13),1) +
     &         iand(ishft(vctrl(j),-1),1)
               if (.not.unito) then
                 px(3) = p(j)*psia
                 px(4) = (tempf(j)-degf2)*degf1
                 px(5) = (tempg(j)-degf2)*degf1
                 px(6) = (satt(j)-degf2)*degf1
                 px(7) = boron(j)*den
                 px(8) = uf(j)*eng
                 px(9) = ug(j)*eng
                 write (output,2008) ipx(1),ipx(2),px(3),voidg(j),
     &           (px(k),k=4,6),quala(j),(px(k),k=7,9),i
               else
                 write (output,2008) ipx(1),ipx(2),p(j),voidg(j),
     &           tempf(j),tempg(j),satt(j),quala(j),boron(j),uf(j),
     &           ug(j),i
 2008      format (i4,'-',i6.6,1p,3g13.5,0p,3f12.3,1p,g11.3,2g13.5,i8.7)
               endif
               j = j + ivskp
             enddo
   18        n = n + cmplen(n)
   17      continue
         endif
   14  continue
       if( iand(ihlppr(2),ishft(1,22)).ne.0 ) then
       write (output,2009)
 2009  format ('0   Vol.no.  rhof',9x,'rhog',9x,'liq.v.vel.',3x,
     & 'vap.v.vel.',3x,'sounde',7x,'static qual.',1x,'tot.ht.inp.',2x,
     & 'vap.ht.inp.',2x,'vapor gen.',1x,'flow')
       if (.not.unito) then
         write (output,2010)
 2010  format (13x,2('(lb/ft3)',5x),3('(ft/sec)',5x),13x,
     & 2('(Btu/sec)',4x),'(lb/ft3-sec)','map')
       else
         write (output,2011)
 2011  format (13x,2('(kg/m3)',6x),3('(m/sec)',6x),13x,
     & 2('(Watts)',6x),'(kg/m3-sec)',1x,'map')
       endif
       do 150 j = iv,ive,ivskp
           ipx(1) = volno(j)/1000000
           ipx(2) = volno(j) - ipx(1)*1000000
           ir = iand(imap(j),63)
           if (.not.unito) then
             px(3) = rhof(j)*den
             px(4) = rhog(j)*den
             px(5) = velf(j)*ft
             px(6) = velg(j)*ft
             px(7) = sounde(j)*ft
             px(8) = q(j)*btupsc
             px(9) = qwg(j)*btupsc
             px(10) = vapgen(j)*xmdt
             write (output,2012) ipx(1),ipx(2),(px(k),k=3,7),quals(j),
     &       (px(k),k=8,10),ir
           else
             write (output,2012) ipx(1),ipx(2),rhof(j),rhog(j),velf(j),
     &       velg(j),sounde(j),quals(j),q(j),qwg(j),vapgen(j),ir
 2012  format (i4,'-',i6.6,1p,9g13.5,1x,i3)
           endif
           do k = 2,3
             if (iand(imap(j+k-1),16384) .ne. 0) then
               ir = iand(ishft(imap(j+k-1),-18),31)
               if (.not.unito) then
                 px(5) = velf(j+k-1)*ft
                 px(6) = velg(j+k-1)*ft
                 write (output,2027) ipx(1),ipx(2),k,px(5),px(6),
     &           nrg(ir+1)
 2027  format (i4,'-',i6.6,' coord.',i1,18x,1p,2g13.5,66x,a3)
               else
                 write (output,2027) ipx(1),ipx(2),k,velf(j+k-1),
     &           velg(j+k-1),nrg(ir+1)
               endif
             endif
           enddo
  150  continue
c  Write level tracking parameters
       loop = lines
       j = filndx(4)
       do 181 m = 1,nvols(j)
         if (iand(vctrlx(j),1).ne.0 .and.
     &   iand(ishft(imap(j),-28),1).ne.0) then
           loop = loop + 1
           if (loop .ge. linep1) then
             loop = 0
             write (output,7030)
 7030  format ('0   vol.no.',7x,'voidla',7x,'voidlb', 7x,'vollev',9x,
     & 'vlev')
             if (.not.unito) then
               write(output,7031)
 7031  format (46x,'(ft)',5x,'(ft/sec)')
             else
               write(output,7032)
 7032  format (47x,'(m)',6x,'(m/sec)')
             endif
           endif
           ipx(1) = volno(j)/1000000
           ipx(2) = volno(j) - ipx(1)*1000000
           ipx(13) = iand(ishft(imap(j),-28),1)
           if (ipx(13) .gt. 0) then
             if (.not.unito) then
               px(3) = vollev(j)*ft
               px(4) = vlev(j)*ft
               write(output,7040) ipx(1),ipx(2),voidla(j),
     &         voidlb(j),px(3),px(4)
             else
               write (output,7040) ipx(1),ipx(2),voidla(j),
     &         voidlb(j),vollev(j),vlev(j)
             endif
           endif
 7040  format(i4,'-',i6.6,4g13.5)
         endif
         j = j + ivskp
  181  continue
c  Edit junction information
       write (output,2013)
 2013  format ('0',3x,'Jun.no.',3x,'from vol.',5x,'to vol.',2x,
     & 'liq.j.vel.',3x,'vap.j.vel.',3x,'mass flow',4x,'jun.area',5x,
     & 'throat',6x,'jun-flag',1x,'flow',2x,'no. advs.  choked')
       if (unito) go to 210
       write (output,2014)
 2014  format (37x,'(ft/sec)',5x,'(ft/sec)',5x,'(lb/sec)',5x,'(ft2)',8x,
     & 'ratio',8x,'efvcahs',1x,'regi',1x,'last  edit  total')
       go to 211
  210  write (output,2015)
 2015  format (37x,'(m/sec)',6x,'(m/sec)',6x,'(kg/sec)',5x,'(m2)',9x,
     & 'ratio',8x,'efvcahs',1x,'regi',1x,'last  edit  total')
  211  n = filndx(3)
       j = ij
       do 24 is = 1,nloops
         write (output,2004) is,lpdat(is)%lsysnm
 2004  format ('0System',i3,' ',a10)
         do 27 it = 1,lpdat(is)%licn
         jy = njc(n)
         if (jy .eq. 0) go to 28
         write (output,2003) cmpnam(n),cmpalf(cmptyp(n))
         do 29 l = 1,jy
           ipx(1) = junno(j)/1000000
           ipx(2) = junno(j) - ipx(1)*1000000
           i3 = iand(jc(j),1)
           i = 1000000*iand(ishft(jcex(j),-15),1) +
     &         100000*iand(ishft(jcex(j),-2),1) +
     &         10000*iand(ishft(jc(j),-17),3) +
     &         1000*iand(ishft(jc(j),-4),1) +
     &         100*iand(ishft(jc(j),-8),1) +
     &         100*iand(ishft(jcex(j),-29),1) +
     &         10*iand(ishft(jc(j),-9),1) +
     &         iand(ishft(jc(j),-12),3)
           i4 = junftl(j)/1000000
           i5 = junftl(j+1)/1000000
           i6 = i4
           if (iand(jc(j),4) .ne. 0) i6 = -i6
           i7 = i5
           if (iand(jc(j),8) .ne. 0) i7 = -i7
           i8 = junftl(j) - i4*1000000
           i9 = junftl(j+1) - i5*1000000
           ipx(7) = 0
           ipx(8) = 0
           ir = iand(ishft(jcex(j),-3),31)
           if (unito) go to 250
           px(3) = velfj(j)*ft
           px(4) = velgj(j)*ft
           px(5) = mflowj(j)*xlb
           px(6) = ajun(j)*ft2
           write (output,2016) ipx(1),ipx(2),i6,i8,i7,i9,(px(jj),
     &     jj=3,6),athrot(j),i,nrg(ir+1),i3,ipx(7),ipx(8)
           go to 300
  250      write (output,2016) ipx(1),ipx(2),i6,i8,i7,i9,velfj(j),
     &     velgj(j),mflowj(j),ajun(j),athrot(j),i,nrg(ir+1),
     &     i3,ipx(7),ipx(8)
c change in format 2016 made by mbs
 2016      format (i4,'-',i6.6,2(i5,'-',i6.6),1p,5g13.5,i9.7,3x,a3,1x,
     &     i2,i7,i7)
c change in format 2016 made by mbs
  300    j = j + ijskp
   29    continue
   28    n = n + cmplen(n)
   27    continue
   24  continue
       write (output,2017)
 2017  format ('0   Jun.no.  voidfj',7x,'voidgj',7x,
     & 'fij',10x,'fwalfj',5x,'fwalgj',5x,'fjunft',5x,'fjunrt',5x,
     & 'formfj',5x,'formgj',3x,'no.  advs.  ccfl')
       if (unito) go to 310
       write (output,2018)
 2018  format (39x,'(lb-s2/ft5)',66x,'last edit  total')
       go to 311
  310  write (output,2019)
 2019  format (39x,'(N-s2/m5)',68x,'last edit  total')
  311  do 400 j = ij,ije,ijskp
           ipx(1) = junno(j)/1000000
           ipx(2) = junno(j) - ipx(1)*1000000
           k = max(ij1vn(j)-1,0)*ivskp + iv
           l = max(ij2vn(j)-1,0)*ivskp + iv
           ipx(4) = iand(ishft(jcex(j),-1),1)
           ipx(5) = 0
           ipx(6) = 0
           if (unito) go to 350
           px(3) = fij(j)*0.5914096e-3
           write (output,2108) ipx(1),ipx(2),voidfj(j),voidgj(j),
     &     px(3),fwalfj(j),fwalgj(j),fjunft(j),fjunrt(j),formfj(j),
     &     formgj(j),ipx(4),ipx(5),ipx(6)
           go to 400
  350      write (output,2108) ipx(1),ipx(2),voidfj(j),voidgj(j),
     &     fij(j),fwalfj(j),fwalgj(j),fjunft(j),fjunrt(j),formfj(j),
     &     formgj(j),ipx(4),ipx(5),ipx(6)
 2108      format (i4,'-',i6.6,1p,3g13.5,6g11.3,i2,i7,i7)
  400  continue
       endif
       return
       end
