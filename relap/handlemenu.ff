       subroutine handlemenu (value)
$if def,gui
!  Process menu callbacks.
!
$if -def,ifortc,2
       use opengl_glut
       use opengl_gl
$if def,ifortc,2
       use ifopngl
       use opengl_glut
       use hynmod
       use vreqsd
       use voldat
       implicit none
       integer value
!
       include 'fast.h'
!
       interface
!
       include 'key.h'
       include 'keysp.h'
       include 'submitch.h'
!
       end interface
!
       integer i,num
       character*20 vno
       character :: cmmd(4)*2=(/'fv','fj','hv','hj'/)
       character :: csno*14='wcntrlvar,    '
!
       fhoption2 = 0
       select case (value)
       case (1)
!  Increase x.
         call key ('X',1,1)
       case (2)
!  Decrease x.
         call key ('x',1,1)
       case (3)
!  Increase y.
         call key ('Y',1,1)
       case (4)
!  Decrease y.
         call key ('y',1,1)
       case (5)
!  Increase z.
         call key ('Z',1,1)
       case (6)
!  Decrease z.
         call key ('z',1,1)
       case (7)
!  Increase elevation.
         call keysp (GLUT_KEY_UP,1,1)
       case (8)
!  Decrease elevation.
         call keysp (GLUT_KEY_DOWN,1,1)
       case (9)
!  Increase azimuthal.
         call keysp (GLUT_KEY_LEFT,1,1)
       case (10)
!  Decrease azimuthal.
         call keysp (GLUT_KEY_RIGHT,1,1)
       case (11)
!  Increase twist.
         call key ('T',1,1)
       case (12)
!  Decrease twist.
         call key ('t',1,1)
       case (13)
!  Increase size.
         call key ('S',1,1)
       case (14)
!  Decrease size.
         call key ('s',1,1)
       case (15)
!  Toggle go/pause.
         call key ('g',1,1)
       case (16)
!  Toggle onestep.
         call key ('o',1,1)
       case (17)
!  Quit advancements.
         call key ('Q',1,1)
       case (18)
!  Toggle values display.
         call key ('v',1,1)
       case (19:22)
!  Hydrodynamic noding and volume displays.
         call glutSetMenu (mmenu(0))
         call glutDetachMenu (GLUT_RIGHT_BUTTON)
         if (value .eq. 19) then
           mmenu(0) = mmenu(1)
         else
           mmenu(0) = mmenu(2)
         endif
         call glutSetMenu (mmenu(0))
         call glutAttachMenu (GLUT_RIGHT_BUTTON)
         select case (value)
         case (19)
!  Hydrodynamic noding display.
           call submitch ('dhn',.true.)
         case (20)
!  Volume to volume connections display.
           call submitch ('dvv',.true.)
         case (21)
!  Volume to heat structure connections display.
           call submitch ('dvh',.true.)
         case (22)
!  Volume information display.
           call submitch ('dvi',.true.)
         end select
       case (33)
!  SCDAP information display.
         call glutSetMenu (mmenu(0))
         call glutDetachMenu (GLUT_RIGHT_BUTTON)
         mmenu(0) = mmenu(5)
         call glutSetMenu (mmenu(0))
         call glutAttachMenu (GLUT_RIGHT_BUTTON)
         if (displno .ne. 5) call submitch ('dsc',.true.)
       case (23)
!  Couple noding display.
         call glutSetMenu (mmenu(0))
         call glutDetachMenu (GLUT_RIGHT_BUTTON)
         mmenu(0) = mmenu(3)
         call glutSetMenu (mmenu(0))
         call glutAttachMenu (GLUT_RIGHT_BUTTON)
         call submitch ('dcn',.true.)
       case (24)
!  Exit window.
         call key ('E',1,1)
       case (25)
!  Escape.
         call key (char(27),1,1)
       case (26)
!  Original (restore) view.
         call key ('r',1,1)
       case (27)
!  Increment hydrodynamic system.
         call key ('A',1,1)
       case (28)
!  Decrement hydrodynamic system.
         call key ('a',1,1)
       case (29:32)
!  Find or Highlight a volumes or junction.
         fhoption = value
         hycommand = cmmd(fhoption-28)
         call fhnumbers
       case (34:48)
!  Display SCADAP core quantities.
         call submitch ('D' // TRIM(t9a(value - 34)),.true.)
       case (49)
!  Display all the SCADAP core quantities.
         call submitch ('Dall',.true.)
       case (50)
!  Pause/trip command.
         call key ('p',1,1)
         call glutDetachMenu (GLUT_RIGHT_BUTTON)
         call glutSetMenu (mmenu(4))
         call glutAttachMenu (GLUT_RIGHT_BUTTON)
       case (51)
!  Delete all guitrips.
         ngtrips = 0
         call glutPostRedisplay
       case (52)
!  Delete a guitrip.
         fhoption = value
         hycommand = '-'
         call fhnumbers
       case (53)
!  Add or modify a guitrip.
         if (intyp .eq. 2) then
           if (chindx .gt. 1) then
             chindx = 1
             bcd = ' '
             call glutPostRedisplay
           endif
         endif
         fhoption = value
         hycommand = ' '
         call fhnumbers
       case (54)
!  Delete any keyed input for a new or modified guitrip.
         if (chindx .gt. 1) call key (char(27),1,1)
       case (55)
!  Leave pause/trip display.
         call glutDetachMenu (GLUT_RIGHT_BUTTON)
         call glutSetMenu (mmenu(0))
         call glutAttachMenu (GLUT_RIGHT_BUTTON)
         if (chindx .gt. 1) call key (char(27),1,1)
         call key (char(27),1,1)
       case (56:83)
!  Display SCADAP axial dependent quantities.
         call submitch ('C' // TRIM(t9c(value - 55)),.true.)
       case (85)
!  Increment the value of interactive graph counter 
         nag = nag + 1
!  Call the function for display 2dgraph windows.
         call displaygr
       case (86)
!  Value command for Couple data.
         call submitch ('V' // cpltblv(value - 84),.true.)
       case (100)
!  Value command for volume number.
         call submitch ('Vvolno',.true.)
       case (101)
!  Value command for volume error number.
         call submitch ('Vvolerr',.true.)
       case (102:172)
!  Value command for volume data.
         call submitch ('V' // t2(value - 101),.true.)
       case (200)
!  Value command for junction number.
         call submitch ('Vjunno',.true.)
       case (201:227)
!  Value command for junction data.
         call submitch ('V' // t3(value - 200),.true.)
       case (251:259)
!  Watch command for general data.
         call submitch ('w' // t1(value - 250),.true.)
       case (260)
!  Watch command for testda quantities.
         fhoption = value
         hycommand = 'w' // t1(value - 250)
         call fhnumbers
       case (261:262)
!  Watch command for sysmer and systms quantities.
         fhoption = value
         hycommand = 'w' // t11(value - 260)
         call fhnumbers
       case (271:278)
!  Watch command for reactor kinetics data.
         call submitch ('w' // t6(value - 270),.true.)
       case (301:314)
!  Watch command for Scdap general data.
         call submitch ('w' // t9a(value - 300),.true.)
       case (401:471)
!  Alphanumeric fields for volume quantities for watch command.
         fhoption = value
         hycommand = 'w' // t2(value - 400)
         call fhnumbers
       case (501:526)
!  Alphanumeric fields for junction quantities for watch command.
         fhoption = value
         hycommand = 'w' // t3(value - 500)
         call fhnumbers
       case (551:560)
!  Alphanumeric fields for heat structure quantities for watch command.
         fhoption = value
         hycommand = 'w' // t4(value - 550)
         call fhnumbers
       case (571:575)
!  Alphanumeric fields for reflood related quantities for watch command.
         fhoption = value
         hycommand = 'w' // t8(value - 570)
         call fhnumbers
       case (601:636)
!  Alphanumeric fields for hydrodynamic component quantities.
         fhoption = value
         hycommand = 'w' // t5(value - 600)
         call fhnumbers
       case (701:707)
!  Alphanumeric fields for Scdap components quantities.
         fhoption = value
         hycommand = 'w' // t9b(value - 700)
         call fhnumbers
       case (708:734)
!  Alphanumeric fields for Scdap components/axial quantities.
         fhoption = value
         hycommand = 'w' // t9c(value - 707)
         call fhnumbers
       case (735)
!  Alphanumeric fields for Scdap components/axial/mesh quantities.
         fhoption = value
         hycommand = 'w' // t9d
         call fhnumbers
       case (736:737)
!  Alphanumeric fields for creep rupture quantities.
         fhoption = value
         hycommand = 'w' // t9e(value - 735)
         call fhnumbers
       case (801:828)
!  Alphanumeric fields for Couple quantities.
         vno = 'w' // t10a(value - 800)
         i = len_trim(vno)
         hycommand = vno(1:i) // ',1'
         call submitch (hycommand,.true.)
       case (829:839)
!  Alphanumeric fields for additional Couple quantities.
         fhoption = value
         hycommand = 'w' // t10b(value - 828)
         call fhnumbers
       case (900)
!  Open new 2d graph window.
         call key ('O',1,1)
       case (901)
         call key ('C',1,1)
!  Open new 2d graph window.
!  Volume numbers for n command.
       case (1001:2000)
         write (vno,"(i9.9)") vol(value - 1000)%volno
         call submitch ('n' // vno,.true.)
!  SCDAP component numbers for s command.
       case (4001:4100)
         write (vno,"(i9.9)") scdcmpno(value - 4000)
         call submitch ('n' // vno,.true.)
       case (4101:4200)
!  Display SCADAP nodal quantities.
         write (vno,"(a,i8)") TRIM(t9d),
     & scdcmpno(value - 4100)
         call submitch ('N' // vno,.true.)
!  Control system quantities.
       case (10000)
         fhoption = value
         hycommand = csno
         call fhnumbers
       end select
       return
$endif
       end subroutine handlemenu
