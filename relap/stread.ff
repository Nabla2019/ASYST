*mlist
       subroutine  stread (n,nuse,record,a)
c
c
c  Reads and initializes steam tables from thermodynamic properties
c  file data.
c
c  Cognizant engineer: jet.
c
c      stread  - read and initialize steam tables from thermodynamic
c                properties file data
c
c      Calling sequence:
c
c                call  stread (ip1,ip2,cp3,rp4)
c
c      Parameters:
c
c                ip1 = n      = FORTRAN unit number from which
c                               thermodynamic properties file data is
c                               read (input)
c
c                ip2 = nuse   = number of words available in rp4 for
c                               storage of steam tables (input)
c                             = number of words of rp4 actually needed f
c                               steam tables (output)
c                             = -1 if error detected during steam table
c                                read or initialization (output)
c
c                cp3 = record = character array into which information
c                               about the steam tables and generating
c                               program is placed (output)
c
c                rp4 = a      = array into which steam tables are read
c                               (output)
c
c      I/O units:
c
c                ip1 (input);  see above
c
c                * (default output)
c
c
c      This routine adapted from sth2xi routine written by R. J. Wagner
c      for light water steam tables
c
c
       implicit none
c
       real  a(*)
       integer  n,nuse
       character*80  record(*)
c
       include 'stcom.h'
c
       integer  i,ios,ntot
c
c
c--rewind thermodynamic properties file
       rewind n
c
c--get thermodynamic properties file title, and information about the
c--generating program
c
       read (n,end=10,err=20,iostat=ios) record(1)
       read (n,end=10,err=20,iostat=ios) record(2)
c
c--get triple point and critical point data, minimum and maximum
c--temperatures and pressures, table statistics, and table pointers
c
       read (n,end=10,err=20,iostat=ios) ttrip,ptrip,vtrip,tcrit,pcrit,
     &                                   vcrit,
     &                                   tmin,pmin,tmax,pmax,
     &                                   nt,np,nst,nsp,
     &                                   it3bp,it4bp,it5bp,nprpnt,it3p0
c
c--get number of words in steam tables
c
       read (n,end=10,err=20,iostat=ios) ntot
c
c--check number of words in steam tables against number of words
c--available for steam tables storage
c
       if (ntot .gt. nuse) go to 30
       nuse = ntot
c
c--get steam tables
c
       read (n,end=10,err=20,iostat=ios) (a(i),i=1,ntot)
       go to 50
c
c--premature end of data encountered
c
   10  write (*,1001)
       go to 40
c
c--error reading steam table data
c
   20  write (*,1002) ios
       go to 40
c
c--insufficient space
c
   30  write (*,1003)
c
   40  nuse = -1
   50  return
c
 1001  format ('0***** end of data encountered reading thermodynamic ',
     &           'property file')
 1002  format ('0***** read error encountered reading thermodynamic ',
     &           'property file, iostat =',i4)
 1003  format ('0***** insufficient space furnished for thermodynamic ',
     &           'property file')
       end
