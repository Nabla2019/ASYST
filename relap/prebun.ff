*mlist
       subroutine prebun
c
c
c  Does forced convection boiling heat transfer correlations
c  for horizontal bundles.
c
c  Cognizant engineer:  ras.
c
*in32 is
*in32 ls
*in32 end
c
c  Called by HTRC1
c
       implicit none
       include 'comctl.h'
       include 'contrl.h'
       include 'fast.h'
       include 'htrcom.h'
       include 'statec.h'
       include 'stcblk.h'
       include 'stcom.h'
       include 'ufiles.h'
       include 'voldat.h'
c
       real xcuthi,xcutlo,diff1,dpdt,f,hmac,hmic,
     & pmic,prandl,px,zero
       logical err
c
       parameter (zero = 0.0)
       parameter (xcutlo = 0.95)
       parameter (xcuthi = 0.99)
c  diff1 = 1./(xcuthi - xcutlo)
       parameter (diff1 = 25.0)
c
       hmic = 0.0
       htcg = 0.0
       qfgo = 0.0
       pmic = 1.0
       prandl = csubpf(iv)*viscf(iv)/thconf(iv)
c
       if (help .ne. 0) write (output,150) thconf(iv),viscf(iv),
     & csubpf(iv),sigma(iv),dtsat,rhof(iv),rhog(iv),hfgp
  150  format (5x,'prebun - thconf',8x,'viscf',9x,'csubpf',8x,'sigma',
     & 9x,'tw-tsatt',6x,'rhof',10x,'rhog',10x,'hfgp'/12x,1p,8e14.6)
         tf = min(tempf(iv), tsatt(iv))
c                   Polley-Ralston-Grant void multiplier.
         f = (1./voidf(iv))**0.744
c                   Polley-Ralston-Grant ESDU correlation
         rey  = gcrosf*drod/viscs
         hmac = f*0.211*(thcons/drod)*(prandl**0.34)*(rey**.651)
c
c  Calculate the micor term of chen's correlation.
c  Set the Chen supression factor to 1.0.
c
         if (nfluid .eq. 1) then
           call psatpd (tw,px,dpdt,1,err)
         elseif (nfluid .eq. 2) then
           call pstpd2 (tw,px,dpdt,1,err)
         else
           call strsat (fa(ndxstd),1,tw,px,dpdt,err)
         endif
         if (err) px = pcrit
         pmic = px - p(iv)
         if (pmic.gt.0.0 .and. dtsat.ge.0.0) then
           pmic = pmic**0.75
           hmic = 0.00122*thconf(iv)**0.79*csubpf(iv)**0.45*
     &     rhof(iv)**.49*(dtsat/(hfgp*rhog(iv)))**.24*pmic/
     &     (sqrt(sigma(iv))*viscf(iv)**.29)
         endif
c
       htcf = hmac + hmic
       htcoef = htcf
       qfluxo = htcoef*tw - hmic*tsatt(iv) - hmac*tempf(iv)
       qffo = qfluxo
       htzht = htcoef
       htzhff = hmac
       htzhft = hmic
       htzhgg = htcg
       htzhgt = 0.0
       htzhgp = 0.0
       htqot = qfluxo
       htqof = qffo
       htqog = qfgo
c
       if (help .ne. 0) write (output,350) hmac, f, hmic, htcoef,
     1 qfluxo, chf
  350  format ('    prebun ',3x,'hmac',10x,'f',13x,'hmic',10x
     & ,'htcoef',8x,'qfluxo',8x,'chf'/12x,1p,7e14.6)
       return
       end
