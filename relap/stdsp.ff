*mlist
       subroutine stdsp (i1, n, i)
c  Cognizant engineer: rjw.
c
c  Donors the separator void fraction using simple vover and vunder.
c
       implicit none
       integer i,i1,n
       include 'fast.h'
       include 'cmpdat.h'
       include 'comctl.h'
       include 'contrl.h'
       include 'jundat.h'
       include 'lvectr.h'
       include 'przdat.h'
       include 'separ.h'
       include 'statec.h'
       include 'ufiles.h'
       include 'voldat.h'
c
c  Local variables.
       real pi180, vel, voidfl, voidgl
       real arg1, arg2, vn(2), v1, v2, v3, v4, wf
       integer ij, ijc, j, jx, jx2, k, m
       parameter (pi180=3.14159265359/180.0)
c
c  Loop over junctions.
         do 1000 jx = 1,njc(n)
           k = ij1nx(i)
           jx2 = jx - 2
           if (jx2 .le. 0) then
             if (jx2 .lt. 0) then
               voidgl = 1.0
               if (vover(n) .eq. 0.0) then
                 if (voidg(k) .le. 0.0) voidgl = 0.0
               else
                 if (voidg(k) .le. vover(n)) voidgl = voidg(k)/vover(n)
               endif
               vel = max(abs(velgj(i)), 1.0e-20)
               voidfl = 1.0 - voidgl
               voidgl = min(voidgl, 0.9*voidg(k)*v(k)/(ajun(i)*vel*dt))
             else
               voidfl = 1.0
               if (vunder(n) .eq. 0.0) then
                 if (voidf(k) .le. 0.0) voidfl = 0.0
               else
                 if (voidf(k) .le. vunder(n)) voidfl = voidf(k)/
     &           vunder(n)
               endif
               vel = max(abs(velfj(i)),1.0e-20)
               voidgl = 1.0 - voidfl
               voidfl = min(voidfl, 0.9*voidf(k)*v(k)/(ajun(i)*vel*dt))
             endif
             if (velgj(i) .ge. 0.0) then
               voidgj(i) = voidgl
               vodgjr(i) = voidgj(i)/voidg(k)
             endif
             if (velfj(i) .ge. 0.0) then
               voidfj(i) = voidfl
               vodfjr(i) = voidfj(i)/voidf(k)
             endif
           endif
           i = i + ijskp
 1000    continue
       return
       end
