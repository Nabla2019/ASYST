*mlist
       subroutine rmblnk (ch,scr,n,isw)
c
c
c  Subroutine removes extraneous blanks.
c
c  Cognizant engineer: rjw.
c
       implicit none
       character ch*(*),scr*(*)
       integer isw,n
c
c  ch   character array for both input and output.
c  scr  scratch character array.
c  n    number of characters in and out.
c  isw  if nonzero, append $ at end of output characters.
c
       integer i,k
       logical sw
c
       scr(1:n) = ch(1:n)
       sw = .false.
       k = 0
       do 10 i = 1,n
         if (scr(i:i) .ne. ' ') then
           k = k + 1
           ch(k:k) = scr(i:i)
           sw = .true.
         else
           if (sw) then
             k = k + 1
             ch(k:k) = ' '
             sw = .false.
           endif
         endif
   10  continue
       if (isw .ne. 0) then
         k = k + 1
         ch(k:k) = '$'
       endif
       n = k
       return
       end
