       module intrac
!  Contains interactive variable data.
!
       implicit none
       integer, parameter, private :: kr=selected_real_kind(12,200)
!
       type intras
         real(kr) intrcv,intrva
         integer intrni
         character*8 intrla
       end type intras
!  intrcv  Units conversion factor.
!  intrva  Value of variable.
!  intrni  Number of interactive variable.
!  intrla  Alphanumeric name of interactive variable.
!
       integer intrno
!  intrno  Number of interactive variables.
       type (intras), allocatable :: intra(:)
!  intra  Holds interactive data.
!
       contains
!
       subroutine intraclear
!  Deallocates storage.
       implicit none
!
       if (allocated(intra)) deallocate (intra)
       return
       end subroutine intraclear
!
       subroutine intraread (fl)
!  Reads interactive data from restart-plot file.
       implicit none
       logical fl
       include 'ufiles.h'
!
       integer iwrd,len,lent
!
       read (rstplt) len,iwrd
       inquire (iolength=lent) intrno
c      if len .ne. lent) go to 200
       read (rstplt) intrno
       if (fl) then
         if (allocated(intra)) deallocate (intra)
         if (intrno .ne. 0) allocate (intra(intrno))
       endif
       if (intrno .ne. 0) then
         read (rstplt) len,iwrd
         inquire (iolength=lent) intra
c        if len .ne. lent) go to 200
         read (rstplt) intra
       endif
       return
       end subroutine intraread
!
       subroutine intrawrite
!  Writes interactive data to restart-plot file.
       implicit none
       include 'ufiles.h'
!
       integer, parameter :: iwrd=1
       integer len
!
       inquire (iolength=len) intrno
       write (rstplt) len,iwrd
       write (rstplt) intrno
       if (intrno .ne. 0) then
         inquire (iolength=len) intra
         write (rstplt) len,iwrd
         write (rstplt) intra
       endif
       return
       end subroutine intrawrite
!
       end module intrac
