*mlist
       subroutine zfslgj (vgj,c0,alpg,i,kk,diamjt)
c
c
c  calculates the drift velocity and distribution coefficient using the
c  Zuber-Findlay slug flow correlation.
c  The subroutine is called by fidisj for low flows in small vertical
c
c  Cognizant engineer: rar,wlw (jmp).
c
c  The definition of the zuber-findlay slug flow correlation is taken
c  from Zuber, N. et al., 1967, Steady-State And Transient Void
c  Fraction In Two-phase Flow Systems. Geap-5417.
c
c  Argument list.
c     vgj    = drift velocity
c     c0     = distribution coefficient
c     alpg   = vapour volume fraction
c     i      = junction index in junction block arrays
c     kk     = donor volume index in volume block arrays
c     diamjt = physical junction diameter
c
c  Direct outputs.
c     vgj
c     c0
c
      implicit none
       include 'comctl.h'
       include 'contrl.h'
       include 'fast.h'
       include 'jundat.h'
       include 'scrtch.h'
       include 'voldat.h'
c
       integer i,ix,kk
       real c0,vgj,alpg,diamjt
c
c
c  Distribution coefficient and drift velocity.
       c0 = 1.2
c  Modify c0 for subcooled boiling.
       ix = vctrls(kk)
       if (gammsw(ix) + gammsc(ix) .gt. 0.0) c0 = c0*(1.0 -
     & exp(-18.0*alpg))
       vgj = 0.35*sqrt(gravcn*
     &   max((rhofj(i) - rhogj(i)),1.0e-5)
     &     *diamjt/rhofj(i))
c  Impose physical limits at high void fraction.
       if (alpg .gt. 0.8) then
         c0 = 5.0*((alpg - 0.8) + (1.0-alpg)*c0)
         vgj = 5.0*(1.0 - alpg)*vgj
       endif
       return
       end
