*mlist
       subroutine psatpd (t,press,presdt,itype,err)
c
c
c  Calculates saturation pressure (press) and dpdt (presdt) for a
c  given temperature (t) if itype=1 and saturation temperature (t) and
c  dpdt (presdt) for a given pressure (press) if itype=2.
c
c  Cognizant engineer: rjw (hxc).
c
       implicit none
c
       real t,press,presdt
       integer itype
       logical err
c
c  Local variables.
       integer i
       logical istop
       real a1,a2,a3,crp,dt,k(9),p,pxxy(5),tcinv,theta,theta1,txxy(5)
c
       data k/-7.691234564e0,-26.08023696e0,-168.1706546e0,
     & 6.423285504e1,-1.189646225e2,4.167117320e0,
     & 2.097506760e1,1.e9,6.e0/
       data pxxy/1.3e5,9.3e5,2.6e6,5.2e6,9.4e6/
       data txxy/326.0,415.0,475.0,520.0,560.0/
       data crp/22120000.0/,tcinv/1.544878727e-3/
c
       if (itype.ne.2) then
c  Calculate saturation pressure and dpdt for the given temperature.
         if (t.lt.273.16 .or. t.gt.647.3) go to 201
         theta = t*tcinv
         theta1 = 1.0 - theta
         a1 = theta1*(k(1) + theta1*(k(2) + theta1*(k(3)+theta1*
     &   (k(4) + k(5)*theta1))))
         a2 = 1.0/(theta*(1.0 + theta1*(k(6) + k(7)*theta1)))
         a3 = 1.0/(k(8)*theta1*theta1 + k(9))
         press = crp*exp(a1*a2 - theta1*a3)
         presdt = -(a2*(k(1) + theta1*(2.0*k(2) + theta1*(3.0*k(3) +
     &   theta1*(4.0*k(4) + 5.0*k(5)*theta1))) + a1*(1.0 + k(6)*
     &   (theta1 - theta) + k(7)*theta1*(1.0 - theta*3.0))*a2) +
     &   (k(8)*theta1*theta1 - k(9))*a3**2)*press*tcinv
         err=.false.
         return
       else
c
c  Find saturation temperature and dpdt for a given pressure.
         if (press .gt. crp) go to 201
         do 103 i = 1,5
           if (press .lt. pxxy(i)) then
             t = txxy(i)
             go to 106
           endif
  103    continue
         t = 614.0
  106    istop = .false.
         do 130 i = 1,15
           theta = t*tcinv
           theta1 = 1.0 - theta
           a1 = theta1*(k(1) + theta1*(k(2) + theta1*(k(3) + theta1*
     &     (k(4) + k(5)*theta1))))
           a2 = 1.0/(theta*(1.0 + theta1*(k(6) + k(7)*theta1)))
           a3 = 1.0/(k(8)*theta1*theta1 + k(9))
           p = crp*exp(a1*a2 - theta1*a3)
           presdt = -(a2*(k(1) + theta1*(2.0*k(2) + theta1*(3.0*k(3) +
     &     theta1*(4.0*k(4) + 5.0*k(5)*theta1))) + a1*(1.0 + k(6)*
     &     (theta1 - theta) + k(7)*theta1*(1.0 - theta*3.0))*a2) +
     &     (k(8)*theta1*theta1 - k(9))*a3**2)*p*tcinv
           if (istop) go to 134
           dt = (press - p)/presdt
           t = max(min(t + dt,647.3),273.16)
           if (abs(dt) .lt. t*1.0e-5) istop = .true.
  130    continue
  134    err = .false.
         return
       endif
  201  err = .true.
       return
       end
