*mlist
       subroutine petukv
c
c
c  Calculates petukov single phase liquid heat transfer correlation.
c
c  This correlation used in place of dittus-boelter correlation for
c  single phase liquid for analyses of the advanced neutron source
c  reactor.
c
c  Cognizant engineer: rjw.
c
c  Called by subroutine dittus when htopta = 2.
c
c  Subroutines called:  viscol
c
c  Variables used and returned are through common htrcom.
       implicit none
       include 'contrl.h'
       include 'fast.h'
       include 'htrcom.h'
       include 'stcblk.h'
       include 'voldat.h'
       include 'ufiles.h'
c
       logical err
       integer iones(2)
       real fric,viscfs,xnum,xden,twsat
c
       data iones /1,1/
c
       if ( span(iv) .gt. 0.0 ) then
         fric   = (1.0875 - 0.1125*ptans(iv)/span(iv))
       else
         fric = 1.0
       endif
       fric   = fric/(1.82*log10(rey+1.01) - 1.64)**2.0
c
c  Evaluate subcooled forced convection contribution.
c  Base visc on tsat if tw> tsat.
       if (tw .gt. tsatt(iv)) then
         twsat = tsatt(iv)
       else
         twsat = tw
       endif
       call viscos (volmat(iv),iones,iones,1,1,twsat,p(iv),rhof(iv),
     & satt(iv),'liquid',viscfs,err)
       xnum   = (viscf(iv)/viscfs)**0.11
       xnum   = (thconf(iv)/htdiam)*0.125*fric*rey*pr*xnum
       xden   = (11.7+1.8/(pr**0.33333))*((0.125*fric)**0.5)
       xden   = xden*(pr**0.66667-1.0) + (1.0+3.4*fric)
       htcoef = xnum/xden
       return
       end
