*mlist
       subroutine emissv (epsgs, absgs, t, p, r, tw, isteam, dd, alpd,
     & epsl, kav, kad)
c
c  Calculates emissivity and absorptivity of steam and emissivity
c  of water droplets.
c
c  Cognizant engineer: ewc.
c
c  This routine uses the thomson model for steam emissivity as
c  described in hottel and sarofim,radiative transfer, pp. 219-221.
c     epsgs = emissivity of steam
c     absgs = absorptivity of steam
c     t = steam temperature (k)
c     p = steam pressure (pa)
c     r = radiation pathlength in steam (m)
c     tw = wall temp of radiating surface (k)
c     if isteam=1,calculate both epsgs and absgs.
c     if isteam=2,calculate epsgs only.
c     dd = droplet diameter (m)
c     alpd = droplet volume fraction
c     epsl = droplet emissivity
c     kav = absorption coefficient for the vapor phase
c     kad = absorption coefficient for the liquid phase
c     radiation common
c
      implicit none
c
c  Local variables.
      integer i,ict,isteam,n
       real absgs,alpd,aten,c1,c2,dd,dw,epsgs,epsl,p,r,s,sigma,sum,t,
     & temp,ts,tw,u,w,wmax,wmin
      real k,knew,kav,kad
      dimension wmin(10),wmax(10),k(10)
      data c1,c2,sigma/3.747e-5,1.4394,5.670e-5/
c
c     data for six water vapor absorption bands.
c     data taken from hottel and sarofim, radiative transfer,
c     p. 220, mcgraw-hill,1967.
c
c     minimum wave number (cm-1) for each band
      data (wmin(i),i=1,6)/195.5,1283.0,3399.0,5043.0,
     &6942.0,8468.0/
c
c     maximum wave number (cm-1) for each band
      data(wmax(i),i=1,6)/804.5,1892.0,4008.0,5652.0,
     &7551.0,9077.0/
c
c     absorption coefficient (atmos-1*cm-1) for each band
      data(k(i),i=1,6)/0.0959,0.2874,0.2069,0.0166,
     &0.0136,0.00053/
c
      kad=0.
      kav=1.
      epsl=0.
      if(r.le.1.e-6) then
        epsgs=0.
        absgs=0.
        return
      endif
c
c     convert optical thickness u to atmos*cm units
      u=p*r*9.8694e-4
      do 60 ict=1,2
c  Shield for *very* low steam temperatures.
        ts=max(t,20.)
        if(ict.eq.2) ts=tw
        sum=0.0
        do 50 n=1,6
          knew=k(n)*300./t
          dw=wmax(n)-wmin(n)
          w=(wmax(n)+wmin(n))/2.
          if(knew.gt.1.e-20)then
            temp=min(100.0,knew*u)
            aten=exp(-temp)
          else
            aten=1.
          endif
          s=c1*w**3/(exp(c2*w/ts)-1.)*(1-aten)*dw
   50   sum=sum+s
        absgs=sum/(sigma*ts**4)
        if(ict.eq.1) epsgs=absgs
        if(isteam.eq.2) go to 70
   60 continue
c
   70 continue
      temp=1.-absgs
      temp=max(temp,1.e-4)
      kav=-log(temp)/r
c
c  Calc. epsl.
      if(alpd.ge.1.e-5) then
        kad=1.11*alpd/dd
        epsl=1.0-exp(-kad*r)
      endif
      return
      end
