*mlist
*if def,selap
       function fnk(im,tp,kx)
!  Computes thermal conductivities by calling matpro.
!
!  Cognizant engineer: ewc (wxs kld).
!
!  Calling subroutines:  effht, atrmlc.
!  Subroutines called:  cthcon, fthcon, zotcon, zutcon, conmat.
!    Modified by  E.W. Coryell  12/1/89 to utilize block ifs and to
!    use srs materials.
!    Modified by K.L. Davis 1/19/94 to added tungsten.
!
!     input variables   description
!        im             material indicator
!        tp             temperature ( k )
!
!
!  In this routine matpro subroutines/functions or function conmat are
!  called for evaluation of thermal conductivities (w/m-k).
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
!
       real(kr) fnk
       real tp1
       integer im,kx
       real(kr) tp
!
       include 'contrl.h'
       include 'matdat.h'
       include 'eht0.h'
       include 'scddat.h'
       include 'hfuson.h'
       include 'scdcom.h'
       include 'scdout.h'
!
!  Local variables.
c  zloc1 = current elevation being analyzed (m)
       real(kr) zloc1
       real(kr) dr(1)
       real(kr) ccon,cdkdt,con,dkdt,gmix(10),gpres,tc
       real(kr) conmat,gascns,uthcon,zotcon,zutcon,tungk
       real(kr) sotcon
       integer ierr
       real(kr) altcon,aotcon,sthcon,athcon
       data gmix/3*0.0,1.0,6*0.0/
!
       zloc1 = zcond(kx)
       select case (im)
       case (1)
!  Zry.
         if (chngno(70)) then
           call cthconr (tp,ccon,cdkdt)
         else
           call cthcon (tp,ccon,cdkdt)
         endif
         fnk = ccon
       case (2)
!  Zr-u-o compound, liquid.
         fnk = zutcon(afulq,afzlq,tp)
       case (3)
!  Zr-u-o compound, refrozen.
         fnk = zutcon(afusl, afzsl, tp)
       case (4)
!  Tungsten.
         fnk = tungk(tp)
       case (5)
!  Zro2.
         fnk = zotcon(tp)
       case (6)
!  Unirradiated fuel.
         call fthcon(tp, fraden, fotmtl, con, dkdt)
         fnk = con
       case (7)
!  Cracked fuel.
         call fthcon (tp, fraden, fotmtl, con, dkdt)
!  Note. fcfulk(kx)=user-defined factor on fuel thermal conductivity.
         con = fcfulk(kx)*con
         if (acond(kx) .lt. 5.0) then
           fnk = acond(kx)*con
         else
           fnk = con
         endif
       case (8)
!  Relocated fuel.
         call fthcon(tp,fraden,fotmtl,con,dkdt)
         fnk = rlcond*con
       case (9:12)
!  Steam-gas-atmosphere.
!  im=10-12:  user specified.
         fnk = conmat(im,tp,zloc1)
       case (13)
!  Metallic uranium.
         fnk = uthcon(tp)
       case (14)
!  N-reactor debonding gap.
         gpres = 1.0e5
         fnk = gascns(gmix,tp,gpres)
       case (15)
!  Al.
         fnk = altcon (tp)
       case (16)
!  Al oxide.
         fnk = aotcon (tp)
       case (17)
!  Li.
         call litcon (tp,con,ierr)
         fnk = con
       case (18)
!  Ss 304.
         fnk = sthcon(tp)
       case (19)
!  Steel oxide.
         fnk = sotcon(tp)
       case (20)
!  Control material.
         fnk = athcon(ictype,tp)
       case (21)
!  Molybdenum.
         fnk = 154.188 - tp*(4.2528e-3 + tp*(3.443e-6 - 2.713e-10*tp))
       case (22)
!  Copper.
c         tc = tp - 273.15
c         fnk = 3.991e-2 - (4.484e-2 - 1.779e-5*tc)*tc
c  hm
         tc = tp - 273.15
         fnk = 399.1 - (4.484e-2 - 1.779e-5*tc)*tc
       case (23)
c* added t haste May 2008
c Ta
       tp1=max(293.16,(min(tp,1820.16))) 
        fnk = 42.864 + 0.0379*tp1  - 0.00001*tp1**2
      case (24)                     
c Brass for parameter (from J Stuckert, 59 %Cu, 39.5% Zn, 1.5% Pb) 
        fnk = 250.0
        case (25)                  
c molybdenum parameter (assume same as Mo quench)
        fnk = 154.188-tp*(4.2528e-3+tp*(3.443e-6-2.713e-10*tp))	 
       case (50:59)
         fnk = conmat(im,tp,zloc1)
       end select
       return
       end
*endif
