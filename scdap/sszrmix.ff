*mlist
*if def,selap
      subroutine sszrmix(i,wconss,wcongt,dwcongt,dhsszr)
c
c  sszrrx computes the reaction of stainless steel and Zircaloy 
c  (if any) and the resultant chemical heat of reaction
c
c  Cognizant engineer: ewc (ljo fpg).
c
c******************************************************************
c
c     sszrrx               1-April-96
c
c     the BWR canister/control blade modules were developed at ORNL
c     by L.J.Ott under the auspices of the NRC sponsored Boiling
c     Water Reactor Severe Accident Technologies programs.
c
c     for information or assistance, call
c                 L.J.Ott (fts)-624-0324 or (615)-574-0324
c                 F.P.Griffin (fts)-626-4684 or (615)-576-4684
c
c******************************************************************
c
c
c     argument definition:
c        i     = axial node
c        mfesol= mass of solid iron available for reaction with zr, lbm
c        dhsszr= heat of reaction (or mixing) for this time step, btus
c
      implicit none
c
      real           delhmx, dhfeni, dhfezr, dhnizr, dhsszr,
     *                dt   , fwfe   , fwni   , fwzr   , x     , xfe   ,
     *                xni   , xzr   ,    wtotmx, wconss, wcongt,
     *                dwcongt,amzrrx
c
      integer        i
c
c *** statement functions:
c
c     The equations for the enthalpies of mixing of the binary
c     pairs (Fe-Ni,Fe-Zr,Ni-Zr) were taken from an article entitled
c     'Thermodynamic Investigation on Liquid Iron-Nickel-Zirconium
c      Alloys' by Wang,Luck,and Predel in the 'Journal of Phase
c      Equilibria',14,48-53,1993.
c     This paper also suggested the use of Hillert's method ('Calphad',
c     4,1,1980) to calculate the enthalpy of mixing of Fe-Ni-Zr from
c     the binary data. It is believed that the ternary system of
c     Fe-Ni-Zr adequately represents the Zircaloy-stainless steel
c     interaction products (in regards to the calculation of the
c     heat of reaction). This is based on Hofmann's (KfK 4670)
c     posttest analyses of his separate-effects experiments in which
c     the integrated composition of the interaction products were
c     83% Zr, 12% Fe, 3% Ni, 2% Cr (% by weight) whereas the composition
c     of the stainless steel is nominally 74% Fe, 18% Cr, and 8% Ni.
c     The ammount of Zr (83%) in the interaction compounds is
c     approximately the eutectic composition for all the binaries
c     of Zr with Fe, Ni, and Cr; but it appears from the chemical
c     analyses that Cr is less a participant than Fe and Ni.
c
c     Fe-Ni heat of mixing (x=xni): kj/gmole
      dhfeni(x) = x*(1.0-x)*(-7.09-23.26*x)
c
c     Fe-Zr heat of mixing (x=xzr): kj/gmole
      dhfezr(x) = x*(1.0-x)*(-89.663-92.411*x)
c
c     Ni-Zr heat of mixing (x=xzr): kj/gmole
      dhnizr(x) = x*(1.0-x)*(-285.467+255.114*x)
c
c    Average mixing of Zr and SS is composed of 83% of Zr, 12% of Fe, 3% of
c    Ni and 2% of Cr. Referenced subroutine sszrrx.ff computed with
c    5% of Ni and 0% of Cr so that it was  supposed the proportion of Ni and Fe
c    can be conserved
c    Reacted thickness is 
      wtotmx=wconss+wcongt
      fwzr=wcongt/wtotmx
      fwfe=wconss*12.0/17.0/wtotmx
      fwni=1.0-fwzr-fwfe
      xzr=(fwzr/91.22)/(fwzr/91.22+fwfe/55.847+fwni/58.71)
      xfe=(fwfe/55.847)/(fwzr/91.22+fwfe/55.847+fwni/58.71)
      xni=1.0-xzr-xfe      
c
c     calculate enthalpy of mixing of Fe-Ni-Zr (by Hillert's method)
c     (kj/gmole)
c
      if(xzr.ge.0.9999.or.(xfe.ge.0.9999.or.xni.ge.0.9999))then
         delhmx=0.0
      else
         delhmx=-1.0*(4.0*xfe*xni/((2.0*xfe+xzr)*(2.0*xni+xzr))*
     *          dhfeni(1.0-xfe-xzr/2.0)+xni/(1.0-xzr)*dhnizr(xzr)+
     *          xfe/(1.0-xzr)*dhfezr(xzr))
      endif
c
c     reaction of stainless steel and zircaloy
c     (P.Hofmann,et.al.,'Results of Separate-Effects Tests,'
c     International CORA Workshop 1990,KfK,Karlsruhe,FRG,Oct.1,1990;
c     also, P.Hofmann and M.Markiewicz,'Chemical Behavior of
c     (Ag,In,Cd) Absorber Rods in Severe LWR Accidents,'
c     KfK 4670, CNEA NT-16/89, August 1990)
c
c
c     mass of Zircaloy reacted  (gmoles)
c
               amzrrx=dwcongt/91.22
c
c     heat of mixing : J
c
               dhsszr=amzrrx*delhmx*1000.0
c

c
      return
      end
*endif
