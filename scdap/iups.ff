*mlist
*if def,selap
       subroutine iups
c
c  this subroutine performs upper plenum structure initialization
c  of the volume sequence variables ntuvol, volusq, ntsur, and icups
c  (perform once after all ups are defined)
c
c  Cognizant engineer: ljs (fpg).
c
       implicit none
c
*call scddat
*call upccon
*call ufiles
c
c  local variables (must be defined every call)
c
c  is      index that represents current upper plenum structure number
c  iax     index that represents current upper plenum axial level number
c  ivol    index that represents current upper plenum volume sequence
c          number
       integer is, iax
       integer ivol
c
       ntuvol=0
       if(ntups.ge.1) then
         do 10 is=1,ntups
           do 20 iax=1,ntupax(is)
c  loop over upper plenum structures and axial levels
             if(volupl(iax,is).gt.0) then
c  volumes adjacent to left surface nodes
               if(ntuvol.eq.0) then
                 ntuvol=1
                 volusq(ntuvol)=volupl(iax,is)
                 ntsur(ntuvol)=1
                 icups(ntsur(ntuvol),ntuvol)=1000*is+iax
               else
                 do 30 ivol=1,ntuvol
                   if(volupl(iax,is).eq.volusq(ivol)) then
                     ntsur(ivol)=ntsur(ivol)+1
                     icups(ntsur(ivol),ivol)=1000*is+iax
                     goto 40
                   endif
   30            continue
                 ntuvol=ntuvol+1
                 volusq(ntuvol)=volupl(iax,is)
                 ntsur(ntuvol)=1
                 icups(ntsur(ntuvol),ntuvol)=1000*is+iax
   40            continue
               endif
             endif
             if(volupr(iax,is).gt.0) then
c  volumes adjacent to right surface nodes
               if(ntuvol.eq.0) then
                 ntuvol=1
                 volusq(ntuvol)=volupr(iax,is)
                 ntsur(ntuvol)=1
                 icups(ntsur(ntuvol),ntuvol)=-1000*is-iax
               else
                 do 50 ivol=1,ntuvol
                   if(volupr(iax,is).eq.volusq(ivol)) then
                     ntsur(ivol)=ntsur(ivol)+1
                     icups(ntsur(ivol),ivol)=-1000*is-iax
                     goto 60
                   endif
   50            continue
                 ntuvol=ntuvol+1
                 volusq(ntuvol)=volupr(iax,is)
                 ntsur(ntuvol)=1
                 icups(ntsur(ntuvol),ntuvol)=-1000*is-iax
   60            continue
               endif
             endif
   20      continue
   10    continue
c  print hydrodynamic connection information
         write(output,200)
  200    format(/' Edit of upper plenum structure hydraulic',
     &   ' connections after input processing'/
     &   ' left (+) and right (-) surfaces =',
     &   '   1000 * structure + axial level')
         do 100 ivol=1,ntuvol
           write(output,210) volusq(ivol),
     &     (icups(is,ivol),is=1,ntsur(ivol))
  210      format(' volume =',i10,',    surfaces =',6i7)
  100    continue
         write(output,300)
  300    format(' ')
       endif
       return
       end
*endif
