*mlist
*if def,selap
      subroutine zrmwsl(mw1l,mw2l,tgasi,tgaso,xslab,dtm,ws,dz,dx,qmwtr,
     *                   xoold,woold,xazold,xonew,wonew,xaznew,xbeta,
     *                   xzrrx,delws,hsb,hsr,qslab,tslabo,bi,bo,bx)
c
c
c  zrmwsl is the zircaloy-steam interaction driver for a slab geometry.
c
c  Cognizant engineer: ewc (ljo fpg).
c
c******************************************************************
c
c     zrmwsl             1-jan-92
c
c     the BWR canister/control blade modules were developed at ORNL
c     by L.J.Ott under the auspices of the NRC sponsored Boiling
c     Water Reactor Severe Accident Technologies programs.
c
c     for information or assistance, call
c                 L.J.Ott (fts)-624-0324 or (615)-574-0324
c                 F.P.Griffin (fts)-626-4684 or (615)-576-4684
c
c******************************************************************
c
      implicit none
c
*call scddat
*call cboilx
      real            ai    , ao    , ax    , bi    , bo    , bx    ,
     *                bxm   , delwn , delwo , delws , delwsb,dlwsb2,
     *                delxaz, delxn , delxo , delxsb, dtm   , dts   ,
     *                dtst  , dtsub , dx    , dz    , f     , frod  ,
     *                hsb   , hsr   , p1    , pqkr  , qkr   , qmwtr ,
     *                qslab , rhozr , tgasi , tgaso , tk    ,
     *                tslab , tslabo, wgaini, wgaino, wo    , womax ,
     *                wonew , woold , ws    , xaz   , xazi  , xaznew,
     *                xazo  , xazold, xbeta , xo    , xonew , xoold ,
     *                xoxi  , xoxo  , xslab , xslabm, xzrrx
c
      integer         i     , jb    , loop  , mw1l  , mw2l
c
c********************************************************************
c********************************************************************
c*                                                                  *
c*          *****               zrmwsl                *****         *
c*          *****  Zircaloy-steam interaction driver  *****         *
c*          *****         for slab geometry           *****         *
c*                                                                  *
c********************************************************************
c********************************************************************
c
c               parameters needed in model equations
c     ----------------------------------------------------------
c
c symbol i/o  units     description
c ------ ---  -----     ----------------------------------------------
c
c delws   o   lb/min    steam reacted in this timestep
c dtm     i    min      current time step
c dx      i    ft       azimuthal distance
c dz      i    ft       node height
c hsb     i   btu/lb    enthalpy of steam at tgas
c hsr     i   btu/lb    enthalpy of steam at tclad
c mw1l    i             low temperature oxidation correlation to be used
c mw2l    i             mid-temperature oxidation correlation to be used
c qmwtr   o   btu/ft/m  heat generation from Zr/steam reaction
c tgasi and
c tgaso   i    F        gas phase temperatures
c xaznew  o    cm       alpha-Zr-O thickness at end of timestep
c xazold  i    cm       alpha-Zr-O thickness at begining of timestep
c xbeta   o    cm       thickness of beta-Zr at end of timestep
c xonew   o    cm       ZrO2 thickness at end of timestep
c xoold   i    cm       ZrO2 thickness at begining of timestep
c xslab   i    cm       original slab thickness (no oxidation)
c xzrrx   o    cm       thickness of Zircaloy reacted this timestep
c wonew   o   gm/cm**2  Zircaloy weight gain at end of timestep
c woold   i   gm/cm**2  Zircaloy weight gain at begining of timestep
c ws      i   lb/min    steam available for reaction
c
c         the following variables are used in the implicit solution
c         of the transient heat conduction problem for the slab
c
c qslab   i btu/hr*ft   heat input/loss for the slab
c tslabo  i    F        last timestep temperature for the slab
c bi,bo,bx
c         i btu/hr*ft*F transfer and 'known'matrix elements
c
c********************************************************************
c
c
      parameter  (p1=16.0/18.016*0.4536/0.3048**2)
      parameter  (pqkr=1000.0/32.0*140000.0/252.0*0.3048**2)
c
      rhozr = rhocld * 16.01846
c
      ai=bi
      ao=bo
      bxm=bx*(dtm/60.0)
      dts=dtm*60.0
      tslab=tslabo
      tslab=min(tslab,tmelbx)
      if(tslab.le.1200.0)then
         qmwtr=0.0
         xonew=xoold
         xaznew=xazold
         xbeta=max(0.0,xslab-2.0/3.0*xonew-xaznew)
         xzrrx=0.0
         delws=0.0
         return
      end if
c
c     maximum oxygen uptake in this time step (kg/m*m), given the
c     available steam at the Zircaloy surface (WS)
c
      womax=ws*dtm*p1/(dz*dx)
c
c     initial (ie, end of last timestep) thicknesses (m)
c
      xo=xoold/100.0
      xaz=xazold/100.0
      xslabm=xslab/100.0
c
c     initial Zircaloy weight gain (kg/m*m)
c
      wo=woold*10.0
c
c
c*******************************************************************
c
c     the timestep will be subdivided into smaller timesteps, and
c     the slab temperature will be calculated for the subtimesteps
c     so that the increased slab temperature can be accounted for
c     due to the oxidation.
c
c*******************************************************************
c
      loop=4
      dtsub=dts/float(loop)
c
      xoxo=xo
      xazo=xaz
      wgaino=wo
      delxo=0.0
      delxaz=0.0
      delwo=0.0
      jb=0
c
      frod=(7000.0-(hsr-hsb))/7000.0
c
      do 200 i=1,loop
         dtst=dtsub*float(i)
c
c     calculate solid state oxidation rate
c
         tk=(tslab+459.67)/1.8
c
         call zoxdat(mw1l,mw2l,tk,dtsub,xoxo,xazo,wgaino,xoxi,xazi,
     *                wgaini)
         delxsb=xoxi-xoxo
         delwsb=wgaini-wgaino
         delxaz=xazi-xazo
         if(delwsb.eq.0.0.or.delxsb.eq.0.0)then
            delwsb=0.0
            delxsb=0.0
            delxaz=0.0
            go to 198
         end if
c
c     check for physical limitations (ie, xslabm or womax or xbeta)
c
         xzrrx=2.0/3.0*(delxo+delxsb+xo)
c     (oxide layer .ge. slab thickness ?)
         if(xzrrx.ge.xslabm)then
            jb=1
            delxn=max(0.0,xslabm-2.0/3.0*(delxo+xo))
            f=delxn/delxsb
            delxsb=delxn
            delwsb=f*delwsb
            delxaz=0.0
         end if
c     (oxygen uptake .ge. oxygen available ?)
         if(delwo+delwsb.ge.womax)then
            jb=1
            delwn=max(0.0,womax-delwo)
            f=delwn/delwsb
            delwsb=delwn
            delxsb=f*delxsb
            delxaz=0.0
         end if
c     (beta zircaloy depleted: oxide growth and alpha decrease)
         xzrrx=2.0/3.0*(delxo+delxsb+xo)
         if(xzrrx+xazo+delxaz.gt.xslabm)then
            delxaz=xslabm-xzrrx-xazo
            dlwsb2=2.0/3.0*delxsb*rhozr*32.0/91.22
            delwsb=min(delwsb,dlwsb2)
         end if
c
c     update variables
c
  198    xoxi=xoxo+delxsb
         wgaini=wgaino+delwsb
         xazi=max(0.0,xazo+delxaz)
         xbeta=max(0.0,xslabm-2.0/3.0*xoxi-xazi)
         delxo=xoxi-xo
         delwo=wgaini-wo
         wgaino=wgaini
         xoxo=xoxi
         xazo=xazi
c
c     heat generation from oxygen uptake
c     (units of btu/ft/hr for QKR)
c
         qkr  =pqkr*delwo*dz*dx*frod/dz/(dtst/3600.0)
c
c
         ax=bxm/(dtst/3600.0)
         tslab=(ax*tslabo+qslab+qkr+ai*tgasi+ao*tgaso)/(ai+ao+ax)
c
         if(jb.eq.1)go to 208
         if(tslab.gt.5000.0.or.tslab.le.0.0)then
            write(6,207)
         end if
  200 continue
  207 format(5x,'warning--zrmwsl--calculated cladding temperature ',
     *       'out of range.')
  208 continue
c
c     set-up output variables
c
      qmwtr=pqkr*delwo*dz*dx*frod/dz/dtm
      xonew=xoxo*100.0
      xaznew=xazo*100.0
      xbeta=xbeta*100.0
      xzrrx=delxo*100.0*2.0/3.0
      delws=delwo*dz*dx/dtm/p1
      wonew=wgaino/10.0
c
      return
      end
*endif
