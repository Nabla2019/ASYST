*mlist
*if def,selap
       subroutine uoxd( flstm0,   h2oxd,   deltc,   slbwd,  dzcond,
     & tfuel,uthick,uwg,uwg0,h2uox,quox,sruox,wgmax,noss)
c
c  uoxd calculates metallic uranium oxidation based on the data and
c  analyses of R.E. Wilson et al, "isothermal reaction of uranium with
c  steam
c
c  Cognizant engineer: ewc (stp).
c
c     coded by s.t. polkinghorne july 1987
c
c     variables (i=input, o=output, l=local)
c     i   deltc   = time step (s)
c     i   dzcond  = axial node height (m)
c     l   flstm   = steam flow rate less zr oxidation steam
c                   removal rate (kg/s)
c     i   flstm0  = steam flow rate from previous time step (kg/s)
c     i   h2oxd   = hydrogen generation rate due to zr
c                   oxidation (kg/s)
c     o   h2uox   = hydrogen generation rate due to uranium
c                   oxidation (kg/s)
c     i   noss    = no steam starvation when noss = 1
c     o   quox    = uranium oxidation heat generation (w/m)
c     i   slbwd   = fuel element width (m)
c     o   sruox   = steam removal rate due to uranium
c                   oxidation (kg/s)
c     i   tfuel   = uranium temperature (k)
c     l   uden    = uranium density (kg/m3)
c     l   uheat   = heat of reaction (j/kg of uranium reacted)
c     i   uthick  = uranium fuel thickness (m)
c     o   uwg     = oxygen weight gain in reacted uranium (kg/m2)
c     i   uwg0    = weight gain from previous time step (kg/m2)
c     l   wglim   = maximum rate oxygen can be reacted (kg/m2*s)
c     o   wgmax   = maximum oxygen weight gain in reacted
c                   uranium (kg/m2)
c
       implicit none
c
       integer noss
       real flstm0,h2oxd,deltc,slbwd,dzcond,tfuel,uthick,uwg,uwg0,h2uox,
     * quox,sruox,wgmax
c
*call ufiles
c
c  Local variables.
       real flstm,oxcon,wglim
       real uoxwtk
       external uoxwtk
       real uden,uheat,eps
c
c  Data statements.
      data uden/19050./, uheat/2.496e6/, eps/1.0e-8/
c
c     compute maximum weight gain (kg/m2).  assume 1/2 the
c     fuel thickness can oxidize.
      wgmax = (32./238.)*uden*uthick / 2.0
c     compute new steam flow rate at axial node i (old steam flow
c     rate - zr steam consumption rate)
      flstm = abs(flstm0) - 9.0*h2oxd
c     return if no steam flow
      if(flstm .lt. eps .and. noss .ne. 1)go to 100
c     return if all uranium has oxidized
      if(abs(uwg0-wgmax) .lt. eps)go to 100
c     compute weight gain rate limit (kg/m2*s).  assume
c     uranium surface area equals clad surface area.
      wglim = abs((8./9.)*flstm) / (slbwd * dzcond)
      if(noss .eq. 1)wglim = 1.0e6
c     compute new weight gain (kg/m2)
      oxcon = uoxwtk(tfuel)
      uwg = sqrt(uwg0**2 + oxcon*deltc)
c  Compare weight gain to weight gain limit.
      if((uwg-uwg0) .gt. (wglim*deltc))then
         uwg = wglim*deltc + uwg0
      endif
c  Compare weight gain to maximum weight gain.
      if(uwg .gt. wgmax)then
         uwg = wgmax
      endif
c  Compute hydrogen generation, heat generation, and steam removal rate.
      h2uox = (uwg-uwg0)*slbwd*dzcond / (8.0*deltc)
      quox  = (238./32.)*uheat*(uwg-uwg0)*slbwd / deltc
      sruox = h2uox*9.0
c
c     write(output,10)deltc,dzcond,flstm,flstm0,h2oxd
c  10 format(5x,"in uoxd:  deltc,dzcond,flstm,flstm0,h2oxd = ",
c    + 1p,e11.4,2x,e11.4,2x,e11.4,2x,e11.4,2x,e11.4)
c     write(output,20)h2uox,quox,slbwd,sruox,tfuel
c  20 format(5x,"          h2uox,quox,slbwd,sruox,tfuel    = ",
c    + 1p,e11.4,2x,e11.4,2x,e11.4,2x,e11.4,2x,e11.4)
c     write(output,30)uthick,uwg,uwg0,wglim,wgmax
c  30 format(5x,"          uthick,uwg,uwg0,wglim,wgmax     = ",
c    + 1p,e11.4,2x,e11.4,2x,e11.4,2x,e11.4,2x,e11.4)
      return
  100 continue
      h2uox = 0.0
      quox  = 0.0
      sruox = 0.0
      return
      end
*endif
