*mlist
*if def,selap
      subroutine bkmlt2(i,k,j,kc,frac,wsscbo)
c
c
c     bkmlt2 performs structural melting/freezing calculations for
c     an interstitial blockage (specifically the WSSCBB array).
c
c  Cognizant engineer: ewc (ljo fpg).
c
c******************************************************************
c
c     bkmlt2             1-April-96
c
c     the BWR canister/control blade modules were developed at ORNL
c     by L.J.Ott under the auspices of the NRC sponsored Boiling
c     Water Reactor Severe Accident Technologies programs.
c
c     for information or assistance, call
c                 L.J.Ott (fts)-624-0324 or (615)-574-0324
c                 F.P.Griffin (fts)-626-4684 or (615)-576-4684
c
c******************************************************************
c
      implicit none
c
      real            cpsmel, scp   , delt  , dumelx, dusscb, frac  ,
     *                fss(11)       , fwssoo, sum(4), sumsox, sumss3,
     *                sumss4, tnew  , w1t2  , w2t1  , woxtm ,
     *                wsscbo(11,2) , wssi
c
      integer         i     , j     , k     , kc    ,
     *                l     , m
c
c-----common blocks
c
*call scddat
*call blinit
*call blkage
*call cboilx
*call cr2
c
c  use conversions of k = (f + 459.67)/1.8
c  and btu/(lbm*f) = j/(kg*k) / 4186.8
         cpsmel=scp((tmelcb+459.67)/1.8)/4186.8
         fwssoo=(1.0/(1.0-fwssss)-1.0)
c
      sum(3)=0.0
      sum(4)=0.0
      do 10 m=1,2
         do 10 l=1,11
   10       sum(m+2)=sum(m+2)+wsscbo(l,m)
      sumss4=wsscbo(1,2)+wsscbo(2,2)+wsscbo(3,2)
      sumss3=wsscbo(1,1)+wsscbo(2,1)+wsscbo(3,1)
      if(abs(sum(3)+sum(4)).ge.1.0e-20)then
         delt=tblk(i,k,j,kc)-tmelcb
         if(delt.gt.0.0)then
            dusscb=(sum(3)+sum(4))*cpsmel*delt*frac
            dumelx=sumss4*hfssb*frac
            if(sum(4).lt.1.0e-15)then
               do 15 l=1,11
                  wsscbb(i,j,l,1)=wsscbb(i,j,l,1)+wsscbb(i,j,l,2)
   15             wsscbb(i,j,l,2)=0.0
               tblk(i,k,j,kc)=tmelcb
            else if(dusscb.ge.dumelx.or.sumss4.lt.1.0e-15)then
               do 20 l=1,11
                  wssi=min(frac*wsscbo(l,2),wsscbb(i,j,l,2))
                  wsscbb(i,j,l,1)=wsscbb(i,j,l,1)+wssi
   20             wsscbb(i,j,l,2)=max(wsscbb(i,j,l,2)-wssi,0.0)
               dusscb=dusscb-dumelx
               if(sum(3)+sum(4).ge.1.0e-20)then
                  delt=dusscb/((sum(3)+sum(4))*cpsmel*frac)
               else
                  delt=0.0
               end if
               tnew=max(min(tmelcb+delt,tblk(i,k,j,kc)),tmelcb)
               tblk(i,k,j,kc)=tnew
            else
               w2t1=dusscb/hfssb
               do 30 l=1,3
                  fss(l)=wsscbo(l,2)/sumss4
                  wssi=
     *              min(fss(l)*w2t1,wsscbo(l,2)*frac,wsscbb(i,j,l,2))
                  wsscbb(i,j,l,1)=wsscbb(i,j,l,1)+wssi
                  wsscbb(i,j,l,2)=max(wsscbb(i,j,l,2)-wssi,0.0)
   30             continue
               woxtm=fwssoo*w2t1
               sumsox=(sum(4)-sumss4)*frac
               if(sumsox.gt.0.0)then
                  if(woxtm.ge.sumsox)then
                     do 40 l=4,11
                        wssi=min(frac*wsscbo(l,2),wsscbb(i,j,l,2))
                        wsscbb(i,j,l,1)=wsscbb(i,j,l,1)+wssi
   40                   wsscbb(i,j,l,2)=max(wsscbb(i,j,l,2)-wssi,0.0)
                  else
                     do 50 l=4,11
                        fss(l)=wsscbo(l,2)*frac/sumsox
                        wssi=
     *             min(fss(l)*woxtm,wsscbb(i,j,l,2),wsscbo(l,2)*frac)
                        wsscbb(i,j,l,1)=wsscbb(i,j,l,1)+wssi
                        wsscbb(i,j,l,2)=max(wsscbb(i,j,l,2)-wssi,0.0)
   50                   continue
                  end if
               end if
               tblk(i,k,j,kc)=tmelcb
            end if
         else
            delt=-delt
            dusscb=(sum(3)+sum(4))*cpsmel*delt*frac
            dumelx=sumss3*hfssb*frac
            if(abs(sum(3)).lt.1.0e-20)then
            else if(dusscb.ge.dumelx.or.sumss3.lt.1.0e-15)then
               do 60 l=1,11
                  wssi=min(frac*wsscbo(l,1),wsscbb(i,j,l,1))
                  wsscbb(i,j,l,2)=wsscbb(i,j,l,2)+wssi
   60             wsscbb(i,j,l,1)=max(wsscbb(i,j,l,1)-wssi,0.0)
               dusscb=dusscb-dumelx
               if(sum(3)+sum(4).ge.1.0e-20)then
                  delt=dusscb/((sum(3)+sum(4))*cpsmel*frac)
               else
                  delt=0.0
               end if
               tnew=min(max(tmelcb-delt,tblk(i,k,j,kc)),tmelcb)
               tblk(i,k,j,kc)=tnew
            else
               w1t2=dusscb/hfssb
               do 70 l=1,3
                  fss(l)=wsscbo(l,1)/sumss3
                  wssi=
     *              min(fss(l)*w1t2,wsscbb(i,j,l,1),wsscbo(l,1)*frac)
                  wsscbb(i,j,l,2)=wsscbb(i,j,l,2)+wssi
                  wsscbb(i,j,l,1)=max(wsscbb(i,j,l,1)-wssi,0.0)
   70             continue
               woxtm=fwssoo*w1t2
               sumsox=(sum(3)-sumss3)*frac
               if(sumsox.gt.0.0)then
                  if(woxtm.ge.sumsox)then
                     do 80 l=4,11
                        wssi=min(frac*wsscbo(l,1),wsscbb(i,j,l,1))
                        wsscbb(i,j,l,2)=wsscbb(i,j,l,2)+wssi
   80                   wsscbb(i,j,l,1)=max(wsscbb(i,j,l,1)-wssi,0.0)
                  else
                     do 90 l=4,11
                        fss(l)=wsscbo(l,1)*frac/sumsox
                        wssi=
     *             min(fss(l)*woxtm,wsscbb(i,j,l,1),wsscbo(l,1)*frac)
                        wsscbb(i,j,l,2)=wsscbb(i,j,l,2)+wssi
                        wsscbb(i,j,l,1)=max(wsscbb(i,j,l,1)-wssi,0.0)
   90                   continue
                  end if
               end if
               tblk(i,k,j,kc)=tmelcb
            end if
         end if
      end if
      if(kc.eq.1)tcb1(i,j)=tblk(i,k,j,kc)
      if(kc.eq.2)tcb2(i,j)=tblk(i,k,j,kc)
      if(kc.eq.3)tcb3(i,j)=tblk(i,k,j,kc)
      if(kc.eq.4)twscbb(i,k,j)=tblk(i,k,j,kc)
c
      return
      end
*endif
