*mlist
*if def,selap
       function actdkp (pmax,optime,dktime,actnid)
c
c
c  actdkp calculates actinide decay power.
c
c  Cognizant engineer: ewc.
c
c     calling subroutines:  nheati
c     engineer/programmer:  e.r.carlson/g.h.beers
c
c     input variables                  description
c        actnid         u239 production per fission
c        dktime         time since shut down or time since start of
c                         decay (s)
c        optime         operating life time of the fuel rod (s)
c        pmax           maximum power over the life time of the fuel rod
c                         (w/m3)
c
c     output variables                 description
c        actdkp         actinide decay power (w/m3)
c
c ----------------------------actdkp outline--------------------------
c     set data
c     determine current u239 decay energy per fission
c     determine current np239 decay energy per fission
c     determine total actinide decay power based on the fission rate
c      at the maximum power over the life time of the fuel rod
c     return
c --------------------------------------------------------------------
c
       implicit none
c
c  Local variables.
       real actnid,diff,dktime,e1,e2,efptot,exdcut,exdudk,fu239,fnp239,
     * optime,pmax,y1,y2
       real actdkp
      data  e1 /.474/  ,e2 /.419/  ,y1 /4.91e-04/  ,y2 /3.41e-06/
     #     ,efptot/194.02/
c
      exdcut = exp( - y1 * optime )
      exdudk = exp( - y1 * dktime )
      diff   = y1 - y2
      fu239  = e1 * actnid * ( 1.0 - exdcut ) * exdudk
      fnp239 = e2 * actnid * ( y1 / diff
     +              * (1.- exp(-y2*optime)) * exp(-y2*dktime)
     +              - y2 / diff * ( 1.0 - exdcut ) * exdudk )
      actdkp = (pmax / efptot) * ( fu239 + fnp239 )
      return
      end
*endif
