*mlist
*if def,selap
       subroutine cour5q(ncevr5, ir5vec, qcoupl, qwgcou, gamcou,
     & icvnod, inv)
c
c  cour5q puts into the relap5 energy terms the heat transported from th
c  surface nodes of the couple finite element mesh to the coolant interf
c  with the rods.
c
c  Cognizant engineer: ewc.
c
c  This subroutine is called by tran.
c
c  This is a COUPLE ROUTINE
*in32 ir5vec
*in32 icvnod
*in32end
c
c  ncevr5 = number of nodes in couple finite element mesh at which
c           convection can occur.
c  ir5vec(n) = offset index for relap5 volume that interfaces with n-t
c              node in this set.
c  qcoupl(n) = total heat input into volume with offset index ir5vec(n
c              (w).
c  qwgcou(n) = heat input to vapor phase (w).
c  gamcou(n) = volumetric vapor generation rate due to heat transfer
c              from couple node (kg/m**3.s)
c  icvnod(n) = COUPLE mesh node number corresponding with n-th
c              convective node.
c  inv       = offset index for RELAP5 control volume representing the
c              lower plenum.
c
       implicit none
c
c  Local variables.
       integer m,n,ncevr5,mlopln,inv
       integer ir5vec(*), icvnod(*)
       real qcoupl(*), qwgcou(*), gamcou(*)
       include 'fast.h'
       include 'comctl.h'
       include 'voldat.h'
       include 'scddat.h'
       include 'debcom.h'
       include 'contrl.h'
       include 'ufiles.h'

       if (ncevr5 .le. 0) return
       do n = 1,ncevr5
         if (ir5vec(n) .lt. 0) cycle
         m = ir5vec(n) + filndx(4)
         mlopln = inv + filndx(4)
c  If lower plenum volume is filled with debris, then stop heat transfer
c  to this volume.
c  Note. The volume size below which heat is not added to lower
c  plenum volume should be consistent with that used in subroutine TRAN.
         if (m.eq.mlopln .and. v(m)/vollh0.lt.0.05001) cycle
         q(m) = q(m) + qcoupl(n)
         qwg(m) = qwg(m) + qwgcou(n)
         gammaw(m) = gammaw(m) + gamcou(n)
       enddo
       return
       end
*endif
