*mlist
*if def,selap
      subroutine aloxd ( alwgsw, oxdw0, alwlim, stmlim, deltc, tempt,
     +           slbwd, dzcond, dtoxwg, oxdw, oxdh, h2oxd, sroxd )
c
c
c   Called  by atrsso to compute aluminum oxidation for atr SCDAP
c   component
c
c   Called  by oxdebr to compute aluminum oxide layer for couple
c   debris bed if it contains aluminum or aluminum/uranium material
c
c  Cognizant engineer: ewc.
c
c argument list---
c   input : oxdw0--al oxidized previously (kg/m2)
c           alwlim--al available for oxidation (kg/m2)
c           stmlim--steam availble for oxidation (kg/m2)
c           deltc --timestep (sec)
c           tempt --temperature (k)
c           dzcond--nodal length (m)
c           slbwd --nodal width (m)
c   output :oxdw  --al oxidized at the end of cuurent step (kg/m2)
c           oxdh  --oxidation energy (w/m)
c           h2oxd --hydrogen generation rate (kg/sec)
c           sroxd --steam removal rate (kg/sec)
c           dtoxwg--al oxidized for cuurent timestep (kg/m2)
c
       implicit none
c
c  Local variables.
       integer idebug
       real aloxwg,alwgsw,alwlim,area,deltc,dtoxwg,dzcond,h2oxd,oxdh,
     * oxdw,oxdw0,slbwd,sroxd,stmlim,tempt
c
      idebug = 1
      area = dzcond*slbwd
      aloxwg = deltc*exp(-3.699e4/tempt)
      if (alwgsw .le. 2.3e-02) then
         oxdw = (oxdw0**3 + 6.6667e1*aloxwg)**0.333333
      else
         oxdw = oxdw0 + 4.1667e4*aloxwg
      endif
c --- oxidation limits
      dtoxwg = oxdw - oxdw0
      dtoxwg = min(alwlim,stmlim,dtoxwg)
      oxdw = oxdw0 + dtoxwg
      oxdh = dtoxwg*4.1447e6*slbwd/deltc
      h2oxd = dtoxwg*0.1111*area/deltc
      sroxd = dtoxwg*area/deltc
      return
      end
*endif
