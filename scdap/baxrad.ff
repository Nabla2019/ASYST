*mlist
*if def,selap
      subroutine baxrad(i,j)
c
c
c  baxrad determines the heat radiated radially from
c  structures-to-structures within the interstitial region
c  (ie, the canister and control blade) of the BWR core.
c
c  (all structural radiation calc. assume steam to be an absorbing
c   /transmitting medium)
c
c  Cognizant engineer: ewc.
c
c******************************************************************
c     baxrad              1-jan-92
c
c     the BWR canister/control blade modules were developed at ORNL
c     by L.J.Ott under the auspices of the NRC sponsored Boiling
c     Water Reactor Severe Accident Technologies programs.
c
c     for information or assistance, call
c                 L.J.Ott (fts)-624-0324 or (615)-574-0324
c                 F.P.Griffin (fts)-626-4684 or (615)-576-4684
c******************************************************************
c
      implicit none
c
      real            qrlcjc, rrlcjc
c
      integer         i     , j     , k
c
c-----commons
c
c
*call scddat
*call axtrns
*call blinit
*call cboilx
c
c
c ************** interstitial side (ie control blade side) *************
c
c
c-***-control blade
c
      qradrc=0.0
      if(scb(i,j).eq.0.0)go to 20
c
c-----from the canister to control blade
c
      do 10 k=1,2
         if(iblkcb(i,k,j).eq.2)go to 10
         if(scn(i,k,j).eq.0.0)go to 10
         if(fccbr(k,j).eq.0.0)go to 10
         rrlcjc=cbl(j)/boxl(k,j)*
     1          (1.0-emisbo(i,k,j))/emisbo(i,k,j)+
     2           1.0/(boxl(k,j)*fccbr(k,j)/cbl(j))/(1.0-emisvo(i,k,j))+
     3          (1.0-emiscb(i,j))/emiscb(i,j)
         qrlcjc=0.1713*cbl(j)*hdz(i,j)/rrlcjc*
     1          ((tboxo(i,k,j)/100.0+4.6)**4-
     2          (tcb3o(i,j)/100.0+4.6)**4)
         qradrc=qradrc+qrlcjc
   10    continue
c
c-***-canister
c
   20 do 30 k=1,2
         qradrb(k)=0.0
         if(iblkcb(i,k,j).eq.2)go to 30
         if(scn(i,k,j).eq.0.0)go to 30
c
c-----from the control blade to canister k
c
         if(scb(i,j).eq.0.0)go to 30
         if(fccbr(k,j).eq.0.0)go to 30
         rrlcjc=boxl(k,j)/cbl(j)*
     1          (1.0-emiscb(i,j))/emiscb(i,j)+
     2           1.0/fccbr(k,j)/(1.0-emisvo(i,k,j))+
     3          (1.0-emisbo(i,k,j))/emisbo(i,k,j)
         qrlcjc=0.1713*boxl(k,j)*hdz(i,j)/rrlcjc*
     1          ((tcb3o(i,j)/100.0+4.6)**4-
     2          (tboxo(i,k,j)/100.0+4.6)**4)
         qradrb(k)=qradrb(k)+qrlcjc
   30    continue
c
c
      return
      end
*endif
