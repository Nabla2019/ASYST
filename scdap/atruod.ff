*mlist
*if def,selap
      subroutine atruod ( uwg0, uwlim, stmlim, deltc, tempt, slbwd,
     +                     dzcond, dtuox, uwg, quox, h2uox, sruox )
c
c  Descriptive sentence for atruod goes here.
c
c  Cognizant engineer: ewc.
c
c  argument list :
c  input--
c    uwg0 - previous u oxidation weigh gain (kg/m2)
c    uwlim - u available for current step oxidation (kg/m2)
c    stmlim - steam available for current oxidation (kg/m2)
c    deltc - time step (sec)
c    tempt - temperature (k)
c    dzcond - nodal length (m)
c    slbwd - nodal width (m)
c  output--
c    dtuox - u oxidized during current step (kg/m2)
c    uwg - u oxidation weigh gain st end of step (kg/m2)
c    quox - oxidation energy (w/m)
c    h2uox - hydrogen generation rate (kg/sec)
c    sruox - steam removal rate (kg/sec)
c
       implicit none
c
c  Local variables.
       real area,deltc,dtexp,dtuox,dtuwg,dzcond,h2uox,quox,slbwd,sruox,
     * stmlim,tempt,uwg,uwg0,uwlim
c
      dtuox = 0.0
      uwg = uwg0
      h2uox = 0.0
      sruox = 0.0
      quox = 0.0
      if ( tempt.lt.873.0 ) return
      if ( tempt.ge.873.0 .and. tempt.lt.1473.0 ) then
        dtexp = 0.166*deltc*exp(-9.361e3/tempt)
      else
        dtexp = 1.35*deltc*exp(-1.258e4/tempt)
      endif
      area = slbwd*dzcond
      uwg = sqrt(uwg0**2+dtexp)
      dtuwg = uwg - uwg0
      dtuwg = min(uwlim*1.344537e-1,min(stmlim*8.888889e-1,dtuwg))
      uwg = uwg0 + dtuwg
      dtuox = 7.4375*dtuwg
      h2uox = 0.125*dtuwg*area/deltc
      sruox = 1.125*dtuwg*area/deltc
      quox = 1.905e4*dtuwg*slbwd/deltc
c
      return
      end
*endif
