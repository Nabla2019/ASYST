*mlist
*if def,selap
      subroutine tridg1(a,b,t,nd)
c
c
c  tridg1 solves the tridiagonal "a" matrix with the
c  constant "b" vector for the unknown "t" vector
c
c  Cognizant engineer: ljs (fpg).
c
c
c  nd  is the number of equations,
c  "a" is the tridiagonal matrix, a(l,n), l=1,3 are -, 0, +
c      elements for the equation associated with the nth node,
c  "b" is the constant vector in the matrix equation, the solution is
c      returned to the calling program in "t"
c  "z" and "u" are working vectors which key to the original
c      formulation from hirasaki.
c
c  cognizant engineer:  fpg@ornl.gov
c
      implicit none
c
*call scddat
c
c  local variables (must be defined every call)
c
      integer jt, l, m, nd
      real a(3,nd), b(nd), el, t(nd), u(nmupcn), z(nmupcn)
c
      z(1)=b(1)
      u(1)=1.0/a(2,1)
      do 100 l=2,nd
         el  =-a(1,l)*u(l-1)
         z(l)=b(l)+el*z(l-1)
         u(l)=1.0/(a(2,l)+el*a(3,l-1))
  100 continue
      t(nd)  =z(nd)*u(nd)
      jt=nd+1
      do 200 l=2,nd
         m=jt-l
         t(m)=(z(m)-a(3,m)*t(m+1))*u(m)
  200 continue
c
      return
      end
*endif
