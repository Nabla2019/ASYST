*mlist
*if def,selap
       subroutine cpress (ibrch,ictype,naz,ngeom,nrcond,numnod,
     * pcool,tcond,xcond,pgas)
c
c  Descriptive sentence goes here.
c
c  Cognizant engineer: ljs.
c
c
       implicit none
*call scddat
c  Local variables.
      integer ibrch,ictype,naz,ngeom,nrcond,numnod
      real pcool,tcond,xcond,pgas
      dimension tcond(ndrd,ndax), xcond(ndrd,ndax)
      dimension tcavrd(ndax)
      integer n
      real pgax,tcavrd
      real avfunc,pgfunc
      external avfunc,pgfunc
c
c  calculation of general cylindrical component (control rod) internal
c  gas pressure.
c     calling subroutines:  cylin
c     engineer/programmer:  w. sengpiel
c
c     input variables                  description
c        ibrch          indicator of a breach in the cladding  0=no,
c                         1=yes
c        ictype         indicator for procedure of internal gas pressure
c                         calculation
c        naz            number of heat conduction axial zones
c        ngeom          geometry index for heat conduction
c        nrcond         number of radial heat conduction nodes in the
c                         absorber material
c        numnod         number of nodal points in radial direction for
c                         heat conduction solution, at elevation z(i).
c        pcool          coolant pressure  (n/m2)
c        xcond          radial node location from component center  (m)
c
c     output variables                 description
c        pgas           pressure of gas inside control rod (n/m2)
c
c     local variables                  description
c        pgax           internal gas pressure varying with axial
c                         coordinate
c        tcavrd         component temperature ( k ) averaged over nrcond
c                         radial nodes
c
c general cylindrical component internal gas pressure calculation
c internal gas pressure is assumed to be a function of temperature alone
c
      if( ibrch .gt. 0 ) then
c failed cladding
c control rod internal gas pressure is set equal to coolant pressure
        pgas= pcool
      else
c intact cladding
c computation of component (control rod) internal gas pressure
c radial averaging of fuel temperatures for n=1,...naz axial
c        heat conduction zones
        pgas= 0.
        do n=1,naz
          tcavrd(n)= avfunc(ngeom,xcond(1,n),tcond(1,n),nrcond)
          pgax=  pgfunc(ictype,tcavrd(n))
          if( pgax .gt. pgas ) pgas= pgax
        enddo
      endif
      return
      end
*endif
