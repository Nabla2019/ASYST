*mlist
*deck smrcon
*if def,selap
      subroutine smrcon (tin,agr,afc,atgt,agas,fluc,cond)
c
c
c  smrcon computes an area (volume) weighted average of the thermal
c  condutivity for the components of the HTGR core, consisting of
c  graphite, fuel compact, target material, and helium coolant.
c
c  Cognizant engineer: ewc (krk).
c
c This is a COUPLE ROUTINE
c
      implicit none
c
c
c          Component thermal conductivities are obtained from
c          the function routines called, and for helium, the
c          conductivity is from:
c            Reference: correlation taken from ATHENA code
c                       k = 2.639e-03*t**0.7085  (w/m-k)
c                       (1/k)*dt = 1300.0*t**0.2915
c                   or: The Effective Thermal Conductivity of
c                       Prismatic MHTGR Fuel; J.C.Han, et.al.,
c                       MIT, Sept 30,1989
c
c            ***** note ***** note ***** note ***** note *****
c
c          This subroutine, along with associated routines, are
c          preliminary, and for scoping purposes only.  Change
c          the appropriate function when better correlations and
c          improved models are available.
c
c             Input:  graphite c-s area         agr     (m**2)
c                     fuel compact c-s area     afc     (m**2)
c                     target material c-s area  atgt    (m**2)
c                     coolant channel c-s area  agas    (m**2)
c                     fluence                   fluc    (n/cm**2)
c                     element temperature       tin     (k)
c             Output: effective conductivity    cond    (j/m-s-k)
c
c             Called from userp
c
      real     agr, afc, atgt, agas, atot, fluc, tin, cond, grcon,
     #         fcon, tgtcon, cond1, cond2, cond3,  conda
      external sgrcon, sfcon, stgcon
c
c     calculate cond
c
c     for the graphite, fuel compact, and target material, calculate
c     a volume weighted average conductivity (cross-sectional area)
c
      call sgrcon (tin, fluc, grcon, cond1)
      call  sfcon (tin, fcon, cond2)
      call stgcon (tin, tgtcon, cond3)
      atot  = agr + afc + atgt
c
      conda  = (agr*cond1 + afc*cond2 + atgt*cond3)/atot
c
c     now use the relationship developed J.C.Han, et.al., (MIT)
c     reference above to compute the core wide effective conductivity
c     for the HTGR core, which includes radiation and gas coolant
c     channel effects, based on a gas volume ratio of 0.64.
c     (equation 2.11, page 15)
c
      cond = (0.2023 + 2.07e-11*tin**3) * conda
c
      return
      end
*endif
