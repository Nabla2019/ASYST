*mlist
*if def,selap
       subroutine mixent (enth,wuo,wzr,wzro,wag,wss,wsso,wzuo,afru,
     & afrz,wbc,tmpref,tmpeq)
!  mixent calculates the temperature of a mixture given its enthalpy.
!
!  Cognizant engineer: ewc.
!
!        enth   = mass times enthalpy of mixture. (j)  (input)
!        wuo    = mass of uo2 in mixture. (kg)  (input)
!        wzr    = mass of zr in mixture. (kg)  (input)
!        wzro   = mass of zro2 in mixture. (kg)  (input)
!       wag    = mass of ag-in-cd in mixture. (kg)  (input)
!       wss    = mass of stainless steel in mixture. (kg)  (input)
!       wsso   = mass of stainless steel oxide in mixture. (kg) (input)
!       wzuo   = mass of zr-u-o compound in mixture. (kg)  (input)
!       afru   = atomic fraction of u in zr-u-o compound. (input)
!       afrz   = atomic fraction of zr in zr-u-o compound. (input)
!       wbc    = mass of boron carbide in mixture. (kg)  (input)
!       tmpref = reference temperature for enthalpy calculation (K).
!                (input)
!       tmpeq  = temperature of mixture. (k)  (output)
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
!
       include 'scddat.h'
       include 'miscon.h'
       include 'cpmdat.h'
       include 'contrl.h'
       include 'ufiles.h'
!
       real(kr) enth,wuo,wzr,wzro,wag,wss,wsso,wzuo,afru,afrz,wbc,tmpeq
!
!  Local variables.
       integer itmax,iter     
       real(kr) wmixcm(maxcon),atmfr(maxcon),wpotot,enteps,ratent
       real(kr) dltges,dltgs1,tmpref,tmpmax,enthl,etotmx,tmpmix,emixgs
       real(kr) acp,aenthl,fcp,fenthl,scp,senthl,zocp,zonthl,zucp
       real(kr) zunth1
!
!  Data statements.
       data enteps/ 0.00001/
       data itmax/ 10/
!   subroutine crucbr puts upper bound of 3900.0 on molten pool
!   temperature.
       data dltges / 300.0/, tmpmax / 3900./
!
       if(enth.lt.100.0)then
       tmpeq=tmpref
       go to 300
       end if
       wpotot=wuo+wzr+wzro+wag+wss+wsso+wzuo
       wmixcm(1)=wzr
       wmixcm(2)=0.0
       wmixcm(3)=wss
       wmixcm(4)=wag
       wmixcm(5)=0.0
       wmixcm(6)=wuo
       wmixcm(7)=wzro
       wmixcm(8)=0.0
       wmixcm(9)=0.0
       wmixcm(10)=0.0
       wmixcm(11)=0.0
       call calaf(wmixcm,wpotot,atmfr)
       etotmx=enth
       tmpmix=tmpmax  
       iter=0
       dltgs1=dltges
100    continue
       enthl=zunth1(atmfr(1),atmfr(2),atmfr(3),atmfr(4),atmfr(5),
     # atmfr(6),atmfr(7),tmpmix,atmfr(8),atmfr(9),atmfr(10),atmfr(11),
     # tmpref)
       emixgs=wpotot*enthl
       ratent=abs((emixgs-etotmx)/etotmx)
       if(emixgs.lt.etotmx)then
         if(abs((emixgs-etotmx)/etotmx).lt.enteps.or.
     #   iter.gt.itmax)then
         go to 200
         else
         tmpmix=tmpmix+dltgs1
         dltgs1=dltgs1/10.
         iter=iter+1
         go to 100
         end if
       else
         if(tmpmix.le.tmpref)then
         tmpeq=tmpref 
         go to 300
         end if
       tmpmix=tmpmix-dltgs1
       go to 100
       end if
200    continue
       tmpeq=tmpmix-0.5*dltgs1
       if(tmpeq.lt.tmpref)tmpeq=tmpref
300    continue
       return
       end
*endif
