*mlist
*deck sfcon
*if def,selap
      subroutine sfcon (tin, fcon, tk)
c
c
c  sfcon computes the integral of the reciprocal of thermal
c  conductivity for fuel compact.
c
c  Cognizant engineer: ewc.
c
      implicit none
c
*call contrl
*call ufiles
c
c          subroutine fcon computes the integral of the reciprocal
c          of thermal conductivity for fuel compact.  The thermal
c          conductivity is also made available as tk in the
c          argument list.
c                                                  krk, may 1990
c
c               *****  note  *****  note  *****  note  *****
c
c          The following data has been entered as a temporary set
c          for scoping calculations only
c
c          Reference:
c              Thermal Conductivity -
c              HTGR Core Thermal Design Methods And Analysis,
c              A.S.Shenoy, et.al., General Atomic Project 1926,
c              December 31, 1974
c
c          Thermal conductivity if a function of both temperature
c          and shim content. The following approximates the
c          reference data with a shim content of 23% by volume.
c
c                Input : temperature          tin            (k)
c                Output: integral ((1/k)*dT)  fcon  (m-s-k**2/j)
c                        conductivity         tk       (j/m-s-k)
c
c          Called from userp, smrcon
c
c          Cauculations in SI units
c
      real     tin, tk, rtk, rtkint, fcon, temp, cond, dx, ddx,
     #         delt, rftemp, pol8, tval, dtemp
      integer i,indx,n,nt,m
      external pol8
c
      dimension  temp(10), dtemp(9), cond(10)
c
       data indx/5/
      data temp /0.0, 1000.0, 1173.0, 1273.0, 1373.0, 1473.0, 1573.0,
     #                2000.0, 3000.0, 4000.0/
      data dtemp/ 1000.0, 173.0, 100.0, 100.0, 100.0, 100.0, 427.7,
     #            1000.0, 1000.0/
      data cond /12.14, 12.14, 12.14, 11.10, 10.47, 10.05, 9.63,
     #            9.63,  9.63,  9.63/
c
      data rftemp / 0.0/, nt / 10 /
c
c     integrate the 1/k value up to temperature tin
c
      rtkint  = 0.0
      n = 1
c
c     check location of reference temp
c
   10 if (temp(n) .ge. rftemp) then
         if (tin .le. temp(n)) then
            dx  = tin - rftemp
            tk  = pol8 (cond,temp,rftemp,10,indx)
            rtk = 1.0/tk
            rtkint = rtkint + rtk*dx
            go to 50
         else
            dx  = temp(n) - rftemp
            tk  = pol8 (cond,temp,rftemp,10,indx)
            rtk = 1.0/tk
            rtkint = rtkint + rtk*dx
         endif
      else
         n = n + 1
         go to 10
      endif
c
c     integrate to data entry below tin (tc)
c
      i = n
   30 delt = dtemp(i)
      m = n + 1
      if (temp(m) .ge. tin) go to 40
      tval   = 0.5*(temp(n) + temp(m))
      tk     = pol8 (cond,temp,tval,10,indx)
      rtk    = 1.0/tk
      rtkint  = rtkint + rtk*delt
      n = n + 1
      i = i + 1
      if (n .gt. nt) then
         write (output,600) tin, temp(nt)
  600 format ('  0subroutine sfcon, temp. of ',1p,g13.5,
     # ' exceeds last table',' value of ',g13.5,' degree K')
      fail = .true.
        return
      endif
      go to 30
c
   40 continue
      ddx    = tin - temp(n)
      tk     = pol8 (cond,temp,tin,10,indx)
      rtk    = 1.0/tk
      rtkint = rtkint + rtk*ddx
c
   50 continue
      fcon = rtkint
      return
      end
*endif
