*mlist
*if def,selap
       function fncp(im,tp)
!  Computes specific heat capacities by calling matpro.
!
!  Cognizant engineer: ewc (wxs kld).
!
!     Calling subroutines:  effht, atrmlc.
!     Subroutines called:  ccp, fcp, zocp, zucp, cpmat.
!     Engineer/programmer: W. Sengpiel.
!     Modified by E.W. Coryell 12/1/89 to utilize block ifs and to
!     use srs materials.
!     Modified by K.L. Davis 1/19/94 to add tungsten and corrected
!     unreachable coding call to lithium.
!
!     Input variables   description
!        im             material indicator
!        tp             temperature ( k )
!
!  In this subroutine matpro functions or function cpmat are called for
!  evaluation of specific heat capacities (j/kg-k).
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       real(kr) fncp
       integer im
       real(kr) tp
!
       include 'contrl.h'
       include 'matdat.h'
       include 'eht0.h'
       include 'hfuson.h'
!
!  Local variables.
       real(kr) ccp,ccpr,cpmat,fcp,ucpnr,zocp,zucp,tungcp,acp,scp,socp
       real(kr) cpal,cpalo,cp
       real tp1
       integer ierr
!
       select case (im)
       case (1)
!  Zry.
         if (chngno(70)) then
           fncp = ccpr(tp)
         else
           fncp = ccp(tp)
         endif
       case (2)
!  Zr-u-o compound, liquid.
         fncp = zucp(afulq,afzlq,tp)
       case (3)
!  Zr-u-o compound, refrozen.
         fncp = zucp(afusl,afzsl,tp)
       case (4)
!  Tungsten.
         fncp = tungcp(tp)
       case (5)
!  Zro2.
         fncp = zocp(tp)
       case (6)
!  Unirradiated fuel.
         fncp = fcp(tp,facmot,fotmtl,fraden)
       case (7)
!  Cracked fuel.
         fncp = fcp(tp,facmot,fotmtl,fraden)
       case (8)
!  Relocated fuel.
         fncp = fcp(tp,facmot,fotmtl,fraden)
       case (9)
!  Steam-gas-atmosphere.
         fncp = cpmat(im,tp)
       case (10:12)
!  User specified.
         fncp = cpmat(im,tp)
       case (13)
!  Metallic uranium.
         fncp = ucpnr(tp)
       case (14)
!  N-reactor debonding gap.
         fncp = 0.0
       case (15)
!  Al.
         fncp = cpal (tp)
       case (16)
!  Al oxide.
         fncp = cpalo (tp)
       case (17)
!  Li.
         call licp (tp, cp, ierr)
         fncp = cp
       case (18)
!  Ss-304.
         fncp = scp(tp)
!  Steel oxide.
       case (19)
         fncp = socp(tp)
       case (20)
!  Absorber material.
         fncp = acp(ictype, tp)
       case (21)
!  Molybdenum.
         fncp = 237.48 + tp*(3.846e-2 + 1.453e-5*tp)
       case (22)
!  Copper.
         fncp = 378.2 + tp*(0.1473 - 2.968e-5*tp)
c Ta
      case (23)                     
       tp1=max(293.16,(min(tp,3000.16)))
        fncp = 128.09 + 0.0445*tp1 - 3.0e-5*tp1**2 + 8.0e-9*tp1**3
		
c  Brass for parameter
      case (24)                      
       tp1=max(323.16,(min(tp,823.16)))
        fncp = 483.21 - 0.599*tp1 + 0.0012*tp1**2 - 6.0e-7*tp1**3 	
	
c molybdenum  parameter (assume same as in Mo quench)
c data stored here are extracted from: kfk-15/77-2 (8/98)
      case (25)                       
        fncp = 237.48 + tp*(3.846e-2 + 1.453e-5*tp)	
       case (50:59)
         fncp = cpmat(im,tp)
       end select
       return
       end
*endif
