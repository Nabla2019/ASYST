*mlist
*if def,selap
       function cpmat(im,t)
!  Computes specific heat capacity by interpolation of user-specified
!  tables ttemp (=temperature) and tcp (=specific heat capacity).
!
!  Cognizant engineer: ewc.
!
!  Calling subroutines: fncp.
!     input variables   description
!        im             material indicator
!        t              temperature ( k )
!
       use gentblc
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
!
       real(kr) cpmat
       integer im
       real(kr) t
!
       include 'matdat.h'
!
       real(kr) dr(1)
       integer i,jknt,k
       logical err
!
       if (im .le. 12) then
         jknt = im - 8
       else
         jknt = im - 45
       endif
!
       if (umgntno(1,jknt) .gt. 0) then
!  Evaluate material from general tables.
         k = umgntni(1,jknt)
         if (t .ne. gen(k)%gtarg) then
           call gtpolat (gen(k)%gtinfo,gtptr(k)%gtbl,t,dr(1:1),err)
           gen(k)%gtarg = t
           gen(k)%gtval(1) = dr(1)
         else
           dr(1) = gen(k)%gtval(1)
         endif
         cpmat = dr(1)
       else
!  Evaluate material from fixed temperature tables.
         if (t .ge. ttemp(markmx(jknt),jknt)) then
           cpmat = tcp(markmx(jknt),jknt)
         elseif (t .le. ttemp(1,jknt)) then
           cpmat = tcp(1,jknt)
         else
           i = 1
           do
             if (t.ge.ttemp(i,jknt) .and. t.le.ttemp(i+1,jknt)) then
               cpmat = tcp(i,jknt) +
     &         (tcp(i+1,jknt) - tcp(i,jknt))*(t - ttemp(i,jknt))/
     &         (ttemp(i+1,jknt) - ttemp(i,jknt))
               exit
             endif
             i = i + 1
           enddo
         endif
       endif
       return
       end
*endif
