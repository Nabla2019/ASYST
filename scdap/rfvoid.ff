*mlist
*if def,selap
       function rfvoid(ict,tcld,t,burnup,pcool,pgs)
c
c  Cognizant engineer: ewc (wxs).
c
c  rfvoid computes relative fuel void volume by means of empirical
c  correlations
c
c     calling subroutines:  fpress
c
c     input variables                  description
c        ict            flag indicating use of empirical correlations
c                       ( ict=1,2 ) or user-specified tables ( ict=3 )
c        tcld           cladding temperature ( k )
c        t              average fuel temperature ( k )
c        burnup         burnup ( mw-s/kg u )
c        pcool          coolant pressure ( n/m2 )
c        pgs            internal gas pressure at last time step (n/m2)
c
       implicit none
c
       integer ict
       real tcld,t,burnup,pcool,pgs
       real rfvoid
*call prdat
c
c  Local variables.
       real bu,temp
c
      bu= burnup/ 8.64e+4
      temp= t
      if( ict .eq. 1 ) then
c evaluation of relative fuel void volume rfvoid based on standard pwr
c            fuel rod data
          if( tcld .le. 750. .or. pcool .lt. pgs ) then
              if( temp .lt. 500. ) temp= 500.
              if( temp .gt. 1100. ) temp= 1100.
              rfvoid= ( 1.175 -(1.1084e-4)*temp ) *
     +                exp( (-7.50e-3)*bu ) *( 1. -0.1247 *
     +                exp( -0.6442*bu ) )
          else
              if( temp .gt. 1100. ) temp= 1100.
              rfvoid= 0.36 *( 1. -0.1441*exp( (-4.473e-2)*bu ) )
          endif
      else
      if( ict .eq. 2 ) then
c evaluation of relative fuel void volume rfvoid based on standard bwr
c            fuel rod data
          if( tcld .le. 750. .or. pcool .lt. pgs ) then
              if( temp .lt. 500. ) temp= 500.
              if( temp .gt. 1200. ) temp= 1200.
              rfvoid= 1.198 *exp( (-3.598e-3)*bu ) *
     +                ( 1. -0.167 *exp( -1.288*bu ) )
          else
              if( temp .gt. 1200. ) temp= 1200.
              rfvoid= 0.16 *( 1. -0.5343*exp( (-7.226e-2)*bu ) )
          endif
      else
c evaluation of relative fuel void volume rfvoid by interpolation of tab
      if( temp .gt. txmpf(1) .and. temp .lt. txmpf(mtmax) ) goto 10
      if( temp .le. txmpf(1) ) rfvoid= ffvt(1)
      if( temp .ge. txmpf(mtmax) ) rfvoid= ffvt(mtmax)
      return
   10 continue
      if( txmpf(mt) .lt. temp ) then
   15     continue
          if( txmpf(mt+1) .ge. temp ) then
              rfvoid= ffvt(mt) +( ffvt(mt+1) -ffvt(mt) ) *( temp -
     +                txmpf(mt) )/( txmpf(mt+1) -txmpf(mt) )
          else
              mt= mt +1
              goto 15
          endif
      else
          mt= mt -1
          goto 10
      endif
c
      endif
      endif
      return
      end
*endif
