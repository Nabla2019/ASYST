*mlist
*if def,selap
      subroutine absorp(p,ts,tw,bl,absor,emissv,aemisv,je)
c
c
c   this subroutine will determine the absorptivity and emissivity
c   of steam, given:
c                    steam pressure (psia)           p
c                    steam temperature (deg f)       ts
c                    wall temperature (deg f)        tw
c                    beam length (ft)                bl
c                    flag for calc. absorptivity     je
c                           =1    yes
c                           =2    no
c                    multiplier for calc.emissivity  aemisv
c
c  Cognizant engineer: ewc (ljo fpg).
c
c   the routine is based on the report by c.b.ludwig ,et al
c   'handbook of infrared radiation from combustion gases',
c   nasa-sp-3080, pages 280-287, 1973
c
c   the total emissivity of water versus optical depth and temperature
c   are represented by the et, epl, and emiss data arrays.
c
c   the method outlined in rohsenow and hartnett ('handbook of heat
c   transfer', mcgraw-hill 1973), chapter 15, part d (pages 15-73 to
c   15-75) will be used to evaluate the absorptivity.
c
c   for help in evaluating the mean beam length for an application,
c   see pages 305-307 of holman's 'heat transfer',1976 by mcgraw-hill.
c
c******************************************************************
c
c     absorp              1-jan-92
c
c     the BWR canister/control blade modules were developed at ORNL
c     by L.J.Ott under the auspices of the NRC sponsored Boiling
c     Water Reactor Severe Accident Technologies programs.
c
c     for information or assistance, call
c                 L.J.Ott (fts)-624-0324 or (615)-574-0324
c                 F.P.Griffin (fts)-626-4684 or (615)-576-4684
c
c******************************************************************
c
      implicit none
c
      real           absor , aemisv, bl    , d1    , d2    , emiss(8,8),
     *               emissv, epl(8), et(8) , fxo   , p     , pop   ,
     *               ts    , tsk   , tw    , twk
c
      integer        itc   , itr   , je
c
c     et - units of deg k
c
      data et   /
     *     250.0, 500.0, 750.0, 1000.0, 1500.0, 2000.0, 2500.0, 3000.0/
c
c     epl - units of cm*atm
c
      data epl  /0.1, 0.2, 0.5, 1.0, 2.0, 5.0, 10.0, 20.0/
c
      data emiss/
     *    0.016, 0.012, 0.008, 0.005, 0.0023, 0.0012, 0.00063, 0.0004,
     *    0.026, 0.020, 0.014, 0.010, 0.0047, 0.0023, 0.0013 , 0.0007,
     *    0.046, 0.037, 0.029, 0.021, 0.0105, 0.0054, 0.0029 , 0.0018,
     *    0.064, 0.054, 0.044, 0.034, 0.0200, 0.011 , 0.0066 , 0.0040,
     *    0.088, 0.079, 0.067, 0.055, 0.0347, 0.021 , 0.012  , 0.0080,
     *    0.116, 0.114, 0.105, 0.095, 0.070 , 0.047 , 0.030  , 0.019 ,
     *    0.157, 0.158, 0.150, 0.137, 0.108 , 0.078 , 0.053  , 0.034 ,
     *    0.194, 0.203, 0.203, 0.192, 0.161 , 0.124 , 0.088  , 0.060 /
c
c
c
c *** convert input variables to required units
c
c-----temperature to deg k (from deg f)
c
      tsk=(ts+459.67)/1.8
      twk=(tw+459.67)/1.8
c
c-----pressure*beam length product to cm*atm (from psia*ft)
c
      pop=p*bl*30.48/14.695
c
c *** evaluate steam emissivity
c
      call pola(emiss,8,8,et,epl,tsk,pop,fxo,itc,itr,5,d1,d2)
      fxo=fxo*aemisv
      emissv = min(1.0,fxo)
      emissv = max(0.00001,emissv)
c
c *** evaluate steam absorptivity
c
      go to(8,18),je
    8 if(pop.ge.3.0)then
         pop=pop*twk/tsk
         call pola(emiss,8,8,et,epl,twk,pop,fxo,itc,itr,5,d1,d2)
         fxo=fxo*(tsk/twk)**0.50*aemisv
      else
         pop=pop*(twk/tsk)**2.0
         call pola(emiss,8,8,et,epl,twk,pop,fxo,itc,itr,5,d1,d2)
         fxo=fxo*(tsk/twk)*aemisv
      end if
      absor = min(1.0,fxo)
      absor = max(0.00001,absor)
c
c *** return to calling routine
c
   18 return
      end
*endif
