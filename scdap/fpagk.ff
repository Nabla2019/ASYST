*mlist
*if def,selap
      subroutine fpagk (maxbin, nbin, pdiam, pdiff, ptau,
     &   agdiff, aggs, agturb, agca, agga, trblnt,
     &   tm, ta, ca, ga, g0,
     &   kaggl)
c
c  fpagk defines the agglomeration coefficients.
c
c  Cognizant engineer: ewc (esm).
c
      implicit none
*call cons
c
c     Earl S. Marwil
c     Scientific Computing Unit
c     EG&G Idaho, Inc.
c     PO Box 1625
c     Idaho Falls, ID 83415-2408
      integer maxbin, nbin
      real pdiam(maxbin), pdiff(maxbin), ptau(maxbin)
      logical agdiff, aggs, agturb, agca, agga, trblnt
      real tm, ta, ca, ga, g0
      real kaggl(maxbin,maxbin)
      integer ibin, jbin
      real temp
      real deltau, kd, vtm, vta, vca, vga, vgs
      real dtot, ez, a, ea
c     Agglomeration Coefficients
      do 20 ibin = 1, nbin
         do 10 jbin = ibin, nbin
            kd = 0.0
            vtm = 0.0
            vta = 0.0
            vca = 0.0
            vga = 0.0
            vgs = 0.0
            dtot = pdiam(ibin) + pdiam(jbin)
            if (agdiff) then
c              Diffusion
c
c              Fick's first law of diffusion used to derive
c              coefficients
               kd = 2.0 * pi * dtot * (pdiff(ibin) + pdiff(jbin))
            end if
            deltau = ptau(jbin) - ptau(ibin)
            if (aggs) then
c              Gravitational Settling
c
c              Gravitational force - bouyancy force = drag force
c              Drag force derived from Stokes Law
c              Assume low relative Reynolds number
c              Assume no slip at particle surface (pdiam > .1 micrometer
c
c              W.C. Hinds, Aerosol Technology, John Wiley & Sons,
c              New York, NY, 1982, page 84.
c
c              Assume particle density >> vapor density
c              and neglect bouyancy force
               vgs = deltau * g0
            end if
            if (agturb .and. trblnt) then
c              Turbulent Motion
c              velocity = length * turbulent motion coefficient
c
c              V.L Streeter, ed., Handbook of Fluid Dynamics,
c              McGraw-Hill, Inc., New York, NY, 1961, page 10-9.
               vtm = (dtot / 4.0) * tm
c              Turbulent Acceleration
               vta = deltau * ta
            end if
            if (agca) then
c              Centrifugal Acceleration
               vca = deltau * ca
            end if
            if (agga) then
c              General Acceleration
               vga = deltau * ga
            end if
c           Calculate efficiency and area for acceleration effects
c
c           Assume low relative Reynolds number
c
c           N.A. Fuchs, The Mechanics of Aerosols, Pergamon Press,
c           New York, NY, 1964, page 165.
            temp = dtot / pdiam(jbin)
            temp = max(temp,1.0)
            ez = temp ** 2 - 1.5 * temp + 0.5 / temp
            a = pi * (dtot / 2.0) ** 2
            ea = ez * a
            temp = kd + ea * (vtm + vta + vca + vga + vgs)
            kaggl(ibin,jbin) = temp
            kaggl(jbin,ibin) = temp
   10       continue
   20    continue
      return
      end
*endif
