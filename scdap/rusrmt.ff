*mlist
*if def,selap
       subroutine rusrmt
!  Read input data for user-defined materials.
!
!  Cognizant engineer: E. W. Coryell
!
*in32 idat
*in32end
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       include 'contrl.h'
       include 'comctl.h'
       include 'fast.h'
       include 'ufiles.h'
       include 'scddat.h'
       include 'matdat.h'
       include 'scntl.h'
!
       real(kr) rdatv(ndax)
       integer idat(1)
       equivalence (rdatv(1),idat(1))
       integer i,imat,knt,knt1,next,nfield,where
       integer l20(9),l21(9)
       logical prcoldf,tfail1,tfail2
       data l20 /0,0,1,3,0,1,2,0,0/
       data l21 /0,0,10,10,0,1,2,1,1/
!
!  Restrict use to 9, 10, 11, 12, and 50 through 59 for nn. For nn =
!  50 through 59, nn must be entered in pairs, even for the  material,
!  odd for its oxide.
!  40002nn1 - User specified material specific heat.
       if (scntrl(7)) return
       umgntno(1,:) = -3
       umgntno(2:3,:) = 0
       umgntni(:,:) = 0
       do knt = 1,imax
         if (knt .le. 4) then
           imat = knt + 8
         else
           imat = knt + 45
         endif
!  Check for input for user specified material defined by general
!  tables.
         prcoldf = .false.
         umgntno(1,knt) = -1
         tfail1 = .false.
         l20(1) = 40009000 + imat*10
         call inplnk (l20(1),next,where,nfield,fa(filndx(1)))
         if (nfield .lt. 0) then
           tfail1 = .true.
         elseif (nfield .gt. 0) then
           l20(6) = 1
           call inp2 (fa(filndx(1)),rdatv,l20)
           if (l20(6) .lt. 0) then
             tfail1 = .true.
           else
             if (idat(1).lt.0 .or. idat(1).ge.1000) then 
               fail = .true.
               write (output,"('0******** Word 1 on card',i9,' must be g
     &reater than or equal to 0 and less than 1000.')") l20(1)
             elseif (idat(1) .eq. 0) then
               prcoldf = .true.
               umgntno(1,knt) = -2
               if (l20(6) .ne. 1) then
                 fail = .true.
                 write (output,"('0******** Too many words on card',i9,
     & ' when word 1 is 0.')")
               endif
             else
               if (l20(6) .ne. 3) then
                 fail = .true.
                 write (output,"('0******** Incorrect number of words on
     & card,'i9,' when entering general table numbers for user specified
     & materials.')") l20(1)
               else
                 tfail2 = .false.
                 do i = 1,3
                   if (idat(i).le.0 .or. idat(i).ge.1000) then
                     tfail2 = .true.
                     idat(i) = 0
                   endif
                   umgntno(i,knt) = idat(i)
                 enddo
                 if (tfail2) then
                   fail = .true.
                   write (output,"('0******** General table numbers on c
     &ard',i9,' are out of range, are being set to zero.')")
                 endif
                 write (output,"('0User specified material property',
     & i3,' is being defined by general tables: specific heat,'i4,
     &' density,',i4,' thermal conductivity,'i4,'.')") 
     &           knt,umgntno(1:3,knt)
               endif
             endif
           endif
         else
           prcoldf = .true.
         endif
         if (tfail1) then
           fail = .true.
           write (output,"('0******** Error in card',i9,' specifying opt
     &ions for user specified material',i3,', material cannnot be proces
     &sed.')") l20(1),imat
         endif
!
!  Check input where fixed temperature tables are entered.
         if (prcoldf) then
           l21(1) = 40009001 + imat*10
           if (umgntno(1,knt) .ne. -2)
     &     call inplnk (l21(1),next,where,nfield,fa(filndx(1)))
           if (nfield .gt. 0) then
             l21(6) = 1
             call inp2 (fa(filndx(1)),rdatv,l21)
             if (l21(6) .le. 0) then
               write (output,2001) l21(1)
 2001  format ('0******** Card ',i8,' is missing.')
               fail = .true.
             else
               markmx(knt) = l21(6)
               tfail2 = .false.
               do knt1 = 1,markmx(knt)
                 tcp(knt1,knt) = rdatv(knt1)
                 if (tcp(knt1,knt) .lt. 0.0) tfail2 = .true.
               enddo
               if (tfail2) write (output,2010) l21(1),'Specific heat'
 2010  format ('0******** Card ',i9,' - Out of range ',a,'.')
               write (output,"('0User specified material',i3,' specific 
     &heat values:'/(6es13.5))") imat,tcp(:,knt)
             endif
!  4ccc2nn2 User specified material density.
             l21(1) = 40009002 + imat*10
             l21(6) = 1
             call inp2 (fa(filndx(1)),rdatv,l21)
             if (l21(6) .le. 0) then
               write (output,2001) l21(1)
               fail = .true.
             else
               tfail2 = .false.
               do knt1 = 1,markmx(knt)
                 tro(knt1,knt) = rdatv(knt1)
                 if (tro(knt1,knt) .lt. 0.0) tfail2 = .true.
               enddo
               if (tfail2) write (output,2010) l21(1),'Density'
               write (output,"('0User specified material',i3,' density v
     &alues:'/(6es13.5))") imat,tro(:,knt)
             endif
!  4ccc2nn3 User specified material thermal conductivity.
             l21(1) = 40009003 + imat*10
             l21(6) = 1
             call inp2 (fa(filndx(1)),rdatv,l21)
             if (l21(6) .le. 0) then
               write (output,2001) l21(1)
               fail = .true.
             else
               tfail2 = .false.
               do knt1 = 1,markmx(knt)
                 tk(knt1,knt) = rdatv(knt1)
                 if (tk(knt1,knt) .lt. 0.0) tfail2 = .true.
               enddo
               if (tfail2) write (output,2010) l21(1),'Thermal conductiv
     &ity'
               write (output,"('0User specified material',i3,' thermal c
     onductivity values:'/(6es13.5))") imat,tk(:,knt)
             endif
           endif
         endif
       enddo
!  Check that user specified materials, 50 - 59, have been entered in
!  pairs.
       do knt = 5,imax,2
         if (knt .le. 4) then
           imat = knt + 8
         else
           imat = knt + 45
         endif
         if (umgntno(1,knt).ne.-3 .xor. umgntno(1,knt+1).ne.-3) then
           fail = .true.
           write (output,"('0******** User specified materials'i3,'and',
     &i3,' are related as the normal material and its oxide, and either
     &both or neither of them must be entered.')") imat,imat+1  
         endif
       enddo
       return
       end
*endif
