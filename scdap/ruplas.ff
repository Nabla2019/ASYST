*mlist
       subroutine ruplas (sigma, dtdt, tplask, trupk)
c
c   calculate plastic deformation and rupture temperatures
c
c  Cognizant engineer: ewc.
c
       implicit none
c
       real sigma,dtdt,tplask,trupk
c
c ********* input *************
c      sigma  = hoop stress (pa)
c      dtdt   = heatup rate (k/s)
c ******** output ***************
c      trupk  = rupture temperature (k)
c      tplask = plastic deformation temperature (k)
c
c   rupture temperature is from equation 3-2, page 10 in nureg-0630
c
c  Local variables.
       real degkc,delt,heatup,pakpsi,rate,sigmak,tplas,trup
c                            conversion from pa to kpsi
       data pakpsi /1.45037747e-7/
c                            conversion from k to c
       data degkc /273.2/
c                            base heat-up rate deg c/s
       data heatup /  28.0/
c                            convert sigma from pa to kpsi
       sigmak = sigma * pakpsi
c                            calc. heatup rate, ratio to 28 k/sec
c                            limit to range between 0 and 1
       rate = min(1.0,max(0.0,dtdt/heatup))
c                            compute rupture temperature (c)
       trup   = 3960. - 20.4 * sigmak / (1.0 + rate) -
     *  8510000.0 * sigmak / (100.0 * (1.0 + rate) + 2790.0 * sigmak)
c
c                            check that ballooning does not occur
c                            during compressive stress
       if (sigma .lt. 0.0) trup = 1300.0
c
c                            calculate delta temperature in deg c
       if (trup .lt.  700.0) delt = 70.0
       if (trup .le. 1300.0 .and. trup .ge. 700.0)
     *       delt  = 70.0 + (trup - 700.0) * (85.0 / 600.0)
       if (trup .gt. 1300.0) delt = 155.0
c
c                            find cladding temperature at which
c                            plastic deformation begins (c)
       tplas  = trup - delt
c                            convert to degrees k
       tplask = tplas + degkc
       trupk  = trup  + degkc
       return
       end
