*mlist
$if def,selap
      subroutine ifprod
c 
c Check combinations of Scdap components for Couple mesh.
c For a new run, get the number of fission product species to be
c tracked, and set their index pointers.
c 
c Cognizant engineer: G. A. Jayne          Feb. 1991
c 
c This is a COUPLE ROUTINE
      implicit none
c 
c Input arguments -
c none
c 
c Output argument -
c none
c 
*call ufiles
*call contrl
*call comctl
*call cpmdat
*call coupl
*call scddat
*call scdpow
*call fgrcom
*call ndxara
*call slumpv
*call chcons
c Local variables.
      integer ic, icm, n1, n, natr, nful, nrk, nrkno, nsp, nuse(19)
      integer nkk
      if (nslphs(mcp) .ne. 2) go to 60
      if (ncpowi(mcp) .eq. 0.or.ncpowi(mcp).eq.3) go to 60
      natr = 0
      nful = 0
      nsp = 0
      nrk = 0
      nrkno = 0
      nkk = ntpslp(mcp)
c Check Scdap components connected to this Couple mesh.
      do 10 ic = 1, ncmesh(mcp)
        icm = icmesh(ic,mcp)
        if (idcomp(icm) .eq. 5) then
c ATR component
          natr = natr + 1
        else if (idcomp(icm) .eq.  0.or. idcomp(icm) .eq. 6) then
c Fuel Rod component or CORA simulator component
          nful = nful + 1
        end if
        if (nspowi .eq. 3.or.nspowi.eq.1) then
          nrk = nrk + 1
        else
          nrkno = nrkno + 1
        end if
   10 continue
      if (natr .gt. 0 .and. nful .gt. 0) then
        write (output,9000) mcp
 9000 format ('0******** Both ATR and Fuel Rod (or CORA) type',
     +' components connect to Couple mesh',i4)
        fail = .true.
      end if
      if (ncpowi(mcp) .eq. 1 .and. nrkno .gt. 0) then
        write (output,9010) mcp
 9010 format ('0******** Input ncpowi = 1, but some Scdap',
     +' components connected to Couple mesh',i4 /
     +'    do not get power from Relap5 reactor kinetics.')
        fail = .true.
      end if
      if (ncpowi(mcp) .eq. 2 .and. nrk .gt. 0) then
        write (output,9020) mcp
 9020 format ('0******** Input ncpowi = 2, but some Scdap',
     +' components connected to Couple mesh',i4 /
     +'    get power from Relap5 reactor kinetics.')
        fail = .true.
      end if
      if (nrk .gt. 0 .and. nrkno .gt. 0) then
        write (output,9030) mcp
 9030 format ('0******** Some components connected to Couple mesh',i4,
     +' get power from Relap5 reactor kinetics and some do not.')
        fail = .true.
      end if
      if (.not. newrst) go to 60
      do 20 n = 1, 19
        nuse(n) = 0
   20 continue
      do 40 ic = 1, ncmesh(mcp)
        icm = icmesh(ic,mcp)
        if (idcomp(icm) .ne. 5) goto 40
c Set flags for those fission product species selected.
        do 30 n = 1, 19
          if (nuse(n) .eq. 0) then
            if (fpmasi(n,icm) .gt. 0.0) then
              nuse(n) = 1
              nsp = nsp + 1
            end if
          end if
   30   continue
c Initialize Scdap component flag for slumping to Couple.
        iscoup(icm) = 0
   40 continue
      if (nsp .gt. 0) then
        write (output,9040) mcp
 9040 format ('0 Fission product species to track for Couple mesh',i4)
        n1 = 0
        do 50 n = 1, 19
          if (nuse(n) .eq. 1) then
            n1 = n1 + 1
            ifpdb (n1,mcp,nkk) = n
            write (output,9050) n1, n, chaidx(n)
 9050 format (10x, 2i5, 5x, a4)
          end if
   50   continue
      else
        write (output,9060) mcp
 9060 format ('0   No fission product',
     +' tracking requested in Couple mesh',i4)
      end if
      nsptrk(mcp,nkk) = nsp
   60 continue
      return
      end
$endif
