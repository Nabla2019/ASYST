*mlist
*if def,selap
       subroutine tdist (cladt,ii,naz,tc0bal,zcond,ztmax,
     + tm1bal, tp1bal, zm1bal, zp1bal)
c
c
c  tdist establishes the nodal temperatures and elevations adjacent to
c  the node with the maximum temperature.
c
c  Cognizant engineer: ewc (vxs ghb).
c
c calling subroutines:  driveb
c
c input variables                    description
c
c     cladt             average cladding temperature for axial node
c     ii                ballooning node
c     naz               number of axial nodes
c     tc0bal            temperature of ballooning node
c     ztmax             elevation of ballooning node
c     zcond             elevations of axial nodes
c
c output variables                   description
c
c     tm1bal            temperature of lower adjacent node
c     tp1bal            temperature of upper adjacent node
c     zm1bal            elevation of lower adjacent node
c     zp1bal            elevation of upper adjacent node
c
       implicit none
*call scddat
c
       integer ii,naz
       real cladt,tc0bal,zcond,ztmax,tm1bal,tp1bal,zm1bal,zp1bal
       dimension cladt(ndax), zcond(ndax)
c
c  Local variables.
       integer ii1,ii2
c
c     set temperatures of adjacent nodes for temperature distribution
c     if there are at least 3 axial nodes and ii is not at an end node,
c     then temperatures may be set directly from nodal temperatures
c
      if  ( naz .ge. 3  ) then
          if  ( ii .ge. 2  .and.  ii .le. ( naz - 1 ) )   then
              ii1 = ii - 1
              ii2 = ii + 1
c  if ii is an end node, temperatures must be taken from the two nodes
c  closest to the end node.
          else if  ( ii .eq. naz )  then
              ii1 = ii - 2
              ii2 = ii - 1
          else if  ( ii .eq. 1 )   then
              ii1 = 2
              ii2 = 3
          endif
          tp1bal = cladt( ii2 )
          tm1bal = cladt( ii1 )
          zp1bal = zcond( ii2 )
          zm1bal = zcond( ii1 )
c     if only 2 axial nodes exist, the adjacent nodal temperature and
c     a fractional amount of the bolooned node temperature are used
      else  if  ( naz .eq. 2  )   then
          if  ( ii .eq. 1  )  then
              tp1bal = cladt( 2 )
              tm1bal = tc0bal * 0.99
              zp1bal = zcond( 2 )
              zm1bal = ztmax  * 0.92
          else
              tp1bal = tc0bal * 0.99
              tm1bal = cladt( 1 )
              zp1bal = ztmax  * 1.08
              zm1bal = zcond( 1 )
          endif
c     if only 1 axial node exists, fractional amounts of the balooned no
c     temperature are used for both temperature in the distribution
      else
          tp1bal = tc0bal * 0.99
          tm1bal = tp1bal
          zp1bal = ztmax  * 1.08
          zm1bal = ztmax  * 0.92
      endif
      return
      end
*endif
