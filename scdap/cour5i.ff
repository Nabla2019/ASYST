*mlist
*if def,selap
       subroutine cour5i (ir5vol,ivlrad,ir5vec,ir5rad,ir5fis,hcoef1,
     & ncevr5,nrad,fail)
c
c
c  Calculates the offset index for referencing the relap5 volumes
c  that interface with couple nodes that have convective and radiative
c  heat transfer.
c
c  Cognizant engineer: ewc.
c
c  Subroutine is called by iscdap.
c
*in32 ir5vol
*in32 ivlrad
*in32 ir5vec
*in32 ir5rad
*in32 ir5fis
*in32end
c
       implicit none
       integer ir5vol(*),ivlrad(*),ir5vec(*),ir5rad(*),ir5fis(*)
       real hcoef1(*)
       integer ncevr5,nrad
       logical fail
       include 'fast.h'
       include 'comctl.h'
       include 'voldat.h'
       include 'ufiles.h'
c
c Local variables.
       integer i,k,m
c  ir5vol=9 digit relap5 volume number for convection nodes.
c  ivldad=9 digit volume number for radiation set.
c  ir5vec=offset index for convection set.
c  ir5rad=offset index for radiation set.
c  ir5fis=offset index for fission products.
c  ncevr5=number of nodes in convective set.
c  nrad=number of nodes in radiation set.
c  hcoef1 = heat transfer coefficient or multiplier
c
       if (ncevr5.le.0) return
       ir5vec(1:ncevr5) = -1
       ir5fis(1:ncevr5) = -1
       ir5rad(1:nrad) = -1
c  Convective nodes.
       do k = 1,ncevr5
c        if (ir5vol(k).le.0 .and. hcoef1(k).gt.1.e-10) cycle
         if (ir5vol(k).le.0) cycle
         i = filndx(4)
         do m = 1,nvols(i)
           if (volno(i).eq.ir5vol(k)) then
             ir5vec(k) = i - filndx(4)
             exit
           endif
           i = i + ivskp
         enddo
         if (ir5vec(k).eq.(-1)) then
           write (output,9000) k,ir5vol(k)
 9000  format ('0******** Error, RELAP5 volume number not found for conv
     &ective set sequence number',i6,' subroutine cour5i.',
     &  ' volume no. =',i10)
           fail = .true.
         endif
       enddo
       return
       end
*endif
