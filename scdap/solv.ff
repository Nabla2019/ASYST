*mlist
*if def,selap
       subroutine solv (nn,econd,fcond,tcond)
c
c  solv performs a gaussian elimination of the nxn matrix "econd".
c
c  Cognizant engineer: gab.
c
c     calling subroutines:  heatcn
c
c     input variables                  description
c        nn             matrix dimension
c        numnod         number of nodal points in radial direction for
c                         heat conduction solution, at elevation z(i)
c        econd          global stiffness matrix  (w/k)
c        fcond          global thermal load matrix  (w)
c
c     i/o variables                    description
c        tcond          fuel rod radial temperature profile at elevation
c                         z(i)   (k)
c
       implicit none
c
       integer nn
*call scddat
c
       real econd,fcond,tcond
      dimension  econd( ndrd,ndrd ), fcond( ndrd ), tcond( ndrd )
c
c  Local variables.
       integer i,j,k,k1,l,m
       real ba,c
c
      l = nn - 1
      do 20 i=1,l
           m = i + 1
           do 20 j=m,nn
                c = econd(j,i) / econd(i,i)
                do 10 k=i,nn
                     econd(j,k) = econd(j,k) - econd(i,k) * c
   10           continue
                fcond(j) = fcond(j) - fcond(i) * c
   20 continue
      tcond(nn) = fcond(nn) / econd(nn,nn)
      do 40 i=1,l
           k = nn - i
           k1 = k + 1
           ba = fcond(k)
           do 30 j=k1,nn
                ba = ba - econd(k,j) * tcond(j)
   30      continue
           tcond(k) = ba / econd(k,k)
   40 continue
      return
      end
*endif
