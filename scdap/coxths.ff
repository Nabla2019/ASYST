*mlist
*if def,selap
       function coxths(ctemp,kpick,imodel)
c  Cognizant engineer: ewc.
c
c  Function coxths returns the growth rate constant for oxide thickness,
c  oxygen-stabilized alpha layer thickness (inner and outer), and
c  thickness of the oxygen-stabilized alpha layer between the outer and
c  inner alpha layers.  For temperatures of 1273-1853 K data and
c  analyses from J. V. Cathcart of ORNL are used to compute the ZrO2 and
c  oxygen-stabilized alpha thicknesses on the outer surface and similar
c  equations from P. Hofmann of the kernforschungszentrum Karlsruhe
c  (KFK) for oxygen-stabilized alpha layers on the cladding inner
c  surface when there is pellet-cladding mechanical inter-action (pcmi).
c  For temperatures of 1853- 2100 K, data and analyses from V. F Urbanic
c  and T. H. Heidrick , "High Temperature Oxidation Of Zircaloy-2 And
c  Zircaloy-4 In Steam," Journal Of Nuclear Materials 75,(1978) pp. 251-
c  261, are used to compute ZrO2 layer thickness.
c
c  coxths  output growth rate constant (m**2/s).
c  ctemp   input cladding temperature (k).
c  kpick   input integer 1-4, where
c          kpick=1 is growth rate constant for oxide thickness;
c          kpick=2 is growth rate constant for oxygen-stabilize alpha
c          layer nearest outer cladding surface;
c          kpick=3 is growth rate constant for oxygen-stabilize alpha
c          layer nearest uo2 fuel;
c          kpick=4 is growth rate constant for oxygen-stabilize alpha
c          layer between outer and inner alpha layers.
c  if imodel = 0 then cathcart-pawel constants are used;
c  if imodel <> 0 then baker-just constants are used.
c
       implicit none
       real coxths,ctemp
       integer kpick,imodel
       external fabend
c
c  kpick = 1.
       if (kpick .eq. 1) then
         if (imodel .ne. 0) then
           coxths = 1.75e-4*exp(-22899./ctemp)
         else if (ctemp .gt. 1820.0) then
           coxths = 8.868e-05*exp(-21538./ctemp)
         else
           coxths = 2.090e-06*exp(-18010./ctemp)
         endif
c  kpick = 2.
       else if (kpick .eq. 2) then
         if (imodel .ne. 0) then
           coxths = 1.75e-4*exp(-22899./ctemp)
         else
           coxths = 15.142e-05*exp(-23190./ctemp)
         endif
c  kpick = 3.
       else if (kpick .eq. 3) then
         coxths = 0.32e-04*exp(-4.9e04/(1.987*ctemp))
c  kpick = 4.
       else if (kpick .eq.4 ) then
         coxths = 0.70e-04*exp(-4.4e04/(1.987*ctemp))
       else
         write(6,101)
  101  format(' ****error *** kpick out of range in routine coxths')
         call fabend
       endif
       return
       end
*endif
