*mlist
*in32 itble
*in32end
       subroutine polat (itble,ftble,arg,val,err)
c
c
c  polat performs linear interpolation.
c
c  Cognizant engineer: rjw.
c
       implicit none
c
c  Performs linear interpolation on data in ftble using arg as search
c  arguments.  Interpolated values returned in val.
c  itble has three words:
c     itble(1) - the number of items per set.  If negative, error if
c                arg is not bounded by independent variable of ftble.
c                If positive, arg not bounded is not an error and
c                returned values are appropriate end values in ftble.
c     itble(2) - the total number of items.
c     itble(3) - the last used subscript.
c  ftble has table values.  The first word of each set in the table is
c  the search variable.
c
       integer itble(*)
       real ftble(*),arg,val(*)
       logical err
       integer i,ie,in,ig,k
       real r
c
       err = .false.
       in = abs(itble(1))
       ie = itble(2)
       i = itble(3)
       if (ie .eq. in) go to 20
   10  if (arg - ftble(i)) 13,25,11
   13  if (i .eq. 1) go to 20
       i = i - in
       go to 10
   11  ig = i + in
   14  if (ig .gt. ie) go to 20
       if (ftble(ig) - arg) 17,24,12
   17  i = ig
       go to 11
   12  r = (arg-ftble(i))/(ftble(ig)-ftble(i))
       do 15 k = 2,in
       val(k-1) = r*(ftble(ig+k-1)-ftble(i+k-1)) + ftble(i+k-1)
   15  continue
       go to 1000
   20  if (itble(1) .ge. 0) go to 25
       err = .true.
       return
   24  i = ig
   25  do 26 k = 2,in
         val(k-1) = ftble(i+k-1)
   26  continue
 1000  itble(3) = i
       return
       end
