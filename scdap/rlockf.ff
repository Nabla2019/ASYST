*mlist
*if def,selap
       subroutine rlockf (ncrum,rci,naz,voidfr,rlcond)
c
c  rlockf calculates a conductivity correction factor due to axial fuel
c
c  Cognizant engineer: gab.
c
c     calling subroutines:  fuelan
c     engineer/programmer:  g. a. berna
c
c     input variables                  description
c        naz            number of heat conduction axial zones
c        ncrum          indicator of fuel stack crumbling in axial zone
c                         at end of time step
c        rci            radius to inside cladding surface  (m)
c        voidfr         void fraction in the fuel of an axial zone
c
c     output variables                 description
c        rlcond         conductivity correction factor due to fuel axial
c                         relocation
c
c     local variables                  description
c        alf            ratio of gas to fuel
c        conf           thermal conductivity of the fuel (w/m-k)
c        cong           thermal conductivity of the gas (w/m-k)
c        xf             linear distance of the radius which is fuel (m)
c        xg             linear distance of the radius which is void (m)
c
       implicit none
*call scddat
c
       integer naz,ncrum
       real rci,voidfr,rlcond
      dimension rci(ndax) ,ncrum(ndax) ,voidfr(ndax) ,rlcond(ndax)
c
c  Local variables.
       integer i
       real alf,conf,cong,xf,xg
c
c  this model calculates the conductivity correction factor for regions
c  with relocated fuel.
c  this model assumes the fuel and gas result in a series resistance.
c
      do 100 i=1,naz
           if( ncrum(i) .eq. 1 ) then
c  assume constant fuel and gas conductivities.
                conf = 2.0
                cong = 0.5
                xg = voidfr(i) * rci(i)
                xf = rci(i) - xg
                alf = xg / xf
                rlcond(i) = cong * ( 1.0 + alf )
     +                    / ( cong + conf * alf )
           endif
  100 continue
      return
      end
*endif
