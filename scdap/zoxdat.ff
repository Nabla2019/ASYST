*mlist
*if def,selap
       subroutine zoxdat (mw1,mw2,tk,dts,xoxo,xazo,wgaino,xoxi,xazi,
     $ wgaini)
!
!  Determines the zircaloy reaction with an oxidizing environment in
!  which the following parameters are calculated.
!
!  Cognizant engineer: ewc (ljo fpg).
c
c******************************************************************
c
c     zoxdat             1-jan-92
c
c     the BWR canister/control blade modules were developed at ORNL
c     by L.J.Ott under the auspices of the NRC sponsored Boiling
c     Water Reactor Severe Accident Technologies programs.
c
c     for information or assistance, call
c                 L.J.Ott (fts)-624-0324 or (615)-574-0324
c                 F.P.Griffin (fts)-626-4684 or (615)-576-4684
c
c******************************************************************
c
      implicit none
c
      real            dts   , f     , tk    , wgain1, wgain2, wgaini,
     *                wgaino, xaz1  , xaz2  , xazi  , xazo  , xox1  ,
     *                xox2  , xoxi  , xoxo
c
      integer         mw1   , mw2
c
c********************************************************************
c********************************************************************
c*                                                                  *
c*           *****  Zircaloy-steam reaction model  *****            *
c*                                                                  *
c********************************************************************
c********************************************************************
c*                                                                  *
c*    Determines Zircaloy reaction with an oxidizing environment:   *
c*    that is, the following parameters are calculated,             *
c*             1). the ZrO2 layer growth                            *
c*             2). the alpha-Zr-oxygen saturated layer growth       *
c*             3). the total Zircaloy weight gain                   *
c*                                                                  *
c*    For temperatures < 650 C no reaction is considered.           *
c*    For temperatures > 650 C, three temperature regions must      *
c*    be evaluated:                                                 *
c*          < 900 C, oxide layer approximately obeys cubic kinetics *
c*          >1050 C, oxide layer follows parabolic kinetics         *
c*          >1580 C, reaction accelerates due to change in oxide    *
c*                   microstructure                                 *
c*    The region between 900-1050 C can be closely approximated by  *
c*    logarithmic interpolation between the middle and lower        *
c*    regions.                                                      *
c*                                                                  *
c*    At temperatures < 900 C, the user has the option of three     *
c*    correlations (input parameter MW1):                           *
c*    reference: T.J.Haste,'Zircaloy oxidation kinetics in the      *
c*               temperature range 700-1300 c',                     *
c*               IAEA-TC-657/4.7, September 1988                    *
c*        (MW1)                                                     *
c*          1    UKEA/Springfields no.1                             *
c*          2    UKEA/Springfields no.2                             *
c*          3    KfK (Leistikow and Schanz)                         *
c*                                                                  *
c*    At temperatures > 1050 C and < 1580 C, the user has the option*
c*    of three correlations (input parameter MW2):                  *
c*    reference: T.J.Haste,'Zircaloy oxidation kinetics in the      *
c*               temperature range 700-1300 c',                     *
c*               IAEA-TC-657/4.7, September 1988                    *
c*               V.F.Urbanic and T.R.Heidrick,'High-Temperature     *
c*               oxidation of Zircaloy-2 and Zircaloy-4 in steam',  *
c*               Journal of Nuclear Materials, 75(1978),251-261     *
c*        (MW2)                                                     *
c*          1    Cathcart/Pawel                                     *
c*          2    KfK (Leistikow and Schanz)                         *
c*          3    Urbanic/Heidrick                                   *
c*                                                                  *
c*    At temperatures > 1580 C, the Urbanic/Heidrick correlations   *
c*    are used:                                                     *
c*    reference: V.F.Urbanic and T.R.Heidrick,'High-Temperature     *
c*               oxidation of Zircaloy-2 and Zircaloy-4 in steam',  *
c*               Journal of Nuclear Materials, 75(1978),251-261     *
c*                                                                  *
c********************************************************************
c********************************************************************
c                                                                   *
c     Subroutine arguments:                                         *
c     parameter   I/O   units           description                 *
c        MW1     input   ---   user choice for low temp.correlation *
c        MW2     input   ---   user choice for mid.temp.correlation *
c        TK      input    k    Zircaloy temperature                 *
c        DTS     input    s    time step                            *
c        XOXO    input    m    old oxide thickness                  *
c        XOXI    output   m    new oxide thickness                  *
c        XAZO    input    m    old alpha-Zr(O) layer thickness      *
c        XAZI    output   m    new alpha-Zr(O) layer thickness      *
c        WGAINO  input  kg/m*m old Zircaloy weight gain             *
c        WGAINI  output kg/m*m new Zircaloy weight gain             *
c                                                                   *
c********************************************************************
c********************************************************************
c
c
c Zircaloy-steam reaction:
c
c
c   for temperatures <650 C (923 K):
c
      if(tk.le.923.0)then
         xoxi=xoxo
         xazi=xazo
         wgaini=wgaino
c
c   for temperatures >650 C (923 K) and <900 C (1173 K):
c
      else if(tk.le.1173.0)then
         if(mw1.eq.1)then
            xoxi=(xoxo**3.260+
     *            7.359e-10*dts*exp(-26160.0/tk))**(1.0/3.260)
            xazi=(xazo**2.0  +
     *            5.995e-4*dts*exp(-24680.0/tk))**0.5
            wgaini=(wgaino**2.901+
     *              23.32*dts*exp(-23680.0/tk))**(1.0/2.901)
         else if(mw1.eq.2)then
            xoxi=(xoxo**3.002+
     *            3.174e-8*dts*exp(-27470.0/tk))**(1.0/3.002)
            xazi=(xazo**2.0  +
     *            5.995e-4*dts*exp(-24680.0/tk))**0.5
            wgaini=(wgaino**2.522+
     *              111.4*dts*exp(-23970.0/tk))**(1.0/2.522)
         else if(mw1.eq.3)then
            xoxi=(xoxo**3.083+
     *            7.247e-8*dts*exp(-30150.0/tk))**(1.0/3.083)
            xazi=(xazo**2.0  +
     *            2.721e-2*dts*exp(-29450.0/tk))**0.5
            wgaini=(wgaino**2.781+
     *              838.7*dts*exp(-28080.0/tk))**(1.0/2.781)
         else
            stop 'zoxdat-1'
         end if
c
c   for temperatures > 900 C (1173 K) and <1050 C (1323 K):
c
      else if(tk.lt.1323.0)then
         if(mw1.eq.1)then
            xox1=(xoxo**3.260+
     *            7.359e-10*dts*exp(-26160.0/1173.0))**(1.0/3.260)
            xaz1=(xazo**2.0  +
     *            5.995e-4*dts*exp(-24680.0/1173.0))**0.5
            wgain1=(wgaino**2.901+
     *              23.32*dts*exp(-23680.0/1173.0))**(1.0/2.901)
         else if(mw1.eq.2)then
            xox1=(xoxo**3.002+
     *            3.174e-8*dts*exp(-27470.0/1173.0))**(1.0/3.002)
            xaz1=(xazo**2.0  +
     *            5.995e-4*dts*exp(-24680.0/1173.0))**0.5
            wgain1=(wgaino**2.522+
     *              111.4*dts*exp(-23970.0/1173.0))**(1.0/2.522)
         else if(mw1.eq.3)then
            xox1=(xoxo**3.083+
     *            7.247e-8*dts*exp(-30150.0/1173.0))**(1.0/3.083)
            xaz1=(xazo**2.0  +
     *            2.721e-2*dts*exp(-29450.0/1173.0))**0.5
            wgain1=(wgaino**2.781+
     *              838.7*dts*exp(-28080.0/1173.0))**(1.0/2.781)
         else
            stop 'zoxdat-2'
         end if
         if(mw2.eq.1)then
            xox2=(xoxo**2.0+
     *            2.252e-6*dts*exp(-18060.0/1323.0))**0.5
            xaz2=(xazo**2.0+
     *            1.523e-4*dts*exp(-24230.0/1323.0))**0.5
            wgain2=(wgaino**2.0+
     *              36.22*dts*exp(-20100.0/1323.0))**0.5
         else if(mw2.eq.2)then
            xox2=(xoxo**2.0+
     *            7.031e-6*dts*exp(-19940.0/1323.0))**0.5
            xaz2=(xazo**2.0+
     *            3.375e-4*dts*exp(-24830.0/1323.0))**0.5
            wgain2=(wgaino**2.0+
     *              46.95*dts*exp(-20750.0/1323.0))**0.5
         else if(mw2.eq.3)then
            xox2=(xoxo**2.0+
     *            1.296e-7*dts*exp(-13586.0/1323.0))**0.5
            xaz2=(xazo**2.0+
     *            1.520e-5*dts*exp(-19830.0/1323.0))**0.5
            wgain2=(wgaino**2.0+
     *              3.6426*dts*exp(-16820.0/1323.0))**0.5
         else
            stop 'zoxdat-3'
         end if
c
c
         f=(1.0/tk-1.0/1173.0)/(1.0/1323.0-1.0/1173.0)
         xoxi=exp(f*(log(xox2)-log(xox1))+log(xox1))
         xazi=exp(f*(log(xaz2)-log(xaz1))+log(xaz1))
         wgaini=exp(f*(log(wgain2)-log(wgain1))+log(wgain1))
c
c   for temperatures >1050 C (1323 K) and <1580 C (1853 K):
c
      else if(tk.le.1853.0)then
         if(mw2.eq.1)then
            xoxi=(xoxo**2.0+
     *            2.252e-6*dts*exp(-18060.0/tk))**0.5
            xazi=(xazo**2.0+
     *            1.523e-4*dts*exp(-24230.0/tk))**0.5
            wgaini=(wgaino**2.0+
     *              36.22*dts*exp(-20100.0/tk))**0.5
         else if(mw2.eq.2)then
            xoxi=(xoxo**2.0+
     *            7.031e-6*dts*exp(-19940.0/tk))**0.5
            xazi=(xazo**2.0+
     *            3.375e-4*dts*exp(-24830.0/tk))**0.5
            wgaini=(wgaino**2.0+
     *              46.95*dts*exp(-20750.0/tk))**0.5
         else if(mw2.eq.3)then
            xoxi=(xoxo**2.0+
     *            1.296e-7*dts*exp(-13586.0/tk))**0.5
            xazi=(xazo**2.0+
     *            1.520e-5*dts*exp(-19830.0/tk))**0.5
            wgaini=(wgaino**2.0+
     *              3.6426*dts*exp(-16820.0/tk))**0.5
         else
            stop 'zoxdat-4'
         end if
c
c   for temperatures >1580 C (1853 K):
c
      else
            xoxi=(xoxo**2.0+
     *            2.080e-6*dts*exp(-16014.0/tk))**0.5
            xazi=(xazo**2.0+
     *            1.520e-5*dts*exp(-19830.0/tk))**0.5
            wgaini=(wgaino**2.0+
     *              10.817*dts*exp(-16610.0/tk))**0.5
      end if
c
c
      return
      end
*endif
