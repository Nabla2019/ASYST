*mlist
*if def,selap
      subroutine ssoxup(tsk,ph2o,ph2,dtm,area,mmtot,wh2oin,
     &                  mxups,xo,
     &                  iflag,amss,dh2o,wh2oo,qmw)
c
c  Descriptive sentence goes here.
c
c  Cognizant engineer: ewc.
c
c
c  at high temperatures (close to its melting point) stainless steel
c  reacts vigorously with steam to yield the metal oxide and large
c  quantities of hydrogen.
c  the reaction is similar to that of zr and h2o, but the reaction
c  energy is considerably less than that of zr/h2o per unit mass of
c  metal reacted.
c  after reviewing the available literature (including l.baker's
c  assessment in anl/lwr/saf 83-3), the decision was made to use
c  white's kinetic correlations (gemp-1004(1968) and bakers report)
c  for the steam rich region and baker's correlation (above report)
c  for the hydrogen excess region. for the steam lean region a mean
c  of the above correlations will be used.
c  also baker's criteria (anl/lwr/saf 83-3) for determining steam rich,
c  steam lean or hydrogen excess atmosphere are used.
c  the oxidation of cr and ni is considered since these
c  elements make up 18% and 8% respectively of stain. steel. (note,
c  that the oxidation of cr is the major contributor to the reaction
c  energy evolved.)
c
c  cognizant engineer:  fpg@ornl.gov
c
c  input parameters:
c
c  tsk     = temperature of stainless steel                    , k
c  ph2o    = partial pressure of steam                         , pa
c  ph2     = partial pressure of hydrogen                      , pa
c  dtm     = time step                                         , sec
c  area    = surface area of stainless steel section           , m2
c  mmtot   = mass of metal available for reaction              , kg
c  wh2oin  = mass of steam available for reaction              , kg
c
c  input/output parameters:
c
c  mxups   = mass of oxides in oxide layer                     , kg
c  xo      = oxide layer thickness                             , m
c
c  output parameters:
c
c  iflag   = flag that indicates steam-rich (1),
c            steam-lean (2), or hydrogen-excess (3)
c            conditions
c  amss    = mass of metal reacted                             , kg
c  dh2o    = mass of h2o reacted                               , kg
c  wh2oo   = mass of h2o remaining                             , kg
c  qmw     = combined heat of reaction                         , j
c
      implicit none
c
*call scddat
*call upccon
c
      real            amcr  , amfe  , amfer , amni  , amss  , amurfe,
     &                area  , dh2o  , dtm   , dxdt  , dxdtb , dxdtl ,
     &                dxdtp , dxdtw , factor, h2ocr , h2ofe , h2ofeo,
     &                h2oni , h2oufe, h2ouni, hcr2o3, hfe3o4, hfeo  ,
     &                hfeoox, hnio  , mmtot , mxups(7)      , ph2   ,
     &                ph2o  , pop1  , pop2  , qmw   , t     , tsk   ,
     &                tsr   , urxfe , urxfeo, urxni , wh2oin, wh2oo ,
     &                xo    , xocm  , soxwgn
c
      integer         iflag
c
c *** statement functions:
c
c-----baker's criteria
c
c     (steam rich/steam lean)
c
      pop1(t)=1122.474716*exp(-4250.702/t)
c
c     (steam lean/hydrogen excess)
c
      pop2(t)=3.136787*exp(-2858.0/t)
c
c-----heat(s) of reaction      (j/kg h2o consumed)
c
c     fe + h2o > feo + h2
c
      hfeo(t)=(9294.8-3.2744*t)/10.0*2326.0
c
c     2cr + 3h2o > cr2o3 + 3h2
c
      hcr2o3(t)=(102987.7+t*(5.42125e-3*t-18.55925))/30.0*2326.0
c
c     ni + h2o > nio + h2
c
      hnio(t)=(-522.34-2.4167*t)/10.0*2326.0
c
c     3fe + 4h2o > fe3o4 + 4h2
c
      hfe3o4(t)=(49031.16+t*(6.55833e-3*t-28.20968))/40.0*2326.0
c
c     3feo + h2o > fe3o4 + h2
c
      hfeoox(t)=(7948.74+0.2784*t)/10.0*2326.0
c
c *** initialization and checks
c
      iflag=0
      qmw=0.0
      dh2o=0.0
      wh2oo=wh2oin
      if(ph2o.le.0.0)return
      if(wh2oin.le.0.0)return
c
c *** convert input to required units
c
      tsr=tsk*1.8
      xocm=xo*100.0
c
c *** determine the amount of feo and unreacted fe in the
c     oxide layer
c
      urxfe =mxups(2)
      urxni =mxups(3)
      urxfeo=mxups(4)
c
c *** determine whether its a steam rich, steam lean or
c     hydrogen excess atmosphere
c
      iflag=1
      if(ph2.le.0.0)go to 18
      if(ph2o/ph2.lt.pop1(tsk))iflag=2
      if(ph2o/ph2.lt.pop2(tsk))iflag=3
      go to(18,28,38),iflag
c
c**********************************************************************
c*****************                                 ********************
c*****************      steam rich atmosphere      ********************
c*****************                                 ********************
c**********************************************************************
c
c     the essential chemical reactions are
c
c                2cr + 3h2o > cr2o3 + 3h2               (1)
c                3fe + 4h2o > fe3o4 + 4h2               (2)
c                 ni +  h2o > nio   +  h2               (3)
c               3feo +  h2o > fe3o4 +  h2               (4)
c
c-----determine kinetic reaction rate (in cm/sec)
c
c     solid state controlled reaction rate (white's correlations)
c
c     linear portion:
c
   18 dxdtl=36.2426*exp(-40176.1/tsr)
c
c     parabolic portion:
c
       dxdtp=((xocm**2 + 0.0010857*soxwgn(tsk)*
     & dtm)**0.5-xocm)/dtm
c
c     load dxdt (the minimum of the above two terms) and convert to m/s
c
      dxdt=min(dxdtl,dxdtp)/100.0
c
c-----check for oxidation of unreacted fe, ni, and feo first
c
c     determine h2o needed to react urxfe, urxni, and urxfeo
c
      h2oufe=urxfe/wtfe/3.0*4.0*wth2o
      h2ouni=urxni/wtni*wth2o
      h2ofeo=urxfeo/wtfeo/3.0*wth2o
      dh2o=h2oufe+h2ouni+h2ofeo
c
c     check availability of h2o
c
      if(dh2o.gt.wh2oin)then
c     reaction limited by available steam
           factor=wh2oin/dh2o
           h2oufe=factor*h2oufe
           h2ouni=factor*h2ouni
           h2ofeo=factor*h2ofeo
           dh2o=wh2oin
           mxups(2)=(1.0-factor)*urxfe
           mxups(3)=(1.0-factor)*urxni
           mxups(4)=(1.0-factor)*urxfeo
      else
c     all unreacted metal in oxide layer reacts
           mxups(2)=0.0
           mxups(3)=0.0
           mxups(4)=0.0
      end if
      mxups(5)=mxups(5)+(urxfe-mxups(2))/wtfe/3.0*wtf3o4
     &               +(urxfeo-mxups(4))/wtfeo/3.0*wtf3o4
      mxups(7)=mxups(7)+(urxni-mxups(3))/wtni*wtnio
c
c-----check availability of h2o for reaction of virgin steel
c
c     determine amount (kg) of steel reacted (given dxdt)
c
      amss=dxdt*area*dnmups*dtm
c
c     determine fe,cr, and ni reacted (kgmoles)
c
      if(amss.le.mmtot) then
c     reaction limited by kinetics
           amfe=amss*fmfe/wtfe
           amcr=amss*fmcr/wtcr
           amni=amss*fmni/wtni
      else
c     reaction limited by available steel
           amfe=mmtot*fmfe/wtfe
           amcr=mmtot*fmcr/wtcr
           amni=mmtot*fmni/wtni
           dxdt=mmtot/area/dnmups/dtm
      endif
c
c     determine h2o needed (kg) for reacting amfe, amcr, and amni
c
      h2ofe=amfe/3.0*4.0*wth2o
      h2ocr=amcr/2.0*3.0*wth2o
      h2oni=amni*wth2o
c
c     check sum of h2ofe, h2ocr, and h2oni versus (wh2oin-dh2o)
c     and adjust dxdt (if necessary)
c
      factor=max(wh2oin-dh2o,0.0)
      if((h2ofe+h2ocr+h2oni).gt.factor)then
c     reaction limited by available steam
           factor=factor/(h2ofe+h2ocr+h2oni)
           dxdt=dxdt*factor
           h2ofe=h2ofe*factor
           h2ocr=h2ocr*factor
           h2oni=h2oni*factor
           amfe=h2ofe/wth2o/4.0*3.0*wtfe
           amcr=h2ocr/wth2o/3.0*2.0*wtcr
           amni=h2oni/wth2o*wtni
           dh2o=dh2o+h2ofe+h2ocr+h2oni
           wh2oo=0.0
      else
c     reaction limited by kinetics or available steel
           amfe=amfe*wtfe
           amcr=amcr*wtcr
           amni=amni*wtni
           dh2o=dh2o+h2ofe+h2ocr+h2oni
           wh2oo=max(wh2oin-dh2o,0.0)
      end if
      amss=dxdt*area*dnmups*dtm
      mxups(5)=mxups(5)+amfe/wtfe/3.0*wtf3o4
      mxups(6)=mxups(6)+amcr/wtcr/2.0*wtc2o3
      mxups(7)=mxups(7)+amni/wtni*wtnio
      mxups(1)=mxups(2)+mxups(3)+mxups(4)+mxups(5)+mxups(6)+mxups(7)
      xo=mxups(1)/area/dnxups
c
c-----calculate heat of reaction
c
      qmw=h2ocr*hcr2o3(tsk)+(h2ofe+h2oufe)*hfe3o4(tsk)+
     &    (h2oni+h2ouni)*hnio(tsk)+h2ofeo*hfeoox(tsk)
c
c *** finished
c
      return
c
c**********************************************************************
c*****************                                 ********************
c*****************      steam lean atmosphere      ********************
c*****************                                 ********************
c**********************************************************************
c
c     essential chemical reactions are:
c
c                2cr + 3h2o > cr2o3 + 3h2                 (1)
c                 fe +  h2o > feo   +  h2                 (2)
c
c     note , that nickel does not react under these conditions.
c
c-----determine kinetic reaction rate (in cm/sec)
c     (white)
c
c     solid state controlled reaction rate (white's correlations)
c
c     linear portion:
c
   28 dxdtl=36.2426*exp(-40176.1/tsr)
c
c     parabolic portion:
c
       dxdtp=((xocm**2 + 0.0010857*soxwgn(tsk)*
     & dtm)**0.5-xocm)/dtm
c
c     load dxdtw (the minimum of the above two terms)
c
      dxdtw=min(dxdtl,dxdtp)
c
c-----determine kinetic reaction rate (in cm/sec)
c     (baker)
c
c     solid state controlled reaction rate (baker's correlation)
c
       dxdtp=((xocm**2 + 0.4752*exp(-45294.4/tsr)*
     & dtm)**0.5-xocm)/dtm
c
c     load dxdtb
c
      dxdtb=dxdtp
c
c-----load dxdt (mean of dxdtw and dxdtb) and convert to m/s
c
      dxdt=(dxdtw+dxdtb)/100.0/2.0
c
c-----check for oxidation of unreacted fe first
c
c     determine h2o needed to react urxfe
c
      h2oufe=urxfe/wtfe*wth2o
      dh2o=h2oufe
c
c     check availability of h2o
c
      if(dh2o.gt.wh2oin)then
c     reaction limited by available steam
           factor=wh2oin/dh2o
           h2oufe=factor*h2oufe
           dh2o=wh2oin
           mxups(2)=(1.0-factor)*urxfe
      else
c     all unreacted fe in oxide layer reacts
           mxups(2)=0.0
      end if
      mxups(4)=mxups(4)+(urxfe-mxups(2))/wtfe*wtfeo
c
c-----check availability of h2o for reaction of virgin steel
c
c     determine amount (kg) of steel reacted (given dxdt)
c
      amss=dxdt*area*dnmups*dtm
c
c     determine fe and cr reacted (kgmoles)
c
      if(amss.le.mmtot) then
c     reaction limited by kinetics
           amfe=amss*fmfe/wtfe
           amcr=amss*fmcr/wtcr
      else
c     reaction limited by available steel
           amfe=mmtot*fmfe/wtfe
           amcr=mmtot*fmcr/wtcr
           dxdt=mmtot/area/dnmups/dtm
      endif
c
c     determine h2o needed (kg) for reacting amfe and amcr
c
      h2ofe=amfe*wth2o
      h2ocr=amcr/2.0*3.0*wth2o
c
c     check sum of h2ofe and h2ocr versus (wh2oin-dh2o)
c     and adjust dxdt (if necessary)
c
      factor=max(wh2oin-dh2o,0.0)
      if((h2ofe+h2ocr).gt.factor)then
c     reaction limited by available steam
           factor=factor/(h2ofe+h2ocr)
           dxdt=dxdt*factor
           h2ofe=h2ofe*factor
           h2ocr=h2ocr*factor
           amfe=h2ofe/wth2o*wtfe
           amcr=h2ocr/wth2o/3.0*2.0*wtcr
           dh2o=dh2o+h2ofe+h2ocr
           wh2oo=0.0
      else
c     reaction limited by kinetics or available steel
           amfe=amfe*wtfe
           amcr=amcr*wtcr
           dh2o=dh2o+h2ofe+h2ocr
           wh2oo=max(wh2oin-dh2o,0.0)
      end if
      amss=dxdt*area*dnmups*dtm
      mxups(4)=mxups(4)+amfe/wtfe*wtfeo
      mxups(6)=mxups(6)+amcr/wtcr/2.0*wtc2o3
      mxups(3)=mxups(3)+fmni*dxdt*area*dnmups*dtm
      mxups(1)=mxups(2)+mxups(3)+mxups(4)+mxups(5)+mxups(6)+mxups(7)
      xo=mxups(1)/area/dnxups
c
c-----calculate heat of reaction
c
      qmw=h2ocr*hcr2o3(tsk)+(h2ofe+h2oufe)*hfeo(tsk)
c
c *** finished
c
      return
c
c**********************************************************************
c***************                                      *****************
c***************      hydrogen excess atmosphere      *****************
c***************                                      *****************
c**********************************************************************
c
c     the oxidation of steel will form a 'spinal' compound in a
c     hydrogen excess atmosphere where
c
c                2cr + fe + 4h2o > feo*cr2o3 + 4h2        (1)
c
c     note ,that nickel does not react under these conditions.
c
c-----determine kinetic reaction rate (in cm/sec)
c
c     solid state controlled reaction rate (baker's correlation)
c
   38  dxdtp=((xocm**2 + 0.4752*exp(-45294.4/tsr)*
     & dtm)**0.5-xocm)/dtm
c
c     load dxdt and convert to m/s
c
      dxdt=dxdtp/100.0
c
c-----check availability of h2o for reaction of virgin steel
c
c     determine amount (kg) of steel reacted (given dxdt)
c
      amss=dxdt*area*dnmups*dtm
c
c     determine fe and cr reacted (kgmoles)
c
      if(amss.le.mmtot) then
c     reaction limited by kinetics
           amfe=amss*fmfe/wtfe
           amcr=amss*fmcr/wtcr
      else
c     reaction limited by available steel
           amfe=mmtot*fmfe/wtfe
           amcr=mmtot*fmcr/wtcr
           dxdt=mmtot/area/dnmups/dtm
      endif
      amfer=amcr/2.0
c
c     determine h2o needed (kg) for reacting amfer and amcr
c
      h2ofe=amfer*wth2o
      h2ocr=amcr/2.0*3.0*wth2o
c
c     check sum of h2ofe and h2ocr versus wh2oin
c     and adjust dxdt (if necessary)
c
      if((h2ofe+h2ocr).gt.wh2oin)then
c     reaction limited by available steam
           factor=wh2oin/(h2ofe+h2ocr)
           dxdt=dxdt*factor
           h2ofe=h2ofe*factor
           h2ocr=h2ocr*factor
           amfe=fmfe*dxdt*area*dnmups*dtm
           amcr=h2ocr/wth2o/3.0*2.0*wtcr
           amfer=h2ofe/wth2o*wtfe
           dh2o=h2ofe+h2ocr
           wh2oo=0.0
      else
c     reaction limited by kinetics or available steel
           amfe=amfe*wtfe
           amcr=amcr*wtcr
           amfer=amfer*wtfe
           dh2o=h2ofe+h2ocr
           wh2oo=max(wh2oin-dh2o,0.0)
      end if
      amurfe=max(amfe-amfer,0.0)
      amss=dxdt*area*dnmups*dtm
      mxups(4)=mxups(4)+amfer/wtfe*wtfeo
      mxups(2)=mxups(2)+amurfe
      mxups(6)=mxups(6)+amcr/wtcr/2.0*wtc2o3
      mxups(3)=mxups(3)+fmni*dxdt*area*dnmups*dtm
      mxups(1)=mxups(2)+mxups(3)+mxups(4)+mxups(5)+mxups(6)+mxups(7)
      xo=mxups(1)/area/dnxups
c
c-----calculate heat of reaction
c
      qmw=h2ocr*hcr2o3(tsk)+h2ofe*hfeo(tsk)
c
c *** finished
c
      return
      end
*endif
