*mlist
*if def,selap
       function avfunc(n,x,t,n1)
c
c  avfunc computes the volume weighted integral average of the input t a
c
c  Cognizant engineer: cma.
c
c  compute the volume weighted integral average of input t array.
c     calling subroutines:  oxidiz
c     engineer/programmer:  c. m. allison
c
c     input variables                  description
c        n1             dimension on t and x arrays
c        n              geometry index, 1 = slab, 2 = cylinder, 3 =
c                         sphere
c        t              array to be averaged, specified at x coordinate
c        x              volume coordinate
c
c -----------------------avfunc outline---------------------------
c :set dimensions
c :if: slab geometry
c     :then:
c        :compute average t
c     :else then:
c :if: cylinder geometry
c     :then:
c        :compute average t
c     :else then:
c :if: sphere geometry
c     :then:
c        :compute average t
c     :else then:
c :endif:
c :return
c ---------------------------------------------------------------
c
       implicit none
c
c  Local variables.
       integer i,nm1
       real sum
       real avfunc
      integer n, n1
      real x, t
      dimension  x(n1) , t(n1)
      nm1 = n1 - 1
      if  ( n1 .eq. 1 )   then
          avfunc = t( 1 )
      else
          if  ( n .eq. 1 )   then
              sum = t(1) * (x(2) - x(1)) + t(n1) * (x(n1)-x(n1-1))
              if (nm1 .ge. 2) then
cdir$ shortloop
                do 10 i = 2,nm1
                  sum = sum + t(i)*(x(i+1) - x(i-1))
   10           continue
              endif
              if (abs(x(n1) - x(1)) .gt. 1.0e-10) then
              avfunc = 0.5 * sum / (x(n1) - x(1))
              else
                avfunc = t(1)
              endif
          else if (n.eq.2) then
              sum = t(1) * x(1) * (x(2)-x(1)) +
     +                  t(n1) * x(n1) * (x(n1)-x(n1-1))
              if (nm1 .ge. 2) then
cdir$ shortloop
                do 20 i = 2,nm1
                  sum = sum + t(i)*x(i)*(x(i+1) - x(i-1))
   20           continue
              endif
              avfunc = sum / (x(n1)**2 - x(1)**2)
          else if (n.eq.3) then
              sum = t(1) * x(1)**2 * (x(2)-x(1)) +
     +                  t(n1) * x(n1)**2 * (x(n1)-x(n1-1))
              if (nm1 .ge. 2) then
cdir$ shortloop
                do 30 i = 2,nm1
                  sum = sum + t(i)*x(i)**2*(x(i+1) - x(i-1))
   30           continue
              endif
              avfunc = 1.5 * sum / (x(n1)**3 - x(1)**3)
          endif
      endif
      return
      end
*endif
