*mlist
*if def,selap
      subroutine fpsmry (mad, t)
c
c  fpsmry provides summary output of mass concentrations and particle
c  number concentrations.
c
c  Cognizant engineer: ewc (esm).
c
      implicit none
c
c     Earl S. Marwil
c     Scientific Computing Unit
c     EG&G Idaho, Inc.
c     PO Box 1625
c     Idaho Falls, ID 83415-2408
c
      real t
*call fplim
*call fpbin
*call fpctrl
      real mad(nst,nsp)
      real total(maxsp)
      real year
      parameter (year = 60.*60.*24.*365.25)
      real y
      integer ist,isp,ibin,isrf
      integer ksp,lsp
      real sum
      character spname(maxsp)*8
      external fpconc, fpspnm
c
      do 10 isp = 1, nsp
         sum = 0.0
         do 20 ist = 1, nst
            sum = sum + mad(ist,isp)
   20       continue
         total(isp) = sum
   10    continue
      call fpconc (mad)
      sum = 0.0
      do 80 ibin = 1,nbin
         sum = sum + pnum(ibin)
   80    continue
      write (*,'(1h1)')
      if (t .gt. year) then
         y = t / year
         write (*,'(1x,a,f13.4,a)') 'Aerosol Summary Data at time ', y,
     &     ' years'
      else
         write (*,'(1x,a,f13.4,a)') 'Aerosol Summary Data at time ', t,
     &     ' seconds'
      end if
      call fpspnm (spname)
      do 70 ksp = 1, nsp, 4
         lsp = min (nsp, ksp+3)
         write (*,*)
         write (*,'(8x,5(5x,a8,1x))') (spname(isp), isp=ksp,lsp),'Part#'
         write (*,'(8x,5(1x,a13))') ('--------------', isp=ksp,lsp+1)
         write (*,'(1x,a7,1p,5g14.6)')
     &      'Liquid ',(mad(iliqfp,isp),isp=ksp,lsp)
         write (*,'(1x,a7,1p,5g14.6)')
     &      'Vapor  ',(mad(ivap,isp),isp=ksp,lsp)
         do 30 ibin = 1,nbin
            write (*,'(1x,a3,i3,1x,1p,5g14.6)') 'Bin', ibin,
     &         (mad(ibin+ivap,isp),isp=ksp,lsp),pnum(ibin)
   30       continue
         if (nsrf .gt. 0) then
            do 40 isrf = 1, nsrf
               write (*,'(1x,a4,i2,1x,1p,5g14.6)') 'Cond', isrf,
     &            (mad(nvolst+isrf,isp),isp=ksp,lsp)
   40          continue
            do 50 isrf = 1, nsrf
               write (*,'(1x,a4,i2,1x,1p,5g14.6)') 'Chem', isrf,
     &            (mad(nvolst+nsrf+isrf,isp),isp=ksp,lsp)
   50          continue
            do 60 isrf = 1, nsrf
               write (*,'(1x,a4,i2,1x,1p,5g14.6)') 'Dep ', isrf,
     &            (mad(nvolst+2*nsrf+isrf,isp),isp=ksp,lsp)
   60          continue
         end if
         write (*,'(1x,a7,1p,5g14.6)') 'Total',
     &      (total(isp),isp=ksp,lsp), sum
   70    continue
      end
*endif
