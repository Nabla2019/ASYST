*mlist
$if def,selap
      subroutine calmas (wurcel, wuocel, walcel, sumal, sumuo2)
c
c
c  calmas calculate masses of certain constituents in material 1 for
c  each element in the Couple debris, given the atomic fractions
c  and material volumes.
c
c  Cognizant engineer: ewc (gaj).
c This is a COUPLE ROUTINE
c
c  Cognizant engineer: G. A. Jayne          Jan. 1991
c
      implicit none
c
c  Input arguments -
c     none
c
c  Output argument -
c     wurcel = mass of uranium
c     wuocel = mass of uranium dioxide
c     walcel = mass of aluminum
c
*call ufiles
*call contrl
*call cpmdat
*call coupl
*call alcm
*call iparm
c
      real walcel(numel), wurcel(numel), wuocel(numel)
c   Local variables.
      real div, vtot, wovtot, wtodiv, wtotel, wtot
      real sumal, sumuo2
      integer m, n
      integer idebug
c
c  Atomic masses of constituents in Couple material 1.
       real am(maxcon)
c                 zr       u      ss   ag-cd-in   b4c     uo2    zro2
c                 al      li      cd      soil
       data am /  91.0 , 238.0 ,  56.0 , 109.0 ,  55.0 , 270.0 , 123.0 ,
     1            26.98,   6.94, 112.41,  60.0                 /
      data idebug /0/
c
      sumal=0.0
      sumuo2=0.0
      do 100 n = 1, numel
      wurcel(n) = 0.0
      wuocel(n) = 0.0
      walcel(n) = 0.0
  100 continue
c
      if (wst(2,mcp) .le. 0.0 .and. wst(6,mcp) .le. 0.0 .and.
     1    wst(8,mcp) .le. 0.0)  go to 1000
c
c       Calculate mass and volume of total debris in Couple mesh.
      wtot = 0.0
      do 120 m = 1, maxcon
      wtot = wtot + wst(m,mcp)
  120 continue
      if (wtot .le. 1.e-3) go to 1000
c
      vtot = 0.0
      do 140 n = 1, numel
      vtot = vtot + a(i103+n-1)
  140 continue
      if (vtot .le. 0.0) go to 1000
c
      if (idebug .gt. 0) then
        write (output,30) timehy, wtot, wst(2,mcp), wst(6,mcp),
     1                    wst(8,mcp), vtot
   30   format (' calmas -timehy, wtot, wst(2,mcp), wst(6,mcp),',
     1                  ' wst(8,mcp),vtot=',/ 5x,6es13.5)
       end if
c
      wovtot = wtot / vtot
c
      do 200 n = 1, numel
      m = n - 1
c
      if (iafp(i8+5*n-1) .eq. 1) then
c         Element has material 1.
        if (a(i103+m) .gt. 0) then
c           Element is not empty.
          wtotel = wovtot * a(i103+m)
c             Calculate masses of constituents based on atomic fractions
          div = am(1)*a(i74+m) + am(2)*a(i76+m) + am(3)*a(i78+m) +
     1          am(4)*a(i80+m) + am(5)*a(i82+m) + am(6)*a(i84+m) +
     2          am(7)*a(i86+m) + am(8)*a(iptalf+m) + am(9)*a(iptlif+m) +
     3          am(10)*a(iptcdf+m) + am(11)*a(iptsif+m)
          wtodiv = wtotel / div
          wurcel(n) = am(2)*a(i76+m) * wtodiv
          wuocel(n) = am(6)*a(i84+m) * wtodiv
          walcel(n) = am(8)*a(iptalf+m) * wtodiv
          sumal=sumal+walcel(n)
          sumuo2=sumuo2+wuocel(n)
          if (idebug .gt. 0) then
            if (wurcel(n).gt.0.0 .or. wuocel(n).gt.0.0 .or.
     1          walcel(n).gt.0.0) then
              write (output,40) n,wurcel(n),wuocel(n),walcel(n)
   40         format ('   n, wurcel,wuocel,walcel=',i3,3es13.5)
            end if
          end if
        end if
      end if
c
  200 continue
c
 1000 continue
      return
      end
$endif
