*mlist
       subroutine tmltmx(afz, afu, afs, afa1, afa2, afux2, afzx2,
     # afalup, aflith, afcadm, afsoil, tmlt)
c
c
c  tmltmx calculates the liquidus temperature of a mixture of
c  various materials.
c
c  Cognizant engineer: rjw.
c
c This is a COUPLE ROUTINE
c
c
c      subroutine arguments:
c      afz=atomic fraction of zr in mixture.
c      afu=metalic u.
c      afs=stainless steel.
c      afa1=silver-indium-cadmium.
c      afa2=boron carbide.
c      afux2=uo2
c      afzx2=zro2.
c      afalup=aluminum.
c      aflith=lithium.
c      afcadm=cadmium.
c      afsoil=soil
c      tmlt = liquidus temperature of mixture (k) [output].
c
       implicit none
*call ufiles
       real afz,afu,afs,afa1,afa2,afux2,afzx2,afalup,aflith,afcadm,tmlt
       real afsoil
c
c  Local variables.
       integer ictype
       real afo1,afu1,afz1,denom,fracto,fracu
       real pliq
       external pliq
       real awu,awal
c
c  Data statements.
       data awu / 238.07/, awal / 26.98/
c
c      check for mixture of al-u.
       if(afalup.gt.1.e-10)then
c      calculate weight fractions of aluminum and uranium.
       fracto=afalup*awal+afu*awu
       fracu=awu*afu/fracto
       tmlt=933.
       if(fracu.gt.0.9)tmlt=1406.
       else
      denom = afu + afz + 3.0*(afzx2 + afux2)
      if (denom .ge. 0.2) then
      afu1 = (afu + afux2) / denom
      afz1 = (afz + afzx2) / denom
      if ((afu1 + afz1) .gt. 1.0) afz1 = 1.0 - afu1
      afo1 = 1.0 - afu1 - afz1
      if (afo1 .gt. 0.66666) afo1 = 0.66666
       tmlt=pliq(afo1,afu1,afz1)
       if(afz1.gt.0.05.and.tmlt.gt.2873.)tmlt=2873.
       else
       if((afa1+afa2).gt.0.5)then
c      control rod material
       ictype=1
       if(afa2.gt.afa1)ictype=2
       tmlt=1123.
       if(ictype.eq.2)tmlt=2743.
c      other material such as stainless steel.
       else
       if(afs.gt.0.3)then
       tmlt=1727.
       else
       if (afsoil .gt. 0.5) then
c      soil
       tmlt = 1428.0
       else
       write(output,900)
       write(output,901)afz, afu, afs, afa1,afa2,afux2,
     # afzx2
  900  format ('0******** - material properties do not',
     # ' exist to calculate liquidus temp. of this mixture',
     # ' - subroutine tmltmx')
  901  format (' afz=',e10.4,' afu=',e10.4,' afs=',e10.4,
     # ' afa1=',e10.4,' afa2=',e10.4,' afux2=',e10.4,
     # ' afzx2=',e10.4)
       write(output,902)afalup,aflith,afcadm
  902  format (' afalup=',e10.4,' aflith=',e10.4,
     # ' afcadm=',e10.4)
                endif
              end if
            end if
          end if
        end if
       return
       end
