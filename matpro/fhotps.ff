       function fhotps(comp, ftemp, hstres, grnsiz, fom, ro)
c
c      fhotps = output fractional volume change rate for uo2 or mixed
c      oxides(change in density / (initial density * seconds)).
c
c      comp   = input plutonia content (weight percent).
c      ftemp  = input temperature of the fuel (k).
c      hstres = input hydrostatic stress on the fuel pellet (pa).
c      grnsiz = input initial grain size (microns).
c      fom    = input fuel oxygen to metal ratio (unitless).
c      ro     = input fractional fuel density(unitless).
c
c      the densification calculated with this function is based on
c      data from the following references.
c      (1)j. l. routbort, j. c. voglewede, and d. s. wilkinson,
c         final-stage densification of mixed oxide fuels, journal of
c         nuclear materials, 80 (1979) pp 348 - 355.
c      (2)a. a. solomon, k. m. cochran and j. a. habermeyer, modelling
c         hot-pressing of uo2, nureg/cr  - pur-101.
c
c      fhotps was developed and programmed by r. e. mason-august 1977.
c      fhotps was revised by r. e. mason to include new data
c           april 1981 (cdr-mp-01).
c
c      standard error of estimate is plus or minus 0.5: of the
c      calculated density.   the standard error must be applied
c      external to this function routine.
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
       r      = 8.31432
       t      = ftemp
       p      = hstres
       g      = grnsiz
       x = abs(log10(fom - 1.9999))
       qu     = 9000.0/(exp((20.0 - 8.0*x)/x) + 1.0) + 36294.4
       if(comp .gt. 0.0) go to 20
       fhotps = 48939.*((1. - ro)/ro)**2.7* p*exp(-qu/t)/(t*g*g)
       go to 30
   20  continue
       fhotps = 1.80e7*((1.-ro)/ro)**2.25*p*exp(-450000./(r*t))/(t*g*g)
   30  continue
       return
       end
