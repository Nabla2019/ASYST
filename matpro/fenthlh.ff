       function fenthl (temp,fcomp,fotmtl,ftmelt,facmot,fhefus,gadoln)
c
c     facmot - fraction of molten fuel
c     fcomp  - puo2 fraction of the fuel
c     fhefus - heat of fussion of the fuel (j/kg)
c     fotmtl - fuel oxygen to metal ratio
c     ftmelt - fuel melting temperature (k)
c     temp   - local temperature (k)
c     gadoln - weight fraction of gadolina in fuel
c
c                           output
c     fenthl - local fuel enthalpy relative to zero degrees-k (j/kg)
c
c     fenthl was originally coded by g.a. berna in november 1979
c     it was updated by l.j. siefken in december 1979
c     further updated by g.a. reymann in february 1980
c     last updated by d. l. hagrman july 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
c     fcpmol = specific heat capacity of molten fuel ( j/(kg*k) )
      data  fcpmol / 503.e0 /
c
c     the following data statements contain constants from matpro-11 fcp
      data  c1u, c2u, c3u, thu, edu  / 296.7  , 2.43e-02, 8.745e07,
     #                                 535.285, 1.577e05 /
      data  c1pu,c2pu,c3pu,thpu,edpu / 347.4  , 3.95e-04, 3.860e07,
     #                                 571.0  , 1.967e05 /
      data c1gd,c2gd,c3gd,thgd,edgd/ 315.86 , 4.044e-02 , 0.0,
     +                               348.0  , 0.0       /
c
c     the following equation is the integral of the fuel specific heat
c     with respect to temperature
      cpdt(c1,th,c2,otm,ed,t,c3) =
     #                          c1*th * (     1.   / (exp(th/t)-1.))
     #                        + c2 * t * t / 2.e0
     #                        + c3 * otm * exp(-ed /(t * 8.314e0) )/2.e0
c
      tx=temp
      if(tx.gt.ftmelt)tx=ftmelt
      fenthl = cpdt(c1u      ,thu      ,c2u      ,fotmtl   ,edu
     #             ,tx       ,c3u      ) * ( 1.e0 - fcomp )
      if( fcomp .gt. 0.e0 )fenthl = fenthl + cpdt(c1pu     ,thpu
     #             ,c2pu     ,fotmtl   ,edpu     ,tx       ,c3pu )*fcomp
      if (temp .le. ftmelt - 2.0) go to 100
      fenthl = fenthl*(1-gadoln) +
     #   gadoln*cpdt(c1gd,c2gd,c3gd,thgd,edgd,tx,fotmtl)
      fenthl = fenthl + fhefus * facmot
      if(temp.le.(ftmelt+2.))go to 100
      fenthl=fenthl+(temp-ftmelt)*fcpmol
  100 continue
      return
      end
