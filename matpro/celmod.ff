      function celmod (ctemp, fnck, cwkf, deloxy)
!
!     celmod calculates cladding young's modulus as a function
!     of temperature, fast neutron fluence, cold work, and average
!     oxygen concentration.  grain orientation is assumed random.
!     the expected standard error is computed but not returned
!     (ucelmd).
!
!     celmod = output young's modulus for zircaloy 2 and 4
!              with random texture (pa)
!     ucelmd = output standard error expected in celmod (pa)
!                (not currently returned)
!     ctemp  = input cladding temperature (k)
!     fnck   = input effective fast fluence (neutrons/(m**2))
!     cwkf   = input effective cold work (unitless ratio of areas)
!     deloxy = input average oxygen concentration excluding
!              oxide layer - average oxygen concentration of
!              as-received cladding  (kg oxygen/kg zircaloy)
!
!     the young's modulus calculated by this function is based on
!     data from the following references
!     (1) l. r. bunnell, g. b. mellinger  and j. l. bates, high
!         temperature properties of zircaloy - oxygen alloys, epri
!         report np - 524 (1977).
!     (2) e. s. fisher and c. j. renken, physical review 135 2a
!         (20 july 1964) pp a482 - 494.
!     (3) p. e. armstrong and h. l. brown, transactions of the
!         metallurgical society of aime 230 (august 1964)
!         pp 962 - 966
!     (4) a. padel and a. groff, journal of nuclear materials 59
!         (1976) pp 325-326
!     (5) w. r. smalley, saxton core ii fuel performance evaluation.
!         part i: materials, wcap-3385-56 (september 1971)
!
!     the expected standard error was derived using additional
!     data from the following references
!     (1) c. c. busby and c.r. woods (eds.) "properties of zircaloy-4
!         tubing", usaec report wapd-tm-585 (december 1966) p 65
!     (2) z. spasic, m. pavlovic and g. simis, conference on
!         the use of zirconium alloys in nuclear reactors, marianske
!         lanze, czech. conf-681086 (1968) pp 277 - 284
!     (3) r. l. mehan, modulus of elasticity of zircaloy-2 between
!         room temperature and 1000 f, kapl-m-rlm-16 (july 1958)
!     (4) d. o. northwood, i. m london, and l. e. bahen, journal
!         of nuclear materials 55 (1975) pp 299-310
!     (5) f. r. shober, j. a. van echo, l. l. marsh jr. and
!         j. r. keeler, the mechanical properties of zirconium  and
!         zircaloy-2, bmi-1168 (1957)
!
!     celmod was coded by r. l. miller in march 1974.
!     last modified by d. l. hagrman march 1982
!
$if def,in32,1
       implicit real*8(a-h,o-z)
!
      if(ctemp .lt. 2098.) then
      c1 = (1.16e+11 + ctemp*1.037e+08) * 5.7015
      c2 = 1.0
      if(fnck .gt. 1.0e+22) c2 = 0.88*(1.0 - exp(-fnck/1.0e+25))
     #                           + exp(-fnck/1.0e+25)
      c3 = -2.6e+10
      celmod = (1.088e+11 - 5.475e+07*ctemp + c1*deloxy + c3*cwkf)/c2
      if(ctemp .lt. 1090. ) go to 101
!
!     calculate a to a + b and a + b to b boundaries
      wfox = deloxy + 0.0012
      taab = 1094. + wfox * (-1.289e+03 + wfox * 7.914e+05)
      if ( wfox .lt. 0.025) go to 15
      taab = 1556.4 + 3.8281e+04 * (wfox - 0.025)
   15 tabb = 392.46 * ((100*deloxy + 0.1242807)**2 + 3.1417)
      if (deloxy .lt. 4.7308937e-03) go to 25
      tabb = (100 * deloxy + 0.12) * 491.157 + 1081.7413
   25 continue
!
      if (ctemp .lt. taab) go to 101
      if (ctemp .gt. tabb) go to 35
      amodl  = (1.088e+11 - 5.475e+07*taab + c1*deloxy + c3*cwkf )/c2
      amodr  = 9.21e+10 - tabb * 4.05e+07
      celmod = amodl + (ctemp - taab ) * (amodr - amodl )/(tabb - taab)
      go to 101
   35 celmod = 9.21e+10 - ctemp * 4.05e+07
  101 continue
!
      else
         celmod = 1.0e-10
      endif
      ucelmd = 6.4e+09
!
      return
      end
