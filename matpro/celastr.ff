       subroutine celastr (costh2,costh4,cosfi2,cosfi4,ct2cf2,ct2cf4,
     & ct4cf2,ct4cf4,ctemp,fnck,cwkf,deloxy,crs)
!  Returns the single crystal compliance constants of H1-alloy as a
!  function temperature and oxygen content.
!
!  Data were derived from:
!  (1) Prasolov: private communication.
!  (2) Fedorov et.al, "Influence of Oxygen Content and Gamma-Irradiation
!      on Young's Module and Internal Friction of Pure Zirconium.
!  (3) Padel, A and Groff, A J., Nucl. Mater. 69 (1976), 325.
!  (4) SCDAP/RELAP5/MOD3.1 CODE.MANUAL, Volume IV: MATPRO-A, Library of
!      Material Properties for Light-Water Accident Analisis,
!      NUREG/CR 6150 EGG 2720 1993.  
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       real(kr) costh2,costh4,cosfi2,cosfi4,ct2cf2,ct2cf4,ct4cf2,ct4cf4,
     & ctemp,fnck,cwkf,deloxy,crs(9)
!
       real(kr) cs(6),poisrt
!
       if (ctemp .lt. 883.) then
! 
!  Alpha Phase of H1-alloy.
         cs(1) = 9.91e-12 + 4.35e-15*(ctemp - 273.) +
     &   0.1644e-16*(ctemp - 273.)**2  
         cs(2) = 7.90e-12 + 2.38e-15*(ctemp - 273.) +
     &   0.0116e-16*(ctemp - 273.)**2
         cs(3) = 30.9e-12 + 4.75e-15*(ctemp - 273.) +
     &   0.1675e-16*(ctemp - 273.)**2
         cs(5) = -3.85e-12 - 2.87e-15*(ctemp - 273.) -
     &   0.1397e-16*(ctemp - 273.)**2
         cs(6) = -2.38e-12 - 1.18e-15*(ctemp - 273.) -
     &   0.0047e-16*(ctemp - 273.)**2
         cs(4) = 2.0*(cs(1) - cs(5))
         cs(1) = cs(1)*(1. - 45.3*deloxy*cs(1)/
     &   (8.e-12 + 3.021*deloxy*cs(1)))
       else
!
!   Transition Region
         cs(1) = 22.4e-12 + (ctemp - 1153.)*
     &   (22.4e-12 - 12.6e-12)/270.
         cs(2) = 22.4e-12 + (ctemp - 1153.)*
     &   (22.4e-12 - 9.4e-12)/270.
         cs(3) = 58.7e-12 + (ctemp - 1153.)*
     &   (58.7e-12 - 33.8e-12)/270.
         cs(5) = -7.5e-12 + (ctemp - 1153.)*
     &   (-7.5e-12 + 5.6e-12)/270. 
         cs(6) = -7.5e-12 + (ctemp - 1153.)*
     &   (-7.5e-12 + 3.1e-12)/270. 
!
       endif
!  
       crs(1) = (cosfi4 - 2.0*ct2cf4 + ct4cf4 + 2.0*ct2cf2 -
     & 2.0*ct4cf2 + costh4)*cs(1) + (1.0 - 2.0*cosfi2 +
     & cosfi4 - 2.0*costh2 + 4.0*ct2cf2- 2.0*ct2cf4 + costh4 -
     & 2.0*ct4cf2 + ct4cf4)* cs(2) + (-cosfi4 + 2.0*ct2cf4 -
     & ct4cf4 + cosfi2 - 3.0*ct2cf2 + 2.0*ct4cf2 + costh2 -
     & costh4)*(2.0*cs(6) + cs(3))
       crs(2) = (cosfi4 - 2.0*ct2cf4 + ct4cf4 - 2.0*cosfi2 +
     & 2.0*ct2cf2 + 1.0)*cs(1) + (cosfi4 - 2.0*ct2cf4 +
     & ct4cf4)*cs(2) + (cosfi2 - cosfi4 + 2.0*ct2cf4 - ct2cf2 -
     & ct4cf4)*(2.0*cs(6) + cs(3))
       crs(3) = (1.0 - 2.0*costh2 + costh4)*cs(1) + (costh2 -
     & costh4)*(2.0*cs(6) + cs(3)) + costh4*cs(2)
       crs(4) = (2.0*(1.0 - costh2 - cosfi2) + 6.0*ct2cf2 -
     & 4.0*ct4cf2)*cs(1) + 4.0*(ct2cf2 - ct4cf2)*cs(2) +
     & (costh2 + cosfi2 - 5.0*ct2cf2 + 4.0*ct4cf2)*cs(3) +
     & 2.0*(-1.0+ costh2 + cosfi2 - ct2cf2)*cs(5) +
     & 8.0*(-ct2cf2 + ct4cf2)*cs(6)
       crs(5) = (costh2 - costh4 + 0.5*cosfi2 - 1.5*ct2cf2 +
     & ct4cf2)*cs(1) + (costh2 - costh4 - ct2cf2 + ct4cf2)*cs(2) +
     & (1.0 - 4.0*costh2 + 4.0*costh4 - cosfi2 +
     & 5.0*ct2cf2 - 4.0*ct4cf2)*0.25*cs(3) - 0.5*(cosfi2 -
     & ct2cf2)*cs(5) - 2.0*(costh2 - costh4 -ct2cf2 + ct4cf2)*cs(6)
       crs(5) = 4.0*crs(5)
       crs(6) = (0.5*costh2 + cosfi2 - cosfi4 - 2.0*ct2cf2 +
     & 2.0*ct2cf4 + ct4cf2 - ct4cf4)*cs(1) + (cosfi2 - cosfi4 -
     & 2.0*ct2cf2 + 2.0*ct2cf4 + ct4cf2 - ct4cf4)*cs(2) +
     & (1.0 - 4.0*cosfi2 + 4.0*cosfi4 - costh2 +
     & 8.0*ct2cf2 - 8.0*ct2cf4 - 4.0*ct4cf2 +
     & 4.0*ct4cf4)*0.25*cs(3) - 0.5*costh2*cs(5) -
     & 2.0*(cosfi2 - cosfi4 - 2.0*ct2cf2 + 2.0*ct2cf4 +
     & ct4cf2 - ct4cf4)*cs(6)
       crs(6) = 4.0*crs(6)
       crs(7) = (cosfi2 - cosfi4 - 2.0*ct2cf2 + 2.0*ct2cf4 +
     & ct4cf2 - ct4cf4)*cs(1) + (cosfi2 - cosfi4 -2.0*ct2cf2 +
     & 2.0*ct2cf4 + ct4cf2 - ct4cf4)*(cs(2) - cs(3)) + costh2*cs(5) +
     & (1.0 - costh2 - 2.0*cosfi2 + 2.0*cosfi4 +
     & 4.0*ct2cf2 - 4.0*ct2cf4 - 2.0*ct4cf2 +
     & 2.0*ct4cf4)*cs(6)
       crs(8) = (ct2cf2 - ct4cf2)*(cs(1) + cs(2) - cs(3)) +
     & (1.0 - costh2 - cosfi2 + ct2cf2)*cs(5) + (costh2 + cosfi2 -
     & 3.0*ct2cf2 + 2.0*ct4cf2)*cs(6)
       crs(9) = (costh2 - costh4 - ct2cf2 + ct4cf2)*(cs(1) +
     & cs(2) - cs(3)) + (cosfi2 - ct2cf2)*cs(5) + (1.0 -
     & 2.0*costh2 + 2.0*costh4 - cosfi2 + 3.0*ct2cf2 -
     & 2.0*ct4cf2)*cs(6)
!
       if (ctemp .ge. 1153) then
!  Beta Phase.
         crs(1) = 22.4e-12 + 30.6e-15*(ctemp - 1153.)
         crs(2) = crs(1)
         crs(3) = crs(1)
!  It is inputed poisson's ratio in beta phase = 0.335.
         poisrt = 0.335
         crs(4) = 2.*(1. + poisrt)*crs(1)
         crs(5) = crs(4)
         crs(6) = crs(4)
         crs(7) = crs(1) - 0.5*crs(4)
         crs(8) = crs(7)
         crs(9) = crs(7)
         if (ctemp .gt. 2133.) crs(1:9) = 1.0e10      
       endif
       return  
       end
