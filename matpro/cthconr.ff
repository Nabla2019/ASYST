       subroutine cthconr (ctemp,ccon,cdkdt)
!  cthconr returns zircaloy thermal conductivity.
!
!  ccon   = output thermal conductivity of zircaloy (w/(m*k)).
!  cdkdt  = output derivative ofthermal conductivity with respect to
!           temperature (w/(make-work)).
!  ctemp  = input cladding temperature (k).
!  cthconr was coded by R. L. Miller December 1975.
!  Last modified by D. L. Hagrman March 1982.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       real(kr) ctemp,ccon,cdkdt
!
       include 'wwer.h'
!
       real(kr) ctcel,ctemp1,ctemp2
       real(kr) polate
       real(kr) cconww(12) 
       data cconww/ 27.8, 1273., 29.0, 1373.,
     &              30.1, 1473., 31.2, 1573.,
     &              32.3, 1673., 33.4, 1773. /
       integer iu,npww
       data npww/6/, iu/1/      
!
       if (iwwer .eq. 0) then
!
         if (ctemp .lt. 2098.0) then
           ccon = 7.511 + ctemp*(2.088e-02 + ctemp*(-1.45e-05 +
     &     ctemp*7.668e-09))
           cdkdt = 2.088e-02 + ctemp*(-2.9e-05 + ctemp*2.3e-08)
         else
           ccon = 36.0
           cdkdt = 0.0
         endif      
       else
!
!  This code block calculates thermal conductivity (w/(m*k)) and 
!  derivative of thermal conductivity with respect to temperature 
!  (w/(m*k*k)) of H1-alloy. Data is derived from:
!  Volkov B. Yu. et. al., "Material Property Library for H1-Alloy
!  Cladding", Preprint IAE-4941/11, 1989.   
!
         ctcel = ctemp - 273.0
         if (ctcel .le. 100.0) then
!
           ccon = 9.5e-3*ctcel + 17.01
           cdkdt = 9.5e-3
         elseif (ctemp .le. 673.0) then
           ccon = 10.3848*ctcel**0.12157*
     &     exp(-0.115e-3*ctcel)
	   cdkdt = 1.26248*ctcel**(-0.87843)*
     &     exp(-0.115e-3*ctcel) - 1.194252e-3*
     &     ctcel** 0.12157*exp(-0.115e-3*ctcel) 
         elseif (ctemp .le. 1273.0) then
	   ccon = 167.88*ctcel**(-0.429)*
     &     exp(1.169e-3*ctcel)
	   cdkdt = -72.0205*ctcel**(-1.429)*
     &     exp(1.169e-3*ctcel) + 0.19625172e-3*
     &     ctcel**(-0.429)*exp(1.169e-3*ctcel) 
         elseif (ctemp .le. 1573.0) then
	   ccon = polate(cconww,ctemp,npww,iu)
           ctemp1 = ctemp*1.05
           ctemp2 = ctemp*0.95
           cdkdt = 0.5*(polate(cconww,ctemp1,npww,iu) - ccon)/
     &     (ctemp*0.05)
           cdkdt = -0.5*(polate(cconww,ctemp2,npww,iu) - ccon)/
     &     (ctemp*0.05) + cdkdt
         else
           ccon = exp(0.000461843*ctemp)*15.0636
           cdkdt = 0.006957018215*exp(0.000461843*ctemp)            
         endif
       endif
       return 
       end
