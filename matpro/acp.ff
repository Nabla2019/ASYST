       function acp(ictype,atemp)
c
c     acp returns the specific heat capacity at constant
c     pressure for two types of neutron absorbers
c
c     acp    = output absorber specific heat capacity at
c              constant pressure (j/(kg*k)
c     ictype = input absorber material type
c                 1 = silver-indium-cadmium (.8,.15.05)
c                 2 = boron carbide
c     atemp  = input absorber temperature (k)
c  acp was coded by d. l. hagrman march 1982.
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      if(ictype .lt. 2) then
         if(atemp .lt. 1050.) then
            cpag = 21.3 + atemp*4.27e-03 + 1.51e+05/(atemp**2)
            cpin = 24.3 + atemp*1.05e-02
            cpcd = 22.2 + atemp*1.23e-02
            acp = 9.17 * (0.808*cpag + 0.143*cpin + 0.049*cpcd)
         else
            acp = 254.1542
         endif
      else
         if(atemp .lt. 2700.) then
            acp = 563. + atemp*(1.54 - atemp*2.94e-04)
         else
            acp = 2577.740
         endif
      endif
      return
      end
