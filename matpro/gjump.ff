      function gjump(gmix,gstemp, gpres, isurf)
c
c     gjump calculates the effective jump distance at gap surfaces
c     as a function of gas temperature at the surface, gas component
c     fractions, gas pressure , gap surface material and
c     whether the surface is a heat source or a heat sink.
c
c     gjump    = output effective jump distance at gap surface (m)
c     gmix(i)  = input mole fractions of the gas components (unitless)
c                   i = 1 is helium
c                   i = 2 is argon
c                   i = 3 is krypton
c                   i = 4 is xenon
c                   i = 5 is hydrogen
c                   i = 6 is nitrogen
c                   i = 7 is oxygen
c                   i = 8 is carbon monoxide
c                   i = 9 is carbon dioxide
c                   i =10 is water vapor
c     gstemp   = input gas temperature at the surface (k)
c     gpres    = input gas pressure (pa)
c     isurf    = input gap surface material parameter
c                   isurf = 0 for zircaloy surface heat sink
c                   isurf = 1 for fuel surface heat source
c                   isurf .gt. 1 returns effective mean free path
c                                (heat source with accomodation
c                                coefficient of 1)
c
c     the function gascon is called by gjump
c
c     gjump was coded by d. l. hagrman october 1979
c
$if def,in32,1
       implicit real*8(a-h,o-z)
       include "ufiles.h"
      dimension gmix(10),w(10),acf(10),acc(10),ar(10),br(10)
      data w  /4.003,39.944,83.800,131.300,
     #  2.016,28.020,32.000,28.010,44.010,18.020/
      data acf /0.34,0.80,0.85,0.85,0.34,0.85,0.85,0.85,0.85,0.85/
      data acc /0.071,0.16,0.85,0.85,0.071,0.20,0.20,0.20,0.85,0.20/
      data ar /1.6669,1.6667,1.6667,1.6667,
     #  1.4045,1.4006,1.4006,1.4006,1.2857,1.2857/
      data br /-2.e-9,3.53e-8,3.53e-8,3.53e-8,
     #  2.5e-9,2.21e-8,2.21e-8,2.21e-8,6.29e-8,6.29e-8/
      if(gstemp .lt. 4000.) go to 20
   10 write (output,900) gstemp,gmix
  900 format(26h1gjump  input bad. gstemp=,1pe12.4,2h k,/
     #       10h0fractions, (10e12.4))
      stop
   20 x = 0.0
      do 30 i = 1,10
   30 x = x + gmix(i)
      if(x .lt. 0.9 .or. x .gt. 1.1) go to 10
      gthcon = 0.0
      do 70 i = 1,10
      if(gmix(i) .le. 1.0e-9) go to 70
      sphi = ar(i) + br(i) * gpres
      ac = 1.0
      if(isurf .eq. 0) ac = acc(i)/(1. - acc(i))
      if(isurf .eq. 1) ac = acf(i)
      yi = sqrt(w(i)*gstemp)*(sphi-1.)/(18.0 * (sphi+1.)*ac*gpres)
      gthcon = gthcon + gmix(i)/yi
   70 continue
      x = gascon(gmix,gstemp, gpres)
      gjump = x/gthcon
      return
      end
