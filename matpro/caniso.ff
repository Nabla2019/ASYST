      subroutine caniso (dep1,dep2,dep3,ctemp,f1p,f2p,f3p,a1d,a2d,
     #                   a3d , a1s, a2s,  a3s,a1e,a2e,a3e)
c
c
c     caniso calculates coefficients of anisotropy for relating
c     effective stress to stress components and relating
c     effective strain to strain components
c
c     a1s   = output coefficient of anisotropy for calculating
c             effective stress (unitless)
c     a2s   = output coefficient of anisotropy for calculating
c             effective stress (unitless)
c     a3s   = output coefficient of anisotropy for calculating
c             effective stress (unitless)
c
c     the form of the equation for effective stress is assumed to be
c     effective stress = ( a1s * (sigma 1 - sigma 2)**2 +
c                          a2s * (sigma 2 - sigma 3)**2 +
c                          a3s * (sigma 3 - sigma 1)**2 )**0.5
c     where sigma 1, sigma 2 and sigma 3 are principal axis
c     stress components
c
c     a1e   = output coefficient of anisotropy for calculating
c             effective strain
c     a2e   = output coefficient of anisotropy for calculating
c             effective strain
c     a3e   = output coefficient of anisotropy for calculating
c             effective strain
c
c     the form of the equation for effective strain is assumed to be
c     effective strain = sum of effective strain increments
c
c     with the strain increments defined in terms of strain
c     increment components by
c
c     del effective strain = (1.0/(a1e*a2e + a2e*a3e + a3ea1e)) *
c                          a1e * (a2e * dep1 - a3e * dep2)**2) +
c                          a2e * (a3e * dep2 - a1e * dep3)**2) +
c                          a3e * (a1e * dep3 - a2e * dep1)**2))**0.5
c
c     where dep1, dep2 and dep3 are plastic strain increment
c     components
c
c     f1p   = output volume weighted average cosine of the angle
c             between basal poles and coordinate axis 1 at the end
c             of the current time step
c     f2p   = output volume weighted average cosine of the angle
c             between basal poles and coordinate axis 2 at the end
c             of the current time step
c     f3p   = output volume weighted average cosine of the angle
c             between basal poles and coordinate axis 3 at the end
c             of the current time step
c
c     a1d   = output high temperature strain anisotropy coefficient
c              at the end of the current time step (unitless)
c     a2d   = output high temperature strain anisotropy coefficient
c              at the end of the current time step (unitless)
c     a3d   = output high temperature strain anisotropy coefficient
c              at the end of the current time step (unitless)
c
c     ctemp = input cladding temperature (k)
c     dep1  = input cladding plastic strain increment along 1 st
c              axis durring the current time step (unitless)
c     dep2  = input cladding plastic strain increment along 2 nd
c              axis durring the current time step (unitless)
c     dep3  = input cladding plastic strain increment along 3 rd
c              axis durring the current time step (unitless)
c     f1p   = input volume weighted average cosine of the angle
c             between basal poles and coordinate axis 1 at the start
c             of the current time step
c     f2p   = input volume weighted average cosine of the angle
c             between basal poles and coordinate axis 2 at the start
c             of the current time step
c     f3p   = input volume weighted average cosine of the angle
c             between basal poles and coordinate axis 3 at the start
c             of the current time step
c     a1d   = input high temperature strain anisotropy coefficient
c             at the start of the current time step.  set = 0.5
c             for the first step
c     a2d   = input high temperature strain anisotropy coefficient
c             at the start of the current time step.  set = 0.5
c             for the first step
c     a3d   = input high temperature strain anisotropy coefficient
c             at the start of the current time step.  set = 0.5
c             for the first step
c
c     the correlations used in this subcode are based on data from:
c        c. r. hann et. al.,  transient deformation properties
c        of zircaloy for loca simulation, np-526, volume 3 (march 1978).
c
c     caniso was coded by d. l. hagrman october 1978
c     modified by d. l. hagrman in june 1979
c     modified by d. l. hagrman july 1981 to keep a1d,a2d,a3d,
c          a1p,a2p, and a3p in the range 0 - 1.5 (cdr-mp-04)
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
c     convert f factors to appropriate anisotropy coeffecients
      a3p = 1.5*f2p
      a2p = 1.5*f1p
      a1p = 1.5*f3p
c
      sao = 1.0
      if(ctemp .ge. 1090.) sao = 7.6060606 - ctemp * 6.060606e-03
      if(ctemp .ge. 1255.) sao = 0.0
      spo =  1.0/( exp((ctemp - 725.)/18.) + 1.0)
      rac = -2.757 + ctemp * 1.343e-02
      if(ctemp .le. 644.0) rac = 0.52762
      if(ctemp .ge. 1090.) rac = 6.5174
      apn = 1.0
      if(a1p .gt. 1.48) apn = 0.0
      if(a2p .gt. 1.48) apn = 0.0
      if(a3p .gt. 1.48) apn = 0.0
      if(a1p .lt. 0.02) apn = 0.0
      if(a2p .lt. 0.02) apn = 0.0
      if(a3p .lt. 0.02) apn = 0.0
      adn = 1.0
      if(a1d .gt. 1.48) adn = 0.0
      if(a2d .gt. 1.48) adn = 0.0
      if(a3d .gt. 1.48) adn = 0.0
      if(a1d .lt. 0.02) adn = 0.0
      if(a2d .lt. 0.02) adn = 0.0
      if(a3d .lt. 0.02) adn = 0.0
      a1p = a1p - rac*apn*dep3
      a2p = a2p - rac*apn*dep1
      a3p = a3p - rac*apn*dep2
c     if(a1p .gt. 1.50) print 901
      if(a1p .gt. 1.5) a1p = 1.49
c     if(a2p .gt. 1.50) print 902
      if(a2p .gt. 1.5) a2p = 1.49
c     if(a3p .gt. 1.50) print 903
      if(a3p .gt. 1.5) a3p = 1.49
c     if(a1p .lt. 0.00) print 904
      if(a1p .lt. 0.00) a1p = 0.01
c     if(a2p .lt. 0.00) print 905
      if(a2p .lt. 0.00) a2p = 0.01
c     if(a3p .lt. 0.00) print 906
      if(a3p .lt. 0.00) a3p = 0.01
      a1d = a1d - rac*adn*dep3
      a2d = a2d - rac*adn*dep1
      a3d = a3d - rac*adn*dep2
c     if(a1d .gt. 1.50) print 901
      if(a1d .gt. 1.5) a1d = 1.49
c     if(a2d .gt. 1.50) print 902
      if(a2d .gt. 1.5) a2d = 1.49
c     if(a3d .gt. 1.50) print 903
      if(a3d .gt. 1.5) a3d = 1.49
c     if(a1d .lt. 0.00) print 904
      if(a1d .lt. 0.00) a1d = 0.01
c     if(a2d .lt. 0.00) print 905
      if(a2d .lt. 0.00) a2d = 0.01
c     if(a3d .lt. 0.00) print 906
      if(a3d .lt. 0.00) a3d = 0.01
  901  format ('0Error in anisotropic cladding deformation, a1 too large
     *, try smaller time steps.')
  902  format ('0Error in anisotropic cladding deformation, a2 too large
     *, try smaller time steps.')
  903  format ('0Error in anisotropic cladding deformation, a3 too large
     *, try smaller time steps.')
  904  format ('0Error in anisotropic cladding deformation, a1 too small
     *, try smaller time steps.')
  905  format ('0Error in anisotropic cladding deformation, a2 too small
     *, try smaller time steps.')
  906  format ('0Error in anisotropic cladding deformation, a3 too small
     *, try smaller time steps.')
      a1e = (a1p*spo + a1d*(1.0 - spo)) * sao +0.5*(1.0 - sao)
      a2e = (a2p*spo + a2d*(1.0 - spo)) * sao +0.5*(1.0 - sao)
      a3e = (a3p*spo + a3d*(1.0 - spo)) * sao +0.5*(1.0 - sao)
      a1s = a1p * sao +0.5*(1.0 - sao)
      a2s = a2p * sao +0.5*(1.0 - sao)
      a3s = a3p * sao +0.5*(1.0 - sao)
c  Convert anisotropy coefficitnts to f factors.
      f1p = a2p/1.5
      f2p = a3p/1.5
      f3p = a1p/1.5
      return
      end
