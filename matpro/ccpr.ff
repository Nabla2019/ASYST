       function ccpr(ctemp)
!  ccpr calculates the specific heat at constant pressure for zircaloys
!
!  ccpr   = output cladding specific heat at constant pressure (j/kg-k).
!  ctemp = input cladding temperature (k).
!  This code is based on data from:
!  (1) C. R. Brooks and E. E. Stansbury, "The Specific Heat of
!  Zircaloy-2 from 50 to 700 C," Journal of Nuclear Materials 18 (1966),
!  p 233.
!  (2) E. A. Eldridge and H. W. Deem, "Specific Heats and Heats of
!  Transformation of Zircaloy-2 and Low Nickel Zircaloy-2," BMI-1803
!  (May 31, 1967).
!  Conversion from J/(kg*K) to cal/(gm*C) is 2.39006d-4
!  (cal/(gm*C))/(J/(kg*K)).
!  Estimated standard error of the prediction of ccpr for the specific
!  heat of zircaloy cladding samples -- (sum of squared residuals/
!  (number of residuals-degrees of freedom))**0.5 -- is:
!     (1) for temperature less than 1090 K, 10j/kg-K),
!     (2) for temperature 1090 K to 1300 K, 25j/kg-K),
!     (3) for temperature above 1300 K ,   100j/kg-K).
!  ccpr coded by R. L. Miller October 1974.
!  Modified by D. L. Hagrman May 1976.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       real(kr) ccpr
       real(kr) ctemp
!
       include 'wwer.h'
!
       real(kr) polate
       real(kr) cpdata(28),cpwwer(24),cpwwr2(26)
       integer iu,npcp,npww,npw2
       data cpdata/   279.,  290.,  281.,  300.,
     &                302.,  400.,  331.,  640.,
     &                375., 1090.,  502., 1093.,
     &                590., 1113.,  615., 1133.,
     &                719., 1153.,  816., 1173.,
     &                770., 1193.,  619., 1213.,
     &                469., 1233.,  356., 1248./
       data npcp/14/,iu/1/
       data cpwwer/    345., 393.,  360., 473.,
     &                  370., 573.,  380., 673.,
     &                 383., 773.,  385., 873.,
     &                 448., 883.,  680., 973.,
     &                 816.,1025.,  770.,1073.,
     &                 400.,1153.,  392.,1173./
       data npww/12/
       data cpwwr2/    412.6,1100.,  420.0,1110.,
     &                 480.0,1120.,  600.0,1134.,
     &                 1000.,1142.,  1400.,1150.,
     &                 1600.,1155.,  1400.,1161.,
     &                 1000.,1168.,  600.0,1177.,
     &                 400.0,1180.,  360.0,1190.,
     &                 348.1,1200./       
       data npw2/13/    
!
       if (iwwer .eq. 0) then
         if (ctemp .lt. 1248.0) then
           ccpr = polate(cpdata,ctemp,npcp,iu)
         else
           ccpr = 356.
         endif
!
!  This block calculates the specific heat at constant pressure for
!  H1-alloy cladding. If iwwer = 1, then data is derived from
!  Volkov B. Yu. et. al, "Material Property Library for H1-calloy 
!  Cladding", Preprint IAE-4941/11, 1989. This property is induced by
!  low heating rate. If iwwer = 2, then data is derived from
!  Ljusternik, V. E. et. al, "Experimental Research of Zirconium Reactor
!  Materials Thermal Properties:110-Alloy", High Temperature Thermal
!  Physics." V.31 N.4, 1993. This property is induced by high-rate
!  heating >1000 K/s. 
!
!      elseif (iwwer .eq. 1) then
!        if (ctemp .lt. 1173.0) then
!          ccpr = polate(cpwwer,ctemp,npww,iu)
!        else
!          ccpr = 392.
       else
         if (ctemp .le. 1100.) then  
           ccpr = 237.5 + 15.91e-2*ctemp
         elseif (ctemp .le. 1200.) then
           ccpr = polate(cpwwr2,ctemp,npw2,iu)
         else
           ccpr = 199.7 + 12.364e-2*ctemp
         endif
       endif
       return
       end
