      function emftxp (ftemp,facmot,ftmelt)
c   fthexp calculates fuel linear thermal expansion for solid and
c   liquid phases as a function of temperature and liquid fraction.
c   ftemp=input fuel temperature(k)
c   facmot=input fuel fraction molten.  facmot=0. fuel all solid
c   =1. fuel all molten
c   ftmelt=input fuel melting temperature (k)
c   fthexp=output fractional fuel thermal expansion
c   no units        (lt - l298)/l298
c   the thermal expansion relationships used in this subroutine are
c   (1)solid is based on data of conway et al (1000c to 2250c) with
c   extrapolation of data down to 200c and up to 2800c,
c   reference= trans.am.nuc.soc.vol6(1) june 1963;
c   (2)molten is based on data of christensen (melting to 3200c),
c   reference= j.am.cer.soc. vol46(12) december 1963.
c   fthexp was set up by v.f.baston in march 1974.
c   recoded by r.c.young, april 74
      data a, b, c, d / -1.723 e-4, 6.797 e-6, 2.896 e-9, 3.103 e-2 /
c   d = expansion at melting = 1.096**(1/3) - 1.
      data e / 3.5 e-5 /, eps / 1.e-10 /, ck / 273.15 e0 /
      tm = ftemp-ftmelt
      if (tm+eps.ge.0.) go to 110
c   solid
      tc = ftemp-ck
      fm = 0.
100   fthexp = fm+a+tc*(b+tc*c)
      emftxp = fthexp
      return
110   tc = ftmelt-ck
      if (tm.gt.eps) go to 120
c   melting
      fm = d*facmot
      go to 100
c   molten
120   fm = d+e*tm
      go to 100
      end
