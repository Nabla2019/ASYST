       function gascns(gmix, gtemp, gpres)
c
c
c  gascns calculates gas thermal conductivity as a function of
c  temperature and gas component fractions.  Gap surface effects
c  are not considered.
c
c  Cognizant engineer: dlh.
c
c
c     gascns   = output gas thermal conductivity (w/(m*k))
c     ugask(i) = output standard error expected for the thermal
c                conductivity of each component of the gas mix
c                (w/(m*k)).  this output is calculated in gascns
c                but not returned
c     gmix(i)  = input mole fractions of the gas components (unitless)
c                   i = 1 is helium
c                   i = 2 is argon
c                   i = 3 is krypton
c                   i = 4 is xenon
c                   i = 5 is hydrogen
c                   i = 6 is nitrogen
c                   i = 7 is oxygen
c                   i = 8 is carbon monoxide
c                   i = 9 is carbon dioxide
c                   i =10 is water vapor
c     gtemp    = input gas temperature (k)
c     gpres    = input gas pressure (pa)
c
c     the gas thermal conductivity calculated by this function is
c     based on data from the following references
c     (1)  w. g. kannuluik and e. h. carman, thermal conductivity
c          of rare gases, the proceedings of the physical society
c          65 (1952) pp 701 - 709.
c     (2)  r. s. gambhir, j. m. gandhi and s. c. saxena, thermal
c          conductivity of rare gases, deuterium and air, indian
c          journal of pure and applied physics 5 (1967) pp 457 - 463.
c     (3)  h. von ubisch, the thermal conductivities of mixtures
c          of rare gases ar 29c and at 520c, arkiv foer fysik 16
c          (1959) pp 93 - 100.
c     (4)  v. k. saxena and s. c. saxena, measurement of the
c          thermal conductivity of helium using a hot-wire type
c          of thermal diffusion column, brittish journal of applied
c          physics 1 (1968) pp 1341 - 1351.
c     (5) d. l. timrot and e. e. totskii, dilatometric method
c          for the experimental determination of the thermal
c          conductivity of corrosive gases and vapors at high
c          temperature, high temperature 3 (1965) pp 685 - 690.
c     (6)  d. l. timrot and a. s. umanskii, investigation of
c          the thermal conductivity of helium in the range
c          400 - 2400 k, high temperature 3 (1965) pp 345 - 351.
c     (7)  l. s. zaitseva, an experimental investigation of the
c          heat conductivity of monatimic gases over wide
c          temperature intervals, soviet physics - technical
c          physics 4 (1959) pp 444 - 450.
c     (8)  h. cheung, l. a. bronley and e. r. wilke, thermal
c          conductivity of gas mixtures, aiche journal 8 (1962)
c          pp 221 - 228.
c     (9)  r. s. brokaw, predicting transport properties of dilute
c          gases, i and ec process design and development 8 (1969)
c          pp 240 - 253.
c     (10) d. l. timrot and a. s. umanskii, thermal conductivity
c          of hydrogen and argon, high temperature 4 (1966)
c          pp 289 - 292.
c     (11) f. g. keyes, the heat conductivity, viscosity, specific
c          heat and prandtl numbers for thirteen gases, np-4621
c          (1952).
c
c     gascns was coded by d. l. hagrman october 1979
c     modified by d. l. hagrman july 1981 to prevent negative
c          steam conductivity at high temperature (cdr-mp-03)
c
$if def,in32,1
       implicit real*8(a-h,o-z)
       include "ufiles.h"
c
      dimension gmix(10),a(9),b(9),c(10),w(10),au(9),bu(9),ugask(10)
c
      data a  /2.639e-03,2.986e-04,8.247e-05,4.351e-05,
     #  1.097e-03,5.314e-04,1.853e-04,1.403e-04,9.460e-06/
      data b  /0.7085,0.7224,0.8363,0.8616,
     #  0.8785,0.6898,0.8729,0.9090,1.312/
      data au  /8.00e-07,4.96e-10,1.45e-09,2.77e-08,
     #  2.10e-06,2.64e-06,2.34e-09,1.00e+00,8.78e-12/
      data bu  /1.50,2.25,2.00,1.50,1.50,1.00,2.00,1.00,3.00/
      data w  /4.003,39.944,83.800,131.300,
     #  2.016,28.020,32.000,28.010,44.010,18.020/
      if(gtemp .lt. 4000.) go to 20
   10 write (output,900) gtemp,gmix
  900 format('0gascns input bad. gtemp=',1p,e12.4,2h k/
     #       10h0fractions, (10e12.4))
      stop 'gascns'
   20 x = 0.0
      do 30 i = 1,10
   30 x = x + gmix(i)
      if(x .lt. 0.9 .or. x .gt. 1.1) go to 10
c
c     find pure gas conductivities
      do 40 i = 1,9
      ugask(i) = au(i) * (gtemp**bu(i))
   40 c(i) = a(i) * (gtemp**b(i))
      ugask(8) = 0.02 * c(8)
      if(gtemp .ge. 400.) ugask(8) = (0.02 + 4.0e-4*(gtemp - 400.)/
     # 3.) * c(8)
      tc = gtemp - 273.15
      c(10) = 17.6e-3 + tc*( 5.87e-5 + tc*( 1.04e-7 - 4.51e-11*tc))
c     use ideal gas law to find density
      if(gmix(10) .le. 1.0e-9) go to 50
      ps = gpres * gmix(10)
      den = 2.1668e-6 * ps / gtemp
      c(10) = c(10) + den*( 103.51e-3 + tc*(0.4198e-3 - 2.771e-8*tc)
     #                         + 2.1482e+11 * den/tc**4.2)
      ugask(10) = 0.06 * c(10)
      if(gtemp .le. 973.15) go to 50
      c(10) = 4.44e-06*(gtemp**1.45) + 9.5e-05 * ((2.1668e-09 *
     # gpres/gtemp)**1.3) - 1.283668e-03
      ugask(10) = 0.06 * c(10)
   50 continue
c     find mixture conductivity
      gascns = 0.
      do 70 i = 1,10
      if(gmix(i) .le. 1.0e-9) go to 70
      sum = 0.0
      do 60 j = 1,10
      if(j .eq. i) go to 60
      if(gmix(j) .le. 1.0e-9) go to 60
      rc = c(i) / c(j)
      rw = w(i) / w(j)
      rwsr = sqrt(rw)
      gij = 1. + 2.41 * (rw-1.) * (rw-0.142) / (1.+rw)**2
      fij=((1. + sqrt(rc*rwsr) )**2)/ sqrt(8.*(1.+rw))
      sum = sum + gij * fij * gmix(j)
   60 continue
      gascns = gascns + c(i) * gmix(i) / (gmix(i) + sum)
   70 continue
      return
      end
