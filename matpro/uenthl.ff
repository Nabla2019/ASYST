       function uenthl(utemp,rftemp)
c
c  calcaulates the change to enthalpy of uranium metal during a constant
c  pressure change in temperature from rftemp to utemp.
c
c     uenthl = output change in enthalpy (j/kg)
c     utemp  = input uranium temperature (k)
c     rftemp = input reference temperature (k)
c
c     uenthl was coded by j. k. hohorst, january, 1986
c
       implicit none
c
       real utemp,rftemp
       real uenthl
c
c  Local variables.
       integer n
       real h,hr,t
c
      t = rftemp
      n = -1
   10 if(t .lt. 938.) then
         h = 3.2603994e+04 + t * (1.0482e+02 + t * (2.6843e-03 +
     +       t * 3.3941e-05))
      else
         if(t .lt. 1049.) then
            h = 1.7641311e+02 * t - 6.9385273e+04
         else
            h = 1.5680756e+02 * t - 4.88190504e+04
         endif
      endif
c
      if(n .lt. 0) then
         n = n + 2
         hr = h
         t = utemp
         go to 10
      else
         uenthl = h - hr
      endif
      return
      end
