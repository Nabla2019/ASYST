      subroutine zoprp(ye,zosol,zoliq,zotc,zomt)
c
c     zoprp returns zircaloy oxide phase transition temperatures
c
c     zosol  = output zircaloy oxide solidus temperature (k)
c     zoliq  = output zircaloy oxide liquidus temperature (k)
c     zotc   = output zircaloy oxide tetragonal-to-cubic
c              phase transition temperature (k)
c     zomt   = output zircaloy oxide monoclinic-to-tetragonal
c              phase transition temperature (k)
c     ye     = input oxygen-to-metal ratio (atoms oxygen/
c              atoms metal)
c
c     zoprp was coded by d. l. hagrman march 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      zotc = 2558.
      zomt = 1478.
c     convert oxide-to-metal ratio to atomic fraction oxygen = x
      x = ye/(1. + ye)
c     solidus correlations
      if(x .le. 0.1) then
         csol = x * 1150. + 2098.
      else
         if(x .le. 0.18) then
               csol = 2213.
         else
            if(x .le. 0.29) then
               csol = 1.3895317e+03 + x*(7.6400748e+03 -
     #                x * 1.7029172e+04)
            else
               if(x .le. 0.63) then
                  csol = 2173.
               else
                  if(x .le. 0.667) then
                     csol = -1.1572454e+04 + x*2.1818181e+04
                  else
                     csol = -1.1572454e+04 + (1.334 - x) *
     #                       2.1818181e+04
                  endif
               endif
            endif
         endif
      endif
c     liquidus correlations
      if(x .le. 0.19) then
         cliq = 2.125e+03 + x*(1.6321637e+03 - x*5321.6374e+03)
      else
         if(x .le. 0.41) then
            cliq = 2.11165553e+03 + x*(1.1590909e+03 -
     #             x * 2.4621212e+03)
         else
            if(x .le. 0.667) then
               cliq = 8.9507792e+02 + x * 3.1168831e+03
            else
               cliq = 8.9507792e+02 + (1.34 - x)*3.1168831e+03
            endif
         endif
      endif
      zosol = csol
      zoliq = cliq
      return
      end
