       function zunthl(u,z,zutemp,rftemp)
!  Returns the change in enthalpy of zirconium-uranium oxygen compounds
!  during a constant pressure change in temperature.
!
!     zunthl = output change in compound enthalpy (j/kg)
!
!     u      = input atomic fraction uranium in compound
!              (atoms uranium/atoms in compound)
!     z      = input atomic fraction zirconium in compound
!              (atoms zirconium/atoms in compound)
!     zutemp = compound temperature (k)
!     rftemp = reference temperature (k)
!
!     the subcodes centhl,fenthl, and zonthl are called
!     by this function
!
!     zunthl was coded by d. l. hagrman march 1982.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
!
       real(kr) zunthl
       real(kr) u,z,zutemp,rftemp
!
       include 'contrl.h'
!
       real(kr) fuo2,fzo2,fzr,hzo2,huo2,huo2r,hzr,uo,x
       real(kr) centhl,centhlr,fenthl,zonthl
       real gaddum
!
       gaddum = 0.0
       x = 1. - u - z
       if ((u + z) .lt. 0.5*x) then
         uo = u
         u = u/(3.0*(u + z))
         z = z/(3.0*(uo + z))
         x = 1.0 - u - z
       else
         if (0.5*x .lt. u) then
           uo = u
           u = u/(3.0*u + z)
           z = z/(3.0*uo + z)
           x = 1.0 - u - z
         endif
       endif
       fuo2 = u/(u + z)
       fzo2 = (0.5*x - u)/(u + z)
       fzr  = 1.0 - fuo2 - fzo2
       if (chngno(70)) then
         hzr = centhlr(zutemp,rftemp)
       else
         hzr = centhl(zutemp,rftemp)
       endif
       huo2r = fenthl(rftemp,0.0,2.0,3113.,0.0,27.4e4,gaddum)
       huo2 = fenthl(zutemp,0.0,2.0,3113.,0.0,27.4e4,gaddum)
       huo2 = huo2 - huo2r
       hzo2 = zonthl(zutemp,rftemp)
       zunthl = (huo2*0.27*fuo2 + hzo2*0.123*fzo2 + hzr*0.091*fzr)/
     & (0.270*fuo2 + 0.123*fzo2 + 0.091*fzr)
       return
       end
