       function fthexp (ftemp,facmot)
c
c      the subprogram fthexp is used to calculate the strain of the
c           fuel caused by thermal expansion.
c      thermal expansion strain is calculated for uo2,pu02, or (u,pu)02
c           as a function of temperature, puo2 content, and fraction of
c           the fuel which is molten.
c      the correlations were derived with the assumption that the
c           thermal expansion strain is zero at 300k.
c
c      fthexp = output fuel strain due to thermal expansion (unitless)
c      ufthex = estimated standard error of fthexp (per cent)
c                 (not currently returned).
c      ftemp  = input fuel temperature (k)
c      facmot = input fuel fraction which is molten (unitless)
c                 facmot = 1.0 - fuel is all molten
c                 facmot = 0.0 - fuel is all solid
c
c      the correlations used in this subroutine are based on data from;
c        (1) p.j.baldock et al, journal of nuclear materials, 18 (1966)
c        (2) n.h.brett and l.e.russel, proceedings of the 2nd
c            international conference on plutonium metallurgy, grenoble,
c            france (1960) pp 397-410.
c        (3) m.d.burdock and h.s.parker, journal of the american
c            ceramic society,39 (1956) pp 181-187.
c        (4) j.a.christensen, journal of the american ceramic society,
c            46 (1963) pp 607-608.
c        (5) j.b.conway et al, transactions of the american nuclear
c            society,6 (1963).
c        (6) f.gronvold, journal of inorganic and nuclear chemistry,1
c            (1955) pp 357-370.
c        (7) m.hoch and a.c.momin, high temperatures-high pressures,1
c            (1969) pp 401-407.
c        (8) c.p.kempter and r.o.elliott, the journal of chemical
c            physics,30 (1958) pp 1524-1526.
c        (9) w.a.lambertson and j.h.handwerk, anl-5053 (1956).
c        (10)m.tokar et al, nuclear technology, 17 (1973) pp 147-152.
c
c      fthexp was originally coded by v.f.baston in march 1974
c      modified by c.s.olsen in feb. 1975
c      last modified by g.a.reymann in july 1978
c     modified by d. l. hagrman to remove phypro common block
c          (cdr-mp-21) may 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
       data c1u ,c2u ,c3u ,edu / 1.0e-05, 3.0e-03, 4.0e-02, 6.9e-20 /
       data c1pu,c2pu,c3pu,edpu/ 9.0e-06, 2.7e-03, 7.0e-02, 7.0e-20 /
c
       ftx (c1,c2,c3,ed,bk,t) = c1*t - c2 + c3*exp(-ed/(bk*t))
c
c     expressions to replace phypro common block
      comp = 0.0
      fdelta = 1.0e-10
      ftmelt = 3113.
       t      = ftemp
       bk     = 1.38e-23
c      bk     = boltzmann's constant (j/k)
       fcomp  = comp/100.0
       if(t .gt. ftmelt) go to 25
       fthexp = ftx(c1u ,c2u ,c3u ,edu ,bk,t)*(1.0 - fcomp) +
     #          ftx(c1pu,c2pu,c3pu,edpu,bk,t)*fcomp
       go to 100
   25  fthexm = ftx(c1u ,c2u ,c3u ,edu ,bk,ftmelt)*(1.0 - fcomp) +
     #          ftx(c1pu,c2pu,c3pu,edpu,bk,ftmelt)*(fcomp)
       if(t .ge. (ftmelt+fdelta)) go to 75
       fthexp = fthexm + .043*facmot
       go to 100
   75  fthexp = fthexm + .043 + 3.6e-05 * (t-(ftmelt+fdelta))
  100  continue
       ufthex = 10.0
       return
       end
