       subroutine  chuptk  (chorg ,ppmh2o, dp ,icm,   dco,  dci,
     # icor ,zro2ai,zro2bi,tcoi ,qci,zoxki,chydai, chydbi, chconc)
c
c     chuptk calculates average concentration of hydrogen in zircaloy
c     cladding.
c
c     chconc = output average concentration of
c              hydrogen in cladding at end of time step (ppm)
c     chydbi = output hydrogen from coolant in cladding at end
c              of time step (ppm)
c     chorg  = input initial hydrogen in cladding (ppm)
c     ppmh2o = input initial fuel water content (ppm)
c     dp     = input fuel pellet diameter (m)
c     icm    = input cladding material flag (icm = 2 for zircaloy 2,
c                                            icm = 4 for zircaloy 4)
c     dco    = input cladding outside diameter (m)
c     dci    = input cladding inside diameter (m)
c     icor   = input reactor chemistry .ge. 2 for pwr, .lt. 2 for bwr
c     zro2ai = input oxide thickness at start of current time step (m)
c     zro2bi = input oxide thickness at end of current time step (m)
c     tcoi   = input zro2-coolant interface temperature (k)
c     qci    = input axial increment heat flux (watt/meter**2)
c     zoxki  = input zircaloy oxide thermal conductivity (w/(m-k))
c     chydai = input hydrogen from coolant in cladding at start
c              of time step (ppm)
c
c     the equations used in this subroutine are based on data from
c     (1) e. hillner, hydrogen absorption in zircaloy during acqueous
c         corrosion, effect of environment, westinghouse report
c         wapd-411  (1964)
c     (2) a. van der linde, calculation of the safe lifetime expectancy
c         of zirconium alloy canning in the fuel elements of the nero
c         reactor, report rcn-41 (1965)
c     (3) f. h. megerth, c. p. ruiz, u. e. wolff, zircaloy-clad uo2
c         fuel rod evaluation program final report, general electric
c         report geap 10371 (1971)
c     (4) w. r. smalley, saxton core ii fuel performance evaluation
c         part i materials, westinghouse report wcap-3385-56 (1971)
c     (5) w. r. smalley, evaluation of saxton core iii fuel
c         materials performance, westinghouse report wcap-3385-57
c         (1974)
c     (6) e. hillner, corrosion and hydriding performance of zircaloy
c         tubing after extended exposure in the shippingport pwr,
c         symposium on zirconium in nuclear applications, astm stp
c         551 (1974)
c
c     this model should not be used outside the temperature range
c     523.15 - 673.15 k (250 - 400 c).
c
c     chuptk coded by d. l. hagrman february 1977
c     modified by d. l. hagrman june 1978
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      if(tcoi .le. 366.5)  go to 40
      if(zro2ai .le. 0.0)  zro2ai = 1.0 e-10
      tcoic = tcoi + qci * zro2ai/zoxki
      wtran = 7.7490e-06 * exp(-7.90e02/tcoic)
      if(icor.ge.2) go to 10
c
c     bwr environment
      a = 4.840e05 * exp(-1.945e-02* tcoi)
      if(tcoi.le.500.) a = 28.92887
      b = 0.12
      if(icm .le. 2)   b = 0.29
      go to 15
c
c     pwr environment
   10 a = 1.203e02 * exp(-7.118e-03* tcoi)
      if(tcoi.le.500.)   a = 3.424614
      b = 0.12
      if(icm .le. 2)     b = 0.48
   15 if(tcoi .gt. 673.) a = 1.0
      c = 1.0
      if(icm .le. 2)     c = 1.0/b
c
c     find hydrogen uptake from coolant during current time step
      d = (9.0e+05) * dco/(dco**2-dci**2)
      if(zro2ai .gt. wtran) go to 30
      if(zro2bi .ge. wtran) go to 25
c
c     all oxidation pretransition
      chupc = d * (b/(8*a)) * (zro2bi - zro2ai)
      go to 50
c
c     part of oxidation pretransition
   25 chupc = d * ((b/(8 * a)) * (wtran  - zro2ai) +
     #       (c *   b/(8 * a)) * (zro2bi - wtran))
      go to 50
c
c     all oxidation post - transition
   30 chupc = d * (c*b/(8 * a)) * (zro2bi - zro2ai)
      go to 50
c
   40 chupc  = 0.0
c
c     sum hydrogen from coolant
   50 chydbi = chydai + chupc
c
c     sum hydrogen from alloy, fuel moisture and coolant
      chconc = chorg + 0.186 * ppmh2o*((dp**2)/(dco**2 - dci**2)) +
     #         chydbi
      return
      end
