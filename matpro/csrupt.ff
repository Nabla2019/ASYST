      subroutine csrupt (ctemp,fncn,cwnf,deltmp,strrpe,ustrpe,strrpt,ust
     +rtp,ustrtn)
c   csrupt calculates the fractional increase in cladding
c   circumference at failure in a steam environment as a
c   function of temperature, fast neutron fluence, and
c   cold work.  the expected standard error is also returned.
c   strrpe = output average circumferential engineering strain
c   at rupture (m/m)
c   ustrpe = output expected standard error in strrpe (m/m)
c   strrpt = output true strain at rupture (m/m)
c   ustrtp = output positive standard error of strrpt which
c   corresponds to ustrpe (m/m)
c   ustrtn = output negative standard error of strrpt which
c   corresponds to ustrpe (m/m)
c   note  a non-symmetric distribution is obtained in terms of
c   stress because the transformation from engineering stress
c   to true stress is non-linear
c   ctemp  = input average cladding temperature (k)
c   fncn   = input effective fast fluence for strain hardening
c   exponent (neutrons/(m**2))
c   cwnf   = input effective cold work for strain hardening
c   exponent (unitless ratio of areas)
c   deltmp = input circumferential temperature variation
c   --hottest less coldest temperatures (k)
c   estimated values of deltmp are
c   (a) for cladding with external thermocouples and in film
c   boiling  deltmp = 150k
c   (b) for cladding with internal heating in steam and
c   without external thermocouples
c   deltmp = 0 if temperature is above 1255k
c   = 50 * (1255 - temperature) / 165 if
c   temperature is between 1090 and 1255k
c   = 50k if temperature is below 1090k
c   (c) for cladding in a furnace
c   deltmp = 0k
c   (d) for self - resistance heated cladding with cool filler
c   deltmp = (heating rate)/(1000k/s) * values from (b)
c   the equations used in this subroutine are based on data from
c   (1) c.c. busby and k.b. marsh, high temperature deformation
c   burst characteristics of recrystallized zircaloy-4 tubing
c   wapd-tm-900 (1970)
c   (2) d.g. hardy,  high temperature expansion and rupture
c   behavior of zircaloy tubing   in topical meeting on
c   water reactor safety conf-7303 (1973)
c   (3) d.o. hobson and p.l. rittenhouse,  deformation and rupture
c   behavior of light water reactor fuel cladding ornl-4727
c   (1971)
c   (4) d. o. hobson, m. f. osborne, and g. w. parker,  comparison
c   of rupture data from irradiated fuel rods and unirradiated
c   cladding,  nucl. tech. 17 (august 1971) p 479
c   (5) r. a. lorenz, d. o. hobson and g. w. parker,  fuel rod
c   failure under loss of coolant conditions in treat,  nucl.
c   tech. 17 (august 1971) p 502
c   (6) r. h. chapman, multirod burst test program quarterly
c   report for july - september, 1975, ornl-tm-5154
c   (december 1975)
c   (7) r. h. chapman, multirod burst test program quarterly
c   report for october - december, 1975,  ornl/nureg/tm-10
c   (may 1976)
c   (8) a. a. bauer, l. m. lowry, and j. s. perrin, evaluating
c   strength and ductility of irradiated zircaloy. quarterly
c   progress report for january through march, 1976, bmi-nureg-
c   1948 (march 1976)
c   (9) a. l. bement, jr., effects of cold work and neutron
c   irradiation on the tensile properties of zircaloy-2,
c   usaec report hw-74955
c   coded by d. l. hagrman july 1976
c   modified by d.l. hagrman june 1978.
      t = ctemp
c   calculate  strain at rupture for symmetric case
      p = 1.0
      q = 1.0
      if (t.gt.1090.) go to 100
      strrpe = 1.98e-1+t*(4.16e-04+t*2.06e-07)
      p = (exp(-2.1e01*cwnf)+3.3e-01)/1.33
      if (fncn.ge.1.0e21) q = (1.0+2.0*exp(-fncn/1.0e23))/3.0
      strrpe = strrpe*p*q
      go to 130
100   if (t.gt.1170.) go to 110
      strrpe = 9.06231055-t*7.491855e-03
      go to 130
110   if (t.gt.1600.) go to 120
      strrpe = -1.436+t*(2.045e-03-t*4.82e-07)
      go to 130
120   strrpe = 6.0208e-1
130   continue
c   modify strain at rupture for temperature gradient effects
      strrpe = strrpe*exp(-1.11e-02*deltmp)
      ustrpe = 8.0e-02
      if (t.lt.800.) go to 140
      ustrpe = (3.6e-01/2.90e+02)*(t-8.00e+02)+8.00e-02
      if (t.lt.1090.) go to 140
      ustrpe = -4.125e-03*(t-1.09e+03)+4.4e-01
      if (t.lt.1170.) go to 140
      ustrpe = 1.1e-01
140   if (ustrpe.gt.(0.9*strrpe)) ustrpe = 0.9*strrpe
c   conversion to true strains follows
      strrpt =  log(1.0+strrpe)
      ustrtp =  log(1.0+strrpe+ustrpe)-strrpt
      ustrtn = strrpt- log(1.0+strrpe-ustrpe)
      return
      end
