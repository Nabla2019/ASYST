      subroutine caneal (cflux,delh,ctemp,rtemp,fnck,fncn,cwkf,cwnf)
c
c     caneal calculates the change in effective fluence and
c     effective cold work during a time step
c
c     fnck  = output effective fast fluence for strength coefficient
c             at time step finish (neutrons/(m**2))
c     fncn  = output effective fast fluence for strain hardening
c             exponent at time step finish (neutrons/(m**2))
c     cwkf  = output effective cold work for strength coefficient
c             at time step finish (unitless ratio of areas)
c     cwnf  = output effective cold work for strain hardening
c             exponent at time step finish (unitless ratio of areas)
c
c     cflux = input fast neutron flux ((neutrons/m**2)/s)
c     delh  = input time step size (s)
c     ctemp = input cladding temperature at start of time step (k)
c     rtemp = input average rate of change of temperature (k/s)
c     fnck  = input effective fast fluence for strength coefficient
c             at time step start (neutrons/(m**2))
c     fncn  = input effective fast fluence for strain hardening
c             exponent at time step start (neutrons/(m**2))
c     cwkf  = input effective cold work for strength coefficient
c             at time step start (unitless ratio of areas)
c     cwnf  = input effective cold work for strain hardening
c             exponent at time step start (unitless ratio of areas)
c
c     the equations used in this subroutine are based on data from
c     (1)  a. a. bauer, l. m. lowry, and j. s. perrin, evaluating
c          strength and ductility of irradiated zircaloy. quarterly
c          progress report for april through june 1976,
c          bmi-nureg-1956 (july 1976)
c     (2)  a. a. bauer, l. m. lowry, and j. s. perrin, evaluating
c          strength and ductility of irradiated zircaloy.
c          quarterly progress report for july through
c          september, 1976, bmi-nureg-1961 (october 1976)
c     (3)  a. a. bauer, l. m. lowry, and j. s. perrin, evaluating
c          strength and ductility of irradiated zircaloy. quarterly
c          progress report for october through december 1976,
c          bmi-nureg-1967 (january 1977)
c
c     coded by d. l. hagrman october 1977
c     last modified by d. l. hagrman july 1982 (cdf-mp-10)
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      t    = ctemp
c     update fluence
      fnck = fnck + cflux * delh
      fncn = fncn + cflux * delh
c     annealing model
      if(t .le. 5.0e+02) go to 12
      if(t .ge. 1.255e+03) go to 15
      te = t + rtemp * delh
      if(te .ge. 1.255e+03) go to 15
      if(cwkf .le. 1.0e-20) go to 10
      qk =  2.33e+18
      bk = (1.504e+00) * (1.0e+00 + (2.2e-25) * fnck)
      disok = qk * rtemp * delh * (t**(-7)) * 6.0
      if(abs(disok) .lt. 1.0e-04) fnisok = 1.0
      if(abs(disok) .ge. 1.5e+02) go to 2
      if(abs(disok) .ge. 1.0e-04) fnisok = (exp(disok) - 1.0)/disok
      cwkf = exp(-bk * delh * exp(-qk * (t**(-6))) * fnisok) * cwkf
   10 if(cwnf .le. 1.0e-20) go to 11
c
      qn =  2.33e+18
      bn = (1.504e+00) * (1.0e+00 + (2.2e-25) * fncn) * 8.0
      dison = qn * rtemp * delh * (t**(-7)) * 6.0
      if(abs(dison) .lt. 1.0e-04) fnison = 1.0
      if(abs(dison) .ge. 1.0e-04) fnison = (exp(dison) - 1.0)/dison
      if(abs(dison) .ge. 1.50e+02) go to 2
      cwnf = exp(-bn * delh * exp(-qn * (t**(-6))) * fnison) * cwnf
   11 if(fnck .le. 1.0e+14) go to 1
c
      bki    = 2.49e-06
      qki    = 5.35e+23
      disoki = qki* rtemp * delh * (t**(-9)) * 8.0
      if(abs(disoki) .lt. 1.0e-04) fniski = 1.0
      if(abs(disoki) .ge. 1.5e+02) go to 2
      if(abs(disoki) .ge. 1.0e-04) fniski = (exp(disoki) - 1.0)/disoki
      fnck   = fnck/1.0e+20
      fnck   = (bki * delh * (exp(-qki * (t**(-8)))) * fniski
     #          + (fnck)**(-1))**(-1)
      fnck   = fnck * 1.0e+20
c
    1 if(fncn  .le. 1.0e+14) go to 12
      bni    = bki * 1000.
      qni    = qki
      fnisni = fniski
      fncn   = fncn/1.0e+20
      fncn   = (bni * delh * (exp(-qni * (t**(-8)))) * fnisni
     #          + (fncn)**(-1))**(-1)
      fncn   = fncn * 1.0e+20
      go to 12
   15 fncn = 0.
      fnck = 0.
      cwkf = 0.
      cwnf = 0.
      go to 12
    2 print 901
  901 format(51h  time step too large for cladding annealing model ,
     #       15h  (under flow) ,
     #       50h  effective coldworks and fluences set to zero     )
c
      cwkf = 0.0
      cwnf = 0.0
      fnck = 0.0
      fncn = 0.0
c
   12 continue
      return
      end
