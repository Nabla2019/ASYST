      function gthcon(gmix, gtemp, gpres, gpthk)
c
c     gthcon calculates the conduction part of gas gap
c     heat conductance as a function of temperature, gas
c     component fractions, gap width and gas pressure.
c     gap surface effects are considered
c
c     gthcon   = output conduction part of gas gap heat
c                conductance (w/((m**2)*k))
c     gmix(i)  = input mole fractions of the gas components (unitless)
c                   i = 1 is helium
c                   i = 2 is argon
c                   i = 3 is krypton
c                   i = 4 is xenon
c                   i = 5 is hydrogen
c                   i = 6 is nitrogen
c                   i = 7 is oxygen
c                   i = 8 is carbon monoxide
c                   i = 9 is carbon dioxide
c                   i =10 is water vapor
c     gtemp    = input gas temperature (k)
c     gpres    = input gas pressure (pa)
c     gpthk    = input gap width (m)
c                a minimum gap width of 4.4e-6 is suggested to account
c                for typical fuel surface roughness
c
c     the surface effects calculated by this dunction are based on
c     data from the references listed below.  thermal conductivity
c     data references are listed in the gascon function.
c     (1)  g. k. white, experimental techniques in low temperature
c          physics, oxford press (1959) pp 181 - 183.
c     (2)  m. w. zemansky, heat and thermodynamics, mcgraw - hill
c          book company, inc (1957).
c     (3)  c. d. hodgman (ed), handbook of chemistry and physics.
c          thirty - eighth edition, chemical rubber publishing co.
c          (1956).
c
c     gthcon was coded by r. c. young march 1975
c     modified by d. l. hagrman october 1979
c     modified by d. l. hagrman july 1981 to prevent negative
c          steam conductivity at high temperature (cdr-mp-03)
c
$if def,in32,1
       implicit real*8(a-h,o-z)
       include "ufiles.h"
      dimension gmix(10),a(9),b(9),c(10),w(10),ac(10),ar(10),br(10)
      data a  /2.639e-03,2.986e-04,8.247e-05,4.351e-05,
     #  1.097e-03,5.314e-04,1.853e-04,1.403e-04,9.460e-06/
      data b  /0.7085,0.7224,0.8363,0.8616,
     #  0.8785,0.6898,0.8729,0.9090,1.312/
      data w  /4.003,39.944,83.800,131.300,
     #  2.016,28.020,32.000,28.010,44.010,18.020/
      data ac /0.06,0.15,0.74,0.74,0.66,0.19,0.19,0.19,0.74,0.19/
      data ar /1.6669,1.6667,1.6667,1.6667,
     #  1.4045,1.4006,1.4006,1.4006,1.2857,1.2857/
      data br /-2.e-9,3.53e-8,3.53e-8,3.53e-8,
     #  2.5e-9,2.21e-8,2.21e-8,2.21e-8,6.29e-8,6.29e-8/
      if(gtemp .lt. 4000.) go to 20
   10 write (output,900) gtemp,gmix
  900 format(25h1gascon input bad. gtemp=,1pe12.4,2h k,/
     #       10h0fractions, (10e12.4))
      stop
   20 x = 0.0
      do 30 i = 1,10
   30 x = x + gmix(i)
      if(x .lt. 0.9 .or. x .gt. 1.1) go to 10
c     find pure gas conductivities
      do 40 i = 1,9
   40 c(i) = a(i) * (gtemp**b(i))
      tc = gtemp - 273.15
      c(10) = 17.6e-3 + tc*( 5.87e-5 + tc*( 1.04e-7 - 4.51e-11*tc))
c     use ideal gas law to find density
      if(gmix(10) .le. 1.0e-9) go to 50
      ps = gpres * gmix(10)
      den = 2.1668e-6 * ps / gtemp
      c(10) = c(10) + den*( 103.51e-3 + tc*(0.4198e-3 - 2.771e-8*tc)
     #                         + 2.1482e+11 * den/tc**4.2)
      if(gtemp .le. 973.15) go to 50
      c(10) = 4.44e-06*(gtemp**1.45) + 9.5e-05 * ((2.1668e-09 *
     # gpres/gtemp)**1.3) - 1.283668e-03
   50 continue
c     find mixture conductivity
      gthcon = 0.
      do 70 i = 1,10
      if(gmix(i) .le. 1.0e-9) go to 70
      sum = 0.0
      do 60 j = 1,10
      if(j .eq. i) go to 60
      if(gmix(j) .le. 1.0e-9) go to 60
      rc = c(i) / c(j)
      rw = w(i) / w(j)
      rwsr = sqrt(rw)
      gij = 1. + 2.41 * (rw-1.) * (rw-0.142) / (1.+rw)**2
      fij=((1. + sqrt(rc*rwsr) )**2)/ sqrt(8.*(1.+rw))
      sum = sum + gij * fij * gmix(j)
   60 continue
      sphi = ar(i) + br(i) * gpres
      yi = sqrt(w(i)* gtemp)*(sphi-1.)*c(i)/(18.0 * (sphi+1.)*ac(i)
     #  * gpres)
      gthcon = gthcon + c(i) * gmix(i)/((gmix(i) + sum) * gpthk + yi )
   70 continue
      return
      end
