*if def,selap
       function zutco1(z,u,s,a1,a2,ux2,zx2,a3,a4,a5,afsoil,zutemp)
!  Calculates the  thermal conductivity of zirconium-uranium-steel-
!  control rod - oxygen compounds or mixtures or elemental aluminum,
!  lithium, or cadmium during a constant pressure change in temperature.
!
!     zutco1  = output thermal conductivity (w/(m*k))
!     z       = input atom fraction zirconium metal in
!               compound/mixture (atoms zirconium/atoms conglomerate)
!     u       = input atom fraction uranium metal in compound/mixture
!               (atoms uranium/atoms conglomerate)
!     s       = input atom fraction stainless steel in
!               compound/mixture (atoms steel/atoms conglomerate)
!     a1      = input atom fraction silver-indium-cadmium in
!               compound/mixture (atoms ag-in-cd/atoms conglomerate)
!     a2      = input atom fraction boron carbide in compound/mixture
!               (atoms boron carbide (b4c)/atoms conglomerate)
!     a3      = input atomic fraction aluminum in compound/mixture
!               atoms aluminum (al) /atoms conglomerate)
!     a4      = input atomic fraction lithium in compound/mixture
!               atoms lithium (li)/atoms conglomerate)
!     a5      = input atomic fraction cadmium in compound/mixture
!               atoms cadmium (cd)/atoms conglomerate)
!     afsoil  = input atomic fraction soil compound/mixture
!               atoms soil/atoms conglomerate
!     ux2     = input atom fraction uranium dioxide in
!               compound mixture (atoms uo2/atoms conglomerate)
!     zx2     = input atom fraction zirconium dioxide in
!               compund/mixture (atoms zro2/atoms conglomerate)
!     zutemp  = compund/mixture temperature (k)
!
!     the subcodes uthcon,athcon,fthcon,cthcon,sthcon,sotcon,siocon,
!     zotcon, althcn, litcon, and cdtcon are called by this.
!
!     zutco1 was coded by j. k. hohorst, january, 1986
!
!     deck name and function name changed from zutcon to zutco1
!     to avoid conflict with the different zutcon in selap.
!     g. a. jayne    nov, 1986.
!
!     this was extended in may, 1989, by s. w. james to include
!     aluminum, lithium, or cadmium.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
!
       include 'contrl.h'
       include "ufiles.h"
!
       real(kr) z,u,s,a1,a2,ux2,zx2,a3,a4,a5,afsoil,zutemp
       real(kr) zutco1
!
!  Local variables.
       integer ier1,ier2,ier3
       real(kr) ca1,ca2,ca3,ca4,ca5,ccon,cdkdt,con,cr,cs,csx2,cux2,czr,
     & czx2,sx2,dkdt,sicon
       real(kr) tmp1
       real(kr) athcon,sotcon,sthcon,uthcon,zotcon,siocon
!
      sx2 = 1.0 -u -z -s -a1 -a2 -a3 -a4 -a5 -afsoil -ux2 -zx2
!
!     it is assumed that what is left, sx2, is stainess steel oxide.
!
      if (sx2 .lt. -1.0e-6) then
         write (output,20) sx2
   20    format ('0zutco1 - sx2 =', e13.5)
         write (output,901) u,z,s,a1,a2,a3,a4,a5,afsoil,ux2,zx2
!        zzzz=0.0
!        zabc=1.0/zzzz
         stop 'zotco1'
      else
         if(sx2 .gt. 1.0) then
            write (output,903) u,z,s,a1,a2,a3,a4,a5,afsoil,ux2,zx2
!           zzzz=0.0
!           zabc=1.0/zzzz
            stop 'zotco1'
         endif
      endif
!
      cr = uthcon(zutemp)
       if (chngno(70)) then
         call cthconr (zutemp,ccon,cdkdt)
       else
         call cthcon (zutemp,ccon,cdkdt)
       endif
      czr = ccon
      cs = sthcon(zutemp)
      ca1 = athcon(1,zutemp)
      ca2 = athcon(2,zutemp)
      call althcn ( zutemp, ca3, ier1 )
      call litcon ( zutemp, ca4, ier2 )
      call cdtcon ( zutemp, ca5, ier3 )
      sicon = siocon (zutemp)
!   for mixture of U and Zr, adjust temperatur to avoid change in
!   conductivity being calculated to take place at temperature for
!   phase change of pure UO2, namely range of 3100 K to 3113 K.
      tmp1=zutemp
      if(tmp1.gt.2873.0.and.tmp1.lt.3120.0)tmp1=3120.0
      call fthcon(tmp1,1.00,2.0,con,dkdt)
      cux2  = con
      czx2 = zotcon(zutemp)
      csx2 = sotcon(zutemp)
!
!
      zutco1 = u*cr + z*czr + s*cs + a1*ca1 + a2*ca2 +
     +         a3*ca3 + a4*ca4 + a5*ca5 + afsoil*sicon +
     +         ux2*cux2 + zx2*czx2 + sx2*csx2
!
!
  901 format(2x,'input fraction sum .gt. 1. u, z, s, a1, a2, a3, a4, a5,
     + afsoil, ux2, zx2 = '/11e10.4)
  903 format(2x,'input fraction sum .lt. 0. u, z, s, a1, a2, a3, a4, a5,
     + afsoil, ux2, zx2 = '/11e10.4)
!
      return
      end
*endif
