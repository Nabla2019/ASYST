      subroutine cabtp(ctemp,chstrs,a,b,tau,psi)
c
c     cabtp calculates parameters for the cladding creep down
c       subroutine ccstrn
c
c     a      = output ultimate strain for infinite correlation
c              (unitless).
c     b      = output rate constant (s**(-1))
c     tau    = output zero flux correlation time (s).
c     psi    = output correlation fluence (neutrons/((m**2)*s))
c
c     ctemp  = input cladding temperature (k)
c     chstrs = input tangential component of cladding stress (pa)
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      t = ctemp
      if(ctemp .gt. 750.) t = 750.
      if(ctemp .lt. 450.) t = 450.
      s =  abs(chstrs)
c
c     approximate stress exponent by comparison to strength
c     coefficient approximation
      ak = 1.5e+09 -  1.5e+06 * t
c
      a = 3.83e-19  * (s**3) /chstrs
      if(s .lt. (0.20 * ak)) a = a * ((0.20 * ak / s )**1.5)
      if(s .gt. (0.75 * ak)) a = a * ((s/(0.75 * ak))**23)
c
      if(t .lt. 615.) go to 10
      b = 4.69e-06 * (s**2) *  exp(-2.51e+04/t)
      tau = 8.6e-11 *  exp(2.51e+04/t)
      psi = 2.9e+06  *  exp(2.51e+04/t)
      go to 20
   10 b = 1.9519804e-16 * (s**2) *  exp(-1.04e+04/t)
      tau = 2.0663116 *  exp(1.04e+04/t)
      psi = 6.967795e+16 *  exp(1.04e+04/t)
   20 if(s .lt. (0.20 * ak)) b = b * ((0.20 * ak / s )**1.5)
      if(s .gt. (0.75 * ak)) b = b * ((s/(0.75 * ak))**23)
      return
      end
