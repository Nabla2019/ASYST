      function fudenh(ftemp,bu,fdens,rsntr,tsint,comp,prvden)
c   fudenh calculates irradiation-induced densification.
c   fudens = output fuel dimensional change (%)
c   ftemp  = input fuel temperature (k)
c   bu     = input burnup (mw-s/kg-u)
c   fdens  = input fuel density (kg/m**3)
c   rsntr  = input maximum density change determined by a resintering
c   test of 1973 k for 24 hours (kg/m**3)
c   tsint  = input fuel sintering temperature (k)
c   comp   = input plutonia content (weight percent)
c   prvden = input total densification from previous time step (%)
c   fudens was developed and programmed by c. s. olsen (january 1975).
c   updated and corrected by b. w. burnham (october 1975).
c   fudens was modified by r. e. mason (november 1978).
      dimension c(2),b(5)
      data c / 10.97, 11.46/
      data b / 3.0, 1.00, 3.0, 2.00, 35.00 /
      dlen2(alen,bu,abu) = -b(1)+alen+b(2)*exp(-b(3)*(bu+abu))+b(4)*exp(
     +-b(5)*(bu+abu))
      dlen3(bu) = -b(2)*b(3)*exp(-b(3)*bu)-b(4)*b(5)*exp(-b(5)*bu)
      fbu = bu*1.0202e-05
      ts = tsint-2.7315e02
c   if rsntr or tsint is not defined by user, the default values are
c   rstnr = 100. and  tsint = 1873 k.
      if (tsint.le.0.0) ts = 1600.0
      roth = c(1)*c(2)/(0.01*comp*c(1)+(1.0-0.01*comp)*c(2))
      de = fdens/(roth*10.0)
c     if ((ftemp.ge.1000.).and.(rsntr.gt.0.)) dlen1 = 0.00285*rsntr
      if (rsntr.gt.0.)dlen1 = 100.*rsntr/(3*fdens)
      if ((ftemp.lt.1000.).and.(rsntr.gt.0.)) dlen1 = 0.00150*rsntr
      if ((ftemp.ge.1000.).and.(rsntr.le.0.)) dlen1 = 66.6*(100.0-de)/(t
     +s-1180.0)
      if ((ftemp.lt.1000.).and.(rsntr.le.0.)) dlen1 = 22.2*(100.0-de)/(t
     +s-1180.0)
      x3 = 0.0
      x4 = 1.0
      al1 = dlen1
      al3 = 3.0-al1
      al4 = 0.0
      if (al3.le.4.27e-03) go to 130
          do 100 i=1,6
          y2 = dlen2(al3,x4,al4)
          y1 = dlen2(al3,x3,al4)
          if (y1*y2.le.0.) go to 110
          x3 = x4
          x4 = x4+1.0
          if (i.eq.6) go to 160
100       continue
110   continue
      x1 = x3
          do 120 j=1,50
          x = x1-dlen2(al3,x1,al4)/dlen3(x1)
          err = abs((x-x1)*100.0/x)
          if (err.le.2.0e-04) go to 140
          x1 = x
120       continue
130   al3 = 2.996
      al2 = 5.384
      go to 150
140   al2 = x
150   continue
      fuden = dlen2(al3,fbu,al2)
      if (bu.lt.1728) fuden = 0.0
      go to 170
160   print 180
      fuden = 0.0
170   continue
      if (abs(fuden).le.abs(prvden)) fudenh = 0.0
      if (abs(fuden).gt.abs(prvden)) fudenh = fuden-prvden
      return
180   format (1x,/45h no roots found between 0 and 6000 mws/mt uo2)
      end
