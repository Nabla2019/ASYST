      function centhl(ctemp,rftemp)
c
c     centhl returns the change in enthalpy of zircaloy during
c     a constant pressure change in temperature from rftemp to ctemp.
c
c     centhl = output change in cladding enthalpy (j/kg)
c     ctemp  = input cladding temperature (k)
c     rftemp = input reference temperature (k)
c
c     centhl was coded by d. l. hagrman march 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      dimension cpdata(30),hdata(15)
      data cpdata/   281., 300.,  302., 400.,  331., 640.,
     #  375.,1090.,  502.,1093.,  590.,1113.,  615.,1133.,
     #  719.,1153.,  816.,1173.,  770.,1193.,  619.,1213.,
     #  469.,1233.,  356.,1248.,  356.,2098.,  356.,2099./
c
      data hdata/   0.00000,  2.91500, 10.51100, 26.39600,
     #   26.52755, 27.61955, 28.82455, 30.15855, 31.69355,
     #   33.27955, 34.66855, 35.67655, 36.29530, 66.55530,
     #   89.09090/
c
      t = rftemp
      n = -1
c     start at low temperatures and look to see where t fits
   10 k = 1
c
c     check for a temperature lower than the table range
      if(t .le. 300.) then
         h = cpdata(1) * (t - 300.)
      else
c     check for a temperature within the table range
   20    if(t .le. cpdata(2*k+2)) then
            h = hdata(k)*1.0e+04 + (t-cpdata(2*k))*(cpdata(2*k-1) +
     #          (t-cpdata(2*k))*(cpdata(2*k+1)-cpdata(2*k-1))/
     #          (2. * (cpdata(2*k+2)-cpdata(2*k))))
         else
            k = k + 1
            if(k .le. 14) then
               go to 20
c     the only remaining possibility is a temperature higher
c     than the table range
            else
               h = hdata(15)*1.0e+04 + (t-cpdata(30))*cpdata(29)
            endif
         endif
      endif
c
c
      if(n .lt. 0) then
         n = n + 2
         hr = h
         t = ctemp
         go to 10
      else
         centhl = h - hr
      endif
      return
      end
