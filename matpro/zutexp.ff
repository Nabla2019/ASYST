       function zutexp(u,z,zutemp,rftemp)
!  zutexp returns zirconium-uranium-oxygen compound thermal expansion
!  strains.
!
!  zutexp = output thermal expansion strain (m/m)
!  u      = input atomic fraction uranium in compound (atoms.
!           uranium/atoms in compound)
!  z      = input atomic fraction zirconium in compound (atoms
!           zirconium/atoms in compound).
!  zutemp = compound temperature (K).
!  rftemp = reference temperature (K).
!
!  The subcodes cthexp, fthexp, and zotexp are called by this function.
!
!  zutexp was coded by D. L. Hagrman March 1982.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
!
       real(kr) zutexp
       real(kr) u,z,zutemp,rftemp
!
       include 'contrl.h'
!
       real(kr) euo2,euo2r,ezo2,ezr,fzr,fuo2,fzo2,strn11,strn22,strn33,
     & uo,x
       real(kr) fthexp,zotexp
!
       x = 1.0 - u - z
       if ((u + z) .lt. 0.5*x) then
         uo = u
         u = u/(3.0*(u + z))
         z = z/(3.0*(uo + z))
         x = 1.0 - u - z
       else
         if (0.5*x .lt. u) then
           uo = u
           u = u/(3.0*u + z)
           z = z/(3.0*uo + z)
           x = 1.0 - u - z
         endif
       endif
       fuo2 = u/(u+z)
       fzo2 = (0.5*x - u)/(u + z)
       fzr = 1.0 - fuo2 - fzo2
       if (chngno(70)) then
         call cthexpr (0.33,0.5,zutemp,rftemp,strn11,strn22,strn33)
       else
         call cthexp (0.33,0.5,zutemp,rftemp,strn11,strn22,strn33)
       endif
       ezr = strn11
       euo2r = fthexp(rftemp,0.0)
       euo2 = fthexp(zutemp,0.0)
       euo2 = euo2 - euo2r
       ezo2 = zotexp(zutemp,rftemp)
       zutexp = (2.46*fuo2*euo2 + 2.12*fzo2*ezo2 + 1.39*fzr*ezr)/
     & (2.46*fuo2 + 2.12*fzo2 + 1.39*fzr)
       return
       end
