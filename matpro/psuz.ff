      subroutine  psuz  ( xzero , dt , temp , xs , krap )
c
c     calculates the position of the advancing liquid zr-u02 interface
c
c     xs     = output position of liquid zr-uo2 interface  (m)
c     krap   = output flag indexing input status
c               krap = 0  input within expected bounds
c               krap = 1  temperature less than 2120 k or greater than
c                         2600 k
c               krap = 2  time step not positive or input interface
c                         position negative
c     temp   = input temperature of liquid zruo2 region  (k)
c     dt     = input time step  (s)
c     xzero  = input position of interface at previous time step
c
c     the position of the liquid zr-uo2 interface calculated by this
c     subroutine is based on data from the following references
c
c     (1)  walter turk, design & verification of modular computer
c          models for interpreting rod melting experiments, stuttgart,
c          germany  (translation by language services, knoxville, tenn);
c          pp 50-52; july 4, 1979.
c
c     (2)  w. gulden, ssyst/exmel capabilities for analyzing pbf severe
c          fuel damage tests, agenda for pbf program review group
c          meeting; idaho falls, idaho; august 7, 1981.
c
c     psuz was coded by g. h. beers, september 1981
c
$if def,in32,1
       implicit real*8(a-h,o-z)
       include "ufiles.h"
c     save contents of incoming krap error flag
      krapo = krap
c     check for critical input errors, time step not positive or
c     xzero negative
      if  ( dt .le. 0 .or. xzero .lt. 0 )   then
          krap = 2
          xs = 1.0
c     write error message if incoming value of krap not equal 2
          if  ( krapo .ne. 2 )   then
              write  ( output , 100 )   xzero
          endif
          return
c     check for temp less than melting temperature of unoxidized zr.
c     set xs = 0 if temp < 2120 (k) and krap = 1
      else  if  ( temp .lt. 2120 )   then
          krap = 1
          xs = 0
          return
c     check for temp greater than 2600 (k).  if temp > 2600,
c     xs will be calculated but krap set to 1
      else  if  ( temp .gt. 2600 )   then
          krap = 1
c     if input is within all bounds, set krap = 0
      else
          krap = 0
      endif
c     calculate the temperature dependent propagation constant
      ds = 8.67e-27 * exp( 1.66e-02 * temp )
c     calculate the position of the liquid zr-uo2 interface
      xs = sqrt( 2. * ds * dt + xzero ** 2 )
      return
  100 format (/// t11 , 'psuz has encountered an input error at ' ,
     +        'xzero = ',e14.6,', and is returning xs equal to 1 m' )
      end
