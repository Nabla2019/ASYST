      subroutine foxy(ftemp,dt,uo2oxi,uo2oxf,p,krap)
c
c     foxy calculates u02 oxygen up-take in steam as a function of
c     temperature, oxidation time, and original oxide weight gain
c
c     uo2oxf = output final oxide weight gain  (kg/m**2)
c     p      = output power generated by oxidation  (w/m**2)
c     krap   = output index of errors in input
c              krap=0 if no input errors
c              krap=1 if fuel temperature exceeds melt (3100 k)
c                     or is below correlation range but positive
c              krap=2 if time step or temperature not positive
c                     or if original oxide weight gain negative
c     ftemp  = input fuel surface temperature  (k)
c     dt     = input time step  (s)
c     uo2oxi = input initial oxide weight gain  (kg/m**2)
c
c     the function foxyk is used in the subroutine
c     foxyk = parabolic oxidation rate constant  (kg**2/m**4-s)
c
c     the equations used in this subroutine are based on data from
c     j. t. bittel, l. h. sjodahl, and j. f. white, journal of the
c     american ceramic soc. 52 (1969) pp. 446-451
c
c     this model is valid in the temperature range 1158k - 2108k
c
c     foxy coded by r. chambers, september 1981.
c
$if def,in32,1
       implicit real*8(a-h,o-z)
       include "ufiles.h"
      ikrap=0
      if(krap.eq.2) then
      ikrap=2
      else
c        check for temperature in excess of 3100 k
         if(ftemp.gt.3100.) then
         krap=1
         ftemp=3100.
         end if
c        check for temperature below 1158k
         if(ftemp.lt.1158.and.ftemp.gt.0.) then
         krap=1
         end if
      end if
c        check for input errors
         if(dt.ge.0..and.uo2oxi.ge.0..and.ftemp.gt.0.) then
c        calculate oxide weight gain
         uo2oxf=sqrt((foxyk(ftemp)*dt)+(uo2oxi**2))
c        calculate power generated by oxidation
         p=((uo2oxf-uo2oxi)*1.84e05)/dt
         else
         krap=2
         uo2oxf = 1.0
         p = 1.0
c           print input if errors encountered for first time
            if(ikrap.eq.0) then
            write(output,10)
            write(output,20) ftemp,dt,uo2oxi,krap
            end if
         end if
   10 format (//,10x,"input error in foxy")
   20 format (10x,"ftemp=",e12.5,3x,"dt=",e12.5,3x,"uo2oxi=",e12.5,
     *3x,"krap=",i5)
      return
      end
