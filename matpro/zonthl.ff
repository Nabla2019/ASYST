      function zonthl(zotemp,rftemp)
c
c     zonthl returns the change in enthalpy of zircaloy oxide
c     during a constant pressure change in temperature
c     from rftemp to zotemp.
c
c     zonthl = output change in oxide enthalpy (j/kg)
c     zotemp = input cladding oxide temperature (k)
c     rftemp = input reference temperature (k)
c
c     zonthl was coded by d. l. hagrman march 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      t = rftemp
      n = -1
   10 if(t .le. 300.) then
         h = 456.6633 * (t - 300.)
      else
         if(t .lt. 1478.) then
            h = t*(565.+3.055e-02*t)+(1.14e+07/t)-2.102495e+05
         else
            if(t .le. 2000.) then
               h = t*604.5 - 1.46e+05
            else
               if(t .lt. 2558.) then
                  h = t*(171.7 + 0.1082*t) + 2.868e+05
               else
                  if(t .lt. 2973.) then
                     h = t*(171.7 + 0.1082*t) + 3.888e+05
                  else
                     h = t*815. + 1.39e+05
                  endif
               endif
            endif
         endif
      endif
      if(n .lt. 0) then
         n = n + 2
         hr = h
         t = zotemp
         go to 10
      else
         zonthl = h - hr
      endif
      return
      end
