       function coxwtk(ctemp)
!  Function coxwtk returns the parabolic oxidation constant for Zircaloy
!  oxidation. For temperatures of 1273K-1853K, data and analyses from
!  J.V. Cathcart of ORNL are used. For temperatures in the range of
!  1853K-2100K, data and analyses from V.F. Urbanic and T.H. Heidrick,
!  "High Temperature Oxidation of Zircaloy-2 And Zircaloy-4 in Steam",
!  Journal of Nuclear Materials 75,(1978) are used.
!
!     coxwtk = output parabolic oxidation constant (kg**2/m**4*s).
!     ctemp  = input cladding temperature (K).
!
!     Coded by N. Hampton November 1981.
!     Last updated by D. L. Hagrman July 1982.
!
$if def,iua
       use uncdata
$endif
       implicit none
       real coxwtk,ctemp
!
       include 'contrl.h'
!
       if (.not.chngno(11)) then
         if (ctemp .gt. 1853.) then
           coxwtk = 2.0*5.426*exp(-16610./ctemp)
         else
           coxwtk = 2.0*16.8*exp(-20065.0/ctemp)
         endif
       else
!
!  Function coxwtk returns parabolic oxidation constant for Zircoloy
!  oxidation using Prater-Courtright for T > 1900 K, interpolation
!  between 1800 K and 1900 K, and Leistikow for T < 1800 K.
!
         if (ctemp .gt. 1800.) then
           if (ctemp .gt. 1900.) then
             coxwtk = 3.295e3*exp(-26440./ctemp)
           else
             coxwtk = 1.2508e12*exp(-63974./ctemp)
           endif
         else
           coxwtk = 0.52418e2*exp(-20962./ctemp)
         endif
       endif
$if def,iua
       if (uncmode) call appwtoxid (coxwtk)
$endif
       return
       end function coxwtk
