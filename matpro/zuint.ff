*if def,selap
       function zuint(z, u, s, a1, a2, ux2, zx2,
     # afalm1, aflth1, afcdm1, afsoil, zutemp, rftemp, errflg)
c
c  calulates the integral 1/thermal conductivity of the zirconium-
c  uranium-steel control rod oxygen compounds or mixtures during a
c  constant pressure change in temperatures.
c
c     zuint   = output integral 1/thermal conductivity
c     z       = input atom fraction zirconium metal in
c               compound/mixture (atoms zirconium /atoms conglomerate)
c     u       = input atom fraction uranium metal in compound/mixture
c               (atoms uranium/atoms conglomerate)
c     s       = input atom fraction stainless steel in
c               compound/mixture (atoms steel/atoms conglomerate)
c     a1      = input atom fraction silver-indium-cadmium in
c               compound/mixture (atoms ag-in-cd/atoms conglomerate)
c     a2      = input atom fraction boron carbide in compound/mixture
c               (atoms boron carbide (b4c)/atoms conglomerate)
c     ux2     = input atom fraction uranium dioxide in
c               compound/mixture (atoms uo2/atoms conglomerate)
c     zx2     = input atom fraction zirconium dixode in
c               compound mixture (atoms zro2/atoms conglomerate)
c   afalm1=atomic fraction of aluminum in mixture
c   aflth1=atomic fraction of lithium in mixture
c   afcdm1=atomic fraction of cadmium in mixture
c   afsoil=atomic fraction of soil in mixture
c     dx      = the difference between the reference temperature and
c               the temperature higher in the data table.
c     rftemp  = input reference temperature (k)
c     zutemp  = input compound/mixture temperature (k)
c
c     zuint was coded by j. k. hohorst, january, 1986.
c     the subcodes used are zutcon, uthcon, athcon, fthcon, cthcon,
c     siocon, sthcon, sotcon, and zotcon
c
       implicit none
       include "ufiles.h"
       real z,u,s,a1,a2,ux2,zx2,afalm1,aflth1,afcdm1,afsoil
       real zutemp, rftemp
       real zuint
c
c  Local variables.
       integer i,n,nt,errflg
       real con,ddx,delt,dtemp,dx,rcon,temp,sx2
      dimension temp(51), dtemp(50)
       real zutco1
       external zutco1
c
      data temp/200.,300.,400.,500.,600.,700.,800.,900.,1000.,
     +         1050.,1100.,1200.,1300.,1400.,1500.,1600.,1671.,
     +         1700.,1727.,1800.,1900.,2000.,2098.,2100.,2200.,
     +         2300.,2400.,2500.,2600.,2700.,2800.,2900.,2973.,
     +         3000.,3100.,3113.,3200.,3300.,
     +         3400.,3500.,3600.,3700.,3800.,3900.,4000.,
     *         4100.,4200.,4300.,4400.,4500.,4600.      /
c
      data dtemp/100.,100.,100.,100.,100.,100., 100., 100.,
     +           50., 50., 100., 100., 100., 100., 100., 71.,
     +           29., 27.,  73., 100., 100.,  98.,   2., 100.,
     +          100., 100., 100., 100., 100., 100., 100.,  73.,
     +           27., 100.,  13.,  87., 100.,
     +          100., 100., 100., 100., 100., 100., 100.,
     *          100., 100., 100., 100., 100., 100.      /
c
      errflg=0
      nt = 51
c
c     check to see if the fractions entered are reasonable
       sx2=1.0-z-u-s-a1-a2-ux2-zx2-afalm1-aflth1-afcdm1-afsoil
      if (sx2 .lt. -1.0e-6) then
         write (output,20) sx2
   20    format ('0zuint - sx2 =', e13.5)
         write (output,901) z,u,s,a1,a2,ux2,zx2,afalm1,aflth1,
     #      afcdm1,afsoil
  901 format(2x,'input fraction sum .gt. 1. z, u, s, a1, a2, ',
     +'ux2, zx2, afalm1, aflth1, afcdm1, afsoil = '/11e10.5)
         goto 45
      else
         if(sx2 .gt. 1.0) then
            write (output,902) z,u,s,a1,a2,ux2,zx2,afalm1,aflth1,
     #        afcdm1,afsoil
  902 format(2x,'input fraction sum .lt. 0. z, u, s, a1, a2, ',
     +'ux2, zx2, afalm1, aflth1, afcdm1, afsoil = '/11e10.5)
            goto 45
         endif
      endif
c
      if(zutemp .lt. rftemp) then
         write (output,903)
  903 format(2x,'Mixture temperature less than reference temperature.')
         goto 45
      endif
c
c     initialize the value of the term to be returned from the function
      zuint = 0.0
c
c     check the location of the reference temperature in the data
c     table input.
c
      n = 1
c
   10 if(temp(n) .ge. rftemp) then
         if(zutemp .le. temp(n)) then
            dx = zutemp - rftemp
       con=zutco1(z, u, s, a1, a2, ux2, zx2, afalm1, aflth1,
     # afcdm1, afsoil, rftemp)
            rcon = 1./con
            zuint = zuint + rcon * dx
            go to 50
         else
            dx = temp(n) - rftemp
       con=zutco1(z, u, s, a1, a2, ux2, zx2, afalm1, aflth1,
     # afcdm1, afsoil, rftemp)
            rcon = 1./con
            zuint = zuint + rcon * dx
         endif
      else
         n = n + 1
         go to 10
      endif
c
c     integrate the curve to the temperature value just under zutemp
c     (the mixture temperature) in the data table.
      i = n
   30 delt = dtemp(i)
      if(temp(n) .ge. zutemp) go to 40
       con=zutco1(z, u, s, a1, a2, ux2, zx2, afalm1, aflth1,
     # afcdm1, afsoil, temp(n))
         rcon = 1./con
         zuint = zuint + rcon * delt
         n = n + 1
         i = i + 1
         if (n .gt. nt) then
           write (output,35) zutemp, temp(nt)
   35  format ('0zuint - temp. of ',1p,g13.5,
     # ' exceeds last table value of ',g13.5)
           errflg=1
           goto 50
         end if
         go to 30
c
   40 n = n - 1
      ddx = zutemp - temp(n)
c
       con=zutco1(z, u, s, a1, a2, ux2, zx2, afalm1, aflth1,
     # afcdm1, afsoil, zutemp)
      rcon = 1./con
      zuint = zuint + rcon * ddx
      goto 50
c error recovery
   45 zuint=0.5
      errflg=1
   50 return
      end
*endif
