       function eubsthk(ctemp,kpick)
!  Function eubsthk returns the growth rate constant for stainless steel and
!  B4C liquefied layer cause by the eutectic interaction between them. Two 
!  KfK reports discuss the correlations.
!  W.Hering P.Hofmann, "Material interactions during severe LWR accidents",
!  KfK 5125, April 1994
!  P.Hofmann et al., "Reaction behaviour of b4c absorber material with stainless
!  steel and zircaloy in severe LWR accidnets", KfK 4598, Juli 1989
!  k= a * exp(-Q/RT) 
!  The unit of R is different in each report for the same correlation. From the result
!  here suppose that R is 8.314 J/mol K as is in KfK 4598
!  The liquefied interface is only observed in SS part. So ignore in this subroutine
!  the layer growth toward B4C material
!  We have the correlation ranging 1073 - 1473 K and 1498 - 1623 K
!  suppose the same correlation can be applied below 1073 K and over 1623 K. 
!  1473 - 1498 K is logarithmicly innterpolated
!
!
!     coxthk= output growth rate constant (m**2/s)
!     ctemp = input temperature (K)
!     kpick=1 is growth rate constant for pellet type b4c.
!     kpick=2 is growth rate constant for powder type b4c.
!
!
       implicit none
       real eubsthk,ctemp
       integer kpick
       real R, dam1, dam2, f
       data R / 8.314 /
!
       if (kpick .eq. 1) then
         if (ctemp .lt. 1473.) then
           eubsthk =  1.49e-2*exp(-2.5e5/(R*ctemp))
         elseif (ctemp .gt. 1498.) then
           eubsthk =  1.15e10*exp(-5.44e5/(R*ctemp))
         else
           dam1 =   1.49e-2*exp(-2.5e5/(R*1473.))
           dam2 =   1.15e10*exp(-5.44e5/(R*1498.))
           f=(1.0/ctemp-1.0/1473.0)/(1.0/1473.0-1.0/1498.0)
           eubsthk = exp(f*(log(dam2)-log(dam1))+log(dam1))         
         endif        
       elseif (kpick .eq. 2) then
         eubsthk = 876.*exp(-3.78e5/(R*ctemp))
       else
         stop 'eubsthk'
       endif 

       return
       end function eubsthk
