       function  cpal (tp)
c
c      cpal    - Compute specific heat of aluminum
c
c      Author:   J. E. Tolli, EG&G Idaho
c
c      Calling sequence:
c
c                cp = cpal (p1)
c
c                where cp is the desired specific heat
c
c      Parameters:
c                p1 = temperature in Kelvin degrees
c
c--set number of table points
c
       implicit none
c
c  Local variables.
       integer i
       real cptbl,cpval,t1,t2,tp
       real cpal
       integer numpts
       parameter (numpts=8)
c
c--set table dimensions
c
       dimension  cptbl(2,numpts)
c
c--specific heat data table, temperature(K),specific heat(Btu/lb-F)
c
       data  cptbl / 310.,0.212 , 366.,0.217 , 422.,0.221 , 477.,0.224 ,
     *               532.,0.226 , 588.,0.227 , 644.,0.228 , 700.,0.228 /
c
c--check for temperature beyond table limits
       if (tp.lt.cptbl(1,1)) then
         cpval = 0.210
         go to 20
       else if (tp.gt.cptbl(1,numpts)) then
         cpval = 0.229
         go to 20
       endif
c--temperature lies within table limits;  interpolate to find specific
c--heat
       do 10 i=2,numpts
         if (tp.le.cptbl(1,i)) then
           cpval = cptbl(2,i-1) +
     *             ( tp - cptbl(1,i-1) ) / ( cptbl(1,i) - cptbl(1,i-1) )
     *             * ( cptbl(2,i) - cptbl(2,i-1) )
           go to 20
         endif
   10  continue
c--convert specific heat value to J/kg-K and put into cpal
   20  cpal = 4.186e3 * cpval
      t1 = 913.0
      t2 = 933.0
      if (tp.le.t2 .and. tp.ge.t1) cpal = cpal + 3.9553e5/(t2-t1)
       return
       end
