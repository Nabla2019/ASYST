      function athexp(ictype,atemp,rftemp)
c
c     athexp returns absorber thermal expansion strains
c     athexp = output thermal expansion strain (m/m)
c     ictype = input absorber material type
c                 1 = silver-indium-cadmium (.8,.15.05)
c                 2 = boron carbide
c     atemp  = input absorber temperature (k)
c     rftemp = input reference temperature (k)
c
c     athexp was coded by d. l. hagrman march 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      t = rftemp
      n = -1
c
      if(ictype .lt. 2) then
   10    if(t .lt. 1050.) then
            eps = 2.25e-05 * (t - 300.)
         else
            if(t .lt. 1100.) then
               eps = -0.25875 + 2.625e-04 * t
            else
               eps = 0.03
            endif
         endif
         if(n .lt. 0) then
            n = n + 2
            epsr = eps
            t = atemp
            go to 10
         else
            athexp = eps - epsr
         endif
      else
   20    eps = -1.1e-03 + t*(3.09e-06 + 1.88e-09*t)
         if(n .lt. 0) then
            n = n + 2
            epsr = eps
            t = atemp
            go to 20
         else
            athexp = eps - epsr
         endif
      endif
      return
      end
