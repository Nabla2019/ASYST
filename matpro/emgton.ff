      function emgton (gmix,gtemp,gpres,gpthk)
c   gthcon calculates gas thermal conductivity as a function of
c   temperature and gas mole fractions.
c   gthcon=output gas thermal conductivity (w/m-k).
c   gmix(i) = input array of gas molar fractions
c   the seven elements of gmix must sum to 1.0
c   constituent gas number key
c   i = 1  helium
c   i = 2  argon
c   i = 3  krypton
c   i = 4  xenon
c   i = 5  hydrogen
c   i = 6  nitrogen
c   i = 7  steam
c   gtemp=input gas temperature (k).
c   gpres=input gas pressure (n/m**2)
c   used for knudsen domain correction and for steam
c   not used if 0.
c   gpthk=input effective gap thickness for knudsen domain (m)
c   = max(gap dimension, surface roughness)
c   not used if 0.
c   the gas conductivity relationships used in this function routine
c   are those stated by bird,stewart,and lightfoot,
c   "transport phenomena",p.26 and p.258 (1960).
c   bird, hirschfelder, curtiss, trans. asme 76(1954)1011-1038,
c   or   handbook of physics, chapter 5.5 (mcgraw-hill) 1954.
c   steam equation is from  mayer, et.al., "thermodynamic and
c   transport properties of steam", the american society of
c   mechanical engineers (1967)
c   coded by r.c.young at inel in mar 1975.
      dimension sigma2(6), eok(6), im(6)
      dimension a(7), r(7), con(7), gmix(7)
      dimension table(164)
      data eps, nt, im / 1.e-8, 82, 6*1 /
c   data sigma / 2.576, 3.418, 3.498, 4.055, 2.915, 3.681/
      data sigma2 / 6.635776, 11.682724, 12.236004, 16.443025,
     + 8.497225, 13.549761/
      data  eok  /  10.2,  124.,  225.,  229.,  38.0,  91.5/
      data   a   / 4.003,39.944, 83.80, 131.3, 2.016, 28.02, 18.016/
c   r = sqrt(a)
      data r / 2.00075e0, 6.3201e0, 9.1542e0, 11.4586e0, 1.41986e0,
     +  5.2934e0, 4.2445e0/
      data (table(i),i=1,78)/
     & 2.785,.30, 2.628,.35, 2.492,.40,
     & 2.368,.45, 2.257,.5, 2.156,.55, 2.065,.60,
     & 1.982,.65, 1.908,.70, 1.841,.75, 1.780,.80,
     & 1.725,.85, 1.675,.90, 1.629,.95, 1.587,1.0,
     $ 1.549,1.05,1.514,1.1, 1.482,1.15, 1.452,1.2,
     & 1.424,1.25,1.399,1.3, 1.375,1.35,1.353,1.4,
     & 1.333,1.45,1.314,1.5, 1.296,1.55,1.279,1.6,
     & 1.264,1.65,1.248,1.7, 1.234,1.75,1.221,1.8,
     & 1.209,1.85,1.197,1.9, 1.186,1.95,1.175,2.0,
     & 1.156,2.1, 1.138,2.2, 1.122,2.3, 1.107,2.4/
      data (table(i),i=79,164)/
     & 1.093,2.5, 1.081,2.6, 1.069,2.7, 1.058,2.8,
     & 1.048,2.9, 1.039,3.0, 1.030,3.1, 1.022,3.2,
     & 1.014,3.3, 1.007,3.4, .9999,3.5, .9932,3.6,
     & .9870,3.7, .9811,3.8, .9755,3.9, .9700,4.0,
     & .9649,4.1, .9600,4.2, .9553,4.3, .9507,4.4,
     & .9464,4.5, .9422,4.6, .9382,4.7, .9343,4.8,
     & .9305,4.9, .9269,5.0, .8963,6.0, .8727,7.0,
     & .8538,8.0, .8379,9.0, .8242,10., .7432,20.,
     & .7005,30., .6718,40., .6504,50., .6335,60.,
     & .6194,70., .6076,80., .5973,90., .5882,100.,
     & .5320,200., .5016,300., .4811,400./
      if (gtemp.gt.10000.) go to 100
      if (gtemp.gt.0.) go to 110
100   write (6,160) gtemp,gmix
110   l = 0
          do 120 i=1,6
          if (gmix(i).le.eps) go to 120
          l = i
          tkoe = gtemp/eok(i)
          if (tkoe.gt.400.) tkoe = 400.
          omega = polate(table,tkoe,nt,im(i))
          con(i) = 0.083224*sqrt(gtemp/a(i))/(sigma2(i)*omega)
          if (i.lt.5) go to 120
c   for diatomic gases, con=(csubp*4/15r + 1/3)*con(monatomic)
          if (i.eq.5) con(5) = con(5)*(1.26566e0+gtemp*(6.4526e-8*gtemp-
     +    2.684e-5))
          if (i.eq.6) con(6) = con(6)*(1.20889e0+gtemp*(1.6776e-4-
     +    1.3421e-10*gtemp))
120       continue
      if (gmix(7).le.eps) go to 130
      l = 7
c   steam
      tc = gtemp-273.15e0
      con(7) = 17.6e-3+tc*(5.87e-5+tc*(1.04e-7-4.51e-11*tc))
      if (gpres.le.1.e5) go to 130
c   use ideal gas law for density - ok for correction at hi temp.
      den = 2.1668e-6*gpres/gtemp
      con(7) = con(7)+den*(103.51e-3+tc*(0.4198e-3-2.771e-8*tc)+
     +2.1482e11*den/tc**4.20e0)
130   if (l.le.0) go to 100
      gthcon = 0.
      emmix = 0.
          do 150 i=1,l
          if (gmix(i).le.eps) go to 150
          emmix = emmix+gmix(i)*a(i)
          sum = 0.
              do 140 j=1,l
              if (j.eq.i) go to 140
              if (gmix(j).le.eps) go to 140
              rk = con(i)/con(j)
              phi = (1.+sqrt(rk*r(i)/r(j)))**2/sqrt(8.*(1.+a(i)/a(j)))
              sum = sum+gmix(j)*phi
140           continue
          gthcon = gthcon+gmix(i)*con(i)/(gmix(i)+sum)
150       continue
c   small gap correction on average mass, as gapcon
      facc = gpres*gpthk
      if (facc.gt.eps) gthcon = gthcon/(1.0+0.12049*(gthcon/facc)*sqrt(g
     +temp*emmix))
      emgton = gthcon
      return
160   format (25h1gthcon input bad. gtemp=,1pe12.4,2h k,/10h0fractions,(
     +10e12.4))
      end
