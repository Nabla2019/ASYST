       function  altcon (tp)
c
c      altcon  - Compute thermal conductivity of aluminum
c
c      Author:   J. E. Tolli, EG&G Idaho
c
c      Calling sequence:
c                k = altcon (p1)
c                where k is the desired thermal conductivity
c      Parameters:
c                p1 = temperature in Kelvin degrees
c
       implicit none
c--set number of table points
       integer numpts
       parameter (numpts=28)
c--set table dimensions
c
c  Local variables.
       integer i
       real tp
       real altcon
       real  ktbl(2,numpts),kval
c
c--thermal conductivity data table, temperature(K),specific heat(W/m-K)
c
       data  ktbl /  200.,237. ,  250.,235. ,  300.,237. ,  350.,240. ,
     *               400.,240. ,  500.,237. ,  600.,232. ,  700.,226. ,
     *               800.,220. ,  900.,213. ,  933.,211. ,  934.,90.7 ,
     *              1000.,93.0 , 1100.,96.4 , 1200.,99.4 , 1300.,102. ,
     *              1400.,105. , 1500.,107. , 1600.,109. , 1700.,111. ,
     *              1800.,112. , 1900.,113. , 2000.,114. , 2200.,115. ,
     *              2400.,115. , 2600.,115. , 2800.,114. , 3000.,113. /
c
c--check for temperature beyond table limits
       if (tp.lt.ktbl(1,1)) then
         kval = 237.0
         go to 20
       endif
c
       if (tp.gt.ktbl(1,numpts)) then
         kval = 113.0
         go to 20
       endif
c
c--temperature lies within table limits;  interpolate to find thermal
c--conductivity
       do 10 i=2,numpts
         if (tp.le.ktbl(1,i)) then
           kval = ktbl(2,i-1) +
     *             ( tp - ktbl(1,i-1) ) / ( ktbl(1,i) - ktbl(1,i-1) )
     *             * ( ktbl(2,i) - ktbl(2,i-1) )
           go to 20
         endif
   10  continue
c
c--set value of altcon
   20  altcon = kval
       return
c
 1000  format ('0******** From altcon: temperature',1p,g14.6,' lies outs
     *ide bounds of thermal conductivity table program.')
       end
