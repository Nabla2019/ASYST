      subroutine chitox (t1,t2,x2,x1,dt,x12,x11,drod2,drod,q)
c   chitox computes the zircaloy oxide and xi layer thicknesses
c   at the end of a time step, the power generated in the cladding
c   during the time step due to the metal-water chemical reaction,
c   and the diameter of the remaining unoxidized portion of the
c   rod.
c   x2    = output zro2 thickness at the end of a time step (m)
c   x12   = output xi thickness at the end of a time step (m)
c   drod2 = output outer diameter of unoxidized part of cladding
c   at the end of the time step (m)
c   (expansion of the lattice due to dissolved oxygen is
c   neglected)
c   q     = output rate of heat generation per meter for a rod of
c   initial diameter drod (w/m)
c   t1    = input cladding temperature at start of a time step (k)
c   t2    = input cladding temperature at end of a time step (k)
c   x1    = input zro2 thickness at start of a time step (m)
c   dt    = input time step duration (s)
c   x11   = input xi thickness at start of a time step (m)
c   drod  = input unoxidized rod outer diameter (m)
c   the correlations were derived from the parabolic rate equations
c   of j.v. cathcart, ornl/nureg/tm-41 (august 1976)
c   chitox was coded by g.a. reymann    september 1976
c   updated by g.a. reymann  dec 1976
        data  rhoz, hrz / 6.5e03 , 6.4e06/
      tave = (t1+t2)/2.
      if (tave.lt.1.0e03) go to 110
      x2 = sqrt((x1)**2+2.25e-06*exp(-18063./tave)*dt)
      x12 = sqrt((x11)**2+6.824e-5*exp(-2.1e4/tave)*dt)
100   drod2 = drod-1.38*x2
      q = (rhoz*hrz*2.17*drod*(x2-x1))/dt
      go to 120
110   x2 = x1
      x12 = x11
      go to 100
120   return
      end
