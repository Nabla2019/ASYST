      subroutine ccstrn(ctemp,delt,cflux,chstrs,cstnb,ccstni,
     # cstnbn,ccstnf)
c
c     ccstrn calculates the tangential component of cladding
c     creep strain in compression at the end of a time step with constan
c     cladding temperature, flux, and stress.
c     ccstnf = output tangential component of cladding creep
c                strain at the end of the time step (m/m)
c     cstnbn = output boundary condition parameter used to
c                specify initial slope of next time step (unitless)
c     ustnu  = estimated uncertainty of strain increment --
c                upper bound on strain increment magnitude (unitless).
c                calculated but not returned.
c     ustnl  = estimated uncertainty of strain increment --
c                lower bound on strain increment magnitude (unitless).
c                calculated but not returned.
c     ctemp  = input cladding temperature (k)
c     delt   = input time step size (s)
c     cflux  = input fast neutron flux (neutrons/((m**2)*s))
c     chstrs = input tangential component of cladding stress (pa)
c     cstnb  = input boundary condition parameter used to
c                specify initial slope of current time step
c                (unitless).
c                this parameter is zero for the first time step
c                and is taken from output for subsequent steps.
c     ccstni = input tangential component of cladding creep
c                strain at the start of the time step (m/m)
c     the equations used in this subroutine are based on data from
c     (1) d. o. hobson, creepdown of zircaloy fuel cladding
c         initial tests, ornl/nureg/tm-181 (april 1978).
c     (2) d. o. hobson, preliminary analysis of surface displacement
c         results in the creepdown irradiation experiment hobbie-1,
c         ornl/nureg/tm-310 (june 1979).
c     (3) v. fidleris, uniaxial in-reactor creep of zirconium
c         alloys, journal of nuclear materials 26 (1968).
c         pp. 51-76.
c     (4) h. stehle et al., mechanical properties of anisotropy
c         and microstructure of zircaloy canning tubes,
c         zirconium in the nuclear industry, astm stp 663
c         (1977) pp. 486-507.
c
c     the subcode cabtp is used in this subroutine
c     ccstrn was coded by d. l. hagrman may 1980
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      call cabtp(ctemp,chstrs,a,b,tau,psi)
c     check for pre or post steady state creep
      rct = cflux/psi + 1.0/tau
      z = a/((1.0 + b/rct) * (a - cstnb))
      dtss = 0.0
      if(z .le. 0. .or. z .ge. 1. ) go to 10
      dtss = -(log(z)/b)
c
c     branch for pre or post steady state creep
      if(delt .ge. dtss) go to 10
      ccstnf =  (a - cstnb) * (1. -  exp(-b*delt)) + ccstni
      cstnbn = -( exp(-b*delt))*( a - cstnb ) + a
      go to 20
   10 ccstnf = (a - cstnb) * (1. -  exp(-b*dtss)) + ccstni
     #  + ( b * a / ( 1. + b/rct) ) * ( delt - dtss)
      cstnbn = a * b / rct
c
c     estimate uncertainty in strain increment
   20 uctr = 2. * abs( (chstrs + 1.3e+08)/1.3e+08)
     #     + 5. * abs( (ctemp - 644.)/644.)
      ustnu = (1. + 0.3 * ( 1. + uctr)) * (ccstnf - ccstni)
      ustnl = (0.4 / ( 1. + uctr )) * (ccstnf - ccstni)
c
      return
      end
