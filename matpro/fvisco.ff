      function fvisco(ftemp,fotmtl,facmot)
c
c     fvisco calculates the dynamic viscosity of uo2 fuel.
c
c     fvisco = output dynamic viscosity (pa*s)
c     uvisco = estimated standardd error of fvisco (pa*s)
c              (not currently returned)
c     ftemp  = input fuel temperature (k)
c     fotmtl = input oxygen to metal ratio (unitless)
c     facmot = input fuel fraction molten (unitless)
c              facmot = 1.0  means fuel is all molten
c              facmot = 0.0  means fuel is all solid
c
c     the equations used in this function are based on data from:
c     (1)  r. e. woodley, the viscosity of molten uranium dioxide,
c          journal of nuclear materials,50,(1974),pp 103-106.
c     (2)  r. scott, a. r. hall, and j. williams, the plastic
c          deformation of uranium oxides above 800 c, journal of
c          nuclear materials,1,(1959),pp 39-48.
c     (3)  h. c. tsai and d. r. olander, the viscosity of molten
c          uranium dioxide,journal of nuclear materials,44,(1972),
c          pp 83-86.
c
c     coded by c. s. watson and d. l. hagrman october 1980
c     modified by d. l. hagrman to remove phypro common block
c          (cdr-mp-21) may 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
c     expressions to replace phypro common block
      fdelta = 1.0e-10
      ftmelt = 3113.
      fvisco = 1.38 * exp(4.942e+04/ftemp)
      if (ftemp .le. ftmelt) go to 120
      fvisco = fvisco * (1. - facmot) + (1.23e-02 - 2.09e-06 * ftemp)
     #         * facmot
      if (ftemp .le. (ftmelt + fdelta)) go to 100
      fvisco = 1.23e-02 - 2.09e-06 * ftemp
  100 uvisco = fvisco*0.33 * (1. + abs(fotmtl - 2.0))
  120 if (ftemp .lt. ftmelt) uvisco = fvisco*0.67 * (1. + abs(fotmtl -
     #    2.0))
      return
      end
