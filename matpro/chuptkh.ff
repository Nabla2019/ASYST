      subroutine chuptkh (chorg,ppmh2o,dp,icm,dco,dci,icor,zro2ai,
     # zro2bi,tcoi,qci,zoxki,chydai,chydbi,chconc)
c   chuptk calculates average concentration of hydrogen in zircaloy
c   cladding.
c   chconc = output average concentration of
c   hydrogen in cladding at end of time step (ppm)
c   chydbi = output hydrogen from coolant in cladding at end
c   of time step (ppm)
c   chorg  = input initial hydrogen in cladding (ppm)
c   ppmh2o = input initial fuel water content (ppm)
c   dp     = input fuel pellet diameter (m)
c   icm    = input cladding material flag (icm = 2 for zircaloy 2,
c            icm = 4 for zircaloy 4)
c   dco    = input cladding outside diameter (m)
c   dci    = input cladding inside diameter (m)
c   icor   = input reactor chemistry .ge. 2 for pwr, .lt. 2 for bwr
c   zro2ai = input oxide thickness at start of current time step (m)
c   zro2bi = input oxide thickness at end of current time step (m)
c   tcoi   = input zro2-coolant interface temperature (k)
c   qci    = input axial increment heat flux (watt/meter**2)
c   zoxki  = input zircaloy oxide thermal conductivity (w/(m-k))
c   chydai = input hydrogen from coolant in cladding at start
c   of time step (ppm)
c   the equations used in this subroutine are based on data from:
c
c   References for the new model           
c   A. M. Garde, Hot Cell Examination of Extended Burnup Fuel from
c   Fort Calhoun, DOE/ET/34030, 1980.
c
c   C. G. Dideon, Fuel Performance under Extended Burnup for the
c   B&W 15 x 15 Design, DOE/ET/34212, 1983.
c
c   L. W. Newman, Hot Cell Examination of Oconee 1 Fuel Rods after
c   Five Cycles of Irradiation, DOE/ET/34212-50, 1986.
c
c   G. P. Smith, The Evaluation and Demonstration Method for Improved 
c   Nuclear Fuel Utilization, DOE/ET/34013-13, for CE 16 x 16 fuel rods
c   and ANO-2 Fuel Rods.
c
c   G. P. Smith, Hot Cell Examination of Extended Burnup Fuel from Calvert
c   Cliffs-1, TR-103302-V2, 1993, for CE 14x14 Calvert Cliffs Fuel rods
c
c   D. D. Lanning et al, 'Corrosion and Hydriding in N-Reactor Pressure
c   Tubes,"  Zirconium in the Nuclear Industry, Eight International
c   Symposium, ASTM-STP-1023, 1989, pp, 3-19.
c
c   A, Seibold and K. N. Woods, "BWR Advanced Material," Proceedings of
c   the International Topical Meeting on Lighwater Reactor Fuel Performance.
c   West Palm Beach Florida, April 17-21, 1994.
c
c   this model should not be used outside the temperature range
c   523.15 - 673.15 k (250 - 400 c).
c   chuptk coded by d. l. hagrman february 1977
c   modified by d. l. hagrman june 1978
c   modified by d. d. lanning, 1995
c
      if (tcoi.le.366.5) go to 140
      if (zro2ai.le.0.0) zro2ai = 1.0e-10
      tcoic = tcoi+qci*zro2ai/zoxki
      wtran = 7.7490e-06*exp(-7.90e02/tcoic)
      if (icor.ge.2) go to 100
c   bwr environment
      b = 0.12
      if (icm.le.2) b = 0.29
      go to 110
c   pwr environment
100   b = 0.15
      if (icm.le.2) b = 0.48
110   c = 1.0
      if (icm.le.2) c = 1.0/b
c   find hydrogen uptake from coolant during current time step
      d = (9.0e+05)*dco/(dco**2-dci**2)
      if (zro2ai.gt.wtran) go to 130
      if (zro2bi.ge.wtran) go to 120
c   all oxidation pretransition
      chupc = d*(b/8)*(zro2bi-zro2ai)
      go to 150
c   part of oxidation pretransition
120   chupc = d*((b/8)*(wtran-zro2ai)+(c*b/8)*(zro2bi-wtran))
      go to 150
c   all oxidation post - transition
130   chupc = d*(c*b/8)*(zro2bi-zro2ai)
      go to 150
140   chupc = 0.0
c   sum hydrogen from coolant
150   chydbi = chydai+chupc
c   sum hydrogen from alloy, fuel moisture and coolant
      chconc = chorg+0.186*ppmh2o*((dp**2)/(dco**2-dci**2))+chydbi
      return
      end
