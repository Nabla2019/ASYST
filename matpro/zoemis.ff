       subroutine zoemis (ctmax,zroxid,emissv)
c
c  Calculates the emissivity of the cladding surface as a function of
c  maximum cladding temperature and oxide thickness.
c
c  ctmax  = input maximum cladding temperature (k).
c  zroxid = input oxide layer thickness (m).
c  emissv = output cladding surface emissivity (unitless).
c  puemis = output positive standard error expected in emissv
c           when compared to in-reactor data (not currently returned).
c  uuemis = output negative standard error expected in emissv
c           when compared to in-reactor data (not currently returned).
c
c  The equations used in this subroutine are based on data from
c     (1) aec fuels and materials development program progress
c         report no. 76, usaec report gemp - 1008 (1968). section by
c         e. f. juenke and s. j. sjodahl, p  239
c     (2) t. b. burgoyne and a. garlick, paper presented at
c         specialists meeting on the behaviour of water reactor
c         fuel elements under accident conditions, spatind norway
c         (september 1976)
c     (3) e. v. murphy and f. havelock, emissivity of zirconium
c         alloys in air in the temperature range 100 - 400 c
c         j. nuc. mat., 60 (1976) pp 167-176.
c
c  Coded by r. l. miller sept 1974.
c  Modified by d. l. hagrman october 1976.
c
c  Error estimate calculations disabled by marking coding with cx in
c  columns 1 and 2.
c
       implicit none
       real ctmax,zroxid,emissv
c
       real puemis,uuemis
c
c  Model for temperatures below 1500k follows.
       if (zroxid .lt. 3.88e-06) then
         emissv = 3.25e-01 + 1.246e05*zroxid
       else
         emissv = 8.08642e-01 - 5.00e01*zroxid
       endif
cx     puemis = 0.1
cx     uuemis = 0.1
       if (ctmax .le. 1500.0) return
c
c  Modification for maximum temperatures above 1500 k follows.
       emissv = emissv*exp((1.50e03 - ctmax)/3.00e02)
       if (emissv .lt. 0.325) emissv = 0.325
cx     puemis = puemis/exp((1.50e03 - ctmax)/3.00e02)
cx     uuemis = puemis
c
c  Standard error cut off at impossible values follows.
cx     if (puemis .gt. (1.00 - emissv)) puemis = 1.00 - emissv
cx     if (uuemis .gt. emissv) uuemis = emissv
       return
       end
