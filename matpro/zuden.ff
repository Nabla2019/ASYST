      function zuden(u,z,zutemp)
c
c     zuden returns zirconium-uranium-oxygen compound
c     density
c
c     zuden  = output compound density (kg/m**3)
c     u      = input atomic fraction uranium in compound
c              (atoms uranium/atoms in compound)
c     z      = input atomic fraction zirconium in compound
c              (atoms zirconium/atoms in compound)
c     zutemp = compound temperature (k)
c
c     the subcodes cthexp, fthexp, zotexp, cden, fden,
c     and zoden are called by this function
c
c     zuden was coded by d. l. hagrman march 1982.
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      x = 1. - u - z
      if((u + z) .lt. (0.5 * x)) then
         uo = u
         u = u/(3. * (u + z))
         z = z/(3. * (uo + z))
         x = 1. - u - z
      else
         if((0.5 * x) .lt. u) then
            uo = u
            u = u/(3. * u + z)
            z = z/(3. * uo + z)
            x = 1. - u - z
         endif
      endif
      fuo2 = u/(u+z)
      fzo2 = (0.5 * x - u)/(u + z)
      fzr  = 1. - fuo2 - fzo2
      if(zutemp .ge. 3113.) then
         facmot = 1.0
      else
         facmot = 0.0
      endif
      rzr = cden(zutemp)
      ruo2 = fden(zutemp,facmot)
      rzo2 = zoden(zutemp)
      zuden = (0.270*fuo2 + 0.123*fzo2 + 0.091*fzr)/
     #  (0.270*fuo2/ruo2 + 0.123*fzo2/rzo2 +  0.091*fzr/rzr)
      return
      end
