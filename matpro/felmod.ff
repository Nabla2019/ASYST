      function felmod (ftemp,fraden,fotmtl,fcomp)
c
c     felmod calculates fuel elastic modulus as a function of
c     temperature, fractional density, oxygen to metal ratio and
c     puo2 content
c
c     ftemp  = input fuel temperature (k)
c     fraden = input fractional fuel density (ratio of actual
c              density to theoretical density)
c     fotmtl = input oxygen to metal ratio of fuel (atoms oxygen/
c              atoms metal)
c     fcomp  = input puo2 content of fuel (weight fraction)
c     felmod = output fuel young's modulus (n/m**2)
c     ufelmd = output standard error expected in felmod (n/m**2)
c                (not currently returned)
c
c     the equations used in this function are based on data from
c     (1) a. padel and c. de novion, j. nucl. mat. 33 (1969)
c         pp 40 - 51
c     (2) j. belle and b. lustman, fuel elements conference,
c         tid-7546 (1958) pp 480-481
c     (3) a. r. hall, j. nucl. mat. 37 (1970) pp 314 - 323
c
c     felmod coded by v. f. baston march 1974
c     modified by c. s. olsen in february and july 1975
c     modified by d. l. hagrman, e. t. laats, and m. a. morgan
c     june 1977
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      ys = 23.34e10 * (1. - (2.752*(1. - fraden)))*(1. -1.0915e-04*
     #     ftemp)
c
      ufelmd = 6.0e09
      if (ftemp .lt. 1.6e030) go to 10
      ufelmd = ufelmd + ys * (ftemp - 1.6e03)/6.0526e03
      if (ftemp .lt. 3113.15) go to 10
      ys     = 1.0
      ufelmd = 0.0
      go to 80
   10 unstoc = abs(fotmtl - 2.0)
      if (unstoc .gt. 1.0e-03) go to 20
      if (fcomp  .lt. 1.0e-03) go to 80
   20 b = 1.34
      if (fotmtl .lt. 2.0) b = 1.75
      y = ys * exp(-b * unstoc) * (1. + 0.15 * fcomp)
      ufelmd = (ufelmd**2 + (y - ys)**2)**0.5
      ys = y
   80 felmod = ys
c
      return
      end
