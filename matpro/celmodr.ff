       function celmodr(ctemp,fnck,cwkf,deloxy)
!  celmodr calculates cladding Young's modulus as a function of
!  temperature, fast neutron fluence, cold work, and average oxygen
!  concentration.
!
!  Grain orientation is assumed random. The expected standard error is
!  computed but not returned (ucelmd).
!
!  celmodr = output Young's modulus for zircaloy 2 and 4 with random
!            texture (pa).
!  ucelmd =  output standard error expected in celmodr (pa) (not
!            currently returned).
!  ctemp   = input cladding temperature (K).
!  fnck    = input effective fast fluence (neutrons/(m**2)).
!  cwkf    = input effective cold work (unitless ratio of areas).
!  deloxy  = input average oxygen concentration excluding oxide layer -
!            average oxygen concentration of as-received cladding (kg
!            oxygen/kg zircaloy)
!
!  The Young's modulus calculated by this function is based on data from
!  the following references:
!  (1) L. R. Bunnell, G. B. Mellinger and J. L. Bates, "High
!      Temperature Properties of Zircaloy - Oxygen Alloys," EPRI Report
!      NP - 524 (1977).
!  (2) E. S. Fisher and C. J. Renken, Physical Review 135 2a (20 July
!      1964) pp 482 - 494.
!  (3) P. E. Armstrong and H. L. Brown, Transactions of the
!      Metallurgical Society of AIME 230, (August 1964)  pp 962 - 966.
!  (4) A. Padel and A. Groff, Journal of Nuclear Materials 59 (1976) pp
!      325-326
!  (5) W. R. Smalley, "Saxton Core II Fuel Performance Evaluation, Part
!      I: Materials, WCAP-3385-56 (September 1971).
!
!  The expected standard error was derived using additional data from
!  the following references:
!  (1) C. C. Busby and C. R. Woods (eds.), "Properties of Zircaloy-4
!      Tubing", USAEC Report WAPD-TM-585 (December 1966) p 65.
!  (2) Z. Spasic, M. Pavlovic and G. Simis, Conference on the Use of
!      Zirconium Alloys in Nuclear Reactors, Marianske Lanze, Czech.
!      Conf-681086 (1968) pp 277 - 284.
!  (3) R. L. Mehan, "Modulus of Elasticity of Zircaloy-2 Between Room
!      Temperature and 1000 F," ~KAPL-M-RLM-16 (July 1958).
!  (4) D. O. Northwood, I. M. London, and L. E. Bahen, Journal of
!      Nuclear Materials 55 (1975)  pp. 299-310.
!  (5) F. R. Shober, J. A. Van Echo, L. L. Marsh jr. and J. R. Keeler,
!      "The Mechanical Properties of Zirconium and Zircaloy-2," 
!       BMI-1168 (1957).
!
!  celmodr was coded by R. L. Miller in March 1974.
!  Last modified by D. L. Hagrman March 1982.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       real(kr) celmodr
       real(kr) ctemp,fnck,cwkf,deloxy
!
       include 'wwer.h'
!
       real(kr) amodl,amodr,c1,c2,c3,taab,tabb,ucelmd,wfox
!
       if (iwwer .eq. 0) then
         if (ctemp .lt. 2098.) then
           c1 = (1.16e11 + ctemp*1.037e8)*5.7015
           c2 = 1.0
           if (fnck .gt. 1.0e22) c2 = 0.88*(1.0 -
     &     exp(-fnck/1.0e25)) + exp(-fnck/1.0e25)
           c3 = -2.6e10
           celmodr = (1.088e11 - 5.475e7*ctemp + c1*deloxy +
     &     c3*cwkf)/c2
           if (ctemp .ge. 1090.) then
!
!  Calculate a to a + b and a + b to b boundaries.
             wfox = deloxy + 0.0012
             taab = 1094. + wfox*(-1.289e3 + wfox*7.914e5)
             if (wfox .ge. 0.025) then
               taab = 1556.4 + 3.8281e4*(wfox - 0.025)
             endif
             tabb = 392.46*((100*deloxy + 0.1242807)**2 +
     &       3.1417)
             if (deloxy .ge. 4.7308937e-03) then
               tabb = (100*deloxy + 0.12)*491.157 + 1081.7413
             endif
             if (ctemp .ge. taab) then
               if (ctemp .le. tabb) then
                 amodl = (1.088e11 - 5.475e7*taab + c1*deloxy +
     &           c3*cwkf)/c2
                 amodr = 9.21e10 - tabb*4.05e7
                 celmodr = amodl + (ctemp - taab)*(amodr - amodl)/
     &           (tabb - taab)
               else
                 celmodr = 9.21e10 - ctemp*4.05e7
               endif
             endif
           endif
         else
           celmodr = 1.0e-10
         endif
         ucelmd = 6.4e9
!
!  This code block calculates cladding Young's modulus as a function of
!  temperature and oxygen content in the alpha phase; data is derived
!  from:
!  (1) Volkov B. Yu. et.al, "Material Property Library for H1-Alloy
!  Cladding,", Preprint IAE-4941/11, 1989.
!  (2) Fedorov G. B. et.al, "Influence of Oxygen Content and
!  Gamma-Irradiation on the Elastic Module and Internal Friction of Pure
!  Zirconium," Metal Physics, Vol 32 (3) , p.621, 1971.
!
       else
         if (ctemp .le. 1073.) then
           celmodr = 1.121e11 - 6.438e7*ctemp + 3.021e12*deloxy
         elseif(ctemp .le. 2133.) then
           celmodr = 9.129e10 - 4.5e7*ctemp
         else 
           celmodr = 1.0e-10
         endif
       endif
       return
       end
