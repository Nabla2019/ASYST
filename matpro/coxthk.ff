       function coxthk(ctemp,kpick)
!  Function coxthk returns the growth rate constant for oxide thickness,
!  oxygen-stabilized alpha layer thickness (inner and outer),and
!  thickness of the oxygen-stabilized alpha layer between the outer and
!  inner alpha layers. for temperatures of 1273K-1853K data and analyses
!  from J.V. Cathcart of ORNL are used to compute the ZrO2 and oxygen-
!  stabilized alpha thicknesses on the outer surface and similar
!  equations from P. Hofmann of the Kernforschungszentrum Karlsruhe
!  (KFK) for oxygen-stabilized alpha layers on the cladding inner
!  surface when there is pellet-cladding mechanical inter-action (pcmi).
!  For temperatures of 1853K-2100K, data and analyses from v.f urbanic
!  and t.h. heidrick , "high temperature oxidation of zircaloy-2 and
!  zircaloy-4 in steam, "Journal of Nuclear Materials 75, (1978) pp.
!  251-261, are used to compute zro2 layer thickness.
!
!  Coded by N. Hampton November 1981.
!  Last updated by D. L. Hagrman July 1982.
!
!     coxthk= output growth rate constant (m**2/s)
!     ctemp = input cladding temperature (K)
!     kpick = input integer 1-4, where:
!     kpick=1 is growth rate constant for oxide thickness.
!     kpick=2 is growth rate constant for oxygen-stabilized
!              alpha layer nearest outer cladding surface.
!     kpick=3 is growth rate constant for oxygen-stabilized
!              alpha layer nearest uo2 fuel.
!     kpick=4 is growth rate constant for oxygen-stabilized
!              alpha layer between outer and inner alpha layers.
!
!  NOTE by E.W. Coryell.
!  kpick=3 or 4 are NOT used by SCDAP/RELAP5.
!  Design report is "CLADDING OXIDATION", N.L. Hampton, D.L. Hagrman
!  Nov. 1981, EGG-CDD-5647.
!
$if def,iua
       use uncdata
$endif
       implicit none
       real coxthk,ctemp
       integer kpick
!
       include 'contrl.h'
!
       if (.not.chngno(11)) then
         if (kpick .eq. 1) then
           if (ctemp .gt. 1853.) then
             coxthk = 2.0*1.035e-6*exp(-16014./ctemp)
           else
             coxthk = 2.0*1.12569e-6*exp(-18063./ctemp)
           endif
         elseif (kpick .eq. 2) then
!  Growth rate constant for oxygen-stabilized alpha layer nearest
!  outer cladding surface.
           coxthk = 2.0*0.76149e-4*exp(-24228./ctemp)
         elseif (kpick .eq. 3) then
!  Growth rate constant for oxygen-stabilized alpha layer nearest Uo2.
           coxthk = 0.32e-4*exp(-4.9e4/(1.987*ctemp))
         elseif (kpick .eq. 4) then
!  Growth rate constant for oxygen-stabilized alpha layer between outer 
!  and inner alpha layers.
           coxthk = 0.70e-4*exp(-4.4e4/(1.987*ctemp))
         endif
       else
!
         if (kpick .eq. 1) then
           if (ctemp .gt. 1800.) then
             if (ctemp .gt. 1900.) then
               coxthk = 2.98116e3*exp(-28420./ctemp)
             else
               coxthk = 2.0078e14*exp(-75793./ctemp)
             endif
           else
             coxthk = 7.82*exp(-20214./ctemp)
           endif
         elseif (kpick .eq. 2) then
!  Growth rate constant for oxygen-stabilized alpha layer nearest outer 
!  cladding surface.
           if (ctemp .gt. 1800.) then
             if (ctemp .gt. 1900.) then
               coxthk = 5.14006e-2*exp(-10252./ctemp)
             else
               coxthk = 3.08617e-5*exp(-3842./ctemp)
             endif
           else
             coxthk = 5.08e1*exp(-21923./ctemp)
           endif
         endif
       endif
$if def,iua
       if (uncmode) call appwtoxid (coxthk)
$endif
       return
       end function coxthk
