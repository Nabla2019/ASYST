      subroutine ckmn(ctemp,deloxy,fnck,fncn,cwkf,cwnf,rstran,ak,an,am)
c
c     ckmn calculates parameters for the cladding equation of state
c     as a function of temperature, average oxygen concentration,
c     fast neutron fluence, and cold work.
c
c     ak    = output strength coefficient (pa)
c     an    = output strain hardening exponent (unitless)
c     am    = output strain rate sensitivity exponent (unitless)
c     um    = estimated  uncertainty of am
c               (not currently returned)
c     un    = estimated uncertainty of an
c               (not currently returned)
c               note  an - un is negative near 1000k
c     uk    = estimated uncertainty of ak
c               (not currently returned)
c     ctemp  = input cladding meshpoint temperature (k)
c     deloxy = input average oxygen concentration excluding
c              oxide layer - average oxygen concentration of
c              as received cladding  (kg oxygen/kg zircaloy)
c     fnck   = input effective fast fluence for strength
c              coefficient (neutrons/(m**2))
c     fncn   = input effective fast fluence for strain hardening
c              exponent (neutrons/(m**2))
c     cwkf   = input effective cold work for strength
c              coefficient (unitless ratio of areas)
c     cwnf   = input effective cold work for strain hardening
c              exponent (unitless ratio of areas)
c     rstran = input effective true plastic strain rate (s**(-1))
c
c     the equations used in this subroutine are based on data from
c     (1) c.r. woods, properties of zircaloy-4 tubing, wapd-tm-585
c         (1966)
c     (2) ultimate strength data of h.c. brassfield,et al.
c         gemp-482(1968)
c     (3) a.l. bement, jr., effects of cold work and neutron
c         irradiation on the tensile properties of zircaloy-2,
c         hw-74955
c     (4) a. cowan and w.j. langford j.nuclear mater.
c         30 (1969) pp 271-281
c     (5) l.m. howe and w.r. thomas, j. nuclear  mater.
c         1 (1960) p 248
c     (6) a. m. garde light-water-reactor safety research
c         program quarterly progress report april - june 1975
c         anl-75-58
c     (7) a. m. garde light-water-reactor safety research
c         program quarterly progress report july - september 1975
c         anl-75-72
c     (8) r.l. mehan and f.w. wiesinger, mechanical properties
c         of zircaloy-2, kapl-2110
c     (9) d. lee and w.a. backofen tms-aime 239  (1967) pp 1034-1040
c     (10) p. j. crescimanno, high strain rate tensile tests
c          of zircaloy at 550f, wapd-tm-1263 (february 1976).
c     (11) c. r. hann et al, transient deformation properties
c          of zircaloy for  loca simulation, epri np-526
c          volume 3 (march 1978)
c
c     coded by d. l. hagrman august 1977
c     last modified by d. l. hagrman july 1982 (cdf-mp-10)
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      t = ctemp
c
c     limit strain rate to a minimum of 1.0e-05
      if(rstran .lt. 1.0e-05)  rstran = 1.0e-05
c
c     find strain rate exponent, am
      if(t .le. 730.) am = 0.02
c
      a =  20.63172161
      b = - 0.07704552983
      c =   9.504843067e-05
      d = - 3.860960716e-08
      if(t .gt. 730.) am =  a + t*(b+ t*(c+ t*d))
      if(t .ge. 900.) am = -6.47e-02 + t * 2.203e-04
c
c     modify strain rate exponent, am, in alpha - beta region
      if(rstran .ge. 6.34e-03) go to 100
      if(t .le. 1090.0) go to 100
      if(t .ge. 1255.0) go to 100
      if(t .gt. 1172.5) go to  24
      am = am + 6.78e-2*log(6.34e-3/rstran)*((t-1090.)/82.5)
      go to 100
   24 am = am + 6.78e-2*log(6.34e-3/rstran)*((1255.-t)/82.5)
c
c     find uncertainty
  100 if(t .le. 700.) um =  0.01
      if(t .gt. 700.) um = -2.97992e-02 + t * 5.6856e-05
      if(t .gt. 900.) um =  0.16 * am
      am = am *  exp(-69.*deloxy)
c
c     find strain hardening exponent, an
c
      an = -9.490e-02 + t*(1.165e-03 + t*(-1.992e-06 +
     #      t*9.558e-10))
      if(t .ge. 1099.0722) an = -0.22655119 + 2.5e-04 * t
      if(t .ge. 1600.000 ) an =  0.17344880
c
c     find uncertainty
      if(t. le. 700.) un =  0.017
      if(t .gt. 700.) un = -2.8405405e-02 + t * 6.4864864e-05
      if(t.gt. 1255.) un =  0.053
c
c     find irradiation and cold work effects
      an = (8.47e-01 * exp(-3.92e+01*cwnf)  + 1.53e-01 +
     #      cwnf  * (-9.16e-02 + cwnf *2.29e-01)) *
     #      exp(-((fncn)**0.33)/(3.73e+07 + 2.0e+08*cwnf)) * an
      an = an*(1. + (1250. - (1250./(exp((t-1380.)/20.) + 1.)))
     #     *deloxy)
      if (an .gt. 0.8)  an = 0.8
c     lower bound on an prevents overflow in cstrni
      if(an .lt. 1.5e-03) an = 1.5e-03
c
c     find strength coefficient, ak
      ak = 1.17628e+09 + t*(4.54859e+05 + t*(-3.28185e+03 +
     #     t*1.72752))
      if(t .ge. 750.0) ak = 2.5224880e+06 * exp(2.8500027e+06/(t**2.0))
      if(t .ge. 1090.) ak = 184.1376039e+06 - 1.4345448e+05 * t
      if(t .ge. 1255.) ak = 4.330e+07 + t*(-6.685e+04 +
     #                      t*(3.7579e+01 - t*7.33e-03))
      if(t.gt.2100.)ak=1.0
c
c     find uncertainty
      if(t .le. 700.) uk = 77.00e+06
      if(t .gt. 700.) uk = 110.43693e+06 - t * 4.7767045e+04
      if(t .gt. 800.) uk = ak/3.0
c
c     find irradiation and cold work effects
      fkcc = fnck
      if(t .gt. 1255.) fkcc = 0.0
      ak = ak * (1.0 + 5.46e-01*cwkf + 9.7e-27 * fkcc)
      ak = ak*(1. + (1120.-(990./(exp((t-1301.5)/61.) + 1.)))
     #     *deloxy)
c
      return
      end
