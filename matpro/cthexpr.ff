       subroutine cthexpr (costh2,cosfi2,ctemp,rftemp,strn11,strn22,
     & strn33)
!
!  cthexpr calculates the thermal expansion strain for a piece of
!  zircaloy cladding as a function of temperature and of cladding
!  texture.
!
!  strn11 = output thermal expansion strain in the circumferential
!           direction (m/m).
!  strn22 = output thermal expansion strain in the axial
!           direction (m/m).
!  strn33 = output thermal expansion strain in the thickness
!           direction (m/m).
!  ctemp  = input cladding temperature (k).
!  cosfi2 = input volume fraction weighted average of the squared
!           sine of phi (unitless).
!  costh2 = input volume fraction weighted average of the squared
!           cosine of theta (unitless).
!
!  Values for cosfi2 and costh2 may be obtained through use of the
!  subroutine ctxtur.
!
!  These calculations are based on data and techniques in the
!  following sources.
!  (1) L. R. Bunnell et al, "High Temperature Properties of Zircaloy-
!      Oxygen Alloys," EPRI NP-524 (March 1977).
!  (2) D. L. Douglass, "The Physical Metallurgy of Zirconium," Atomic
!      Energy Review,1,#4 (December 1963).
!  (3) C. Kittel, "Introduction to Solid State Physics," 3rd Edition,
!      New York, John Wiley and Sons Inc., 1966.
!  (4) G. B. Skinner, H.L. Johnston, "Thermal Expansion of Zirconium
!      Between 298 and 1600k," Journal of Chemical Physics, 21 (1953).
!
!  cthexpr was coded by G. A. Reymann January 1980.
!  Last modified by D. L. Hagrman March 1982.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       real(kr) costh2,cosfi2,ctemp,rftemp,strn11,strn22,strn33
!
       include 'wwer.h'
!
       real(kr) strn1r,strn2r,strn3r,strs11,strs33,t
       integer n
!
!  Calculate single crystal strains.
!  strs11 = single crystal strain perpendicular to the c-axis.
!  strs33 = single crystal strain along the c-axis.
!
       t = rftemp
       n = -1
       if (iwwer .gt. 0) go to 100   
    5  if (t .ge. 2098.) go to 40
       if (t .gt. 1244.0) go to 20
       if (t .gt. 1083.0) go to 10
!
       strs11 = 4.95e-06*t - 1.485e-03
       strs33 = 1.26e-05*t - 3.780e-03
       go to 30
!
   10  strs11 = (2.77763 + 1.09822*
     & cos((t - 1083.0)*3.141593/161.0))
       strs11 = strs11*1.0e-03
       strs33 = (8.76758 + 1.09822*
     & cos((t - 1083.0)*3.141593/161.0))
       strs33 = strs33*1.0e-03
       go to 30
!
   20  strs11 = 9.7e-06*t - 1.04e-02
       strs33 = 9.7e-06*t - 4.40e-03
       go to 30
!
!  The following block calculates the thermal expansion strain for a
!  piece of H1-alloy cladding as a function of temperature and of 
!  cladding texture; data is derived from       
!  P. F. Prasolov et al, Journal Atomnaya Energiya, V.68(2), p.98, 1990,
!  Moscow, Atomizdat, 1982.
  100  if (t .ge. 2133.) go to 47
       if (t .gt. 1153.0) go to 120
       if (t .gt. 883.0) go to 110
!     
       if (ctemp.le. 573.) then
         strs11 = 0.1338985e-8*t**2 + 3.85875e-6*t -
     &   0.127813365e-2
       else
         strs11 = 0.13725577e-2 + 5.4e-6*(t - 573.)
       endif
       strs33 = 0.3336985e-8*t**2 + 5.6539e-6*t -
     & 0.199649865e-2
       go to 30 
!
  110  strs11 = 3.0465577e-3 + 2.312e-8*(t - 883.) -
     & 7.358e-8*
     & (t - 883.)**2 + 1.7211e-10*(t - 883.)**3
       strs33 = 5.5977e-3 + 2.312e-8*(t - 883.) - 7.358e-8*
     & (t - 883.)**2 + 1.7211e-10*(t - 883.)**3
       go to 30 
! 
  120  strs11 = 1.076459e-3 + 9.7e-6*(t - 1153.)
       strs33 = 3.6276e-3 + 9.7e-6*(t - 1153.)      
!
!  Calculate cladding strains using single crystal strains.
   30  strn11 = (cosfi2 + costh2*(1.0 - cosfi2))*strs11 +
     & (1.0 - costh2)*(1.0 - cosfi2)*strs33
!
       strn22 = (1.0 - cosfi2 + costh2*cosfi2)*strs11 +
     & (1.0 - costh2)*cosfi2*strs33
!
       strn33 = (1.0 - costh2)*strs11 + costh2*strs33
!
       go to 50
   40  strs11 = 9.9506e-03
       strs33 = 1.59506e-02
       go to 45
   47  strs11 = 1.0582459e-2
       strs33 = 1.31336e-2
   45  strn11 = 0.67*strs11 + 0.33*strs33 + 0.0067
       strn22 = strn11
       strn33 = strn11
!
   50  if (n .lt. 0) then
         n = n + 2
         strn1r = strn11
         strn2r = strn22
         strn3r = strn33
         t = ctemp
         go to 5
       else
         strn11 = strn11 - strn1r
         strn22 = strn22 - strn2r
         strn33 = strn33 - strn3r
       endif
!  ustrn2 = uncertainty in the tube axial thermal strain.
!  ustrn1 = uncertainty in the tube circumferential thermal strain.
!  ustrn1 = 0.12*strn11.
!  ustrn2 = 0.08*strn22.
!
       return
       end
