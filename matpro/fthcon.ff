      subroutine fthcon (ftemp,fraden,fotmtl,con,dkdt)
c
c     fthcon calculates the fuel thermal conductivity and its
c     derivative with respect to temperature as a function of
c     temperature, density, oxygen to metal ratio, composition
c     and burnup
c
c     con    = output fuel thermal conductivity (w/(m*k))
c     dkdt   = output derivative of fuel thermal conductivity
c              with respect to temperature (w/m*k*k))
c     ftemp  = input fuel temperature (k)
c     fraden = input fuel density (ratio of actual density to
c              theoretical density)
c     fotmtl = input oxygen to metal ratio of fuel (atoms oxygen/
c              atoms metal)
c
c     the following inputs are by common block
c     comp       = input puo2 content of fuel (percent puo2
c                  in total fuel weight)
c     bu         = input burnup (mw-s/kg-u)
c
c     the following uncertainty is computed but not returned
c     ucon   = output expected standard error of the fuel
c              thermal conductivity (w/(m*k))
c
c     the equations used in this subroutine are based on data from
c     (1)  j. a. christensen et al., uranium dioxide thermal
c          conductivity, transactions of the american nuclear
c          society  7 (1964) pp. 391 - 392
c     (2)  t. g. godfrey et al., thermal conductivity of
c          uranium dioxide and armco iron by an improved
c          heat flow technique, ornl-3556 (1954)
c     (3)  j. l. bates, high temperature thermal conductivity
c          of round robin uranium dioxide, bnwl-1431 (1970)
c     (4)  r. l. gibby, the effect of plutonium content on the
c          thermal conductivity of (u,pu)o2 solid solutions,
c          journal of nuclear materials 38 (1971) pp 163 - 177
c     (5)  j. c. weilbacher, diffusivite thermique de l oxide
c          d uranium et de l oxide de thorium a haute temperature,
c          high temperatures - high pressure 4, (1972) pp 431 - 438
c     (6)  l. a. goldsmith and j. a. m. douglas, measurements
c          the thermal conductivity of uranium dioxide at 670 -
c          1270 k, journal of nuclear materials 47, (1973) pp 31 - 42
c     (7)  i. c. hobson et al., effect of porosity and stoichiometry
c          on the thermal conductivity of uranium dioxide, journal
c          of physics section d: applied physics 7 (1974)
c          pp 1003 - 1015
c     (8)  r. l. gibby, the thermal diffusivity and thermal
c          conductivity of stoichiometric (u0.8,pu0.2)o2, bnwl-704
c          (1968)
c     (9)  r. l. gibby, the effect of oxygen stoichiometry on the
c          thermal diffusivity and conductivity of (u0.75,pu0.25)
c          o2-x bnwl-927 (1969)
c     (10) l. a. goldsmith and j. a. m. douglas, the thermal
c          conductivity of plutionium-uranium dioxide at
c          temperatures up to 1373 k, journal of nuclear materials
c          43 (1972) pp 225 - 233
c     (11) h. e. schmidt, die waermeleitfaehigkeit von uran and
c          uran-plutinium dioxyd bei hohen temperaturen, forschung
c          ingenieur-weisen 38 (1976) pp 149 - 151
c     (12) d. r. olander, fundamental aspects of nuclear reactor
c          fuel elements, tid-26711-p1 (1976)
c
c     fthcon was origionally coded by c. s. olsen feb 1975
c     modified by g. a. reymann august 1978
c     modified by d. l. hagrman march 1982
c     modified by d. l. hagrman to remove phypro common block
c          (cdr-mp-21) may 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      dimension c(2)
c
c     expressions to replace phypro common block
      comp = 0.0
      ftmelt = 3113.
c
c     if fuel has melted set output and return
      if(ftemp .lt. ftmelt) go to 5
      con = 11.5
      dkdt = 0.
      ucon = 3.45
      return
    5 continue
c
c     find constants
      frpu = comp/100.
      a = 0.339 + 12.6 * abs( 2.0 - fotmtl )
      if(frpu .gt. 0.75) frpu = 0.75
      b = 6.867e-02 * (1.0 + 0.6238 * frpu )
      t = ftemp
      i = 1
c
c     find specific heat at const. vol. and volume change
   10 tr = 535.285/t
      cv = 296.7 * tr * tr * exp(tr) / (( exp(tr) - 1.0)**2 )
      dv = 1.0 + 3.0*(1.0e-05 * t - 3.0e-03 + 4.0e-02 *
     #     exp(-5.0e+03/t))
      if(frpu .lt. 0.0001) go to 20
c
c     use rule of mixtures for mixed oxides
      tr = 571.0/t
      cv = cv * (1.0 - frpu) + ( 347.4 * tr * tr  *  exp(tr) /
     #     (( exp(tr) - 1.0 )**2 )) * frpu
      dv = (dv - 1.0 ) * (1.0 - frpu)  +  3.0 * ( 9.0e-06 * t
     #    - 2.7e-03 + 7.0e-02 * exp(-5.072e+03/t)) * frpu  + 1.0
c
c     find porosity correction
   20 beta = 6.50 - 4.69e-03 * t
      if(t .ge. 1364.647) beta =  15.811308 - t*(.01833647-t*5.e-6)
      if(t .gt. 1833.647) beta = - 1.0
c
c     find electronic contribution
      cone = (5.2997e-03 *  t  *  exp(-13358.0/ t )) *
     #       (1.0 + 0.169 * ((13358.0/ t + 2.0)**2 ))
      btemp = t
      if(t .ge. 1800.) btemp = -3240. + t * (4.6 - t * 0.001)
      if(t .ge. 2300.) btemp =  2050.
c
c     find conductivity
      c(i) = (cv * fraden)/(dv * (a + b * btemp ) *
     #       (1.0 + beta * (1.0 - fraden))) + cone
c
c     find derivative of fuel conductivity
c     i    = i + 1
c     t    = ftemp + ((-1.0)**i)
c     if(i .lt. 3) go to 10
      con  =  c(1)
c     dkdt = (c(2) - c(1))
c
c     find uncertainty
c     ucon =(0.2 * (1.0 - frpu) + 0.7 * frpu ) * (1.0 + abs(2.0 -
c    #       fotmtl) * 10.)
      return
      end
