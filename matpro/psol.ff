      function psol(x,u,z)
c
c     psol calculates the solidus temperature of
c     uranium - zircaloy - oxygen compounds
c
c     psol   = output solidus temperature (k)
c     x      = input atomic fraction oxygen in compound (unitless)
c     u      = input atomic fraction uranium in compound (unitless)
c     z      = input atomic fraction zircaloy in compound (unitless)
c
c     the solidus temperature calculated by this function
c     is based on data from the following references
c     (1)r. e. latta and r. e. fryxell, determination of
c        solidus-liquidus temperatures in the uo2+x syst5m
c        (-0.50 less than x less than 0.20), journal of nuclear
c        materials 35 pp. 195-210 (1970).
c     (2)k. a. romberger, c. f. bates jr. and h. h. stone,
c        phase equlibrium studies in the uo2 - zro2 system,
c        journal of inorganic and nuclear chemistry 29 pp. 1619-1630
c        (1966).
c     (3)r. ruh and h. j. garrett, nonstoichiometry of zro2 and
c        its relation to tetragonal-cubic inversion in zro2,
c        journal of the american ceramic society 50 pp 257-261 (1966).
c     (4)r. f. domagala and j. d. mcpherson, system zirconium-oxyg5n,
c        journal of metals 6 transactions aime 200 pp 238-246 (1954).
c
c     psol was coded by d. l. hagrman october 1980
c     last updated by d. l. hagrman july 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
c     find oxygen to metal ratio.  assume symmerty about 2.0
      ye = x/( u + z )
      if(ye .gt. 2.23) then
         ye = 2.23
      endif
      if(ye .gt. 2.) ye = 4. - ye
c     zircaloy - zircaloy oxide model
      tz = 0.0
      if(z .lt. 1.0e-06) go to 10
      xoz = x
      tz = 2098. + xoz * 1150.
      if(xoz .le. 0.10) go to 10
      tz = 2213.
      if(xoz .le. 0.18) go to 10
      tz = 1.3895317e+03 + xoz*(7.6400748e+03 - xoz*1.7029172e+04)
      if(xoz .le. 0.29) go to 10
      tz = 2173.
      if(xoz .le. 0.63) go to 10
      tz = -1.1572454e+04 + xoz*2.1818181e+04
      if(xoz .le. 0.667) go to 10
      xoz = 1.334 - xoz
      tz = -1.1572454e+04 + xoz*2.1818181e+04
c     uranium - uranium oxide model
   10 tu = 0.0
      if(u .lt. 1.0e-06) go to 20
      dstoc = abs(x/(u+z) - 2.)
      tu = 3119. + dstoc * (-873.7 - dstoc*1469.)
      if(tu .lt. 2700.) tu = 1373.
c     uranium dioxide - zircaloy dioxide model
   20 td = 0.0
      if(ye .lt. 1.0e-02) go to 30
      zou = z/(u+z)
      td = 3119. + zou*(-1.1303949e+03 + zou*1.0073297e+03)
      if(zou .le. 0.55) go to 30
      td = 2773.6667 + zou*51.515151
      if(zou .le. 0.88) go to 30
      td = 1.6896667e+03 + zou*1.2833333e+03
c
c     combine binary estimates assuming oxygen to metal
c     ratio is most important
   30 w = (ye/2.)**2
      psol = (1. - w ) * ((z/(z+u))* tz + (u/(z+u)) * tu) + w * td
c     estimate uncertainty
      upsol = 10. + 050. * (1. - abs(z - u))
     #        +  100. * ye * abs(ye - 2.)
      return
      end
