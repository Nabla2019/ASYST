      subroutine frestr (ftemp, timest, grnint, gradt , powden,
     #                  grnsiz, nshatr, fdens , colgrn, ncolgn)
c
c      frestr calculates fuel restructuring which includes;
c          . calculation of equiaxed grain growth
c          . calculation of columnar grain growth.
c          . calculation of fuel shattering.
c          . calculation of fuel density in columnar grains.
c      powden = input power density in fuel region (j/m**3).
c      ftemp  = input fuel temperature (k).
c      gradt  = input temperature gradient (k/m)
c      timest = input time step  (s).
c      grnint = input fuel grain size (m).
c      bu     = input total burnup of fuel (mws/kg).
c
c      the following five parameters must be stored in an array
c      external to this subroutine. the array size should be equal to
c      the number of radial nodes being considered.
c      grnsiz = output equiaxed fuel grain size(m).
c      ncolgn = input/output; 1 if columnar grains formed and 0 if not.
c      nshatr = input/output; 1 if fuel is shattered and 0 if not.
c      fdens  = input/output fuel density (kg/m**3).
c      colgrn = output columnar grain length (m)
c
c      frestr was coded by c.s.olsen  november 1975.
c      frestr was recoded by r.e.mason july 1979.
c     modified by d. l. hagrman to remove phypro common block
c          (cdr-mp-21) may 1982
c
c      frestr was developed from the following sources.
c
c      j. b. ainscough, b. w. oldfield and j. o. ware, "isothermal
c      grain growth kinetics in sintered uo2 pellets," journal of
c      nuclear materials, 49 (1973/74).
c
c      r. n. singh, "isothermal grain-growth kinetics in sintered
c      uo2 pellets," journal of nuclear materials, 64 (1977).
c
c      j. r. macewan, grain growth in sintered uranium dioxide,
c      aecl-1184 (1961).
c
c      v. h. stehle, "kornwachstum von uo2," bericht der deutschen
c      keramische gesellschaft, 40 (1963).
c      40 (1963).
c
c      d. w. brite et al, eei/epri fuel densification project 131
c      final report (1975).
c
c      m. d. freshley et al, plutonia fuel study, epri np-637 (1978).
c
c      h. kawamata, h. kaneko, h. furuya, and m. koizumi, "migration
c      rate of lenticular voids in uo2 under the influence of
c      temperature gradient," journal of nuclear materials (1977).
c
c      b. j. buescher and r. o. meyer,"thermal-gradient migration of
c      helium bubbles in uranium dioxide," journal of nuclear
c      materials, 48 (1973).
c
c      w. oldfield and j. b. brown, jr., "bubble migration in uo2
c      - a study using laser image furnace," material science and
c      engineering, 6 (1970).
c
c      c. ronchi and c. sari, "properties of lenticular pores in
c      uo2, (u,pu)o2, and pu02," journal of nuclear materials, 50
c      (1974).
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
c     expressions to replace phypro common block
      bu = 0.0
      ftmelt = 3113.
c
c      calculation of columnar grain growth parameters.
       velent = 49.2227 * gradt * exp(-44980.0/ftemp)/(ftemp*ftemp)
       if (ncolgn .eq. 1) go to 20
       ncolgn = 0
       if(velent * timest .le.  0.0005) go to 20
c      the uncertainity of the columnar grain growth threshold
c      is + or - 100 k.
       colgrn = timest * velent
       ncolgn = 1
       nshatr = 0
c
   20  continue
       if (ncolgn .eq. 1) fdens  = 10750.6
c      calculation of equiaxed grain growth or columnar grain width
c
       grnsiz = (grnint**4. +.102687e-12 * exp(-35873.2/ftemp) * timest
     #   /(ftemp * (1. - 5.74664e-6*bu) * (1. - 5.74664e-6*bu))) **.25
c      the standard error of estimate of equiaxed grain sizes is
c      + or - 8.400e-06.
c
c      calculation of fuel shattering
       if((nshatr .eq. 1) .or. (ncolgn .eq. 1)) go to 100
       nshatr = 0
       if (ftemp .ge. ftmelt) go to 100
       efracs = 6. * (0.24 - 6.e-4 * (ftemp - 1673.)) / grnsiz
       if (powden .ge. efracs) nshatr = 1
  100  continue
       return
       end
