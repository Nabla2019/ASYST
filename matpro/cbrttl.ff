       function cbrttl (am5,y8,y88,y9,y99,y100,ctemp,bwtfro,rcool,aa1,
     #                  ab1,ac1,ad1,ae1,af1,ag1,ah1,ai1,s)
c
c      the function cbrttl describes the onset of zircaloy cladding
c      embrittlement by oxygen absorption.
c
c      cbrttl  = output cladding embrittlement index. if cbrttl
c                .gt. 0.0 the cladding is considered brittle.
c
c      am5     = input original cladding cladding wall thickness (m)
c      y8      = input outside oxide thickness (m)
c      y88     = input inside  oxide thickness (m)
c      y9      = input outside oxygen stabilized alpha thickness (m)
c      y99     = input inside  oxygen stabilized alpha thickness (m)
c      y100    = input sum of other inside layers (m)
c      ctemp   = input cladding temperature (k)
c      bwtfro  = input cladding average oxygen concentration in excess
c                of as-fabricated amount (weight fraction)
c      rcool   = input cooling rate before quench (k/s)
c      aa1-ai1 = input oxygen  concentrations in prior beta at nine
c                equidistant nodes. aa1 is the concentration at the
c                beta/alpha+beta interface and ai1 is the
c                concentration at
c                  a) the inner cladding surface for outside oxidation
c                     only, or
c                  b) the minimum concentration for two-sided oxidation
c                     (weight fraction)
c      s       = input index, 1.0 for outside oxidation only,
c                             2.0 for two-sided oxidation
c
c      the correlations used in this function are based on the
c      following references.
c        (1) r.e. pawel, journal of nuclear materials,50 (1974)
c        (2) h.m. chung, a.m. garde, and t.f. kassner --
c             a. anl-78-49 (june 1978).
c             b. anl-78-3  (january 1978).
c             c. anl-78-25 (may 1978).
c        (3) d.o. hobson and p.l. rittenhouse, ornl-4758 (jan. 1972).
c        (4) s.l. seiffert and r.r. hobbins, tfbp-tr-292 (october 1978).
c
c      cbrttl was originally coded by g.a. reymann in feb. 1979.
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
       dimension an(9)
c
       if(ctemp .lt. 1244.0) go to 100
       cbrttl  = 0.0
c
       if(rcool .gt.  100.0) go to 50
c  low cooling rate calculation.
       an(1) = aa1
       an(2) = ab1
       an(3) = ac1
       an(4) = ad1
       an(5) = ae1
       an(6) = af1
       an(7) = ag1
       an(8) = ah1
       an(9) = ai1
c
       y2    = (am5 - 2.*(y8+y88)/3. - (y9+y99) - y100)*100.0/8.0
c      y2    = internodal spacing in the beta.
       if(an(1) .lt. 1.0e-02) go to 20
c
       do 10  j = 2,9
         k=j
         if(an(j) .lt. 1.0e-02) go to 15
 10    continue
c
 15    anumb = 9.0 - float(k)
c      bcrit = beta thickness with less than 1.0: oxygen by weight.
       bcrit = anumb*y2 + ((.01-an(k))/(an(k-1)-an(k)))*y2
       go to 25
 20    bcrit = 8.0*y2
c
 25    bcrit = s * bcrit
       if(bcrit .lt. 0.03)   cbrttl = 1.0
       go to 100
c
c                      high cooling rate calculation
c
 50    if(ctemp .gt. 1700.0) cbrttl = 1.0
       if(ctemp .gt. 1700.0) go to 100
c
       bcon  = (bwtfro + 0.0012) * 100.0
c      bcon  = average concentration in the beta (weight per cent).
       if(bcon  .gt. 0.65) cbrttl = 1.0
c
c      calculation of saturation concentration in the beta follows.
c
       sat   = 0.12
c      sat   = saturation concentration in the beta (weight per cent).
       if(ctemp .lt. 1239.0) go to 55
       sat   = -4.287e-03 + sqrt((ctemp/392.46) - 3.1417)
       if(ctemp .lt. 1373.0) go to 55
       sat   = (ctemp - 1081.7)/491.157
 55    continue
c
       bsat  = 1.0e04 * (bwtfro+.0012)/sat
c      bsat  = per cent of saturation concentration in the beta.
       if(bsat  .gt. 90.0)  cbrttl = 1.0
c
 100   continue
       return
       end
