       function chscp (ppmhyd,ctemp,icor,icm,dco,dci,time,woxo,
     # chorg,ppmh2o,dp)
c
c     chscp calculates the apparent addition to specific heat
c     because of energy used in solution of hydrides present
c     in zircaloys
c
c     chscp  = output apparent addition to specific heat because
c              of hydrides present in zircaloys (j/kg-k)
c     ppmhyd = input concentration of hydrogen in the sample (ppm)
c     ctemp  = input temperature (k)
c       the following inputs are used only if ppmhyd is
c       not a positive number (see chuptk description)
c     icor   = input reactor chemistry index
c                       .ge.2 for pwr
c                       .lt.2 for bwr
c     icm    = input cladding material index (2= zirc2, 4 = zirc4)
c     dco    = input cladding outside diameter (inches)
c     dci    = input cladding inside diameter (inches)
c     time   = input time at temperature (s)
c     woxo   = input initial oxide film weight (mg/dm**2)
c     chorg  = input initial hydrogen content (ppm)
c     ppmh2o = input fuel water content (ppm)
c     dp     = input fuel pellet diameter (inches)
c
c     this code is based on data from
c         j. scott, a calorimetric investigation of zirconium,
c         titanium and zirconium alloys from 60 to 960 c.  phd
c         thesis (university of tennessee, 1957)
c
c     chscp coded by d. l. hagrman may 1976
c
c     conversion from j/(kg*k) to cal/(g*k) is
c     2.39006e-04 (cal/(g*c))/(j/(kg*k))
c
c     estimated standard error of the prediction of
c     chscp for the apparent addition to specific heat
c     because of energy used in solution of hydrides present
c     in zircaloy cladding samples -- (sum of squared
c     residuals/(number of residuals-degrees of freedom))**0.5 --
c     is half of the predicted value
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      a = 1.332e05
      b = 4.401e03
      c = 4.570e01
      if(ppmhyd .gt. 0.0) go to 20
      ppmhyd = chuptk(ctemp,icor,icm,dco,dci,time,woxo,chorg,
     #                ppmh2o,dp)
   20 t      = ctemp
      if(ppmhyd .le. 0.0) ppmhyd = 1.0
      tsol   = b/(log(a/ppmhyd))
      chscp  = c*a*b/(t**2)* exp(-b/t)*(1/( exp((t-tsol)/(tsol/50))+1))
      return
      end
