      subroutine chyprp(deloxy,csol,cliq,ctranb,ctrane)
c
c     chyprp returns zircaloy phase transition temperatures
c
c     csol   = output zircaloy solidus temperature (k)
c     cliq   = output zircaloy liquidus temperature (k)
c     ctrane = output high temperature boundary of the
c              alpha-beta phase region (k). b to a + b boundary
c              equal csol means no boundary at given deloxy
c     ctranb = output low temperature boundary of the
c              alpha-beta phase region (k). a + b to a boundary
c              equal csol means no boundary at given deloxy
c     deloxy = input oxygen concentration - oxygen concentration
c              of as received cladding (kg oxygen/kg zircaloy)
c
c     chyprp was coded by d. l. hagrman march 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
c     convert weight fraction oxygen to atomic fraction oxygen = x
c     wfox equals total oxygen content in weight fraction units
      wfox = deloxy + 0.0012
      x = wfox/(wfox + 0.1754 *(1. - wfox))
c
c     solidus correlations
      if(x .le. 0.1) then
         csol = x * 1150. + 2098.
      else
         if(x .le. 0.18) then
            csol = 2213.
         else
            if(x .le. 0.29) then
               csol = 1.3895317e+03 + x*(7.6400748e+03 -
     #                x * 1.7029172e+04)
            else
               if(x .le. 0.63) then
                  csol = 2173.
               else
                  if(x .le. 0.667) then
                     csol = -1.1572454e+04 + x*2.1818181e+04
                  else
                     csol = -1.1572454e+04 + (1.334 - x) *
     #                       2.1818181e+04
                  endif
               endif
            endif
         endif
      endif
c
c     liquidus correlations
      if(x .le. 0.19) then
         cliq = 2.125e+03 + x*(1.6321637e+03 - x*5.3216374e+03)
      else
         if(x .le. 0.41) then
            cliq = 2.11165553e+03 + x*(1.1590909e+03 -
     #             x * 2.4621212e+03)
         else
            if(x .le. 0.667) then
               cliq = 8.9507792e+02 + x * 3.1168831e+03
            else
               cliq = 8.9507792e+02 + (1.34 - x)*3.1168831e+03
            endif
         endif
      endif
c
c     calculate alpha-beta phase boundaries
      if(wfox .lt. 0.025) then
         ctranb = 1094. + wfox * (-1.289e+03 + wfox * 7.914e+05)
      else
         ctranb = 1556.4 + 3.8281e+04 * (wfox - 0.025)
      endif
      if(ctranb .gt. csol) then
         ctranb = csol
      endif
      if(deloxy .lt. 4.7308937e-03) then
         ctrane = 392.46 * ((100.*deloxy + 0.1242807)**2 + 3.1417)
      else
         ctrane = (100. * deloxy + 0.12) * 491.157 + 1081.7413
      endif
      if(ctrane .gt. csol) then
         ctrane = csol
      endif
      return
      end
