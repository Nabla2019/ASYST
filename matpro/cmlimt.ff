       subroutine cmlimt (ctemp,deloxy,fnck,fncn,cwkf,cwnf,cinwid,
     & cinrad,cdpres,caxrad,caxstr,rstran,deltmp,strnyt,strnye,
     & strnue,strnie,stsrpt,strrpe,cyldst,cyldse,cultse,cbrste,
     & cbrsst,ctstrt)
!  cmlimt calculates the following limit points of mechanical 
!  deformation. (1) true tangential stress at burst is calculated as a
!  function of temperature, fast neutron fluence and cold work.
!  (2) true tangential strain at failure for azimuthally symmetric
!  deformation is calculated from the true tangential burst stress, the
!  pressure at burst, the axial radius of curvature at burst, the axial
!  stress at burst and the initial midwall radius.  (3) cladding stress
!  and strain at the transition between elastic and plastic deformation
!  (yield) are calculated as a function of temperature, fast neutron
!  fluence, cold work, average oxygen concentration and strain rate.
!  (4) typical instability strains, burst pressures (engineering hoop
!  stress) and circumferential elongations at failure are also
!  calculated and returned.
!
!     strnyt = output true strain at yield (m/m)
!     strnye = output engineering strain at yield (m/m)
!     strnue = output uniform strain (m/m)
!     strnie = output typical circumferential engineering strain
!              at instability (m/m)
!     stsrpt = output true tangential failure strain for
!              azimuthally symmetric deformation (m/m)
!     strrpe = output typical circumferential engineering
!              strain at rupture(m/m)
!     cyldst = output true yield strength (pa)
!     cyldse = output engineering yield strength (pa)
!     cultse = output engineering ultimate strength (pa)
!     cbrsst = output effective true tangential stress at burst
!              for idealized symmetric deformation with circumference
!              equal to the actual cladding circumference (pa)
!     ctstrt = output true tangential component of stress at
!              burst (pa)
!     cbrste = output typical engineering hoop stress at burst (pa)
!     uctstt = output estimated uncertainty of ctstrt (pa)
!                (not currently returned)
!     ctemp  = input average cladding temperature (k)
!     deloxy = input average oxygen concentration excluding
!              oxide layer - average oxygen concentration of
!              as received cladding  (kg oxygen/kg zircaloy)
!     fnck   = input effective fast fluence for strength
!              coefficient (neutrons/(m**2))
!     fncn   = input effective fast fluence for strain hardening
!              exponent (neutrons/(m**2))
!     cwkf   = input effective cold work for strength
!              coefficient (unitless ratio of areas)
!     cwnf   = input effective cold work for strain hardening
!              exponent (unitless ratio of areas)
!     cinwid = input initial cladding wall thickness (m)
!     cinrad = input initial cladding midwall radius (m)
!     cdpres = input pressure differential across cladding at
!                burst (pa)
!     caxrad = input axial radius of curvature (m)
!     caxstr = input true axial stress (m).  set equal to zero
!                in the subcode if caxrad is greater than 10 meters
!     rstran = input strain rate. (1./s)
!     deltmp = input circumferential temperature variation.
!                used only to find typical values (k)
!
!     estimated values of deltmp are
!          (a) for cladding with external thermocouples and in film
!              boiling  deltmp = 150k
!          (b) for cladding with internal heating in steam and
!              without external thermocouples
!                  deltmp = 10k if the temperature is above 1255k
!                         = 50 * (1255 - temperature) / 165 + 10 if
!                           temperature is between 1090 and 1255k
!                         = 50k if temperature is below 1090k
!          (c) for cladding in a furnace
!                  deltmp = 10k
!          (d) for self - resistance heated cladding with cool filler
!                  deltmp = (heating rate)/1000k/s) * values of (b)
!
!  The subcodes used in this subroutine are ckmn and celmod.
!
!     the equations used in this subroutine are based on data from
!     (1)  r. h. chapman, multirod burst test program quarterly
!          report for april - june 1977, ornl/nureg/tm-135 (1977).
!     (2)  r. h. chapman, j. l. crowley. a. w. longest and e. g.
!          sewell, effect of creep time and heating rate on
!          deformation of zircaloy-4 tubes tested in steam with
!          internal heaters, ornl/nureg/tm-245 and nureg/cr-0345
!          (1978).
!     (3)  r. h. chapman, multirod burst test program quarterly
!          progress report for april - june 1976,
!          ornl/nureg/tm-74,  (1977)
!     (4)  r. h. chapman, multirod burst test program progress
!          report for july - december 1977, ornl/nureg/tm-200 and
!          nureg/cr-0103  (1978).
!     (5)  r. h. chapman, multirod burst test program progress
!          report for january - march 1978, ornl/nureg/tm-217 and
!          nureg/cr-0225  (1978).
!     (6)  r. h. chapman, multirod burst test program quarterly
!          progress report for january - march 1976,
!          ornl/nureg/tm-36  (1976).
!     (7)  r. h. chapman, multirod burst test program quarterly
!          progress report for october - december 1976,
!          ornl/nureg/tm-95  (1977).
!     (8)  r. h. chapman, multirod burst test program quarterly
!          progress report for january - march 1977,
!          ornl/nureg/tm-108  (1977).
!     (9)  d. o. hobson and p. l. rittenhouse, deformation and
!          rupture behavior of light water reactor fuel cladding,
!          ornl-4727  (1971)
!     (10) h. m. chung and t. f. kassner, deformation
!          characteristics of zircaloy cladding in vacuum and
!          steam under transient-heating conditions.  summary
!          report, anl-77-31 and nureg/cr-0344 (1978).
!     (11) a. a. bauer, w. j. gallagher, l. m. lowry and
!          a. j. markworth, evaluating strength and ductility
!          of irradiated zircaloy. quarterly progress report july
!          through september, 1977, bmi-nureg-1985  (1977).
!     (12) t. f. cook, s. a. ploger and r. r. hobbins,
!          postirradiation examination results for the irradiation
!          effects test ie-5, tree-nureg-1201  (1978).
!     (13) e. h. karb, results of the fr-2 nuclear tests on the
!          behavior of zircaloy clad fuel rods, paper presented
!          at the 6th water reactor safety research information
!          meeting, gaithersburg, md,  (1978).
!     (14) k. wiehr and he. schmidt, out-of-pile-versuche zum
!          aufblahvorgang von zirkaloy-hullen, kfk 2345,  (1977).
!     (15) k. wiehr, f. erbacher, u. harten, w. just, h. j. neitzel,
!          p. schaffner and h. schmidt, jahreskolloquium 1977 des
!          project nukleare sicherheit, kfk report  (1977).
!
!     the correlation for typical enginering hoop stress at burst
!     was taken from
!     (1)  j. d. kerrigan: frail. a fuel rod subcode,
!          srd-137-76  (1976).
!
!     last modified by d. l. hagrman july 1982 (cdf-mp-10)
!
$if def,in32,1
       implicit real*8(a-h,o-z)
       integer, parameter :: kr=selected_real_kind(12,200)
!
       real(kr) celmod,celmodr
!
       include 'contrl.h'
!
       if (chngno(70)) then
         call ckmnr (ctemp,deloxy,fnck,fncn,cwkf,cwnf,rstran,ak,an,am)
         elmod = celmodr(ctemp,fnck,cwkf,deloxy)
       else
         call ckmn (ctemp,deloxy,fnck,fncn,cwkf,cwnf,rstran,ak,an,am)
         elmod = celmod(ctemp,fnck,cwkf,deloxy)
       endif
       t = ctemp
       ag = ak*((rstran/1.0e-03)**am)
!
!     calculate strain at yield
      strnyt = ((ag/elmod)**(1.0/(1.0 - an)))
      strnye = exp(strnyt) - 1.0
!
!     calculate yield strength
      cyldst = (ag/(elmod**an))**(1.0/(1.0 - an))
      cyldse = cyldst/ exp(strnyt)
!
!     calculate strain at maximum one dimensional load
      strnut = an/(1.0 + am)
      strnue = exp(strnut) - 1.0
!
!     calculate ultimate engineering strength
      cultse = (ag * (strnut**an))/ exp(strnut)
!
!     calculate tangential component of true stress at burst
      if(t .lt. 1050.) ratio = 46.861429*exp(-(1.9901087e+06/(t**2)))
      if(t .le. 750.0) ratio = 1.36
      if(t .ge. 1050.) ratio = 7.7
!
!     find annealed state strength coefficient
      fkcc = fnck
      if(t .gt. 1255.) fkcc = 0.0
      aka    =  (ak/(1. + (1120.-(990./( exp((t-1301.5)/61.) + 1.)))
     #           * deloxy))/(1. + 0.546*cwkf + 9.7e-27*fkcc)
      ctstrt = ratio * aka + 0.4 *(5.54e-18 * fnck + 0.546 * cwkf
     #         * aka )
!
!     find uncertainty estimate for ctstrt
      uctstt = 0.17 * ctstrt
!
!     find true tangential failure strain for azimuthally symmetric
!     deformation
      if(caxrad .lt. 1.0e-03) caxrad = 1.0e-03
      if(caxrad .gt. 1.0e+01) caxstr = 0.0
      axfac  = caxstr*cinwid/(2.0 * cdpres*caxrad)
      stsrpt = log((ctstrt*cinwid/(cdpres*cinrad))**0.5 + axfac *
     #        (1.0 + 0.5 * axfac) )
!
!     find typical circumferential engineering strain at rupture
      strrpe = ((ctstrt*cinwid/(cdpres*cinrad))**0.5 - 1.0) *
     #           exp(-0.01 * deltmp)
!
!     find effective true tangential stress at burst for idealized
!     symmetric deformation and typical circumferential strain
      cbrsst = (cdpres * cinrad / cinwid ) * ((1.0 + strrpe)**2.0)
!
!     find typical circumferential engineering strain at instability
      strnie = ((ak * cinwid * (10.**(2.*am)) / (cdpres * cinrad
     #           * (0.866**(1. + am + an))))**0.5 - 1.0 )
      if(strnie .lt. 0.05) strnie = 0.05
      strnie = strnie *  exp(-0.01 * deltmp)
!
!     find typical engineering hoop stress at burst
      ft     = (ctemp - 273.15) * 1.8 + 32.
      cbrste =(10**(5.00e+00+ft*(3.27e-04-ft*(1.14e-06-ft*2.56e-10))))/
     #         1.4505e-04
      return
      end
