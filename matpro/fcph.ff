      function fcph(ftemp,facmot,fotmtl,gadoln)
c   the function fcph is used to calculate the specific heat capacity
c   of uo2, puo2, and (u,pu)o2 fuels as a function of temperature,
c   fraction of fuel which is molten, puo2 content, and oxygen-to-
c   metal ratio.
c   fcph    = output fuel specific heat capacity (j/(kg*K))
c   ftemp  = input fuel meshpoint temperature (K)
c   facmot = input fuel fraction molten (unitless)
c   facmot = 1.0 - fuel is all molten
c   facmot = 0.0 - fuel is all solid
c   fotmtl = input oxygen to metal ratio (unitless)
c   fotmtl = (atoms oxygen)/(atoms metal) if not known,
c            enter 2.0
c   gadoln = weight fraction of gadolinia in the fuel
c   ufcp   = estimated standard error for uo2 (j/(kg*K))
c            (not currently returned)
c   pufcp  = estimated standard error for puo2 (j/(kg*K))
c            (not currently returned)
c   the equations used in this function are based on data from;
c   (1) j.kerrisk and d.clifton, nuclear technology,16 (1972).
c   (2) o.kruger and h.savage, journal of chemical physics,45
c   (1968).
c   the effect of oxygen-to-metal ratio was estimated from
c   equations published by;
c   (3) c.affortit and j.marcon, revue internationale des hautes
c   temperatures et des refractaires,7 (1970).
c   the specific heat capacity of molten uo2 was taken from
c   (4) l.leibowitz et al, journal of nuclear materials,39 (1970)
c   fcp was originally coded by v.f.baston in march 1974.
c   last modified by g.a.reymann in may 1978.
       common  / phypro / ftmelt,fhefus,ctmelt,chefus,ctranb,
     +                     ctrane,ctranz,fdelta,bu,comp,deloxy
c   the phypro common block is set by call to the subroutine
c   phypro which is part of the materials properties package
c   used at the inel.
c   quantities contained in it used in this subcode are;
c   ftmelt = fuel melting temperature (K)
c   fdelta = liquid-solid coexistence temperature (K)
c   comp   = puo2 content (wt.%)
c   when the function fcph is used alone, not as part of the
c   inel code, values for ftmelt, fdelta, and comp must be
c   input.
       data c1u ,c2u ,c3u ,thu ,edu / 296.7  , 2.43e-02, 8.745e07,
     +                                 535.285, 1.577e05 /
       data c1pu,c2pu,c3pu,thpu,edpu/ 347.4  , 3.95e-04, 3.860e07,
     +                                 571.0  , 1.967e05 /
       data c1gd,c2gd,c3gd,thgd,edgd/ 315.86 , 4.044e-02 , 0.0,
     +                                348.0  , 0.0       /
      cp(c1,c2,c3,th,ed,t,fotmtl) =
     # c1*(th**2)*exp(th/t)/((t**2)*((exp(th/t)-1.)**2))+c2*t+
     # (fotmtl/2.)*(c3*ed/(8.314*(t**2)))*exp(-ed/(8.314*t))
      t = ftemp
      r = facmot
      tm = ftmelt
      fcomp = comp/100.0
      fcpmol = 503.0
c   fcpmol = specific heat capacity of molten fuel (j/(kg*K))
      if (t.gt.(tm+fdelta)) go to 100
      fcph = cp(c1u,c2u,c3u,thu,edu,t,fotmtl)*(1.0-fcomp)+cp(c1pu,c2pu,
     &c3pu,thpu,edpu,t,fotmtl)*fcomp
      fcph = fcph*(1.0-gadoln) + gadoln*cp(c1gd,c2gd,c3gd,thgd,edgd,t,
     + fotmtl)
      if (t.lt.(tm-0.1)) go to 110
      fcph = (1.0-r)*fcph+r*fcpmol
      go to 110
100   fcph = fcpmol
      ufcp = 2.0
      pufcp = 5.6
110   continue
      return
      end
