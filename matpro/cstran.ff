       subroutine cstran (ctemp,deloxy,fnck,fncn,cwkf,cwnf,rstran,
     & strest,strant)
!  cstran calculates cladding strain as a function of true cladding
!  stress, true cladding strain rate, cladding temperature, average
!  oxygen concentration in cladding, fast neutron fluence, and cold
!  work.
!
!     no internal iterations are contained in cstran.  it is
!     assumed that the user will iterate the input value
!     of rstran.  see subcode cstrni for iterated version.
!
!     strant = output effective true plastic strain (unitless)
!     strest = input effective true stress (pa)
!     rstran = input effective true plastic strain rate (s**(-1))
!     ctemp  = input cladding meshpoint temperature (k)
!     deloxy = input average oxygen concentration excluding
!              oxide layer - average oxygen concentration of
!              as received cladding  (kg oxygen/kg zircaloy)
!     fnck   = input effective fast fluence for strength
!              coefficient (neutrons/(m**2))
!     fncn   = input effective fast fluence for strain hardening
!              exponent (neutrons/(m**2))
!     cwkf   = input effective cold work for strength
!              coefficient (unitless ratio of areas)
!     cwnf   = input effective cold work for strain hardening
!              exponent (unitless ratio of areas)
!
!  The subcode ckmn is used in this subroutine.
!
!  Conversion from pa  to  psi  is 1.4505e-04 (psi/pa).
!
!     cstran was coded by r. l. miller and r. r. hobbins
!     in march 1974
!     modified by d. l. hagrman to reduce yield strength
!          to measured values july 1981.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
!
       real(kr) ctemp,deloxy,fnck,fncn,cwkf,cwnf,rstran,strest,strant
!
       include 'contrl.h'
!
       real(kr) ak,an,am,ana,anl
       integer ii
!
       if (chngno(70)) then
         call ckmnr (ctemp,deloxy,fnck,fncn,cwkf,cwnf,rstran,ak,an,am)
       else
         call ckmn (ctemp,deloxy,fnck,fncn,cwkf,cwnf,rstran,ak,an,am)
       endif
       strant = (strest/(ak*((rstran/1.0e-3)**am)))**(1.0/an)
!
!  Revise an for small strains.
       if (strant .ge. (an/(1.0 + am))) return
       anl = 0.17
       if (ctemp .gt. 730.) anl = 1.56e-02*ctemp - 11.218
       if (ctemp .ge. 780.) anl = 0.95
       ii = 0
       do
         ii = ii + 1
         ana = an*an/((1.0 + am)*strant)
         if (ana .gt. anl) ana = anl
         strant = ((strest/(ak*((rstran/1.0e-03)**am)))**(1.0/ana) +
     &   strant)*0.5
         if (ii .ge. 9) exit
       enddo
       return
       end
