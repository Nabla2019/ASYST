      function aenthl(ictype,atemp,rftemp)
c
c     aenthl returns the change in enthalpy of neutron absorbers
c     during a constant pressure change in temperature
c     aenthl = output change in absorber enthalpy (j/kg)
c     ictype = input absorber material type
c                 1 = silver-indium-cadmium (.8,.15.05)
c                 2 = boron carbide
c     atemp  = input absorber temperature (k)
c     rftemp = input reference temperature (k)
c
c     aenthl was coded by d. l. hagrman march 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      t = rftemp
      n = -1
c
      if(ictype .lt. 2) then
   10    if (t .lt. 1050.0) then
            hag = t * (21.3 + 2.135e-03*t) - 1.51e+05/t
     #            -6.078816e+03
            hin = t * (24.3 + 5.250e-03*t)
     #            -7.762500e+03
            hcd = t * (22.2 + 6.150e-03*t)
     #            -7.213500e+03
            h = 9.17 * (0.808 *hag + 0.143*hin + 0.049*hcd)
         else
            if(t .lt. 1100.) then
         h = 1.781934e5 + 2.166154e3*(t - 1050.0)
            else
         h = 2.865011e5 + 254.1542*(t - 1100.0)
            endif
         endif
         if(n .lt. 0) then
            n = n + 2
            hr = h
            t = atemp
            go to 10
         else
         aenthl = h - hr
         endif
      else
   20    if(t .lt. 2700.) then
            h = t*(563. + t*(0.77 - t*9.8e-05))
         else
            h = 5.204466e+06 + 2577.740 * (t- 2700.)
         endif
         if(n .lt. 0) then
            n = n + 2
            hr = h
            t = atemp
            go to 20
         else
            aenthl = h - hr
         endif
      endif
      return
      end
