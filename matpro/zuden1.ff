*if def,selap
       function zuden1(z,u,s,a1,a2,ux2,zx2,a3,a4,a5,afsoil,zutemp)
c
c  calculates the density of zirconium-uranium steel-control rod-oxygen
c  compounds or mixtures during a constant pressure change in
c  temperature.
c
c     zuden1  = output density (kg/m**3)
c     z       = input atom fraction zirconium metal in
c               compound/mixture (atoms zirconium/atoms conglomerate)
c     u       = input atom fraction uranium metal in compound/mixture
c               (atoms uranium/atoms conglomerate)
c     s       = input atom fraction stainless steel in
c               compound/mixture (atoms steel/atoms conglomerate)
c     a1      = input atom fraction silver-indium-cadmium in
c               compound/mixture (atoms ag-in-cd/atoms conglomerate)
c     a2      = input atom fraction boron carbide in compound/mixture
c               (atoms boron carbide (b4c)/atoms conglomerate)
c     a3      = input atomic fraction aluminum in compound/mixture
c               atoms aluminum (al) /atoms conglomerate)
c     a4      = input atomic fraction lithium in compound/mixture
c               atoms lithium (li)/atoms conglomerate)
c     a5      = input atomic fraction cadmium in compound/mixture
c               atoms cadmium (cd)/atoms conglomerate)
c     afsoil  = input atomic fraction soil in compound/mixture
c               (atoms soil/atoms conglomerate)
c     ux2     = input atom fraction uranium dioxide in
c               compound mixture (atoms uo2/atoms conglomerate)
c     zx2     = input atom fraction zirconium dioxide in
c               compund/mixture (atoms zro2/atoms conglomerate)
c     zutemp  = compund/mixture temperature (k)
c
c     the subcodes uden, cden, sden, aden, fdenl,zotexp,cthexp,
c     zoden, soden, sioden, fthexp, sthexp, athexp, uthexp, and sothex
c     are called by zuden1.
c     the subroutines alden, liden, and cdden are also called.
c
c     zuden1 was coded by j. k. hohorst, january, 1986
c     it was extended in may, 1989, by s. w. james to include
c     the elements aluminum, lithium, and cadmium.
c
c     deck name and function name changed from zuden to zuden1
c     to avoid conflict with the different zuden in selap.
c     g. a. jayne    nov, 1986.
c
       implicit none
       include "ufiles.h"
       real z,u,s,a1,a2,ux2,zx2,a3,a4,a5,afsoil,zutemp
c
c  Local variables.
       integer ier1,ier2,ier3
       real ra1,ra2,ra3,ra4,ra5,rs,rsx2,ru,rux2,rzr,rzx2,sx2,rsoil
       real aden,cden,fden,sden,sioden,soden,uden,zoden,zuden1
       external aden,cden,fden,sden,soden,uden,zoden
       external alden,cdden,liden,sioden
c
c     it is assumed that the remainder, sx2, is stainless steel oxide.
c
      sx2 = 1.0 -u -z -s -a1 -a2 -a3 -a4 -a5 -afsoil -ux2 -zx2
      if (sx2 .lt. -1.0e-6) then
         write (output,20) sx2
   20    format ('0zuden1 - sx2 =', e13.5)
         write (output,901) u,z,s,a1,a2,a3,a4,a5,afsoil,ux2,zx2
c        zzzz=0.0
c        zabc=1.0/zzzz
         stop' zuden1'
      else
         if(sx2 .gt. 1.0) then
            write (output,903) u,z,s,a1,a2,a3,a4,a5,afsoil,ux2,zx2
c           zzzz=0.0
c           zabc=1.0/zzzz
            stop' zuden1'
         endif
      endif
      ru = uden(zutemp)
      rzr = cden(zutemp)
      rs = sden(zutemp)
      ra1 = aden(1,zutemp)
      ra2 = aden(2,zutemp)
      call alden ( zutemp, ra3, ier1 )
      call liden ( zutemp, ra4, ier2 )
      call cdden ( zutemp, ra5, ier3 )
      rsoil=sioden(zutemp)
      rux2 = fden(zutemp,0.0)
      rzx2 = zoden(zutemp)
      rsx2 = soden(zutemp)
      zuden1  = (0.238*u + 0.091*z + 0.056*s + 0.109*a1 +
     +         0.055*a2 + 0.027*a3 + 0.007*a4 + 0.112*a5 +
     +         0.060*afsoil + 0.270*ux2 + 0.123*zx2 + 0.088*sx2) /
     +         (0.238*u/ru + 0.091*z/rzr + 0.056*s/rs +
     +         0.109*a1/ra1 + 0.055*a2/ra2 + 0.027*a3/ra3 +
     +         0.007*a4/ra4 + 0.112*a5/ra5 + 0.060*afsoil/rsoil +
     +         0.270*ux2/rux2 + 0.123*zx2/rzx2 + 0.088*sx2/rsx2)
c
  901 format(2x,'input fraction sum .gt. 1. u, z, s, a1, a2, a3, a4, a5,
     + afsoil, ux2, zx2 = '/11e10.4)
  903 format(2x,'input fraction sum .lt. 0. u, z, s, a1, a2, a3, a4, a5,
     + afsoil, ux2, zx2 = '/11e10.4)
      return
      end
*endif
