       function zucp(u,z,zutemp)
!  zucp returns the specific heat capacity at constant pressure for
!  zirconium-uranium-oxygen compounds.
!
!  zucp   = output compound specific heat capacity (j/(kg*k))
!
!  u      = input atomic fraction uranium in compound
!           (atoms uranium/atoms in compound)
!  z      = input atomic fraction zirconium in compound
!           (atoms zirconium/atoms in compound)
!  zutemp = compound temperature (k)
!
!  The functions ccp, fcp, and zocp are called by this function.
!
!  zucp was coded by d. l. hagrman march 1982.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       real(kr) zucp
       real(kr) u,z,zutemp
!
       include 'contrl.h'
!
       real(kr) cuo2,czo2,czr,fuo2,fzo2,fzr,uo,x
       real(kr) ccp,ccpr,fcp,zocp
!
       x = 1. - u - z
       if ((u + z) .lt. 0.5*x) then
         uo = u
         u = u/(3.0*(u + z))
         z = z/(3.0* (uo + z))
         x = 1.0 - u - z
       else
         if (0.5*x .lt. u) then         
           uo = u
           u = u/(3.0*u + z)
           z = z/(3.0*uo + z)
           x = 1.0 - u - z
         endif
       endif
       fuo2 = u/(u+z)
       fzo2 = (0.5 * x - u)/(u + z)
       fzr = 1. - fuo2 - fzo2
       if (chngno(60)) then
         czr = ccpr(zutemp)
       else
         czr = ccp(zutemp)
       endif
       cuo2 = fcp(zutemp,0.0,2.0,0.95)
       czo2 = zocp(zutemp)
       zucp = (cuo2*0.27*fuo2 + czo2*0.123*fzo2 + czr*0.091*fzr)/
     & (0.270*fuo2 + 0.123*fzo2 + 0.091*fzr)
       return
       end
