       subroutine corros (tcoi,qci,zro2ai,zoxki,icor,delhi,zro2bi,krap)
c
c     corros calculates the thickness of the oxide layer on
c     the outside of zircaloy cladding between 366 and 670 k
c
c     zro2bi = output new oxide thickness (m)
c     krap   = output/input error flag
c                        krap = 0 if no errors detected
c                        krap = 1 if correlation range exceeded
c                        krap = 2 if impossible input is detected
c     tcoi   = input zro2-coolant interface temperature (k)
c     qci    = input axial increment heat flux (w/m**2)
c     zro2ai = input oxide thickness at end of previous time step (m)
c     zoxki  = input zirconium oxide thermal conductivity (w/m-k)
c     icor   = input reactor chemistry index
c                       .ge.2 for pwr
c                       .lt.2 for bwr
c     delhi  = input time step (s)
c
c     the equations used in this subroutine are based on data from
c     (1) a. van der linde, calculation of the safe lifetime expectancy
c         of zirconium alloy canning in the fuel elements of the
c         nero reactor, rcn-41 (1965)
c     (2) f. h. megerth, c. p. ruiz, u. e. wolff, zircaloy-clad uo2
c         fuel rod evaluation program final report, geap-10371 (1971)
c     (3) w. r. smalley, saxton core ii fuel performance evaluation
c         part i: materials, wcap-3385-56 (1971)
c     (4) w.r.  smalley, evaluation of saxton core iii fuel
c         materials performance, wcap-3385-57 (1974)
c     (5) e. hillner, corrosion and hydriding performance of zircaloy
c         tubing after extended exposure in the shippingport pwr,
c         symposium on zirconium in nuclear applications, astm stp 551
c         (1974)
c
c     this model should not be used outside the temperature range
c     523.15 - 673.15k (250-400 c)
c
c     conversion from (mg/dm**2) oxide weight gain to:
c     (1)oxide thickness in dm   is 6.3e-7 dm/(mg/dm**2)
c     (2)oxide thickness in mils is 2.5e-3 mils/(mg/dm**2)
c     (3)oxide thickness in micrometers is .063 micrometer/(mg/dm**2)
c
c     corros was coded by d.l. hagrman in january 1977.
c     updated by d. l. hagrman to add input check november 1981
c          (cdr-mp-05)
c
$if def,in32,1
       implicit real*8(a-h,o-z)
       include "ufiles.h"
c     input check
c     save input value of the krap error flag
      ikrap = krap
c     test input for non-physical values
      if ( tcoi .le. 0.0
     #   .or. zro2ai .lt. 0.0
     #    .or. zoxki .lt. 0.0
     #     .or. delhi .lt. 0.0 ) then
c
c     set krap error flag to 2 and check input krap value for change
         krap = 2
         if ( ikrap .ne. 2) then
            write (output,901)
            write (output,902) tcoi,zro2ai,zoxki,delhi
         endif
         return
      endif
c     test input against correlation validity range
      if(tcoi .lt. 523.15
     #  .or. tcoi .gt. 673.15) then
        if(ikrap .lt. 2) krap = 1
      endif
c     end of input check
      oxide1 = 0.0
      oxide2 = 0.0
c
      if(tcoi .le. 366.5) go to 40
      if(zro2ai .le. 0.0) zro2ai = 1.0 e-10
      tcoic = tcoi + qci * zro2ai/zoxki
      wtran = 7.7490e-06 * exp(-7.90e02/tcoic)
      if(tcoi .gt. 673.0) go to 13
      if(icor.ge.2)       go to 10
c     bwr environment
      a = 4.840e05 *  exp(-1.945e-02 * tcoi)
      if(tcoi.le.500.) a = 28.92887
      go to 15
c     pwr environment
   10 a = 1.203e02 *  exp(-7.118e-03 * tcoi)
      if(tcoi.le.500.) a = 3.424614
      go to 15
   13 a = 1.0
   15 if(zro2ai .gt. wtran) go to 30
      oxide1 = zro2ai
      do 20 i = 1, 2
   20 oxide1= (4.976e-09*a*exp(-1.566e04/(tcoi + qci*oxide1/zoxki)) *
     #        (delhi/8.64e4) + zro2ai**3.)**0.333
      if(oxide1 .lt. wtran) go to 45
      oxide2 = zro2ai
      do 25 i = 1, 2
   25 oxide2 = 8.288e01 * a * exp(-1.408e04/(tcoi + qci * oxide2/zoxki))
     #         * delhi/8.64e04 + ((zro2ai**3)/(wtran**2))
      go to 50
   30 oxide2 = zro2ai
      do 35 i = 1, 2
   35 oxide2 = 8.288e01 * a * exp(-1.408e04/(tcoi + qci * oxide2/zoxki))
     #         * delhi/8.64e04 + zro2ai
      go to 50
   40 zro2bi = zro2ai
      go to 55
   45 zro2bi = oxide1
      go to 55
   50 zro2bi = oxide2
   55 continue
      return
  901 format(//t2,42h****** input error detected for first time)
  902 format(//t5,6htcoi =,e15.5,8hzro2ai =,e15.5,7hzoxki =,
     #e15.5,7hdelhi =,e12.5)
      end
