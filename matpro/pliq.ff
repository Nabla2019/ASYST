      function pliq(x,u,z)
c
c     pliq calculates the liquidus temperature of
c     uranium - zircaloy - oxygen compounds
c
c     pliq   = output liquidus temperature (k)
c     x      = input atomic fraction oxygen in compound (unitless'
c     u      = input atomic fraction uranium in compound (unitless)
c     z      = input atomic fraction zircaloy in compound (unitless)
c
c     the liquidus temperature calculated by this function
c     is based on data from the following references
c     (1)r. e. latta and r. e. fryxell, determination of
c        solidus-liquidus temperatures in the uo2+x system
c        (-0.50 less than x less than 0.20), journal o6 54l& -9
c        materials 35 pp. 195-210 (1970).
c     (2)k. a. romberger, c. f. bates jr. and h. h. stone,
c        phase equlibrium studies in the uo2 - zro2 syst54#
c        journal of inorganic and nuclear chemistry 29 pp. 1619-163
c        (1966).
c     (3)r. ruh and h. j. garrett, nonstoichiometry of zro2 and
c        its relation to tetragonal-cubic inversion in zro2,
c        journal of the american ceramic society 50 pp 257-261 (1966).
c     (4)r. f. domagala and j. d. mcpherson, system zirconium-oxygen,
c        journal of metals 6 transactions aime 200 pp 238-246 (1954).
c
c     pliq was coded by d. l. hagrman october 1980
c     last updated by d. l. hagrman july 1982
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      data ncnt/0/
c     find oxygen to metal ratio.  assume symmerty about 2.0
      ye = x/( u + z )
      if(ye .gt. 2.23) then
         if(ncnt.le.5)then
           print 900,ye
  900 format(29h     oxygen to metal ratio = ,e16.6,38h outside range of
     # matpro function pliq,15h  reset to 2.23)
           ncnt=ncnt+5
         endif
         ye = 2.23
      endif
      if(ye .gt. 2.) ye = 4. - ye
c
c     zircaloy - zircaloy oxide model
      tz = 0.0
      if(z .lt. 1.0e-06) go to 10
      xoz = x
      tz = 2125. + xoz*(1.6321637e+03 - xoz*5.3216374e+03)
      if(xoz .le. 0.19) go to 10
      tz = 2.1116553e+03 + xoz*(1.1590909e+03 - xoz*2.4621212e+03)
      if(xoz .le. 0.41) go to 10
      tz = 8.9507792e+02 + xoz * 3.1168831e+03
      if(xoz .le. 0.667) go to 10
      xoz = 1.334 - xoz
      tz = 8.9507792e+02 + xoz * 3.1168831e+03
c
c     uranium - uranium oxide model
   10 tu = 0.0
      if(u .lt. 1.0e-06) go to 20
      dstoc = x/(u+z) - 2.0
      tu = 3119. - dstoc*dstoc*1.61e+03
      if(tu .lt. 2700.) tu = 1373.
c
c     uranium dioxide - zircaloy dioxide model
   20 td = 0.0
      if(ye .lt. 1.0e-02) go to 30
      zou = z/(u+z)
      td = 3119. + zou*(44.4208 - zou*1.3423020)
      if(zou .le. 0.50) go to 30
      td = 3119. + zou*(-1.1303949e+03 + zou * 1.0073297e+03)
      if(zou .le. 0.52) go to 30
      td = 2224.2377 + zou * 1114.1132
      if(zou .le. 0.55) go to 30
      td = 3.1809473e+03 + zou *(-1.1165869e+03 + zou*8.9314194e+02)
      if(zou .le. 0.93) go to 30
      td = 2.1444285e+03 + zou * 8.2857142e+02
c
c     combine binary estimates assuming oxygen to metal
c     ratio is most important
   30 w = (ye/2.)**2
      pliq = (1. - w ) * ((z/(z+u))* tz + (u/(z+u)) * tu) + w * td
c
c     estimate uncertainty
      upliq = 10. + 050. * (1. - abs(z - u))
     #        +  100. * ye * abs(ye - 2.)
      return
      end
