      subroutine cthexp(costh2,cosfi2,ctemp,rftemp,strn11,strn22,strn33)
c
c      cthexp calculates the thermal expansion strain for a piece of
c      zircaloy cladding as a function of temperature and of cladding
c      texture.
c
c      strn11 = output thermal expansion strain in the circumferential
c               direction (m/m)
c      strn22 = output thermal expansion strain in the axial
c               direction (m/m)
c      strn33 = output thermal expansion strain in the thickness
c               direction (m/m)
c      ctemp  = input cladding temperature (k)
c      cosfi2 = input volume fraction weighted average of the squared
c               sine of phi (unitless)
c      costh2 = input volume fraction weighted average of the squared
c               cosine of theta (unitless)
c
c      values for cosfi2 and costh2 may be obtained through use of the
c      subroutine ctxtur
c
c      these calculations are based on data and techniques in the
c      following sources
c      (1) l.r. bunnell et al, high temperature properties of zircaloy-
c          oxygen alloys, epri np-524 (march 1977).
c      (2) d.l. douglass, "the physical metallurgy of zirconium", atomic
c          energy review,1,#4 (december 1963).
c      (3) c. kittel, introduction to solid state physics, 3rd edition,
c          new york, john wiley and sons inc., 1966.
c      (4) g.b. skinner, h.l. johnston, "thermal expansion of zirconium
c          between 298 and 1600k", journal of chemical physics,21 (1953)
c
c      cthexp was coded by g. a. reymann january 1980.
c      last modified by d. l. hagrman march 1982.
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
c      calculate single crystal strains
c          strs11 = single crystal strain perpendicular to the c-axis
c          stre33 = single crystal strain along the c-axis
c
      t = rftemp
      n = -1
    5 if(t .ge. 2098.) go to 40
      if(t .gt. 1244.0) go to 20
      if(t .gt. 1083.0) go to 10
c
       strs11 = 4.95e-06*t - 1.485e-03
       strs33 = 1.26e-05*t - 3.780e-03
       go to 30
c
 10    strs11 = (2.77763 + 1.09822*cos((t-1083.0)*3.141593/161.0))
       strs11 = strs11*1.0e-03
       strs33 = (8.76758 + 1.09822*cos((t-1083.0)*3.141593/161.0))
       strs33 = strs33*1.0e-03
       go to 30
c
 20    strs11 = 9.7e-06*t - 1.04e-02
       strs33 = 9.7e-06*t - 4.40e-03
c
c      calculate cladding strains using single crystal strains
c
 30    strn11 = (cosfi2 +costh2*(1.0-cosfi2))*strs11
     #            + (1.0-costh2)*(1.0-cosfi2)*strs33
c
       strn22 = (1.0-cosfi2 + costh2*cosfi2)*strs11
     #            + (1.0-costh2)*cosfi2*strs33
c
       strn33 = (1.0-costh2)*strs11 + costh2*strs33
c
      go to 50
   40 strs11 = 9.9506e-03
      strs33 = 1.59506e-02
      strn11 = 0.67 * strs11 + 0.33 * strs33 + 0.0067
      strn22 = strn11
      strn33 = strn11
c
   50 if(n .lt. 0) then
         n = n + 2
         strn1r = strn11
         strn2r = strn22
         strn3r = strn33
         t = ctemp
         go to 5
      else
         strn11 = strn11 - strn1r
         strn22 = strn22 - strn2r
         strn33 = strn33 - strn3r
      endif
c      ustrn2 = uncertainty in the tube axial thermal strain
c      ustrn1 = uncertainty in the tube circumferential thermal strain
c      ustrn1 = 0.12*strn11
c      ustrn2 = 0.08*strn22
c
       return
       end
