      subroutine emfton (ftemp,fraden,ftmelt,con,dkdt)
c   emfton calculates fuel thermal conductivity as a function of
c   temperature and fractional density.
c   ftemp=input fuel temperature(k)
c   fraden=input fractional fuel density(ratio of actual density to
c   theortical density)
c   ftmelt= fuel melting point
c   con= output fuel thermal conductivity (w/m*k)
c   dkdt= output dirivitive of fuel thermal conductivity with
c   respect to temperature   (w/m*k*k)
c   the thermal conductivity relationship used in this subroutine is
c   based on the data of lyons et al. plus the maxwell-eucken
c   correction for porosity with a pore shape coefficient of 0.5 .
c   references;trans.am.nuc.soc.vol7 no.1 p.107 june 1966 and
c   bnwl-1778.
c   fthcon(emfton) was coded by v.f.baston in march 1974.
c   coding revised by r.c.young - nov 74
c   beta = porosity coeff.    pc100= (1.025/0.95)*100.
      data beta,    pc100  ,   a    ,    b   ,   c      /
     +      0.5, 1.078947e2, 38.24e0, 129.4e0, 6.125e-13/
      if (ftemp.gt.ftmelt) go to 100
      con = (pc100*fraden/(1.+beta*(1.-fraden)))*(a/(b+ftemp)+c*(ftemp**
     +3))
      dkdt = (pc100*fraden/(1.+beta*(1.-fraden)))*(3.*c*(ftemp**2)-a/((b
     ++ftemp)**2))
      go to 110
100   con = 3.0
      dkdt = 0.0
110   return
      end
