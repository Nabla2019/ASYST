      subroutine ctxtur (tensty,costh2,costh4,cosfi2,cosfi4,
     #                   ct2cf2,ct2cf4,ct4cf2,ct4cf4)
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      dimension tensty(9,9),cos2th(9,9),cos4th(9,9),cos2fi(9,9),
     #cos4fi(9,9),c2tc2f(9,9),c2tc4f(9,9),c4tc4f(9,9),an(9),c2(9),c4(9),
     #c4tc2f(9,9),b2(9),b4(9),ca(9)
c
c     ctxtur calculates orientation parameters from basal pole
c     figure intensity grids
c
c     in the following, theta is the angle between the radial
c     or normal direction of the sample and the basal pole
c     direction. phi is the angle between the projection of the
c     basal pole direction onto the plane containing the axial
c     and circumferential directions and the circumferential
c     direction. alpha is the complement of phi.
c
c     costh2 = output volume fraction weighted average of the squared
c              cosine of theta (unitless)
c     costh4 = output volume fraction weighted average of the
c              fourth power of the cosine of theta (unitless)
c     cosfi2 = output volume fraction weighted average of the squared
c              cosine of alpha (unitless ratio)
c     cosfi4 = output volume fraction weighted average of the
c              fourth power of the cosine of alpha (unitless)
c     ct2cf2 = output volume fraction weighted average of the product
c              of the squared cosine of theta and the squared cosine
c              of alpha (unitless)
c     ct2cf4 = output volume fraction weighted average of the product
c              of the squared cosine of theta and the fourth power of
c              the cosine of alpha (unitless)
c     ct4cf2 = output volume fraction weighted average of the product
c              of the fourth power of the cosine of theta and the
c              squared cosine of alpha (unitless)
c     ct4cf4 = output volume fraction weighted average of the product
c              of the fourth power of the cosine of theta and the
c              fourth power of the cosine of alpha (unitless)
c     tensty = input dimensioned array of basal pole intensities
c              from a pole figure. element tensty(1,1) is the average
c              intensity for theta from 0 to 10 degrees and phi
c              from 0 to 10 degrees. element tensty(1,2) is the
c              average intensity for theta from 0 to 10 degrees
c              and phi from 10 to 20 degrees.  etc. for the
c              entire pole figure. (arbitrary units)
c
c     the calculation of orientation parameters is based on
c     an approach suggested by the following references
c     (1) j. j. kearns, thermal expansion and preferred orientation
c         in zircaloy, wapd-tm-472 (nov. 1965)
c     (2) h. s. rosenbaum and j. e. lewis, use of pole figure data
c         to compute elasticity coefficients of zirconium sheet,
c         journal of nuclear materials 67 (1977) pp 273-282
c
c     ctxtur was coded by d. l. hagrman in december 1977
c
c     find angles at grid centers
      do 20 j=1,9
      an(j) = (2*j - 1) * 8.726646e-02
      ca(j) = 1.570796 - an(j)
c
      b2(j) = (cos(ca(j)))**2
      b4(j) = (b2(j))**2
      c2(j) = (cos(an(j)))**2
   20 c4(j) = (c2(j))**2
c
c     find normalization constant
      tnstyn = 0.0
      do 10 j=1,9
      do 10 i=1,9
   10 tnstyn = tnstyn + tensty(i,j) * sin(an(i))
c
c     find rho * sine(theta)
      do 15 j=1,9
      do 15 i=1,9
   15 tensty(i,j) = tensty(i,j)*sin(an(i))/tnstyn
c
c     find rho * sine(theta) * argument
      do 25 j=1,9
      do 25 i=1,9
      cos2th(i,j) = tensty(i,j)*c2(i)
      cos4th(i,j) = tensty(i,j)*c4(i)
      cos2fi(i,j) = tensty(i,j)*b2(j)
      cos4fi(i,j) = tensty(i,j)*b4(j)
      c2tc2f(i,j) = tensty(i,j)*c2(i)*b2(j)
      c2tc4f(i,j) = tensty(i,j)*c2(i)*b4(j)
      c4tc2f(i,j) = tensty(i,j)*c4(i)*b2(j)
   25 c4tc4f(i,j) = tensty(i,j)*c4(i)*b4(j)
c
c     find volume weighted averages
      costh2 = 0.0
      costh4 = 0.0
      cosfi2 = 0.0
      cosfi4 = 0.0
      ct2cf2 = 0.0
      ct2cf4 = 0.0
      ct4cf2 = 0.0
      ct4cf4 = 0.0
      do 30 j=1,9
      do 30 i=1,9
      costh2 = costh2 + cos2th(i,j)
      costh4 = costh4 + cos4th(i,j)
      cosfi2 = cosfi2 + cos2fi(i,j)
      cosfi4 = cosfi4 + cos4fi(i,j)
      ct2cf2 = ct2cf2 + c2tc2f(i,j)
      ct2cf4 = ct2cf4 + c2tc4f(i,j)
      ct4cf2 = ct4cf2 + c4tc2f(i,j)
   30 ct4cf4 = ct4cf4 + c4tc4f(i,j)
c
      return
      end
