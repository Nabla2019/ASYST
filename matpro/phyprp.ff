       subroutine phyprp
c
c     phyprp returns uo2, (u,pu)o2, and zircaloy melting points
c     and heats of  fusion, and zirconium and zircaloy alpha to beta
c     transition temperatures.
c
$if def,in32,1
       implicit real*8(a-h,o-z)
       include 'phypro.h'
c
c     ftmelt = output uo2 or mixed oxide fuel melting points (k)
c     fhefus = output uo2 or mixed oxide fuel heat of fusion (j/kg)
c     ctmelt = output zr clad melting point (k)
c     chefus = output zr clad heat of fusion (j/kg).
c     ctranb = output start of zr-4 alpha-beta transus temperature (k)
c     ctrane = output end of zr-4 alpha-beta transus temperature (k)
c     ctranz = output zr isothermal alpha-beta transus temperature (k)
c     fdelta = output liquid-solid coexistence temperature range (k)
c     bu     = input burnup (mw-s/kg-u)
c     comp   = input puo2 content (wt%)
c     deloxy = input oxygen concentration - oxygen concentration
c              of as received cladding (kg oxygen/kg zircaloy)
c
c     the uo2 and zr cladding  melting points and heats of fusion are
c     taken from the following references.  uo2 fuel melting point of
c     3113k from h.c.brassfield et al gemp-482. uo2 heat of fusion
c     of 17.7 kcal/mole from l.leibowitz et al, j.nuc.mat. 39 p 115
c     (1971). cladding melting point of 2098 k from m.l. picklesimer
c     private communication. cladding heat of fusion of 4.9 kcal/mol
c     from brassfield et al, gemp-482.
c
c     beginning and end of alpha-beta transus are from data in
c     figure iii.33 of anl-76-49
c     isothermal zirconium alpha-beta transus temperature is 1135 k
c     taken from b.lustman & f.kerze "the metallurgy of zirconium"
c     mcgraw-hill book co., new york, 1955
c
c     mixed oxide melting point  was obtained from lyon et al, j. nuc.
c     mat., 22 (1967) p 332
c
c     phypro was coded by v.f. baston in may 1974
c     modified by c.s. olsen in feb. 1975
c     modified by b.w. burnham in nov. 1977
c     modified by d.l. hagrman in june 1979
c
      sldus(c)    = 2840.0 - 5.41395*c + 7.468390e-3*c*c
      liqdus(c)   = 2840.0 - 3.21860*c - 1.448518e-2*c*c
      fbu = bu/86.4
      if (comp .gt. 0.0) go to 10
c
      ftmelt = 3113.15 - 32.0*fbu/10000.0
      fdelta = 1.0e-10
      go to 20
c
   10 c1     = comp
      ftmelt = sldus(c1)  + 273.15  -  32.0*fbu/10000.0
      fdelta = liqdus(c1) - sldus(c1)- 32.0*fbu/10000.0
c
   20 fhefus = 27.4e+4
      ctmelt = 2098.15
      chefus = 22.5e+04
c
      wfox   = deloxy + 0.0012
      ctranb = 1094. + wfox * (-1.289e+03 + wfox * 7.914e+05)
      if(wfox .lt. 0.025) go to 30
      ctranb = 1556.4 + 3.8281e+04 * (wfox - 0.025)
   30 ctrane = 392.46 * ((100.*deloxy + 0.1242807)**2 + 3.1417)
      if(deloxy .lt. 4.7308937e-03) go to 40
      ctrane = (100. * deloxy + 0.12) * 491.157 + 1081.7413
   40 continue
      ctranz = 1135.15
      return
      end
