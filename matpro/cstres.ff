       subroutine cstres (ctemp,deloxy,fnck,fncn,cwkf,cwnf,rstran,
     & strant,strest)
!  cstres calculates cladding effective stress as a function of true
!  cladding strain, true cladding strain rate, cladding temperature,
!  average oxygen concentration in cladding, fast neutron fluence, and
!  cold work.
!
!     strest = output effective true stress (pa)
!     strant = input effective true plastic strain (unitless)
!     rstran = input effective true plastic strain rate (s**(-1))
!     ctemp  = input cladding meshpoint temperature (k)
!     deloxy = input average oxygen concentration excluding
!              oxide layer - average oxygen concentration of
!              as received cladding  (kg oxygen/kg zircaloy)
!     fnck   = input effective fast fluence for strength
!              coefficient (neutrons/(m**2))
!     fncn   = input effective fast fluence for strain hardening
!              exponent (neutrons/(m**2))
!     cwkf   = input effective cold work for strength
!              coefficient (unitless ratio of areas)
!     cwnf   = input effective cold work for strain hardening
!              exponent (unitless ratio of areas)
!
!  The subcode ckmn is used in this subroutine.
!
!  Conversion from pa  to  psi  is 1.4505e-04 (psi/pa).
!
!  cstres was adapted from cstran (a subroutine by R. L. Miller and
!  R. R. Hobbins) by G. A. Berna in April 1975.
!  Modified by D. L. Hagrman to reduce yield strength to measured
!  values July 1981.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
!
       real(kr) ctemp,deloxy,fnck,fncn,cwkf,cwnf,rstran,strant,strest
!
       include 'contrl.h'
!
       real(kr) ak,am,an,anl,arg
!
       if (chngno(70)) then
         call ckmnr (ctemp,deloxy,fnck,fncn,cwkf,cwnf,rstran,ak,an,am)
       else
         call ckmn (ctemp,deloxy,fnck,fncn,cwkf,cwnf,rstran,ak,an,am)
       endif
!
!  Revise an for small strains.
       if (strant .lt. (an/(1.0 + am))) an = an*an/((1.0 + am)*strant)
       anl = 0.17
       if (ctemp .gt. 730.) anl = 1.56e-02*ctemp - 11.218
       if (ctemp .ge. 780.) anl = 0.95
       if (an .gt. anl) an = anl
       arg = ak*((rstran/1.0e-3)**am)
       strest = arg*(strant**an)
       return
       end
