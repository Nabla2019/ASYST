       function coxthkr(ctemp,kpick)
!  Function coxthkr returns the growth rate constant for oxide
!  thickness, oxygen-stabilized alpha layer thickness (inner and outer),
!  and thickness of the oxygen-stabilized alpha layer between the outer
!  and inner alpha layers.
!
!  For temperatures of 1273-1853 K data, analyses from J. V. Cathcart of
!  ORNL are used to compute the ZrO2 and oxygen-sabilized alpha
!  thickness on the outer surface, and similar equations from P. Hofmann
!  of the Kernforschungszentrum Karlsruhe (KFK) are used for oxygen-
!  stabilized alpha layers on the cladding inner surface when there is
!  pellet-cladding mechanical inter-action (PCMI).
!  For temperatures of 1853- 2100 K, data and analyses from V. F.
!  Urbanic and T. H. Heidrick, "High Temperature Oxidation Of Zircaloy-2
!  And Zircaloy-4 In Steam," Journal Of Nuclear Materials 75, (1978) pp.
!  251- 261, are used to compute ZrO2 layer thickness.
!
       use ufilef
$if def,iua
       use uncdata
$endif
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       real(kr) coxthkr
       real(kr) ctemp
       integer kpick
!
!  coxthkr  output growth rate constant (m**2/s).
!  ctemp    input cladding temperature (k).
!  kpick    input integer 1-4, where
!           =1 is growth rate constant for oxide thickness;
!           =2 is growth rate constant for oxygen-stabilize alpha layer
!              nearest outer cladding surface;
!           =3 is growth rate constant for oxygen-stabilize alpha layer
!              nearest uo2 fuel;
!           =4 is growth rate constant for oxygen-stabilize alpha layer
!              between outer and inner alpha layers.
!  imodel   = 0 then Cathcart-Pawel constants are used;
!
       include 'wwer.h'
!  imodel in /wwer/: if .ne. 0 then Baker-Just constants are used; if
!  .ne. 0 then Baker-Just constants are used.
!
!  kpick = 1.
       if (kpick .eq. 1) then
         if (iwwer .ne. 1) then
           if (imodel .ne. 0) then
             coxthkr = 1.75e-4*exp(-22899./ctemp)
           elseif (ctemp .gt. 1820.0) then
             coxthkr = 8.868e-5*exp(-21538./ctemp)
           else
             coxthkr = 2.090e-6*exp(-18010./ctemp)
           endif
         else
           if (ctemp .le. 1773.) then
             coxthkr = 5.190e-7*exp(-15355./ctemp)
           else 
             coxthkr = 17.720e-7*exp(-14680./ctemp)
           endif
         endif     
!  kpick = 2.
       elseif (kpick .eq. 2) then
         if (iwwer .ne. 1) then   
           if (imodel .ne. 0) then
             coxthkr = 1.75e-4*exp(-22899./ctemp)
           else
             coxthkr = 15.142e-5*exp(-23190./ctemp)
           endif
         else
           if (ctemp .le. 1773.) then
             coxthkr = 5.190e-7*exp(-15355./ctemp)
             coxthkr = 4.400e-7*exp(-14088./ctemp) - coxthkr
           else 
             coxthkr = 7.720e-6*exp(-14680./ctemp)
             coxthkr = 5.160e-5*exp(-19520./ctemp) - coxthkr
           endif
         endif
!  kpick = 3.
       else if (kpick .eq. 3) then
         coxthkr = 0.32e-4*exp(-4.9e04/(1.987*ctemp))
!  kpick = 4.
       else if (kpick .eq. 4) then
         coxthkr = 0.70e-4*exp(-4.4e04/(1.987*ctemp))
       else
         write (output,"('0******** kpick out of range in routine coxthk
     &r.')")
         call fabend
       endif
$if def,iua
       if (uncmode) call appwtoxid (coxthkr)
$endif
       return
       end
