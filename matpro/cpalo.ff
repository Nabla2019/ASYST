       function  cpalo (tp)
c
c      cpalo    - Compute specific heat of aluminum oxide
c
c      Author:   J. E. Tolli, EG&G Idaho
c
c      Calling sequence:
c                cp = cpalo (p1)
c                where cp is the desired specific heat
c
c      Parameters:
c                p1 = temperature in Kelvin degrees
c
       implicit none
c
c  Local variables.
       integer i
       real cptbl,cpval,tp
       real cpalo
       integer numpts
c--set number of table points
       parameter (numpts=17)
c
c--set table dimensions
       dimension  cptbl(2,numpts)
c
c--specific heat data table, temperature(K),specific heat(Btu/lb-F)
c
       data  cptbl /  200.,0.121 ,  300.,0.190 ,  400.,0.225 ,
     *                500.,0.250 ,  600.,0.265 ,  700.,0.275 ,
     *                800.,0.280 ,  900.,0.288 , 1000.,0.293 ,
     *               1200.,0.300 , 1300.,0.303 , 1400.,0.309 ,
     *               1500.,0.311 , 1600.,0.317 , 1700.,0.320 ,
     *               1800.,0.325 , 1900.,0.330 /
c
c
c--check for temperature below minimum table value
c
       if (tp.lt.cptbl(1,1)) then
         cpval = 0.121
         go to 20
       endif
c
c--if temperature lies above maximum table value, extrapolate to find
c--specific heat (assume 0.005 increase in specific heat for each 100
c--increase in temperature)
c
       if (tp.gt.cptbl(1,numpts)) then
         cpval = 5.e-5 * ( tp - cptbl(1,numpts) ) + cptbl(2,numpts)
         go to 20
       endif
c
c--temperature lies within table limits;  interpolate to find specific
c--heat
c
       do 10 i=2,numpts
         if (tp.le.cptbl(1,i)) then
           cpval = cptbl(2,i-1) +
     *             ( tp - cptbl(1,i-1) ) / ( cptbl(1,i) - cptbl(1,i-1) )
     *             * ( cptbl(2,i) - cptbl(2,i-1) )
           go to 20
         endif
   10  continue
c--convert specific heat value to J/kg-K and put into cpalo
   20  cpalo = 4.186e3*cpval
      if (tp.le.933.4 .and. tp.ge.933.0) cpalo = cpalo+3.9553e5
       return
c
 1000  format ('0******** From cpalo: temperature',1p,g15.7,' is too low
     *.')
       end
