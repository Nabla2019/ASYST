      subroutine ccrpr (ctemp,chstrs,cflux,delt,strnph,strnih,stranh,rst
     +rnh)
c   ccrpr calculates the primary and irradiation-caused
c   circumferential components of cladding creep strain
c   as a function of cladding temperature,the circumferential
c   component of cladding stress, fast neutron flux,
c   time step size, and the primary circumferential
c   component of cladding strain at the start of a time
c   step.  the strain rate at the start of the time step
c   is also calculated.
c   stranh = output total circumferential component of cladding
c   creep strain at the end of a time step
c   (unitless ratio)
c   strnph = output primary part of circumferential component of
c   cladding creep strain at the end of a time step
c   (unitless ratio)
c   strnih = output irradiation-caused part of curcumferential
c   component of cladding creep strain at the end
c   of a time step (unitless ratio)
c   rstrnh = output circumferential component of cladding
c   creep strain rate at the input primary
c   circumferential creep strain (s**(-1))
c   utranh = output expected standard error of stranh
c   ctemp  = input cladding temperature (k)
c   chstrs = input circumferential component of cladding
c   stress (pa)
c   cflux  = input fast neutron flux (neutrons/((m**2)*s))
c   delt   = input time step size (s)
c   strnph = input primary part of circumferential
c   component of cladding creep strain at the
c   start of a time step (unitless ratio)
c   stranh = input circumferential component of cladding
c   creep strain at the start of a time step
c   (unitless ratio)
c   the equations used in this subroutine are based on data from
c   (1) d. o. hobson, creepdown of zircaloy fuel cladding
c   initial tests, ornl/nureg/tm-181 (april 1978)
c   (2) a. a. bauer, l. m. lowry and j. s. perrin, progress on
c   evaluating strength and ductility of irradiated zircaloy
c   during july through september 1975, bmi-1938 (september
c   1975)
c   (3) a. a. bauer, l. m. lowry and j. s. perrin, evaluating
c   strength and ductility of irradiated zircaloy. quarterly
c   progress report january through march 1977, bmi-nureg-1971
c   (april 1977)
c   (4) r. d. warda, v. fidleris and e. teghtsoonian, dynamic
c   strain aging during creep of a-zr, metallurgical
c   transactions 4 (1973) pp 1201-1206
c   ccrpr was coded by c. e. dixon june 1975
c   modified by g. a. reymann nov 1976
c   modified by d. l. hagrman jan 1980
      t = ctemp
      if (ctemp.lt.500.0) t = 500.0
      if (ctemp.gt.645.0) t = 645.0
      aq = 2.1427e+05+t*(-5.324e+02+t*(1.17889e-01+t*3.3486e-04))
      ex = (exp(-aq/t))/2.76e-07
      a = (1.26e-12)*ex*(chstrs**3.)/abs(chstrs)
      b = (1.81e-16)*ex*(chstrs**2.)
      c = (2.20e-07)*(exp(-5000./t))*chstrs/(t**7)
      rstnph = b*(a-strnph)
      rstnih = 0.0
      if (cflux.gt.1.0e+00) rstnih = c*(cflux**0.65)
      strnih = rstnih*delt+stranh-strnph
      strnph = (a-strnph)*(1.-exp(-b*delt))+strnph
      stranh = strnph+strnih
      rstrnh = rstnph+rstnih
      utranh = stranh/2.0
      return
      end
