       subroutine fswell (fdens, comp, bu, bul, ftemp, gaswl, soldsw)
c
c      comp   = input plutonia content (weight percent)
c      fdens  = input initial density of the fuel (kg/m**3)
c      ftemp  = input temperature of the fuel ring (k)
c      bu     = input burnup to end of time step (mw-s/kg-u)
c      bul    = input burnup to end of last time step (mw-s/kg-u)
c      gaswl  = output fuel swelling due to gas fission products
c                      (fraction)
c      soldsw = output fuel swelling due to solid fission products
c                      (fraction)
c
c      fswell was developed and programmed by r. e. mason - june 1978.
c
c      the uo2 swelling correlation is based on the following data
c               chubb,et al., nuclear technology 18 (1973)
c               turnbull, journ. nucl. mater., 50 (1974)
c               banks, journ. nucl. mater., 54 (1974)
c               daniel et al., wapd - 263 (1962)
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
       bus    = fdens * 2.974e+10 * (bu - bul)
       soldsw = bus * 2.5e-23
       if (ftemp .lt. 2800.) go to 25
       gaswl  = 0.0
       go to 50
   25  continue
       gaswl  = (8.80e-50*((2800.-ftemp)**11.73)*exp(-2.4e-10*bu*fdens)
     #           *exp(-0.0162 * (2800. - ftemp))) * bus
   50  continue
       return
       end
