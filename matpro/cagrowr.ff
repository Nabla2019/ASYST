       function cagrowr(ctemp,cflux,time,catexf,coldw)
!  cagrowr calculates axial cladding growth strain as a function of
!  temperature (ctemp), fast neutron flux (cflux), time, axial texture
!  factor (catexf), and cold work (coldw).
!
!  The equation used in this subroutine was based on data from
!    (1) P .H. Kreyns quoted by E. Duncombe, J. E. Mayer and W. A.
!        Coffman, "Comparisons with Experiment of Calculated Changes and
!        Failure Analysis of Irradiated Bulk Oxide Fuel Test Rods Using
!        the Cygro-1 Computer Program," Westinghouse Report WAPD-TM-583,
!        1966.
!    (2) J. E. Harbottle, "The Temperature and Neutron Dose Dependence
!         of Irradiation Growth in Zircaloy-2" in ASTM-STP-484 (1970).
!    (3) R. C. Daniel, "In-Pile Dimensional Changes of Zircaloy-4 Tubing
!        Having Low Hoop Stresses (lWBR Development Program)",
!        Nuclear Tech. 14 (1972).
!    (4) R. B. Adamson from Fidleris, AECL-4766.
!
!  cagrowr coded by D. L. Hagrman, August 1975.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       real(kr) cagrowr
       real(kr) ctemp,cflux,time,catexf,coldw
!
!  cagrowr = output axial cladding growth strain (m/m).
!  ctemp   = input cladding temperature (k).
!  cflux   = input fast neutron flux ((neutrons/m**2)/s).
!  time    = input time at temperature (s).
!  catexf  = input texture factor for the axial direction (unitless).
!  coldw   = input cold work (meters**2/meter**2).
!
       include 'wwer.h'
!
       real(kr) t
!
       t = ctemp
       if (iwwer .eq. 0) then
         if (t .gt. 633.0) t = 633.0
         if (t .lt. 313.0) t = 313.0
!
         cagrowr = 1.407e-16*exp(2.408e+2/t)*sqrt(cflux*time)*
     &   (1.0 - 3.0*catexf)*(1.0 + 0.02*coldw)
       else
!
!  Block calculates axial cladding growth strain as a function of
!  temperature (ctemp), fast neutron flux (cflux).
!  Data is derived from Volkov B. Yu. et. al, "Material Property Library
!  for H1-Alloy Cladding", Preprint IAE-4941/11, 1989.
!
         if (t .le. 600.0) then
           cagrowr = 2.5e-14*exp(-725.0/t)*cflux**0.43
         elseif (t .le. 675.0) then
           cagrowr = 3.8e-12*exp(-3740.0/t)*cflux**0.43
         else
           cagrowr = 5.6e-08*exp(-10200.0/t)*cflux**0.43
         endif
       endif
       return
       end
