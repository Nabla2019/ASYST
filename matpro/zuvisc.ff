      function zuvisc(u,z,zutemp)
c
c     zuvisc returns zirconium-uranium-oxygen compound
c     viscosities
c
c     zuvisc = output compound viscosity (pa*s)
c     u      = input atomic fraction uranium in compound
c              (atoms uranium/atoms in compound)
c     z      = input atomic fraction zirconium in compound
c              (atoms zirconium/atoms in compound)
c     zutemp = compound temperature (k)
c
c     the subcodes psol and pliq are called by this function
c
c     zuvisc was coded by d. l. hagrman march 1982.
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
      x = 1. - u - z
      if((u + z) .lt. (0.5 * x)) then
         uo = u
         u = u/(3. * (u + z))
         z = z/(3. * (uo + z))
         x = 1. - u - z
      else
         if((0.5 * x) .lt. u) then
            uo = u
            u = u/(3. * u + z)
            z = z/(3. * uo + z)
            x = 1. - u - z
         endif
      endif
      tliq = pliq(x,u,z)
      tsol = psol(x,u,z)
      if(zutemp .le. tsol) then
         zuvisc = 1.38 * exp(4.942e+04/zutemp)
      else
         fuo2 = u/(u+z)
         fzo2 = (0.5 * x - u)/(u + z)
         fzr  = 1. - fuo2 - fzo2
         vuo2 = 1.23e-02 - zutemp*2.09e-06
         vzo2 = 1.22e-04 * exp(10500./zutemp)
         vzr = 1.90e-04 * exp(6500./zutemp)
         vliq = fuo2*vuo2 + fzo2*vzo2 + fzr*vzr
         if(zutemp .lt. tliq) then
            vsol = 1.38 * exp(4.942e+04/zutemp)
            zuvisc = (vliq*(zutemp-tsol)+vsol*(tliq-zutemp))/
     #              (tliq - tsol)
         else
            zuvisc = vliq
         endif
      endif
      return
      end
