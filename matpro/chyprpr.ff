       subroutine chyprpr (deloxy,csol,cliq,ctranb,ctrane)
!  chyprpr returns zircaloy phase transition temperatures.
!
!  chyprpr was coded by D. L. Hagrman March 1982.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       real(kr) deloxy,csol,cliq,ctranb,ctrane
!
!  deloxy = input oxygen concentration - oxygen concentration of as
!           received cladding (kg oxygen/kg zircaloy).
!  csol   = output zircaloy solidus temperature (K).
!  cliq   = output zircaloy liquidus temperature (K).
!  ctranb = output low temperature boundary of the alpha-beta phase
!           region (k). a + b to a boundary equal csol means no boundary
!           at given deloxy.
!  ctrane = output high temperature boundary of the alpha-beta phase
!           region (k). b to a + b boundary equal csol means no boundary
!           at given deloxy.
!
       include 'wwer.h'
!
       real(kr) wfox,x
!
!  Convert weight fraction oxygen to atomic fraction oxygen = x wfox
!  equals total oxygen content in weight fraction units.
       if (iwwer .eq. 0) then  
         wfox = deloxy + 0.0012
         x = wfox/(wfox + 0.1754*(1.0 - wfox))
       else
         wfox = deloxy + 0.0005
       endif 
!
!  Solidus correlations.
       if (iwwer .eq. 0) then 
         if (x .le. 0.1) then
           csol = x*1150.0 + 2098.0
         else
           if (x .le. 0.18) then
             csol = 2213.0
           else
             if (x .le. 0.29) then
               csol = 1.3895317e3 + x*
     &         (7.6400748e3 - x*1.7029172e4)
             else
               if (x .le. 0.63) then
                 csol = 2173.0
               else
                 if (x .le. 0.667) then
                   csol = -1.1572454e4 + x*2.1818181e4
                 else
                   csol = -1.1572454e4 + (1.334 - x)*2.1818181e4
                 endif
               endif
             endif
           endif
         endif
       else
!
!  Data derived from Kulikova K.V., Tonkov V. Ju., "Material Reseach
!  Aspects of the LWR Fuel Rod Zircaloy Cladding Behaviour at LOCA
!  Conditions," Survey 4(74), Moscow, 1982.
!
         if (x .le. 0.137) then
           csol = 2133.0 + 496.75*x - 5302.4*x**2 +
     &     43349.4*x**3
         elseif (x .le. 0.2) then
           csol = 2213.0
         else
           csol = 1471.12 + 6117.89*x - 12042.5*x**2
         endif
       endif         
!  Liquidus correlations.
       if (iwwer .eq. 0) then 
         if (x .le. 0.19) then
           cliq = 2.125e3 + x*(1.6321637e3 - x*5.3216374e3)
         else
           if (x .le. 0.41) then
             cliq = 2.11165553e3 + x*(1.1590909e3 - x*2.4621212e3)
           else
             if (x .le. 0.667) then
               cliq = 8.9507792e2 + x*3.1168831e3
             else
               cliq = 8.9507792e2 + (1.34 - x)*3.1168831e3
             endif
           endif
         endif
       else
         cliq = 2133.0 + 1111.43*x - 2528.57*x**2
       endif           
!
!  Calculate alpha-beta phase boundaries.
       if (iwwer.eq.0) then 
         if (wfox .lt. 0.025) then
           ctranb = 1094.0 + wfox*(-1.289e3 + wfox*7.914e5)
         else
           ctranb = 1556.4 + 3.8281e4*(wfox - 0.025)
         endif
         if (ctranb .gt. csol) then
           ctranb = csol
         endif
         if (deloxy .lt. 4.7308937e-3) then
           ctrane = 392.46*((100.0*deloxy + 0.1242807)**2 +
     &     3.1417)
         else
           ctrane = (100.0*deloxy + 0.12)*491.157 +
     &     1081.7413
         endif
         if (ctrane .gt. csol) then
           ctrane = csol
         endif
       else
         ctranb = 883.0 + 34150.0*x - 137500.0*x**2        
         if (ctranb .gt. csol) then
           ctranb = csol
         endif
         if (x .lt. 0.011) then
           ctrane = 1153.0 + 10666.6*x
         elseif (x .lt. 0.056) then
           ctrane = 1273.0 + 8888.88*(x - 0.011)
         else
           ctrane = 1673.0 + 6666.66*(x - 0.056)
           if (ctrane .gt. csol) then
             ctrane = csol
           endif
         endif
       endif
       return
       end
