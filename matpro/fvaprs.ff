       subroutine fvaprs (ftemp,fom,frpu,uo2vap,puovap,vapmix,uoxvap,
     * diovap)
c
c      ftemp  = input fuel temperature(k).
c      fom    = input fuel oxygen to metal ratio(unitless)
c      frpu   = input puo2 in fuel(weight fraction)
c      uo2vap = output total vapor pressure of urania
c               vapor species (log(pa))
c      puovap = output total vapor pressure of plutonia
c               vapor species (log(pa))
c      vapmix = output total vapor pressure of mixed oxide
c               vapor species (log(pa))
c      uoxvap = output vapor pressure of monatomic oxygen (log(pa))
c      diovap = output vapor pressure of diatomic oxygen (log(pa))
c
c                   - data base -
c      urania;
c              m. tetenbaum and p. d. hunt, "total pressure of
c                  urania-bearing species over oxygen-deficient urania,"
c                  journal of nuclear materials, 34 (1970).
c              r. szwarc and r. e. latta, "vapor pressure of
c                  hypostoichiometric urania as a function of
c                  composition," journal of the american ceramic
c                  society, 51 (1968).
c              m. bober, h. u. karow and k. schretzmann, "vapor
c                  pressure measurements of oxide fuel between 3000 and
c                  5000 k using laser heating laser heating," nuclear
c                  technology, 26 (1975).
c              g. t. reedy and m. g. chasanov, "total pressure of
c                  uranium bearing species over molten urania,"journal
c                  of nuclear materials, 42 (1972).
c              r. j. ackermann, e. g. rauh, m. s. chandrasekharaiah,
c                  "a thermodynamic study of the urania-uranium system"
c                   journal of  physical chemistry, 73 (1969).
c              g. benezech, j. p. coutures and m. fox, transpiration
c                  study of uranium dioxide vaporization processes
c                  between 2200 k and 2600 k, anl-trans-972 (1974).
c              r. w. ohse, p. g. berrie, h. g. bogensberger,
c                  e. a. fischer, "extension of vapor pressure
c                  measurements of nuclear fuels (u,pu)o2 and uo2 to
c                  7000 k for fast reactor safety analysis," journal
c                  of nuclear materials, 59 (1976).
c              c. a. alexander, j. s. 0gden, and g. w. cunningham,
c                  thermal stability of zirconia-and thoria based fuels,
c                  bmi-1789 (1967).
c              r. w. ohse, "high-temperature vapor-pressure studies
c                  of uo2 by the effusion method and its thermodynamic
c                  interpretation," journal of chemical physics, 44
c                  (1966).
c              d. a. bension, application of pulsed electron beam
c                  vaporization to studies of uo2, sand-77-0429 (1977).
c              j. r. babelot, g. d. brumme, p. r. kinsmann and
c                  r. w. ohse, "vapor pressure measurement over
c                  liquid uo2 and (u,pu)o2 by laser surface heating
c                  up to 5000 k," atomwirtschaft (1977).
c
c      plutonia;
c              r. j. ackermann, r. l. faircloth, m. h. rand,
c                  "a thermodynamic study of the vaporization behavior
c                  of the substoichiometric plutonium dioxide phase,"
c                  journal of pysical chemistry, 70 (1966).
c              r. w. ohse and c. ciani, "evaporation behavior
c                  and high-temperature thermal analysis of
c                  substoichiometric plutonium oxide for
c                  1.51<o/pu<2.00,"thermodynamics of nuclear materials,
c                  iaea, vienna (1968).
c
c      oxygen;
c              c. a. alexander, j. s. 0gden, and g. w. cunningham,
c                  thermal stability of zirconia-and thoria based fuels,
c                  bmi-1789 (1967).
c              m.tetenbaum and p. d. hunt, "high temperature
c                  thermodynamic properties of oxygen-deficient
c                  urania," journal of chemical physics, 49 (1968).
c              t. l. markin, v. j. wheeler and r. j. bones,
c                  "high temperature thermodynamic data for uo2+x,"
c                  journal of inorganic nuclear chemistry, 30 (1968).
c              v. j. wheeler, "high temperature thermodynamic
c                  data for uo2-x," journal of nuclear materials, 39
c                  (1972).
c              k. hagemark and m. broli, "equilibrium oxygen
c                  pressures over nonstoichiometric uranium oxides
c                  between uo2+x and u3o8-z at higher temperatures,"
c                  journal of inorganic nuclear chemistry, 28 (1966).
c              p. e. blackburn, "oxygen dissociation pressures
c                  over uranium oxides," journal of physical chemistry
c                  8 (1958).
c              s. aronson and u. belle, "nonstoichiometry in uranium
c                   dioxide," journal of chemical physics, 29 (1958).
c              k. kiukkola, "high-temperature electrochemical study
c                  of uranium oxides in the uo2-u308 region," acta
c                  chemica scandinavica 16 (1962).
c              t. l. markin and r. j. bones, the determination of
c                  some thermodynamic properties of uranium oxides
c                  with o/u ratios between 2.00 and 2.03 using a
c                  high temperature galvanic cell, aere-r 4178 (1962).
c
c      the constants listed in the data statements below are for
c      the following materials  in this order ;
c
c        puo2
c        u02
c        o
c        o2
c
c      coded by richard e. mason april 1979.
c
$if def,in32,1
       implicit real*8(a-h,o-z)
c
       dimension a(7), b(7), c(7), d(7), e(7)
c
c                 x < 1.99        x > 2.00
       data a /-0.5404056e+04,  0.1788581e+05,
     #         -0.1119149e+05,  0.0          ,
     #         -0.4953488e+05, -0.1463821e+05,
     #                         -0.1463821e+05/
       data b / 0.1816551e+02,  0.3058922e+02,
     #          0.9993252e+01,  0.0          ,
     #          0.0          ,  0.0          ,
     #                          0.0          /
       data c /-0.3389145e-02, -0.4706266e-02,
     #         -0.1318286e-02,  0.0          ,
     #          0.0          ,  0.0          ,
     #                          0.0          /
       data d /-0.1306529e+03, -0.2337348e+03,
     #         -0.6917448e+02,  0.0          ,
     #          0.1518089e+02,  0.6206205e+01,
     #                          0.6293306e+01/
       data e / 0.6854587e+04,  0.0          ,
     #          0.0          ,  0.0          ,
     #         -0.1418138e+04,  0.2177517e+02,
     #                          0.1803860e+01/
c
       vapeqn(xa,xb,xc,xd,xe,x,t) = (xa + xe*x)/t + xb*log(t) + xc*t +
     * xd
       if (fom .le. 0.0) fom = 2.0
       x      = abs(fom - 2.)
       if (frpu .ge. 1.0) go to 20
c
c      partial pressure of oxide vapor.
c      partial pressures of monatomic oxygen are calculated up
c      to oxygen to metal ratios of 2.000 after which diatomic
c      oxygen pressures are calculated.
       if (fom .le. 1.990) go to 5
       if (fom .ge. 2.004) go to 6
       if (fom .le. 2.000) go to 4
       go to 9
    4  continue
       uoxvap = d(5) + (a(5) + e(5) * 6.9078)/ftemp
       diovap = 0.0
       go to 10
    5  continue
       uoxvap = d(5) + (a(5) + e(5) * log(x))/ftemp
c      for fom < 1.999 the uncertainity is + or - 0.801
       diovap = 0.0
       go to 10
    6  continue
       diovap = d(6) + a(6)/ftemp + e(6) * x
c      for fom < 2.004 the uncertainity is + or - 0.976
       uoxvap = 0.0
       go to 10
    9  continue
       o      = d(5) + (a(5) + e(5) * 6.9078)/ftemp
       oo2    = 2. * (o + 13384.87/ftemp - 3.52)
       o2 = d(7) + a(7)/ftemp + e(7)*log(2.004 - fom)
       if (oo2 .ge. o2) diovap = oo2
       if (oo2 .lt. o2) diovap = o2
       uoxvap = 0.0
c      the total uncertainity is + or - 0.0000.
c      partial pressure of urania vapor species.
   10  continue
       uo2vap = vapeqn(a(3),b(3),c(3),d(3),e(3),0.0,ftemp)
c      the total uncertainity is + or - 0.206
   18  if(frpu .le. 0.0) go to 100
c      partial pressure over plutonia
   20  continue
       if (fom .ge. 2.) x = 0.0
       m      = 1
       puovap = vapeqn (a(1),b(1),c(1),d(1),e(1),x,ftemp)
c      uncertainity is + or - 0.559
       if ( frpu .ge. 1.0) go to 100
   30  continue
c      partial pressure of mixed oxides.
       vapmix = log10((10.**uo2vap)*(1.-frpu)+frpu*(10.**puovap))
c      the uncertainity is + or - 0.442
c      vapmix = vapeqn (a(2),b(2),c(2),d(2),e(2),x,ftemp)
  100  continue
       return
       end
