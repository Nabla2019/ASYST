       function initds(os,eta)
!***BEGIN PROLOGUE  INITDS
!***PURPOSE  Determine the number of terms needed in an orthogonal
!            polynomial series so that it meets a specified accuracy.
!***LIBRARY   SLATEC (FNLIB)
!***CATEGORY  C3A2
!***TYPE      DOUBLE PRECISION (INITS-S, INITDS-D)
!***KEYWORDS  CHEBYSHEV, FNLIB, INITIALIZE, ORTHOGONAL POLYNOMIAL,
!             ORTHOGONAL SERIES, SPECIAL FUNCTIONS
!***AUTHOR  Fullerton, W., (LANL)
!***DESCRIPTION
!
!  Initialize the orthogonal series, represented by the array OS, so
!  that INITDS is the number of terms needed to insure the error is no
!  larger than ETA.  Ordinarily, ETA will be chosen to be one-tenth
!  machine precision.
!
!
!***REFERENCES  (NONE)
!***ROUTINES CALLED  xermsg
!***REVISION HISTORY  (YYMMDD)
!   770601  DATE WRITTEN
!   890531  Changed all specific intrinsics to generic.  (WRB)
!   890831  Modified array declarations.  (WRB)
!   891115  Modified error message.  (WRB)
!   891115  REVISION DATE from Version 3.2
!   891214  Prologue converted to Version 4.0 format.  (BAB)
!   900315  CALLs to XERROR changed to CALLs to xermsg.  (THJ)
!***END PROLOGUE  INITDS
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
!
       integer initds
       real(kr) os(:),eta
!  os     Array of NOS coefficients in an orthogonal series.
!  eta    Scalar containing requested accuracy of series.
!
       real(kr) err
       integer i,ii,nos
!  nos    Number of coefficients in os.
!
!  First executable statement.
       nos = size(os)
       if (nos .lt. 1) call xermsg ('slatec', 'initds',
     & 'Number of coefficients is less than 1', 2, 1)
!
       err = 0.0
       do ii = 1,nos
         i = nos + 1 - ii
         err = err + abs(os(i))
         if (err .gt. eta) exit
       enddo
!
       if (i .eq. nos) call xermsg ('slatec', 'initds',
     & 'Chebyshev series too short for specified accuracy', 1, 1)
       initds = i
!
       return
       end function initds
