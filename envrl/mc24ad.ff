       subroutine mc24ad (n,icn,a,licn,lenr,lenrl,w)
!  Alias mc24ad
!  Date   01 Jan 1984     Copyright UKAEA, Harwell.
!
       implicit none
       integer, parameter :: kr=selected_real_kind(12,200)
       integer licn,n
       real(kr) a(:),w(:)
       integer icn(:),lenr(:),lenrl(:)
!
       real(kr) amaxl,amaxu,wrowl
       integer i,j,j0,j1,j2,jj
!
       amaxl = 0.0
       w(1:n) = 0.0
       j0 = 1
       do i = 1,n
         if (lenr(i).eq.0) cycle
         j2 = j0 + lenr(i) - 1
         if (lenrl(i).ne.0) then
!  Calculation of 1-norm of l.
           j1 = j0 + lenrl(i) - 1
           wrowl = 0.0
           do jj = j0,j1
             wrowl = wrowl + abs(a(jj))
           enddo
!  amaxl is the maximum norm of columns of l so far found.
           amaxl = max(amaxl,wrowl)
           j0 = j1 + 1
!  Calculation of norms of columns of u (max-norms).
         endif
         j0 = j0 + 1
         do jj = j0,j2
           j = icn(jj)
           w(j) = max(abs(a(jj)),w(j))
         enddo
         j0 = j2 + 1
       enddo
!  amaxu is set to maximum max-norm of columns of u.
       amaxu = 0.0
       do i = 1,n
         amaxu = max(amaxu,w(i))
       enddo
!  grofac is max u max-norm times max l 1-norm.
       w(1) = amaxl*amaxu
       return
       end
