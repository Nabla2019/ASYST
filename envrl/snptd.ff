*deck snptd
        subroutine snptd( ludbg , subnam,
     &                    xname , tensor,
     &                    dimrow, dimcol, dimpln,
     &                    rowone, rowlst,
     &                    colone, collst,
     &                    plnone, plnlst)
        implicit none
c
c   write out a double precision tensor (3-D) with 1: starting index
c      with labels row => row (i), col => column (j) , pln => plane (k)
c
c   subroutine arguments
        integer      ludbg , dimrow, dimcol, dimpln
        integer      rowone, rowlst, colone, collst, plnone, plnlst
        real*8       tensor(dimrow,dimcol,dimpln)
        character*(*) subnam
        character*(*) xname
c
c   local variables
        integer    i     , k     , kup   , row   , pln
        integer    maxlin
        parameter (maxlin=7)
c
        if (rowone .gt. rowlst .or.
     &      colone .gt. collst .or.
     &      plnone .gt. plnlst     ) return
c
        write(ludbg,800) subnam, xname , dimrow, dimcol, dimpln,
     &                   rowone, rowlst, colone, collst,
     &                   plnone, plnlst
c
        do 30 pln = plnone, plnlst
          do 20 row = rowone, rowlst
            do 10 i = colone, collst, maxlin
              kup = min(collst, i + maxlin - 1)
              write(ludbg,802) row, i, pln,
     &                         (tensor(row,k,pln), k=i, kup)
 10         continue
 20       continue
 30     continue
c
 800    format(a,': ',
     &         a,' tensor('i5,','i5,','i5,') for',/,
     &         '  row index = ',i5,' to ',i5,
     &         ', col index = ',i5,' to ',i5,
     &         ', pln index = ',i5,' to ',i5,' => (row,col,pln)')
 802    format('(',i3,',',i3,',',i3'):',1p,8e12.4)
c
        return
        end
