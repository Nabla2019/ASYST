       subroutine spln21 (nx,x,ny,y,f00,f10,f01,f11,idm,t1)
c  Computes two dimensional spline values using first derivatives.
c
c  nx      Number of points in the x direction.
c  x       Array containing nx independent variables supplied by user
c          on entry.
c  ny      Number of points in the y direction.
c  y       Array containing ny independent variables supplied by user
c          on entry.
c  f00     Array containing nx*ny dependent variables supplied by user
c          on entry.
c  f10     Array containing nx*ny first derivatives with respect to x.
c          First and last column derivatives must be supplied by user
c          on entry. Array contains first derivatives with respect to x
c          for all points on return.
c  f01     Array containing nx*ny first derivatives with respect to y.
c          First and last row derivatives must be supplied by user
c          on entry. Array contains first derivatives with respect to y
c          for all points on return.
c  f11     Array containing nx*ny mixed first derivatives with respect
c          to x and y.  Corner derivatives must be supplied by user
c          on entry. Array contains mixed derivatives with respect to x
c          and y for all points on return.
c  idm     Maximum first dimension of f00, f10, f10, and f11.
c  t1      Array of dimension max(nx,ny) used for temporary storage.
c
       implicit none
c
       integer idm,nx,ny
       real f00(idm,ny),f10(idm,ny),f01(idm,ny),f11(idm,ny),t1(*),
     * x(nx),y(ny)
c
       integer i,j
       external spln11
c
       do 1 j = 1,ny
         call spln11 (nx,x,f00(1,j),f10(1,j),1,t1)
    1  continue
       do 2 i = 1,nx
         call spln11 (ny,y,f00(i,1),f01(i,1),idm,t1)
    2  continue
       call spln11 (ny,y,f10(1,1),f11(1,1),idm,t1)
       call spln11 (ny,y,f10(nx,1),f11(nx,1),idm,t1)
       do 3 i = 1,ny
         call spln11 (nx,x,f01(1,i),f11(1,i),1,t1)
    3  continue
       return
       end
