       subroutine mains (a,kk,nn,m,b,n,det,ifs)
c      double pivotal matrix inversion subroutine
c          d.h. gipson       phillips petroleum co.          june, 1962
c
c
       dimension a(kk,kk),b(kk,nn),irow(100),jcol(100)
c
c
c          initialization                                       part %1<
c
    1  do 2 i=1,m
       irow(i)=0
    2  jcol(i)=0
       det=1.0
       ifs=1
       do 23 k=1,m
c
c          find the pivot element
c
    3  pivot=0.
       do 8 j=1,m
       if(jcol(j))8,4,8
    4  do 7 i=1,m
       if(irow(i))7,5,7
    5  x= abs(a(i,j))
       if(x-pivot)7,6,6
    6  pivot=x
       jp=j
       ip=i
    7  continue
    8  continue
c
c          test to see if matrix is singular
c
       pivot=a(ip,jp)
       det=det*pivot
       if(pivot)10,9,10
    9  ifs=0
       return
c
c          remember location of the pivot element
c
   10  irow(ip)=jp
       jcol(jp)=ip
c
c          arithmetic operations on pivotal column
c
       a(ip,jp)=1.0/pivot
       do 12 i=1,m
       if(i-ip)11,12,11
   11  a(i,jp)=-a(i,jp)/pivot
   12  continue
c
c          arithmetic operations on other columns of (a)        p
c
       do 17 j=1,m
       if(j-jp)13,17,13
   13  if(a(ip,j))14,17,14
   14  alpha=a(ip,j)
       a(ip,j)=alpha/pivot
       do 16 i=1,m
       if(i-ip)15,16,15
   15  a(i,j)=a(i,j)+alpha*a(i,jp)
   16  continue
   17  continue
c
c          arithmetic operations on the columns of (b)          part (4)
c
       if(n)18,23,18
   18  do 22 j=1,n
       if(b(ip,j))19,22,19
   19  alpha=b(ip,j)
       b(ip,j)=alpha/pivot
       do 21 i=1,m
       if(i-ip)20,21,20
   20  b(i,j)=b(i,j)+alpha*a(i,jp)
   21  continue
   22  continue
   23  continue
c
c          row permutations
c
       do 30 k=1,m
   24  i=irow(k)
       if(i-k)25,30,25
   25  det=-det
       do 26 j=1,m
       temp=a(i,j)
       a(i,j)=a(k,j)
   26  a(k,j)=temp
       if(n)27,29,27
   27  do 28 j=1,n
       temp=b(i,j)
       b(i,j)=b(k,j)
   28  b(k,j)=temp
   29  irow(k)=irow(i)
       irow(i)=i
       go to 24
   30  continue
c
c          column permutations
c
       do 34 k=1,m
   31  j=jcol(k)
       if(j-k)32,34,32
   32  do 33 i=1,m
       temp=a(i,j)
       a(i,j)=a(i,k)
   33  a(i,k)=temp
       jcol(k)=jcol(j)
       jcol(j)=j
       go to 31
   34  continue
       return
       end
