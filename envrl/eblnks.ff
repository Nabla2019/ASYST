       program eblnks
c  Program to remove blanks from Fortran statements to allow diff to
c  check only significant changes.
c
       implicit none
       integer i,il,is
       character line*96,stm*1440
       logical comf,contf,endf,frstf,prcf,prf,rdf,trmnf
c
       frstf = .true.
       endf = .false.
       prcf = .false.
       prf = .false.
       trmnf = .false.
       rdf = .true.
c  Read line of code.
   10  if (rdf) then
         read (5,'(a)',end=100) line
c  Determine if comment card or continuation card.
         comf = line(1:1).eq.'c' .or. line(1:1).eq.'*' .or.
     *   line(1:1).eq.'$'
         contf = line(6:6).ne.'0' .and. line(6:6).ne.' '
         go to 11
  100    endf = .true.
       endif
   11  rdf = .true.
c  Processs line.
       if (frstf) then
c  Logic if processing first line of a statement.
         if (comf .or. endf) then
           if (comf) prf = .true.
         else
           if (contf) then
             write (6,1001)
 1001  format ('******** Continuation card found as first card of statem
     *ent.')
             stop 'Error'
           else
             is = 1
             il = 1
             prcf = .true.
             frstf = .false.
           endif
         endif
       else
         if (comf .or. endf) then
           if (comf) prf = .true.
           trmnf = .true.
           prcf = .false.
         else
           if (contf) then
             if (line(1:5) .ne. ' ') then
               write (6,1002)
 1002  format ('******** Nonblanks in columns 1 through 5 of a continuat
     *ion card.')
               stop 'Error'
             else
               il = 7
               prcf = .true.
             endif
           else
             trmnf = .true.
             prcf = .false.
             rdf = .false.
           endif
         endif
       endif
c  Process line by moving nonblank characters from line to stm.
       if (prcf) then
         prcf = .false.
         do 21 i = il,72
           if (line(i:i) .ne. ' ') then
             is = is + 1
             if (is .gt. 1440) then
               write (6,1003)
 1003  format ('******** Statement exceeds allowed number of characters.
     *')
               stop 'Error'
             else
               stm(is:is) = line(i:i)
             endif
           endif
   21    continue
       endif
c  Print statement with removed blanks.
       if (trmnf) then
         trmnf = .false.
         frstf = .true.
         write (6,'(72a)') stm(1:is)
       endif
c  Print comment line as read in.
       if (prf) then
         prf = .false.
         do 31 i = 72,2,-1
           if (line(i:i) .ne. ' ') go to 32
   31    continue
         i = 1
   32    write (6,'(a)') line(1:i)
       endif
c  Return to process next line.
       if (.not.endf) go to 10
       stop
       end
